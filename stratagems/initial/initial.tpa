INCLUDE ~%scsroot%/ds/ds.tph~


DEFINE_ACTION_FUNCTION initial BEGIN

////////////////////////
///// BG1/BG2
////////////////////////

   LAF random_useful_items END
   
   LAF detectable_spells_setup STR_VAR path= ~%scsroot%/ds~ END
   LAF detectable_spells STR_VAR path= ~%scsroot%/ds~ table = ~cddetect.2da~ END
   
   ACTION_IF !skip_tobex BEGIN
       LAF detectable_spells STR_VAR path=~%scsroot%/ds~ table = dw_enchantment.2da END
   END
   ACTION_IF enhanced_edition BEGIN
      LAF ee_specific_content END
   END


   LAF detectable_items END

   LAF run STR_VAR file=detectcloud END
   LAF general_creature_patch END
   LAF run STR_VAR file=sim_compat END
   LAF configure_tobex END
   LAF level_one_innates END

   LAF misc_ids END                              

////////////////////////
///// BG2
////////////////////////
   ACTION_IF is_bg2 BEGIN
   
     LAF spell_shield END

     LAF initial_bg2_misc END

     LAF include STR_VAR file=stoneskin.tph END // stoneskins that are immune to breach
     LAF initial_stoneskin END

     LAF detectable_weapon_enchantment END
   
     LAF include STR_VAR file=detecttrap.tph END
     LAF detect_trap END
     



   END ELSE BEGIN
   
            LAF resolve_stat INT_VAR index=109 STR_VAR id=~WEAPON_ENCHANTMENT~ END

   END
////////////////////////
///// BG1
////////////////////////

   ACTION_IF is_bg1 BEGIN

      ACTION_IF !enhanced_edition BEGIN
       LAF include STR_VAR file=~beregost.tph~ END
       LAF Ascension_beregost_fix END
      END
      LAF run STR_VAR file=~bg1fix~ END
   END

END




///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// BG1/BG2
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////
///	Make innate abilities L1 to avoid crashes (we do this
///     selectively for now - I think the Fixpack does it, but
///     at least on BGEE not all are caught)
///////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION level_one_innates BEGIN

   LAF edit_spell STR_VAR spell="PALADIN_SUMMON_DEVA BLACKGUARD_SUMMON_FALLEN_DEVA" editstring="level=>1" allow_missing=1 END


END

///////////////////////////////////////////////////////////////
///	Random useful items
///////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION random_useful_items BEGIN

  LAF clone_item STR_VAR item=~misc02=>dw#noptn misc02=>dw#magat misc02=>dw#attac misc02=>dw#move0 misc02=>dw#noinv misc02=>dw#deseq misc02=>dw#area misc02=>dw#nopre misc02=>dw#dispe~ editstring=~droppable=>0~ END

  ACTION_FOR_EACH item IN dw#mgsee.spl dw#silin.spl dw#brkin.spl BEGIN
        ACTION_IF NOT FILE_EXISTS_IN_GAME ~%item%~ THEN BEGIN
          LAF install STR_VAR file=  ~%item%~ location=~spl~ END
	END

  END
END
///////////////////////////////////////////////////////////////////////////
// general patch on all creatures
///////////////////////////////////////////////////////////////////////////
DEFINE_ACTION_FUNCTION general_creature_patch BEGIN

LAM read_in_saving_throws
LAM read_in_thac0
// much of this is hardcoded for speed

LAF action_check_ini STR_VAR ini=do_not_fix_save_errors RET ignore_saves=value END
LAF action_check_ini STR_VAR ini=do_not_fix_thac0_errors RET ignore_thac0=value END

OUTER_SPRINT skeleton ~~
OUTER_SET counter=0
OUTER_SET counter2=0
COPY_EXISTING_REGEXP GLOB ~.*\.cre~ ~override~
   PATCH_SILENT
   SPRINT filename ~%SOURCE_RES%~
   LPF CRE_read_effect_type RET value=value END
   PATCH_IF value=0 BEGIN
      LPF FJ_CRE_EFF_V2 END
   END
   LPF CRE_read_race RET race=value END
   PATCH_MATCH ~%race%~ WITH
   LYCANTHROPE DOPPLEGANGER BEGIN
       LPF CRE_general STR_VAR arguments=MONSTER END
   END
   DEFAULT END
   LPF CRE_read_class RET class=value END
   PATCH_MATCH ~%class%~ WITH
   SKELETON SKELETON_WARRIOR BEGIN
      SPRINT skeleton ~%skeleton% %filename%~
   END
   DEFAULT END
   // level adjustment - deals with the various BG1 creatures with Lev2=30
   // hardcoded for speed
   PATCH_IF (BYTE_AT 0x235=30) & (BYTE_AT 0x234)<=15 BEGIN
      WRITE_BYTE 0x235 1
   END
   PATCH_IF !ignore_thac0 BEGIN
      LPF CRE_enforce_thac0 STR_VAR arguments=at_worst END
   END
   PATCH_IF !ignore_saves BEGIN
      LPF CRE_enforce_saves STR_VAR arguments=at_worst END
   END
   SET counter+=1
   SET counter2+=1
   PATCH_IF counter2=100 BEGIN
      SET counter2=0
      PATCH_PRINT ~patched %counter% files~
   END
BUT_ONLY
VERBOSE

// handle skeletons with weak resistances

ACTION_CLEAR_ARRAY patch_data
ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
   match=>"class=SKELETON or class=SKELETON_WARRIOR"
   resist_coldGT=>100
   resist_electricityGT=>100
   resist_slashingGT=>50
   resist_piercingGT=>50
   resist_missileGT=>50
END
ACTION_CLEAR_ARRAY patch_data_sw
ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data_sw BEGIN
   match=>"class=SKELETON_WARRIOR"
   general=>UNDEAD
   race=>SKELETON
   delete_effect=>"opcode=297"
   add_effect_inline=>"opcode=>297 parameter2=>1"
END
LAF edit_creature STR_VAR creature=~%skeleton%~ edits=patch_data END
LAF edit_creature STR_VAR creature=~%skeleton%~ edits=patch_data_sw END

END




///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// install ToB Extender and configure (Windows only)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION configure_tobex BEGIN

  ACTION_IF ~skip_tobex~=0 BEGIN
    OUTER_SPRINT ~TOBEX_MOD_DIRECTORY~ ~%scsroot%~
    LOAD_TRA ~%scsroot%/tobex_redist/tobex.tra~
    INCLUDE ~%scsroot%/tobex_redist/tobex.tpa~

    ///////////////////////
    // Mirror Image fix
    ///////////////////////

    ACTION_IF (!(MOD_IS_INSTALLED ~setup-spell_rev~ 20) AND ENGINE_IS ~tob~ AND FILE_EXISTS ~bgmain.exe~) THEN BEGIN
        LAF include STR_VAR file=~mirror_image_aoe.tph~ END
        LAF ~mirror_image_AoE~ END
    END

    ///////////////////////
    // general settings
    ///////////////////////

   ACTION_DEFINE_ASSOCIATIVE_ARRAY tobex_settings BEGIN
        ~No Spell Interruption On Zero Damage=0~ => ~No Spell Interruption On Zero Damage=1~
        ~Use Caster Level On Mirror Image=0~ => ~Use Caster Level On Mirror Image=1~
   END
   ACTION_PHP_EACH tobex_settings AS old=>new BEGIN
      ACTION_IF FILE_CONTAINS_EVALUATED (~tobex_ini/ToBExTweak.ini~ ~%old%~) BEGIN
         COPY_EXISTING ~tobex_ini/ToBExTweak.ini~ ~tobex_ini~
            REPLACE_TEXTUALLY ~%old%~ ~%new%~
         BUT_ONLY
      END
   END
    ///////////////////////
    // deactivate components that clash with SR
    ///////////////////////

    // MI fix

    ACTION_IF MOD_IS_INSTALLED ~setup-spell_rev~ 20 BEGIN
        COPY_EXISTING ~tobex_ini/ToBExCore.ini~ ~tobex_ini~
            REPLACE_TEXTUALLY ~Configurable Damage Effect Bypasses Mirror Images=1~ ~Configurable Damage Effect Bypasses Mirror Images=0~
        BUT_ONLY
    END

    // dispel fix

    ACTION_IF MOD_IS_INSTALLED ~setup-spell_rev~ 30 BEGIN
        COPY_EXISTING ~tobex_ini/ToBExCore.ini~ ~tobex_ini~ 
            REPLACE_TEXTUALLY ~Dispel Formula Fix=1~ ~Dispel Formula Fix=0~
        BUT_ONLY
    END

    // cure sleep fix

    ACTION_IF MOD_IS_INSTALLED ~setup-spell_rev~ 40 BEGIN
        COPY_EXISTING ~tobex_ini/ToBExCore.ini~ ~tobex_ini~
            REPLACE_TEXTUALLY ~Awaken Fix=1~ ~Awaken Fix=0~
        BUT_ONLY
    END


  END ELSE
  ACTION_IF ~skip_hacks~=0 BEGIN
   PRINT ~ToBEx disabled; applying backup EXE hacks instead~

    LAF include STR_VAR file=~taimonhacks.tph~ END

   // Dispel fix

    LAUNCH_ACTION_MACRO ~Taimon_fix_dispel~

   // MI fix
   
    LAUNCH_ACTION_MACRO ~Taimon_MI_AoE~
    ACTION_IF (!FILE_EXISTS_IN_GAME ~dvimhere.mrk~ AND ENGINE_IS ~tob~ AND FILE_EXISTS ~bgmain.exe~) THEN BEGIN
        LAF include STR_VAR file=~mirror_image_aoe.tph~ END
        LAF mirror_image_AoE END
    END


  END

END

///////////////////////////////////////////////////////////////////////////
// EE-specific content
///////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION ee_specific_content BEGIN




END


///////////////////////////////////////////////////////////////////////////
// detectable items
///////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION detectable_items BEGIN
LAF check_ini STR_VAR ini=~AI_Does_Not_Detect_Items~ RET value END
ACTION_IF !value BEGIN
  LAF check_ini STR_VAR ini=integrated RET integrated=value END
  ACTION_IF integrated BEGIN // here's one we made earlier
    COPY ~%scsroot%/ee/autolib.slb~ ~%workspace%~
  END ELSE BEGIN
    ACTION_IF skip_tobex=0 BEGIN
        INCLUDE ~%scsroot%/ssl/tobex_item_labels.tph~
        LAF tobex_item_labels END
        INCLUDE ~%scsroot%/ssl/tobex_immunity_labels.tph~
        LAF tobex_immunity_labels END
    END ELSE BEGIN
        INCLUDE ~%scsroot%/ssl/ssl.tph~
        LAUNCH_ACTION_MACRO ~generate_SSL_autolib~
    END
  END
END ELSE BEGIN // need a blank autolib.slb
  COPY ~.../stratagems-inline/blank~ ~%workspace%/autolib.slb~
  END
END

///////////////////////////////////////////////////////////////////////////
// misc. IDS files
///////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION misc_ids BEGIN

   // gtimes.ids - need to prevent repeated entries, which are messing with
   // automatic replacement.
   // in BG, a day is 7200 seconds, i.e. 24x300 seconds. BGT defines an hour
   // as 300 seconds and no-one seems to disagree. A round is 6 seconds and 
   // a turn is 10 rounds.
   // to avoid redundancy, we do rounds only from 1-10, turns 2-15, hours, 4-12


   // read gtimes.ids into an array

   COPY_EXISTING gtimes.ids override
       COUNT_2DA_ROWS 2 rowcount
       FOR (i=0;i<rowcount;i+=1) BEGIN
           READ_2DA_ENTRY i 1 2 idsname
           READ_2DA_ENTRY i 0 2 idsnum
           SPRINT $gtimes_entries("%idsname%") "%idsnum%"
       END
   BUT_ONLY

   // overlay the day/turn/round data we want
   
   // numbers to ASCII

   ACTION_DEFINE_ASSOCIATIVE_ARRAY ascii_num BEGIN
        1=>ONE
        2=>TWO
        3=>THREE
        4=>FOUR
        5=>FIVE
        6=>SIX
        7=>SEVEN
        8=>EIGHT
        9=>NINE
        10=>TEN
        11=>ELEVEN
        12=>TWELVE
        13=>THIRTEEN
        14=>FOURTEEN
        15=>FIFTEEN
        16=>SIXTEEN
        17=>SEVENTEEN
        18=>EIGHTEEN
        19=>NINETEEN
        20=>TWENTY
   END

   // days
   
   OUTER_SPRINT $gtimes_entries(ONE_DAY) 7200
   OUTER_FOR (i=2;i<21;i+=1) BEGIN
      OUTER_SPRINT name $ascii_num(~%i%~)
      OUTER_SET length=7200*i
      OUTER_SPRINT $gtimes_entries(~%name%_DAYS~) ~%length%~
   END

   // special cases
   OUTER_SPRINT $gtimes_entries(THIRTY_DAYS) 216000
   OUTER_SPRINT $gtimes_entries(FIFTY_DAYS) 360000
   OUTER_SPRINT $gtimes_entries(TWENTYFIVE_DAYS) 180000
   OUTER_SPRINT $gtimes_entries(FORTY_DAYS) 288000

   // rounds
   
   OUTER_SPRINT $gtimes_entries(ONE_ROUND) 6
   OUTER_FOR (i=2;i<11;i+=1) BEGIN
      OUTER_SPRINT name $ascii_num(~%i%~)
      OUTER_SET length=6*i
      OUTER_SPRINT $gtimes_entries(~%name%_ROUNDS~) ~%length%~
   END

   // turns

   OUTER_FOR (i=2;i<16;i+=1) BEGIN
      OUTER_SPRINT name $ascii_num(~%i%~)
      OUTER_SET length=60*i
      OUTER_SPRINT $gtimes_entries(~%name%_TURNS~) ~%length%~
   END

   //hours

   OUTER_FOR (i=4;i<13;i+=1) BEGIN
      OUTER_SPRINT name $ascii_num(~%i%~)
      OUTER_SET length=300*i
      OUTER_SPRINT $gtimes_entries(~%name%_HOURS~) ~%length%~
   END

   // new blank gtimes.ids
   
   COPY ".../stratagems-inline/blank" "override/gtimes.ids"

   // go through the array and output it, tweaking duplicate numbers to be non-duplicates

   ACTION_PHP_EACH gtimes_entries AS name=>num BEGIN
      OUTER_SET num_here=num
      OUTER_WHILE VARIABLE_IS_SET $gtimes_number_used("%num_here%") BEGIN
         OUTER_SET num_here +=1
      END
      APPEND gtimes.ids "%num_here% %name%"
      OUTER_SET $gtimes_number_used("%num_here%")=1
   END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// BG2 only
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Ardanis's Spell Shield patch
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
DEFINE_ACTION_FUNCTION spell_shield BEGIN
  ACTION_IF !demivrgvs_v4 BEGIN
    ACTION_IF demivrgvs BEGIN
       LAF install STR_VAR file=~spwi519.spl~ location=spl END
    END
    LAF include STR_VAR file=~ardanis_spell_shield.tph~ END
    LAF ardanis_spell_shield END
    LAF ds_altered_spell INT_VAR old_clone=226 new_clone=142 STR_VAR file=~spwi519.spl~ END // let other mods know spell is altered
  END
END


DEFINE_ACTION_FUNCTION initial_bg2_misc BEGIN

/////// give Celestials their own Specifics entry

LAF edit_creature
         STR_VAR creature=~devagood devaevil plangood planevil ca#dastr ca#dmond ca#dmova ca#plan~ allow_missing=1
                 editstring=~specifics=>182~
         END

/////// replace a couple of scripts, apparently randomly confused by SCS, with more robust ones

LAF install STR_VAR file=~werewyv.baf ds52329.baf~ location=baf END

// fix the Ascension/Fixpack incompatibility (because I'm a nice guy)

ACTION_IF MOD_IS_INSTALLED ~Setup-Ascension.tp2~ 0 BEGIN
   ACTION_IF !FILE_EXISTS ~override/player1.dlg~ BEGIN
      COPY_EXISTING ~player1.dlg~ ~override~
   END
   OUTER_INNER_PATCH ~~ BEGIN
      PATCH_IF !(FILE_CONTAINS ~override/player1.dlg~ ~slayer3~) BEGIN
          INNER_ACTION BEGIN
                COMPILE ~%scsroot%/%component_loc%/dlg/player1.d~
                   USING  ~%scsroot%/tra/english/player1.tra~
                          ~%s/player1.tra~
          END
      END
   END
   
   ADD_JOURNAL EXISTING @314 @315 @316
END

// rudimentary scripting shift to make enemy-summoned Planetars and Devas avoid healing the party

DEFINE_PATCH_FUNCTION BCS_rep1
STR_VAR trig_in=""  act_in=""
RET trig_out act_out
BEGIN
   INNER_PATCH_SAVE trig_out "%trig_in%" BEGIN
      REPLACE_TEXTUALLY "Allegiance(Myself,GOODCUTOFF)" "" // allow for EE
      REPLACE_TEXTUALLY "See(\[PC\])" "Allegiance(Myself,GOODCUTOFF)See([PC])"
   END
   SPRINT act_out "%act_in%"
END

DEFINE_PATCH_FUNCTION BCS_rep2
STR_VAR trig_in="" act_in=""
RET trig_out act_out
BEGIN
   PATCH_IF INDEX ("See(\[PC\])" "%trig_in%") >=0 BEGIN
      INNER_PATCH_SAVE trig_out "%trig_in%" BEGIN
      REPLACE_TEXTUALLY "Allegiance(Myself,GOODCUTOFF)" "" // allow for EE
      REPLACE_TEXTUALLY "See(\[PC\])" "Allegiance(Myself,EVILCUTOFF)See([EVILCUTOFF.HUMANOID])"
   END
   SPRINT act_out "%act_in%"
   END ELSE BEGIN
      SPRINT trig_out ""
      SPRINT act_out ""
   END
END

LAF edit_script STR_VAR script="plangood devagood" editstring="patch=>rep1 patch=>rep2" END

END

