DEFINE_ACTION_FUNCTION initial BEGIN

////////////////////////
///// BG1/BG2
////////////////////////

   // configure tobex first, DS assumes it
   ACTION_IF !enhanced_edition BEGIN
       LAF configure_tobex END
   END
   

   // a (very) few bugfixes

	// Myconid race is not automatically present in oBG2
	// Add it in 164, or the first available space after 164 in
	// the (unlikely) event that 164 is taken

	ACTION_IF IDS_OF_SYMBOL (race myconid)<0 BEGIN
		OUTER_PATCH "" BEGIN
			found_gap=0
			ids=164
			WHILE !found_gap BEGIN
				LOOKUP_IDS_SYMBOL_OF_INT entry race ids
				PATCH_IF IS_AN_INT entry BEGIN
					found_gap=1
				END ELSE BEGIN
					++ids
				END
			END
		END
		APPEND "race.ids" "%ids%%TAB%MYCONID"
		ACTION_FOR_EACH myconid IN icmyc01 icmyc02 BEGIN
			COPY_EXISTING "%myconid%.cre" override
				WRITE_BYTE 0x272 ids
			BUT_ONLY
			IF_EXISTS
		END
	END

   // bizarrely, Shocking Grasp is listed as a Combat Protection 

	COPY_EXISTING "%WIZARD_SHOCKING_GRASP%.spl" override
		WRITE_BYTE 0x27 10
	BUT_ONLY

   // the '15 hp damage' string for Enrage ought only to play for PCs
   
   CREATE EFF dw#rgstr // display the 'when rage ends, take 15 hp' string
          WRITE_LONG 0x10 139
          WRITE_LONG 0x14 1
          WRITE_LONG 0x1c 20723
          WRITE_LONG 0x24 1
          WRITE_LONG 0x2c 100
          
    COPY_EXISTING spcl321.spl override
          LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=139 opcode=177 parameter1=30 parameter2=2 STR_VAR resource=dw#rgstr END // silent because ToF removes this 139 altogether

   // Fire Shield backlash should bypass MI

    ACTION_FOR_EACH spell IN spcryod spimixd sppr951d sppr952d BEGIN
     
        COPY_EXISTING "%spell%.spl" override
			LPF ALTER_EFFECT INT_VAR match_opcode=12 bypass_mirror_image=1 END
		IF_EXISTS
		
   END
   ACTION_IF !demivrgvs BEGIN
    ACTION_FOR_EACH spell IN spwi403d spwi418d BEGIN
     
		COPY_EXISTING "%spell%.spl" override
			LPF ALTER_EFFECT INT_VAR match_opcode=12 bypass_mirror_image=1 END
		IF_EXISTS
    END
   
   END
   
   // Fire Shield has inconsistent power levels
   
   COPY_EXISTING 
       "spwi403d.spl" override
       "spwi418.spl" override
           LPF ALTER_EFFECT INT_VAR power=4 END
   
   // these innate spells are marked as 'wizard' or 'priest

	ACTION_FOR_EACH spell IN BLACK_DRAGON_INSECT SHADOW_DRAGON_SHADOWS NYMPH_CONFUSION ABAZIGAL_SHOCKWAVE BURNING_MAN_CINDER_SHOWER BLUE_DRAGON_BREATH LAVA_BURST BEGUILE DEMOCHM SPYANC01
	BEGIN
			OUTER_SPRINT resref EVAL "%%spell%%"
			COPY_EXISTING "%resref%.spl" override 	
				WRITE_SHORT 0x1c 4
			IF_EXISTS
	END

	// Dragon and Demon fear should have power=9 (not 0), so that Antimagic Shell and Pro/Magic Scrolls protect from it
	
	ACTION_FOR_EACH spell IN "%DRAGON_FEAR%" "%DEMON_FEAR%" BEGIN
		COPY_EXISTING "%spell%.spl" override
			LPF ALTER_EFFECT INT_VAR power=9 END
		BUT_ONLY
		IF_EXISTS
	END

   // Protection from Magic scrolls don't remove undispellable buffs
   
   ACTION_IF FILE_EXISTS_IN_GAME "dw#pmscr.spl" BEGIN
	OUTER_SPRINT object "dw#pmscr.spl"s
   END ELSE
   ACTION_IF item_revisions BEGIN 
	OUTER_SPRINT object "scrl07.spl"
   END ELSE BEGIN
	OUTER_SPRINT object "scrl07.itm"   
   END
   COPY_EXISTING "%object%" override
       PATCH_FOR_EACH parameter2 IN 1 2 7 BEGIN
       LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=221 timing=1 parameter1=10 parameter2 END
       END
       
   
   // on non-EE, Iron Skins is marked as SPELLPROTECTIONS, not COMBATPROTECTIONS
   // (given it's tweaked in EE, I feel content to call this a bug)
   
	ACTION_IF !enhanced_edition BEGIN
		COPY_EXISTING "%CLERIC_IRONSKIN%.spl" override
			WRITE_BYTE 0x27 7
		BUT_ONLY
		IF_EXISTS
	END

   // on BG1, Clair De'lain should have a bow and doesn't. BGEE tries to fix this, but messes it up. (As of 2.6, it works ok)
   
   ACTION_IF is_bg1 && !enhanced_edition BEGIN
     COPY_EXISTING "%tutu_var%claird.cre" override
      REMOVE_CRE_ITEM "arow02 " "%tutu_var%arow02" "%tutu_var%bow04"
      ADD_CRE_ITEM "%tutu_var%bow04" #0 #0 #0 NONE WEAPON EQUIP TWOHANDED
      ADD_CRE_ITEM "%tutu_var%arow02" #10 #0 #0 NONE QUIVER
     BUT_ONLY
   END
   
   // beholder antimagic ought to be level >=1 so as to break through Spell Shield
   
   spl.edit[%BEHOLDER_ANTIMAGIC_RAY% %HIVE_MOTHER_ANTIMAGIC_RAY% %INNATE_BEHOLDER_ANTIMAGIC_RAY%|allow_missing:i=1]
   [
		m.ab_fx.alter{s_power=(s_power=0?1:s_power)}  
   ]
   
   // in EE, temple dispel ought to be high enough level to remove (most) effects
   
	ACTION_IF enhanced_edition BEGIN // because parameter2=2 for opcode 146 is EE-only	
	
		spl.edit[%CLERIC_DISPEL_MAGIC%]
		[
			m.ab.clone{s_level=2}
			m.ab_fx.delete{p_level=1}
			m.ab_fx.add{s_target=2 s_timing=1 s_opcode=146 s_parameter1=20 s_parameter2=2 s_resource:=%CLERIC_DISPEL_MAGIC%|match_parent="p_level=1"}
		]
	
	END   
	
   // a couple of problems in SR v4
   
   ACTION_IF demivrgvs BEGIN
      
      // summoned Death Knights ought to have their allegiance set to Match Target, else hostile ones get attacked
      
      COPY_EXISTING spcaco.eff override
        WRITE_LONG 0x20 1
      BUT_ONLY

   END

   // some general resources needed
   LAF random_useful_items END
   
   // detectable spells/items

   LAF check_label STR_VAR label=dw#detectablespells RET value END // don't install it twice! (It's idempotent, but wastes time)
   ACTION_IF !value BEGIN
        LAF make_label STR_VAR label=dw#detectablespells END
        LAF detectable_spells INT_VAR skip_legacy=1 END  // core DS - copy of the SoD version, adapted for SCS
   END
   CLEAR_IDS_MAP // make sure any DS changes to splstate/stats are logged
   
   LAF detectable_items END
   LAF run STR_VAR file=detectcloud END
   LAF general_creature_patch END
   LAF run STR_VAR file=sim_compat END
   LAF level_one_innates END
   LAF misc_ids END
   
////////////////////////
///// BG2
////////////////////////
   ACTION_IF is_bg2 BEGIN
   
     // fix the Spell Shield spell on non-EE installs
     ACTION_IF !enhanced_edition BEGIN
        LAF spell_shield END
     END
     // some misc bugfixes
     LAF initial_bg2_misc END
     // eliminate a couple of stoneskins that are immune to breach
     LAF run STR_VAR file=initial_stoneskin END

     // some BG2-specific bits of Detectable X
     
     ACTION_IF !enhanced_edition BEGIN
         LAF ds_detectable_weapon_enchantment END // weapon enchantment
     END
     LAF include STR_VAR file=detecttrap.tph END
     LAF detect_trap END

   END
////////////////////////
///// BG1
////////////////////////

   ACTION_IF is_bg1 BEGIN

      ACTION_IF !enhanced_edition BEGIN
       LAF include STR_VAR file=~beregost.tph~ END
       LAF Ascension_beregost_fix END
      END
      LAF run STR_VAR file=~bg1fix~ END
   END
  
////////////////////////////////////////
///// Core SCS spell-system changes
//////////////////////////////////////////
   
	OUTER_SPRINT component_loc spell
	LAF run STR_VAR file=install_spell_resources version=core-scs tra=spell END
   


////////////////////////////////////////////////////////
///// The old AI-initialise bit ////////////////////////
////////////////////////////////////////////////////////

   OUTER_SPRINT component_loc caster_shared
   LAF run STR_VAR file=ai_initialise END
   OUTER_SPRINT component_loc initial
   CLEAR_IDS_MAP // make sure any changes to splstate/stats are logged

// if they don't exist already (which is the case only if we've ini-skipped fiend_summoning for some reason), build
// NPC copies of the fiend-summoning spells. This is to isolate SR and aTweaks changes from SCS changes.

   ACTION_FOR_EACH spell IN WIZARD_SUMMON_FIEND WIZARD_CACOFIEND WIZARD_GATE CLERIC_GATE BEGIN
      OUTER_SPRINT resref EVAL "%%spell%%"
      OUTER_SPRINT spell_new "%spell%_SCS"
      OUTER_SPRINT resref_new EVAL "%%spell_new%%"
      ACTION_IF !FILE_EXISTS_IN_GAME "%resref_new%.spl" && FILE_EXISTS_IN_GAME "%resref%.spl" BEGIN
            COPY_EXISTING "%resref%.spl" "override/%resref_new%.spl"
      END

   END

////////////////////////
///// On non-EE installs, (hackily) grant total immunity to elemental AoE to anything 100% immune to that damage type
////////////////////////

	ACTION_IF !enhanced_edition BEGIN
		LAF run STR_VAR file=elemental_area_immunity END
	END

////////////////////////
///// Update antimagic descriptions to reflect any changes
////////////////////////

  LAF run STR_VAR file=correct_antimagic_descriptors tra=spell END



////////////////////////
///// Easter eggs
////////////////////////

	LAF check_ini STR_VAR ini=disable_easter_eggs RET value END
	ACTION_IF !value BEGIN
		ACTION_IF is_bg2 BEGIN
		
			itm.copy[book60=>dw#eegg1]
			[
				m_unidentified_name:=@50074
				m_identified_name:=@50074
				m_unidentified_description:=@50075
			]

			are.edit[ar1515]
			[
				m.container_item.add{s_resref:=dw#eegg1|match_parent="p_x=661"}
			]

		END

		ACTION_IF is_bg BEGIN
		
			itm.copy[book60=>dw#eegg2]
			[
				m_unidentified_name:=@50076
				m_identified_name:=@50076
				m_unidentified_description:=@50077
			]

			are.edit[%Candlekeep_Library_L1%]
			[
				m.container_item.add{s_resref:=dw#eegg2|match_parent="p_x=813"}
			]

		END



	END
END


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// BG1/BG2
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////
///	Make innate abilities L1 to avoid crashes (we do this
///     selectively for now - I think the Fixpack does it, but
///     at least on BGEE not all are caught)
///////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION level_one_innates BEGIN

	ACTION_FOR_EACH spell IN "%PALADIN_SUMMON_DEVA%" "%BLACKGUARD_SUMMON_FALLEN_DEVA%" BEGIN
		COPY_EXISTING "%spell%.spl" override
			WRITE_LONG 0x34 1
		IF_EXISTS
		BUT_ONLY
	END

END

///////////////////////////////////////////////////////////////
///	Random useful items
///////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION random_useful_items BEGIN

	ACTION_FOR_EACH item IN dw#noptn dw#magat dw#attac dw#move0 dw#nodor dw#noinv dw#deseq dw#area dw#nopre dw#dispe BEGIN
		COPY_EXISTING "dw#marker_file.itm" "override/%item%.itm"
			WRITE_LONG 0x18 (THIS BAND (BNOT BIT2))
	END

    LAF install INT_VAR overwrite=0 STR_VAR file="dw#mgsee.spl dw#silin.spl dw#brkin.spl" location=~spl~ END

END
///////////////////////////////////////////////////////////////////////////
// general patch on all creatures
///////////////////////////////////////////////////////////////////////////
DEFINE_ACTION_FUNCTION general_creature_patch BEGIN
	
	LAM data_thac0
	LAM data_saving_throws
	// much of this is hardcoded for speed

	LAF check_ini STR_VAR ini=do_not_fix_save_errors RET ignore_saves=value END
	LAF check_ini STR_VAR ini=do_not_fix_thac0_errors RET ignore_thac0=value END

	ACTION_CLEAR_ARRAY skeleton_array
	OUTER_SPRINT skeleton ~~
	OUTER_SET counter=0
	OUTER_SET counter2=0
	OUTER_SET lycanthrope = IDS_OF_SYMBOL (race LYCANTHROPE)
	OUTER_SET doppleganger = IDS_OF_SYMBOL (race DOPPLEGANGER)
	OUTER_SET skeleton = IDS_OF_SYMBOL (class SKELETON)
	OUTER_SET skeleton_warrior = IDS_OF_SYMBOL (class SKELETON_WARRIOR)
	COPY_EXISTING_REGEXP GLOB ".*\.cre" override
		PATCH_TRY
			PATCH_SILENT
			SPRINT filename ~%SOURCE_RES%~
			PATCH_IF (BYTE_AT 0x33)=0 BEGIN
				LPF FJ_CRE_EFF_V2 END
			END
			// these edits are BG1/2 only
			PATCH_IF !is_iwd BEGIN
				PATCH_IF (BYTE_AT 0x272)=lycanthrope || (BYTE_AT 0x272)=doppleganger BEGIN
					WRITE_BYTE 0x271 255
				END ELSE
				PATCH_IF (BYTE_AT 0x273)=skeleton || (BYTE_AT 0x273)=skeleton_warrior BEGIN
					SPRINT $skeleton_array("%SOURCE_RES%") ""
				END
				// level adjustment - deals with the various BG1 creatures with Lev2=30
				// hardcoded for speed
				PATCH_IF (BYTE_AT 0x235=30) & (BYTE_AT 0x234)<=15 BEGIN
					WRITE_BYTE 0x235 1
				END
			END
			// save and THAC0 fixes are in IWD too
			PATCH_IF !ignore_thac0 BEGIN
				LPF cre_enforce_thac0_notstruct STR_VAR arguments=at_worst END
			END
			PATCH_IF !ignore_saves BEGIN
				LPF cre_enforce_saves_notstruct STR_VAR arguments=at_worst END
			END
			SET counter+=1
			SET counter2+=1
			PATCH_IF counter2=100 BEGIN
				SET counter2=0
				PATCH_PRINT ~patched %counter% files~
			END
		WITH DEFAULT
			LPF regexp_warning STR_VAR parent=general_creature_patch END
		END
	BUT_ONLY
	VERBOSE

	// handle skeletons with weak resistances

	ACTION_PHP_EACH skeleton_array AS skeleton=>discard BEGIN
		COPY_EXISTING "%skeleton%.cre" override
			WRITE_BYTE 0x5a (THIS>100)?THIS:100
			WRITE_BYTE 0x5b (THIS>100)?THIS:100
			WRITE_BYTE 0x5f (THIS>100)?THIS:100
			WRITE_BYTE 0x60 (THIS>50)?THIS:50
			WRITE_BYTE 0x62 (THIS>50)?THIS:50
			WRITE_BYTE 0x63 (THIS>50)?THIS:50
			PATCH_IF (BYTE_AT 0x273)=skeleton_warrior BEGIN
				WRITE_BYTE 0x271 4 //general=undead
				WRITE_BYTE 0x272 115 // race=skeleton
				LPF ADD_CRE_EFFECT INT_VAR opcode=297 timing=9 target=1 parameter2=1 END
			END

	END

END






///////////////////////////////////////////////////////////////////////////
// detectable items
///////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION detectable_items BEGIN
	LAF check_ini STR_VAR ini=~AI_Does_Not_Detect_Items~ RET do_not_detect=value END
	LAF check_ini STR_VAR ini=~AI_Is_Omniscient~ RET omniscient=value END
    LAF include STR_VAR file=item_labels.tpa END
    LAF item_labels INT_VAR omniscient do_not_detect END
	ACTION_IF do_not_detect BEGIN
		LAF ds_resolve_stat STR_VAR id=ENCHANTMENT_IMMUNITY END
	END ELSE BEGIN
		LAF run STR_VAR file=immunity_labels END
	END
END

///////////////////////////////////////////////////////////////////////////
// misc. IDS files
///////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION misc_ids BEGIN

	// gtimes.ids - need to prevent repeated entries, which are messing with
	// automatic replacement. We basically follow the model of bg2ee

	// read in a copy of unmodded bg2ee

	LAF 2da_read STR_VAR file=gtimes.ids path="%MOD_FOLDER%/initial/resource" RET_ARRAY gtimes_ee=array gtimes_ee_rows=rows END

	// read in actual gtimes

	LAF 2da_read STR_VAR file=gtimes.ids RET_ARRAY gtimes_old=array gtimes_old_rows=rows END

	// merge arrays

	LAF array_join STR_VAR array1=gtimes_ee array2=gtimes_old RET_ARRAY gtimes=array END
	LAF array_join STR_VAR array1=gtimes_ee_rows array2=gtimes_old_rows RET_ARRAY gtimes_rows=array END

	// remove any duplicates

	ACTION_PHP_EACH gtimes_rows AS row=>discard BEGIN
		OUTER_SET int=$gtimes("%row%" int)
		OUTER_WHILE VARIABLE_IS_SET $gtimes_duplicate_check("%int%") BEGIN
			OUTER_SET ++int
		END
		OUTER_SET $gtimes("%row%" int)=int
		OUTER_SET $gtimes_duplicate_check("%int%")=1
	END

	// write back
	
	LAF 2da_write STR_VAR file=gtimes.ids array=gtimes END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// BG2 only
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Ardanis's Spell Shield patch
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
DEFINE_ACTION_FUNCTION spell_shield BEGIN
  ACTION_IF !demivrgvs BEGIN
    

    LAF include STR_VAR file=~ardanis_spell_shield.tph~ END
    LAF ardanis_spell_shield END
  END
END


DEFINE_ACTION_FUNCTION initial_bg2_misc BEGIN

/////// give Celestials their own Specifics entry
// skip for now, probably better way through

/*
	LAF resolve_ids STR_VAR ids=CELESTIAL idsfile=specifics RET value END
	COPY_EXISTING "%creature%.cre" override
		WRITE_BYTE 0x274 value
	IF_EXISTS
*/

/////// replace a couple of scripts, apparently randomly confused by SCS, with more robust ones

	LAF install STR_VAR file=~werewyv.baf ds52329.baf~ location=baf END

// rudimentary scripting shift to make enemy-summoned Planetars and Devas avoid healing the party and use their powers on allies instead

	ACTION_IF !enhanced_edition BEGIN
		ACTION_FOR_EACH script IN plangood devagood BEGIN
			COPY_EXISTING "%script%.bcs" override
				DECOMPILE_AND_PATCH BEGIN
				   REPLACE_TEXTUALLY "See(\[PC\])" "Allegiance(Myself,GOODCUTOFF)See([PC])"
				END
			BUT_ONLY
			LAF CLONE_SCRIPT_BLOCK 
				  STR_VAR script
						  match=~See(\[PC\])~
						  swap_out1=~See(\[PC\])~
						  swap_in1=~See([EVILCUTOFF.HUMANOID])~
						  swap_out2=~Allegiance(Myself,GOODCUTOFF)~
						  swap_in2=~Allegiance(Myself,EVILCUTOFF)~
			END
		END
	END

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// ToBeX-based tweaks for vanilla BG2
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION configure_tobex BEGIN

    ///////////////////////
    // Mirror Image fix
    ///////////////////////

    LAF include STR_VAR file=~mirror_image_aoe.tph~ END
    LAF ~mirror_image_AoE~ END

    ///////////////////////
    // general settings
    ///////////////////////

   ACTION_DEFINE_ASSOCIATIVE_ARRAY tobex_settings BEGIN
        ~No Spell Interruption On Zero Damage=0~ => ~No Spell Interruption On Zero Damage=1~
        ~Use Caster Level On Mirror Image=0~ => ~Use Caster Level On Mirror Image=1~
   END
   ACTION_PHP_EACH tobex_settings AS old=>new BEGIN
      ACTION_IF FILE_CONTAINS_EVALUATED (~tobex_ini/ToBExTweak.ini~ ~%old%~) BEGIN
         COPY_EXISTING ~tobex_ini/ToBExTweak.ini~ ~tobex_ini~
            REPLACE_TEXTUALLY ~%old%~ ~%new%~
         BUT_ONLY
      END
   END


END
