
DEFINE_ACTION_FUNCTION detect_timestop BEGIN

LAF check_label STR_VAR label=fin_ds_extra RET value END
ACTION_IF !value BEGIN


// this code borrowed from and shared with ascension 2.0

///////////////////////////////////////////////////////////////////////////
/// Detect if Timestop is being cast
///////////////////////////////////////////////////////////////////////////

    CREATE EFF fintsvr1 // set FIN_CASTING_TIME_STOP to 1
          WRITE_LONG 0x10 265
          WRITE_LONG 0x14 1
          WRITE_LONG 0x1c 1
          WRITE_LONG 0x24 1
          WRITE_LONG 0x2c 100
          WRITE_ASCII 0x30 FIN_CAST
          WRITE_ASCII 0x70 ING_TIME
          WRITE_ASCII 0x78 _STOP
          
    COPY_EXISTING fintsvr1.eff "override/fintsvr2.eff" // set it back to 0
          WRITE_LONG 0x1c 0
          
    COPY_EXISTING "%WIZARD_TIME_STOP%.spl" override
         LPF ADD_SPELL_CFEFFECT INT_VAR opcode=177 target=1 parameter2=2 timing=1 STR_VAR resource=fintsvr1 END
         LPF CLONE_EFFECT INT_VAR match_opcode=177 timing=4 duration=6 STR_VAR match_resource=fintsvr1 resource=fintsvr2 END
    BUT_ONLY

///////////////////////////////////////////////////////////////////////////
/// Detect if Timestop is currently running
///////////////////////////////////////////////////////////////////////////

    CREATE EFF fintsrna // set FIN_TIME_STOP_RUNNING to 1
          WRITE_LONG 0x10 265
          WRITE_LONG 0x14 1
          WRITE_LONG 0x1c 1
          WRITE_LONG 0x24 1
          WRITE_LONG 0x2c 100
          WRITE_ASCII 0x30 FIN_TIME
          WRITE_ASCII 0x70 _STOP_RU
          WRITE_ASCII 0x78 NNING

    COPY_EXISTING fintsrna.eff "override/fintsrnb.eff" // set it back to 0
          WRITE_LONG 0x1c 0
          
    COPY_EXISTING "%WIZARD_TIME_STOP%.spl" override
         LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=1 parameter2=2 timing=1 STR_VAR resource=fintsrna END
         LPF CLONE_EFFECT INT_VAR match_opcode=177 timing=4 duration=18 STR_VAR match_resource=fintsrna resource=fintsrnb END
    BUT_ONLY
    COPY_EXISTING "spwish17.spl" override
         LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=1 parameter2=2 timing=1 STR_VAR resource=fintsrna END
         LPF CLONE_EFFECT INT_VAR match_opcode=177 timing=4 duration=36 STR_VAR match_resource=fintsrna resource=fintsrnb END
    BUT_ONLY
    COPY_EXISTING "spcl912b.spl" override
         LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=1 parameter2=2 timing=1 STR_VAR resource=fintsrna END
         LPF CLONE_EFFECT INT_VAR match_opcode=177 timing=4 duration=10 STR_VAR match_resource=fintsrna resource=fintsrnb END
    BUT_ONLY

///////////////////////////////////////////////////////////////////////////
/// Sanity check: turn off FIN_TIME_STOP_RUNNING if we're not in combat
/// (there are some sporadic reports of it being left on)
///////////////////////////////////////////////////////////////////////////

<<<<<<<<.../stratagems-inline/turn_off_timestop.baf
IF
	!Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("FIN_TIME_STOP_RUNNING","GLOBAL",0)
		Continue()
END
>>>>>>>>

EXTEND_TOP "baldur25.bcs" ".../stratagems-inline/turn_off_timestop.baf" // non-SFO for Ascension compatibility

END

END