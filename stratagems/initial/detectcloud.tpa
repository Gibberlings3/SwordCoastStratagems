////////////////////////////////////////////////////////////////////////
////        Controlling table
////////////////////////////////////////////////////////////////////////

<<<<<<<< .../stratagems-inline/clouds.2da
code       resource                  type  shout  timerlength  zone
dw#cldki   WIZARD_CLOUDKILL          spl   404    52           True
dw#incen   WIZARD_INCENDIARY_CLOUD   spl   409    52           True
dw#meteo   WIZARD_METEOR_SWARM       spl   405    18           False
dw#fstor   CLERIC_FIRE_STORM         spl   405    18           False
dw#vstor   CLERIC_STORM_OF_VENGEANCE spl   407    13           False
dw#defog   WIZARD_DEATH_FOG          spl   408    52           True
dw#cldki   wand13                    itm   404	  52           True
dw#clweb   WIZARD_WEB                spl    410   52           False
dw#stink   WIZARD_STINKING_CLOUD     spl    411   52           True
dw#sweet   CLERIC_ZONE_OF_SWEET_AIR  spl    123   99           True
dw#istor   WIZARD_ICE_STORM          spl    412   18           False
dw#suffo   WIZARD_SUFFOCATE          spl    413   18           False
dw#clpes   CLERIC_CLOUD_OF_PESTILENCE spl   413   18           True
dw#wrfog   CLERIC_WRITHING_FOG        spl   412   52           True
>>>>>>>>

////////////////////////////////////////////////////////////////////////
////        Scripts
////////////////////////////////////////////////////////////////////////

<<<<<<<<    .../stratagems-inline/dw#sweet.baf
IF
	!Global("init","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("init","LOCALS",1)
		SetGlobalTimer("DMWWZoneOfSweetAir","GLOBAL",3)
END

IF
	GlobalTimerExpired("DMWWZoneOfSweetAir","GLOBAL")
THEN
	RESPONSE #100
		DestroySelf()
END
>>>>>>>>

<<<<<<<<    .../stratagems-inline/cloud_template.ssl
IF
	Global("CKInit","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("CKInit","LOCALS",1)
		SetGlobalTimer("CKtimer","LOCALS",3)
		SetGlobalTimer("CKtimer2","LOCALS",%timerlength%)
END

IF
	RequireBlock(scszone)
	GlobalTimerNotExpired("DMWWZoneOfSweetAir","GLOBAL")
THEN
	RESPONSE #100
		DestroySelf()
END


IF
	GlobalTimerExpired("CKtimer2","LOCALS")
THEN
	RESPONSE #100
		DestroySelf()
END



IF
	GlobalTimerExpired("CKtimer","LOCALS")
THEN
	RESPONSE #100
		Shout(%shout%)
END
>>>>>>>>

<<<<<<<< .../stratagems-inline/storm_add.baf

	IF
		Allegiance(LastSummonerOf(Myself),ENEMY)
	THEN
		RESPONSE #100
			DestroySelf()
	END

>>>>>>>>

////////////////////////////////////////////////////////////////////////
////        Main function
////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION detectcloud BEGIN

  ACTION_IF is_bg2=1 BEGIN
     //// versions of Met.Sw. and Inc. Cl. for traps
     
     LAF clone_spell 
             INT_VAR allow_missing=1
             STR_VAR spell=~%WIZARD_INCENDIARY_CLOUD% => dw#trpin
                            %WIZARD_METEOR_SWARM% => dw#trpms~
             END
     
     /// replace the trap spells in scripts
     
     LAF swap_text INT_VAR allow_missing=1 STR_VAR files=~gt016.bcs~ swaps=~ForceSpell(LastTrigger,WIZARD_INCENDIARY_CLOUD) => ForceSpellRES("dw#trpin",LastTrigger)~ END
     LAF swap_text INT_VAR allow_missing=1 STR_VAR files=~gt020.bcs~ swaps=~ForceSpell(LastTrigger,WIZARD_METEOR_SWARM) => ForceSpellRES("dw#trpms",LastTrigger)~ END

   END
     // main work
     

     LAF process_table STR_VAR table=clouds.2da inline=yes function=detectcloud_core END
     
       ACTION_IF is_bg2 BEGIN
          EXTEND_TOP ~dw#vstor.bcs~ ~.../stratagems-inline/storm_add.baf~
       END
       COMPILE ".../stratagems-inline/dw#sweet.baf"

END

DEFINE_ACTION_FUNCTION detectcloud_core
   STR_VAR code=""
           resource=""
           type=""
           shout=""
           timerlength=""
           zone=""
BEGIN
           ACTION_IF VARIABLE_IS_SET "%resource%" THEN BEGIN
                     OUTER_SPRINT resource EVAL "%%resource%%"
           END

           ACTION_IF FILE_EXISTS_IN_GAME ~%resource%.spl~ BEGIN
			      //////////////////////////////////////////
			      // the script for the invisible creature
                              //////////////////////////////////////////

			      LAF ssl_to_bcs STR_VAR script=cloud_template inline=yes rename_to="%code%" variables="scszone=%zone%" END

			      //////////////////////////////////////////
                              // the creature itself
                              //////////////////////////////////////////

                              COPY ~%scsroot%\%component_loc%\cre\dw#cloud.cre~ ~override/%code%.cre~
			      LAF edit_creature STR_VAR creature=~%code%~ editstring=~script_override => %code%~ END

                              //////////////////////////////////////////
                              // the effect that summons it
                              //////////////////////////////////////////

                              MAKE_PATCH
                                  opcode=>67
                                  target=>1
                                  power=>9
                                  parameter1=>0
                                  timing=>1
                                  duration=>6
                                  vvc=>NONE
                                  resource=>~%code%~
                              END
                              LAF make_effect STR_VAR effect=~%code%~ edits=patch_data END

			      //////////////////////////////////////////
                              // the spell
                              //////////////////////////////////////////

                              MAKE_PATCH
                                    opcode=>177
                                    target=>1
                                    power=>9
                                    parameter2=>3
                                    duration=>60
                                    resource=>~%code%~
                              END
                              LAF edit_whatever STR_VAR file="%resource%.%type%" editstring=~add_effect=>patch_data~ END
           END
END
