DEFINE_ACTION_FUNCTION immunity_labels BEGIN

<<<<<<<< .../stratagems-inline/immune_stats.2da
resource        stat_value
>>>>>>>>


   COPY ~.../stratagems-inline/immune_stats.2da~ ~%workspace%~

   COPY_EXISTING_REGEXP ~.*\.itm~ ~override~
	PATCH_TRY
        GET_OFFSET_ARRAY fx_eq_array ITM_V10_GEN_EFFECTS
        SET enchantment_level=0
        PHP_EACH fx_eq_array AS int=>offset BEGIN
           PATCH_IF SHORT_AT offset=120 BEGIN
             PATCH_MATCH (LONG_AT (offset+0x8)) WITH
             0 BEGIN
                READ_LONG (offset+0x4) ench_here
                SET ench_here+=1
                SET enchantment_level=enchantment_level>ench_here? enchantment_level : ench_here
             END
             2 BEGIN
                SET enchantment_level=enchantment_level>1? enchantment_level : 1
             END
             DEFAULT
             END
           END
        END
        PATCH_IF enchantment_level>0 BEGIN
            SPRINT to_add ~%SOURCE_RES%.itm   %enchantment_level%~
            INNER_ACTION BEGIN
               APPEND_OUTER ~%workspace%/immune_stats.2da~ ~%to_add%~ UNLESS ~%to_add%~
            END
        END
	WITH DEFAULT
			LPF regexp_warning STR_VAR parent=immunity_labels END
	END
   BUT_ONLY

   LAF ds_process_table INT_VAR default_opcode=120 STR_VAR default_stat=ENCHANTMENT_IMMUNITY table="%workspace%/immune_stats.2da"  END


END