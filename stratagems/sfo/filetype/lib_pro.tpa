//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching clone
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION clone_pro  // beware-this only works for one projectile at a time
    STR_VAR
           pro=""
           edits=""
           editstring=""
           allow_missing="no"
           location="override"
    RET pro_num
    BEGIN
         LAUNCH_ACTION_FUNCTION clone_template
                  STR_VAR file_list= ~%pro%~
                          file_ext=~PRO~
                          location
                          edits
                          editstring
                          allow_missing
                          location
         END
         LAF return_first_pair STR_VAR list= ~%pro%~ RET new=value END
         OUTER_SPRINT input ~ADD_PROJECTILE "%location%/%new%.pro"~
         LAM action_reinclude_this
         OUTER_SET pro_num= ~%%new%%~
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching edit
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_pro
    STR_VAR
           pro=""
           edits=""
           editstring=""
           allow_missing="no"
           location=""
    BEGIN
         LAUNCH_ACTION_FUNCTION edit_template
                  STR_VAR file_list= ~%pro%~
                          file_ext=~PRO~
                          edits
                          editstring
                          allow_missing
                          location
         END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching make
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_pro
    STR_VAR
           pro=""
           edits=""
           editstring=""
           location="override"
    RET pro_num
    BEGIN
         OUTER_SET is_area=0
         OUTER_SPRINT list ~%editstring%~
         OUTER_WHILE ~%list%~ STRING_COMPARE ~~ BEGIN
              LAF return_first_pair STR_VAR list RET key=key value=value list=list END
              ACTION_IF (~%key%~ STRING_EQUAL_CASE is_area & ~%value%~ STRING_EQUAL_CASE yes) BEGIN OUTER_SET is_area=1 END
         END
         ACTION_IF ( ~%%edits%_is_area%~ STRING_EQUAL_CASE yes) BEGIN OUTER_SET is_area=1 END
         ACTION_IF is_area BEGIN 
             OUTER_SPRINT build_before PRO_build_area 
         END ELSE BEGIN
             OUTER_SPRINT build_before PRO_build_before
         END
         LAUNCH_ACTION_FUNCTION make_template
                  STR_VAR file_list= ~%pro%~
                          file_ext=~PRO~
                          edits
                          editstring
                          location
                          build_before
                          build_after = PRO_postbuild
         END
         OUTER_SPRINT input ~ADD_PROJECTILE "%location%/%pro%.pro"~
         LAM action_reinclude_this
         OUTER_SET pro_num= ~%%pro%%~
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               offsets [none]
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               simple data-field edits
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
ACTION_DEFINE_ASSOCIATIVE_ARRAY pro_fields BEGIN
   type => ~0x8, SHORT~
   speed => ~0xa,SHORT~
   travel_wav => ~0x10,ASCII~
   arrive_wav => ~0x18,ASCII~
   travel_wav0 => ~0x10,ASCII~  // this renaming facilitates IWD-in-BG2
   arrive_wav0 => ~0x18,ASCII~
   travel_vvc => ~0x20,ASCII~
   spark_color_num=>~0x28,SHORT~
   travel_anim => ~0x104,ASCII~
   shadow_anim => ~0x10c,ASCII~
   travel_anim0 => ~0x104,ASCII~
   shadow_anim0 => ~0x10c,ASCII~
   pro_seq_num => ~0x114,BYTE~
   shadow_seq_num => ~0x115,BYTE~
   light_intensity => ~0x116,SHORT~
   light_width => ~0x118,SHORT~
   light_height => ~0x11a,SHORT~
   palette => ~0x11c,ASCII~
   color_1 => ~0x124,BYTE~
   color_2 => ~0x125,BYTE~
   color_3 => ~0x126,BYTE~
   color_4 => ~0x127,BYTE~
   color_5 => ~0x128,BYTE~
   color_6 => ~0x129,BYTE~
   color_7 => ~0x12a,BYTE~
   smoke_color_1 => ~0x124,BYTE~
   smoke_color_2 => ~0x125,BYTE~
   smoke_color_3 => ~0x126,BYTE~
   smoke_color_4 => ~0x127,BYTE~
   smoke_color_5 => ~0x128,BYTE~
   smoke_color_6 => ~0x129,BYTE~
   smoke_color_7 => ~0x12a,BYTE~
   facing => ~0x133,BYTE~
   smoke_anim=>~0x134,SHORT~
   trail_anim_1=>~0x136,ASCII~
   trail_anim_2=>~0x13e,ASCII~
   trail_anim_3=>~0x146,ASCII~
   trail_anim_10=>~0x136,ASCII~
   trail_anim_20=>~0x13e,ASCII~
   trail_anim_30=>~0x146,ASCII~
   trail_1_seq_num=>~0x14e,ASCII~
   trail_2_seq_num=>~0x150,ASCII~
   trail_3_seq_num=>~0x152,ASCII~
   trigger_radius=>~0x204,SHORT~
   basic_aoe_radius => ~0x206,SHORT~
   trigger_wav => ~0x208,ASCII~
   trigger_wav0 => ~0x208,ASCII~
   explosion_frequency => ~0x210,SHORT~
   fragment_anim => ~0x212,SHORT~
   secondary_pro => ~0x214,SHORT~
   repetitions=>~0x216,BYTE~
   explosion_anim => ~0x217,BYTE~
   explosion_color=>~0x218,BYTE~
   target_anim=>~0x21a,SHORT~
   explosion_pro=> ~0x21a,SHORT~
   explosion_anim_vvc=>~0x21c,ASCII~
   cone_angle=>~0x224,SHORT~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=pro_fields function_prefix=PRO END


ACTION_DEFINE_ASSOCIATIVE_ARRAY pro_bit_fields BEGIN
      show_sparkle=>~0xc,0~
      sparkle_z_coord=>~0xc,1~
      loop_travel_sound=>~0xc,2~
      loop_arrive_sound=>~0xc,3~
      sparkle_no_direct_target=>~0xc,4~
      draw_below=>~0xc,5~
      use_palette=>~0x100,0~
      enable_smoke=>~0x100,1~
      use_area_lighting=>~0x100,3~
      use_area_height=>~0x100,4~
      enable_shadow=>~0x100,5~
      enable_light_spot=>~0x100,6~
      enable_brighten=>~0x100,7~
      low_brighten=>~0x101,0~
      high_brighten=>~0x101,1~
      keep_pro_visible=>~0x200,0~
      triggered_by_inanimate=>~0x200,1~
      triggered_on_condition=>~0x200,2~
      trigger_in_delay=>~0x200,3~
      use_secondary=>~0x200,4~
      use_fragment=>~0x200,5~
      target_flag_1=>~0x200,6~
      target_flag_2=>~0x200,7~
      num_is_mage_level=>~0x201,0~
      num_is_priest_level=>~0x201,1~
      use_vvc=>~0x201,2~
      is_cone=>~0x201,3~
      go_through_walls=>~0x201,4~
      delay_explosion=>~0x201,6~
      only_one_target=>~0x201,7~
END

LAF build_flag_data_field_editors STR_VAR lookup_table=pro_bit_fields function_prefix=PRO  END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               custom edits
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION PRO_aoe_radius
      STR_VAR arguments=""
BEGIN
      LPF PRO_trigger_radius STR_VAR arguments END
      LPF PRO_basic_aoe_radius STR_VAR arguments END
END

DEFINE_PATCH_FUNCTION PRO_add_explosion_vvc 
      STR_VAR arguments=""
BEGIN
      LPF PRO_use_vvc STR_VAR arguments=1 END
      LPF PRO_explosion_anim_vvc STR_VAR arguments END
END

DEFINE_PATCH_FUNCTION PRO_is_area BEGIN END // a dummy function used for indicating to the make() program that this is an area-effect projectile

DEFINE_PATCH_FUNCTION PRO_target STR_VAR arguments="" BEGIN
   TO_LOWER arguments
   PATCH_MATCH ~%arguments%~ WITH
   ally allies BEGIN
       LPF PRO_target_flag_1 STR_VAR arguments=1 END
       LPF PRO_target_flag_2 STR_VAR arguments=1 END
    END
    enemy enemies BEGIN
       LPF PRO_target_flag_1 STR_VAR arguments=1 END
       LPF PRO_target_flag_2 STR_VAR arguments=0 END
    END
    all BEGIN
       LPF PRO_target_flag_1 STR_VAR arguments=0 END
       LPF PRO_target_flag_2 STR_VAR arguments=0 END
    END
    DEFAULT
    LPF warning STR_VAR warning=~"%arguments% is an invalid argument for PRO_target~ END
    END
END

DEFINE_PATCH_FUNCTION PRO_spark_color STR_VAR arguments="" BEGIN
    DEFINE_ASSOCIATIVE_ARRAY colmap BEGIN
          black=>1
          blue=>2
          chromatic=>3
          gold=>4
          green=>5
          purple=>6
          red=>7
          white=>8
          ice=>9
          stone=>10
          magenta=>11
          orange=>12
    END
    PATCH_IF VARIABLE_IS_SET  ~colmap_%arguments%~ BEGIN
       SET num= ~colmap_%arguments%~
       LPF PRO_spark_color_num STR_VAR arguments= ~%num%~ END
    END ELSE BEGIN
         LPF warning STR_VAR warning=~"%arguments% is an invalid argument for PRO_spark_color~ END
    END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               builder
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION PRO_build_before BEGIN
    INSERT_BYTES   0x000 0x200
    WRITE_ASCII    0x000 ~PRO V1.0~
    LPF PRO_speed STR_VAR arguments=60 END
    LPF PRO_type STR_VAR arguments=2 END
END

DEFINE_PATCH_FUNCTION PRO_postbuild BEGIN END

DEFINE_PATCH_FUNCTION PRO_add_aoe STR_VAR filename="" BEGIN
    INSERT_BYTES 0x200 0x100
END

DEFINE_PATCH_FUNCTION PRO_build_area  BEGIN
    LPF PRO_build_before END
    LPF PRO_add_aoe END
    LPF PRO_type STR_VAR arguments=3 END
    LPF PRO_explosion_anim STR_VAR arguments=~-1~ END
    LPF PRO_target_anim STR_VAR arguments=79 END
    LPF PRO_repetitions STR_VAR arguments=1 END
    LPF PRO_explosion_frequency STR_VAR arguments=100 END
END

DEFINE_PATCH_FUNCTION PRO_build_after BEGIN END

