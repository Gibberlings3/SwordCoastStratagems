   //////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching clone
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION clone_store
    INT_VAR allow_missing=0
            tv=0
    STR_VAR
           store=""
           edits=""
           editstring=""
           allow_missing="no"
    BEGIN
         LAUNCH_ACTION_FUNCTION clone_template
                  STR_VAR file_list=~%store%~
                          file_ext=~STO~
                          edits
                          editstring
         END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching edit
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_store
    INT_VAR allow_missing=0
            tv=0
    STR_VAR
           store=""
           edits=""
           editstring=""
           allow_missing=0
           external_file=""
    BEGIN
         LAUNCH_ACTION_FUNCTION edit_template
                  STR_VAR file_list=~%store%~
                          file_ext=~STO~
                          edits editstring allow_missing tv external_file

         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching install
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION install_store
    STR_VAR
           store=""
           edits=""
           editstring=""
           external_file=""
           location=""
           locbase=""
    BEGIN
         LAUNCH_ACTION_FUNCTION install_template
                  STR_VAR file_list= ~%store%~
                          file_ext=~STO~
                          edits editstring allow_missing tv external_file location locbase 

         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching regexp
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_all_stores
    STR_VAR
           edits=""
           editstring=""
    BEGIN
         LAUNCH_ACTION_FUNCTION regexp_template
                   STR_VAR       file_ext=~STO~
                          edits= ~%edits%~
                          editstring= ~%editstring%~
         END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               offsets
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

<<<<<<<< sto_offsets.2da
TYPE                 offset_loc      offset_length       entrynum_loc  entrynum_length   entry_length
item_purchased       0x2c            4                   0x30          4                 0x4
item                 0x34            4                   0x38          4                 0x1c
drink                0x4c            4                   0x50          4                 0x14
cure                 0x70            4                   0x74          4                 0xc
>>>>>>>>

OUTER_SPRINT ~offset_readin_lookup_2da~ ~sto_offsets~
OUTER_SPRINT ~offset_readin_file_ext~ ~STO~
LAUNCH_ACTION_MACRO read_in_offsets


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               simple data-field edits
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_DEFINE_ASSOCIATIVE_ARRAY sto_fields_absolute BEGIN
   store_type_code=>"0x8,LONG"
   name_string=>"0xc,LONG"
   sell_markup=>"0x14,LONG"
   buy_markup=>"0x18,LONG"
   depreciation_rate=>"0x1c,BYTE"
   will_depreciate=>"0x1d,BYTE"
   stealing_failure_rate=>"0x20,SHORT"
   space=>"0x22,SHORT"
   lore=>"0x3c,LONG"
   identify_cost=>"0x40,LONG"
   rumors=>~0x44,ASCII~
   rumors_donation=>"0x54,ASCII"
   price_peasant=>"0x60,LONG"
   price_merchant=>"0x64,LONG"
   price_noble=>"0x68,LONG"
   price_royal=>"0x6c,LONG"
END

LAF build_simple_data_field_editors STR_VAR lookup_table=sto_fields_absolute function_prefix=STO END


ACTION_DEFINE_ASSOCIATIVE_ARRAY sto_fields BEGIN
     item_resource => ~0x0,ASCII~
     item_charges_1 => ~0xa,SHORT~
     item_charges_2 => ~0xc,SHORT~
     item_charges_3 => ~0xe,SHORT~
     item_num_in_stock => ~0x14,LONG~
     items_infinite => ~0x18,LONG~
     cure_resource => ~0x0,ASCII~
     item_bought=>"0x0,LONG"
     cure=>"0x0,ASCII"
     cure_price=>"0x8,LONG"
     drinkname_string=>"0x4,LONG"
     special_rumor=>"0x0,ASCII"
     drink_price=>"0xc,LONG"
     intoxication=>"0x10,LONG"
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=sto_fields offset_base=~offset_base~ function_prefix=STO END

ACTION_DEFINE_ASSOCIATIVE_ARRAY sto_bit_fields_absolute BEGIN
      room_peasant=>"0x5c,0"
      room_merchant=>"0x5c,1"
      room_noble=>"0x5c,2"
      room_royal=>"0x5c,3"
      can_buy=>"0x10,0"
      can_sell=>"0x10,1"
      can_identify=>"0x10,2"
      can_steal=>"0x10,3"
      can_buy_cures=>"0x10,4"
      can_donate=>"0x10,5"
      can_buy_drinks=>"0x10,6"
      quality_bit_0=>"0x10,1"
      quality_bit_1=>"0x10,2"
      fence=>"0x11,4"
END

LAF build_flag_data_field_editors STR_VAR offset_base=offset_base lookup_table=sto_bit_fields_absolute function_prefix=STO END


ACTION_DEFINE_ASSOCIATIVE_ARRAY sto_bit_fields BEGIN
      state_identified => ~0x10,0~
      state_unstealable => ~0x10,1~
      state_stolen  => ~0x10,2~
END

LAF build_flag_data_field_editors STR_VAR offset_base=offset_base lookup_table=sto_bit_fields function_prefix=STO END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             More memorable type names
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION STO_store_type
   INT_VAR arguments=0
BEGIN
   TO_LOWER arguments
   PATCH_MATCH "%arguments%" WITH
      2 inn BEGIN
         SET num=2
      END
      1 tavern BEGIN
         SET num=1
      END
      3 temple BEGIN
         SET num=3
      END
      4 container_iwd BEGIN
         SET num=4
      END
      5 container BEGIN
         SET num=5
      END
      DEFAULT
         SET num=0
      END
   LPF STO_store_type_code INT_VAR arguments=num END
END


DEFINE_PATCH_FUNCTION STO_read_store_type
   RET value
BEGIN
   LPF STO_read_store_type_code RET num=value END
   PATCH_MATCH "%num%" WITH
      2 BEGIN
         SPRINT value inn
      END
      1 BEGIN
         SPRINT value tavern
      END
      3 BEGIN
         SPRINT value temple
      END
      4 BEGIN
         SPRINT value container_iwd
      END
      5 BEGIN
         SPRINT value container
      END
      0 BEGIN
         SPRINT value store
      END
      DEFAULT
         SPRINT value unknown
      END
END



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             Item-removal hook
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION STO_remove_items
   STR_VAR arguments=""
BEGIN
   PATCH_IF ~%arguments%~ STRING_COMPARE_CASE "" BEGIN
        LPF return_first_entry STR_VAR list= ~%arguments%~ RET arguments=list item=entry END
        REMOVE_STORE_ITEM ~%item%~
        LPF STO_remove_items STR_VAR arguments END
   END
END
OUTER_SPRINT $SFO_do_not_parse_arguments("STO_remove_items") ""

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////              General  adder               syntax: list of item or item(n)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION add_items_to_store
   STR_VAR store=""
           items=""
           item=""
           location=""
           tv="no"
BEGIN
      ACTION_IF "%store%" STRING_EQUAL "" BEGIN
         LAF warning STR_VAR warning="Store unspecified in add_items_to_store" END
      END
      LAF join STR_VAR list1="%items%" list2="%item%" RET items=list END
      ACTION_IF "%items%" STRING_EQUAL "" BEGIN
         LAF warning STR_VAR warning="Tried to add empty list of items to store(s) %store%" END
      END
      LAF return_first_entry STR_VAR list= ~%store%~ RET this_store=entry store=list END
      ACTION_IF ~%tv%~ STRING_EQUAL_CASE ~yes~ BEGIN
        OUTER_SPRINT ~this_store~ ~%tutu_var%%this_store%~
      END
      COPY_EXISTING ~%this_store%.sto~ ~override~
      SPRINT ~items_here~ ~%items%~
        WHILE ~%items_here%~ STRING_COMPARE_CASE "" BEGIN
           SPRINT item_num 1
           LPF return_first_entry STR_VAR list= ~%items_here%~ RET items_here=list entry=entry END
           LPF return_function_and_argument STR_VAR input= ~%entry%~ RET to_add=function item_data=argument END
           PATCH_IF ~%item_data%~ STRING_COMPARE_CASE ~~ BEGIN
              SPRINT item_num ~%item_data%~
           END
           SPRINT input ~ADD_STORE_ITEM %to_add% %location% #1 #0 #0 "IDENTIFIED" #%item_num%~
           LPF patch_reinclude_this STR_VAR input END
        END
      BUT_ONLY
      ACTION_IF "%store%" STRING_COMPARE "" BEGIN
         LAF add_items_to_store STR_VAR store items location tv END
      END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             set the names
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION STO_set_name
   STR_VAR arguments=""
BEGIN
   LPF patch_text_entry STR_VAR function=set_string loc=0xc arguments= ~%arguments%~ END
END

DEFINE_PATCH_FUNCTION STO_say_name
   STR_VAR arguments=""
BEGIN
   LPF say_this_here INT_VAR offset=0xc say= ~%arguments%~ END
END

DEFINE_PATCH_FUNCTION STO_set_drink
   STR_VAR arguments=""
           offset_base="0"
BEGIN
   LPF patch_text_entry INT_VAR loc=0x4+offset_base STR_VAR function=set_string arguments= ~%arguments%~ END
END

DEFINE_PATCH_FUNCTION STO_say_drink
   STR_VAR arguments=""
           offset_base="0"
BEGIN
   LPF say_this_here INT_VAR offset=0x4+offset_base say= ~%arguments%~ END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             set the type
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION STO_store_type
   STR_VAR arguments=""
BEGIN
   TO_LOWER arguments
   PATCH_MATCH "%arguments%" WITH
      store BEGIN
        SET num=0
      END
      tavern BEGIN
        SET num=1
      END
      inn BEGIN
        SET num=2
      END
      temple BEGIN
        SET num=3
      END
      container BEGIN
        SET num=5
      END
      DEFAULT
        LPF warning STR_VAR warning="Store type %arguments% is unrecognised; defaulting to 0" END
        SET num=0
      END
      LPF STO_type_code STR_VAR arguments="%num%" END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////             set buy and sell
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION STO_will_buy
    STR_VAR arguments=""
BEGIN
    PATCH_MATCH "%arguments%" WITH 
    all "ALL" any "ANY" BEGIN
       SPRINT arguments "1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30 31 32 33 34 35 36 37"
    END
    DEFAULT
    END
    // record the items to add
    WHILE "%arguments%" STRING_COMPARE "" BEGIN
       LPF return_first_entry STR_VAR list="%arguments%" RET entry=entry arguments=list END
       SET $items_bought("%entry%")=1
    END
    // collect the existing items
    SPRINT entry_type item_purchased
    SPRINT file_ext STO
    LPM get_offset_array
    PHP_EACH offset_array AS int=>offset BEGIN
       READ_LONG offset item
       SET $items_bought("%item%")=0
    END
    // add the items
    PHP_EACH items_bought AS item=>val BEGIN
       PATCH_IF val BEGIN
           LPF apply_patches STR_VAR file_ext=STO editstring="add_entry_inline=>~type=>item_purchased item_bought=>%item%~" END
       END
    END
END

DEFINE_PATCH_FUNCTION STO_will_not_buy
    STR_VAR  arguments=""
BEGIN
  PATCH_MATCH "%arguments%" WITH
   all "ALL" any "ANY" BEGIN
       LPF apply_patches STR_VAR file_ext=STO editstring="delete_entries_inline=>~type=>item_purchased~"  END
   END
   DEFAULT
      WHILE "%arguments%" STRING_COMPARE "" BEGIN
         LPF return_first_entry STR_VAR list="%arguments%" RET entry=entry arguments=list END
         LPF apply_patches STR_VAR file_ext=STO editstring="delete_entries_inline=>~type=>item_purchased match=>item_bought check=>%entry%~"  END
      END
   END
END
