//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               overarching edit without decompiling   [ALMOST ALWAYS DON'T USE THIS- USE WEIDU INSTEAD!]
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION edit_compiled_dialog
    STR_VAR
           dialog=""
           edits=""
           editstring=""
           allow_missing="no"
    BEGIN
         LAUNCH_ACTION_FUNCTION edit_template
                  STR_VAR file_list= ~%dialog%~
                          file_ext=~DLG~
                          edits editstring allow_missing
         END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////               offsets  (beware: adding or subtracting entries won't work)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

<<<<<<<< dlg_offsets.2da
TYPE            offset_loc      offset_length       entrynum_loc  entrynum_length   entry_length
state           0xc             4                   0x8           4                 0x10
transition      0x14            4                   0x10          4                 0x20
state_trigger   0x18            4                   0x1c          4                 0x8
trans_trigger   0x20            4                   0x24          4                 0x8
action          0x2c            4                   0x2c          4                 0x8
>>>>>>>>


OUTER_SPRINT ~offset_readin_lookup_2da~ ~dlg_offsets~
OUTER_SPRINT ~offset_readin_file_ext~ ~DLG~
LAUNCH_ACTION_MACRO read_in_offsets

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////           (some)    lookup functions
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_DEFINE_ASSOCIATIVE_ARRAY dlg_trans_fields BEGIN
      response_text => ~0x04,LONG~
      journal_text => ~0x08,LONG~
END

LAUNCH_ACTION_FUNCTION build_simple_data_field_editors STR_VAR lookup_table=dlg_trans_fields offset_base=~offset_base~ function_prefix=DLG secondary="yes" END


ACTION_DEFINE_ASSOCIATIVE_ARRAY dlg_bit_fields BEGIN
      associated_text => ~0x00,0~
      associated_trigger => ~0x00,1~
      associated_action  => ~0x00,2~
      terminates_dialog  => ~0x00,3~
      journal_entry  => ~0x00,4~
      add_unsolved_quest => ~0x00,6~
      add_solved_quest  => ~0x01,0~
END
LAF build_flag_data_field_editors STR_VAR offset_base=offset_base lookup_table=dlg_bit_fields function_prefix=DLG secondary="yes" END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////           Bulk compile
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION compile_all 
    STR_VAR location=""
            locbase=""
            locabs=""
BEGIN
   LAF list_files STR_VAR location locbase locabs postfix=d RET list END
   LAF reinclude_this STR_VAR input = "COMPILE EVALUATE_BUFFER %list%" END
END

