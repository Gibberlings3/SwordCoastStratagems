ACTION_IF !VARIABLE_IS_SET sfo_installed BEGIN

  OUTER_SET sfo_installed=1

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Initialise SSL variables
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

OUTER_SPRINT sslvariables "UNUSED_PLACEHOLDER=true"

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Sanity check for AUTO_EVAL_STRINGS
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

 OUTER_SPRINT var testers
 OUTER_SPRINT players var
 ACTION_IF !"%%players%%" STRING_EQUAL testers BEGIN
      FAIL "AUTO_EVAL_STRINGS is not set (you need to include it in your tp2 preamble to use SFO)"
 END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Needed mod data
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

        // Detect Spell Revisions

        ACTION_IF FILE_EXISTS_IN_GAME ~dvimhere.mrk~ THEN BEGIN
           OUTER_SET ~demivrgvs~=1
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Demivrgvs=True~
           APPEND ~SPELL.IDS~ ~1116 CLERIC_SUNSCORCH~ UNLESS ~1116 CLERIC_SUNSCORCH~
        END ELSE BEGIN
           OUTER_SET ~demivrgvs~=0
        END

        ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN
           OUTER_SET ~demivrgvs_v4~=1
        END ELSE BEGIN
           OUTER_SET ~demivrgvs_v4~=0
        END

        // detect Refinements HLAs

        ACTION_IF FILE_EXISTS_IN_GAME ~tg#come.spl~ BEGIN
           OUTER_SET ~refinements~=1
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Refinements=True~
        END ELSE BEGIN
           OUTER_SET ~refinements~=0
        END



///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Set default values of control parameters
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

 ACTION_IF !VARIABLE_IS_SET scsroot BEGIN
      OUTER_SPRINT scsroot "%TP2_BASE_NAME%"
 END
 ACTION_IF !VARIABLE_IS_SET workspace BEGIN
      OUTER_SPRINT workspace "stratagems_external/workspace"
 END
 ACTION_IF !VARIABLE_IS_SET inifile BEGIN
      OUTER_SPRINT inifile "%scsroot%/%scsroot%.ini"
 END
 ACTION_IF !VARIABLE_IS_SET skip_tobex BEGIN
    OUTER_SET skip_tobex=1
 END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Make the workspace directory if it doesn't already exist
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

MKDIR "%workspace%"

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Set the debug variable if it isn't already set; run silent if it's set to zero
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_IF !VARIABLE_IS_SET debug_variable BEGIN
   OUTER_SET debug_variable=0
END
ACTION_IF debug_variable=0 BEGIN SILENT END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Set defaults for component_loc and local_loc
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

  OUTER_SPRINT ~component_loc~ ~~
  OUTER_SPRINT ~local_loc~ ~~

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////               
/////////////              Define some symbols
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

OUTER_SPRINT ~percentage~ ~%~
OUTER_SPRINT ~atsymbol~ ~@~
OUTER_SPRINT ~tilde~ "~"
OUTER_SPRINT ~minus_one~ "-1"
OUTER_SPRINT ~arrow~ ~=>~

<<<<<<<< .../stratagems-inline/blank
>>>>>>>>

///////////////////////////////////////////////////
// check what install we're running on
///////////////////////////////////////////////////

  ACTION_IF (GAME_IS ~tutu tutu_totsc bgt bgee~) BEGIN
     INCLUDE ~%scsroot%/lib/crossplatform.tph~
     OUTER_SET ~is_bg1~=1
  END ELSE BEGIN
     OUTER_SPRINT ~tutu_var~ ~~
     OUTER_SET ~is_bg1~=0
  END

  ACTION_IF GAME_IS ~tutu tutu_totsc bgee~ BEGIN
     OUTER_SET ~is_bg2~=0
  END ELSE BEGIN
     OUTER_SET ~is_bg2~=1
  END

  ACTION_IF GAME_IS ~bgee bg2ee~ BEGIN
     OUTER_SET enhanced_edition=1
  END ELSE BEGIN
     OUTER_SET enhanced_edition=0
  END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// a few resource fixes
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// correct missing section in kensai clab file
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

COPY_EXISTING clabfi04.2da override
   // diagnostic: can we read entry 40 from the second row with at least 10 entries?
   PATCH_SILENT
   PATCH_TRY
      READ_2DA_ENTRY 1 40 10 blah
   WITH DEFAULT
      SET_2DA_ENTRY 1 39 40 "**** ****"
      PRETTY_PRINT_2DA
   END
BUT_ONLY

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Some IDS entries are present on some but not all versions of the game
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////


ACTION_IF !FILE_EXISTS "override/spell.ids" BEGIN
       COPY_EXISTING spell.ids override
END
ACTION_DEFINE_ASSOCIATIVE_ARRAY spell_ids_missing BEGIN
   4611=>DRUID_SHAPESHIFT_BROWNBEAR
   4612=>DRUID_SHAPESHIFT_WOLF
   4613=>DRUID_SHAPESHIFT_BLACKBEAR    
   4632=>AVENGER_SHAPESHIFT_SWORDSPIDER
   4633=>AVENGER_SHAPESHIFT_BABYWYVERN
   4634=>AVENGER_SHAPESHIFT_FIRESALAMANDER
   2490=>WIZARD_POLYMORPH_NATURAL_FORM
   2493=>WIZARD_POLYMORPH_FLIND
   2494=>WIZARD_POLYMORPH_OGRE
   2495=>WIZARD_POLYMORPH_SPIDER
   2496=>WIZARD_POLYMORPH_MUSTARD_JELLY
   2497=>WIZARD_POLYMORPH_BROWN_BEAR
   2498=>WIZARD_POLYMORPH_BLACK_BEAR
   2499=>WIZARD_POLYMORPH_WOLF
   4732=>HELM_TRUE_SIGHT
   3101=>INNATE_CURE_LIGHT_WOUNDS
   3102=>INNATE_SLOW_POISON
   3103=>INNATE_DRAW_UPON_HOLY_MIGHT
   3104=>INNATE_LARLOCHS_MINOR_DRAIN
   3105=>INNATE_HORROR
   3106=>INNATE_VAMPIRIC_TOUCH
   4212=>PALADIN_DETECT_EVIL
   4212=>PALADIN_PROTECTION_FROM_EVIL

END
ACTION_PHP_EACH spell_ids_missing AS code=>name BEGIN
      ACTION_IF !FILE_CONTAINS_EVALUATED ("override/spell.ids" "%code%[ %TAB%]+%name%") BEGIN
         APPEND "spell.ids" "%code% %name%"
      END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// some third-party mods unhelpfully map WIZARD_DISPEL_MAGIC to SPWI326; change this back
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_IF enhanced_edition BEGIN
  COPY_EXISTING spell.ids override
    COUNT_2DA_ROWS 2 rowcount
    FOR (i=1;i<rowcount;i+=1) BEGIN
       READ_2DA_ENTRY i 1 2 ids
       PATCH_IF "%ids%" STRING_EQUAL "WIZARD_DISPEL_MAGIC" BEGIN
           SET_2DA_ENTRY i 0 2 2302
       END
    END
  BUT_ONLY
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// BGEE adds a number of SPCL entries but doesn't provide IDS entries for them
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_IF enhanced_edition BEGIN
   ACTION_DEFINE_ASSOCIATIVE_ARRAY bgee_spl BEGIN
      4238=>SUN_SOUL_FLAMING_FISTS
      4103=>BLACKGUARD_AURA_OF_DESPAIR
      4935=>BLACKGUARD_SUMMON_FALLEN_DEVA
   END
   ACTION_PHP_EACH bgee_spl AS code=>name BEGIN
      ACTION_IF !FILE_CONTAINS_EVALUATED ("override/%spell%.ids" "%code% +%name%") BEGIN
         APPEND "spell.ids" "%code% %name%"
      END
   END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// in case Fixpack hasn't already done it, disable the 0-byte items & broken areas (we do this here for the benefit of the count section)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_FOR_EACH ~dodgyitem~ IN iplot01k iplot04g iplot04h iplot04i BEGIN
   ACTION_IF !FILE_EXISTS ~override/%dodgyitem%.itm~ THEN BEGIN
        DISABLE_FROM_KEY ~%dodgyitem%.itm~
   END
END

ACTION_FOR_EACH ~dodgyarea~ IN xr2400 xr2600 BEGIN
   ACTION_IF !FILE_EXISTS ~override/%dodgyarea%.are~ THEN BEGIN
        DISABLE_FROM_KEY ~%dodgyarea%.are~
   END
END


///////////////////////////////////////////////////
// read in ini
///////////////////////////////////////////////////

  ACTION_IF FILE_EXISTS ~%inifile%~ BEGIN
    COPY ~%inifile%~ ~%workspace%~
     READ_2DA_ENTRIES_NOW ~scs_ini_array~ 1
        FOR (i=0;i<~scs_ini_array~;i=i+1) BEGIN
          READ_2DA_ENTRY_FORMER ~scs_ini_array~ ~i~ 0 ~key~
          READ_2DA_ENTRY_FORMER ~scs_ini_array~ ~i~ 1 ~value~
          TO_LOWER key
          SPRINT $scs_ini_hash(~%key%~) ~%value%~
        END
    BUT_ONLY
  END

///////////////////////////////////////////////////
// include core function library
///////////////////////////////////////////////////


  ACTION_BASH_FOR ~%scsroot%/sfo/general~ ~lib_.*\.tpa~ BEGIN
         INCLUDE ~%scsroot%/sfo/general/%BASH_FOR_RES%.tpa~
  END

///////////////////////////////////////////////////
// include file-specific function library
///////////////////////////////////////////////////

  ACTION_BASH_FOR ~%scsroot%/sfo/filetype~ ~lib_.*\.tpa~ BEGIN
         INCLUDE ~%scsroot%/sfo/filetype/%BASH_FOR_RES%.tpa~
  END

///////////////////////////////////////////////////
// Get resources
///////////////////////////////////////////////////

  ACTION_IF !VARIABLE_IS_SET read_resources BEGIN
     OUTER_SET read_resources=1
  END

  ACTION_IF read_resources BEGIN
   ACTION_FOR_EACH macro IN spellcodes NPC_dialogs saving_throws hit_point_levels stats BEGIN
     PRINT ~Reading in data: %macro%~
     SILENT
     LAUNCH_ACTION_MACRO ~read_in_%macro%~
     VERBOSE
   END
  END
END


