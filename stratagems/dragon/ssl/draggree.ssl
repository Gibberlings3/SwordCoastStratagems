///////////////////////////////////////////////////////////////////////
///	Usual pre-battle stuff
///////////////////////////////////////////////////////////////////////


INCLUDE FILE(%MOD_FOLDER%/dragon/ssl/dragondef.ssl)
INCLUDE FILE(%MOD_FOLDER%/dragon/ssl/dragonsetup.ssl)

////////////////////////////////////////////////////////////////////////////////////////
////	Initial stoneskin
////////////////////////////////////////////////////////////////////////////////////////


IF	
	!GlobalTimerNotExpired("initialstoneskin","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobalTimer("initialstoneskin","LOCALS",600)
		ApplySpell(Myself,DRAGON_STONE_SKIN)
		ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
END

////////////////////////////////////////////////////////////////////////////
////	initial dragonfear
////////////////////////////////////////////////////////////////////////////


IF TRIGGER // note we don't get here unless in a fight
   	   RequireBlock(IsFllyissetat)
	Target(Myself)
THEN DO
	Action(SequencerMyself,FLLYISSETAT_TRIGGER)
	ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
	SetGlobalTimer("dragonbreath","LOCALS",1)
	SetGlobalTimer("redefend","LOCALS",3)
	Continue()
END

IF
   	   IgnoreBlock(IsFllyissetat)
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("initialfear","LOCALS")
THEN
	RESPONSE #100
	SetGlobalTimer("initialfear","LOCALS",600)
	ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
	SetGlobalTimer("dragonbreath","LOCALS",1)
	SetGlobalTimer("redefend","LOCALS",3)
	Continue()
END

//////////////////////////////////////////////////////////////////////////////
////	Heal
//////////////////////////////////////////////////////////////////////////////

IF
	Global("contingency","LOCALS",0)
	Detect(NearestEnemyOf(Myself))
	HPPercentLT(Myself,60)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25942)
		SetGlobal("contingency","LOCALS",1)
		ReallyForceSpell(Myself,CLERIC_HEAL)
END

IF TRIGGER
	HPPercentLT(Myself,60)
THEN DO
	Action(SpellMyself,CLERIC_HEAL)
END

/////////////////////////////////////////////////////////////////////////////////
///	Pursue
////////////////////////////////////////////////////////////////////////////////	

//////////////////////////////////////////////////////////////////////////
///  Pursue!
///////////////////////////////////////////////////////////////////


INCLUDE FILE(%MOD_FOLDER%/genai/ssl/chase.ssl)

//////////////////////////////////////////////////////////////////////////
///	Use breath weapon as often as possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	TargetBlock(EnemiesInReverseOrder)
	!CheckStatGT(scstarget,65,RESISTPOISON)
	TriggerBlock(AoEPoison)
THEN DO
	Action(FastDragonBreath2,GREEN_DRAGON_BREATH,"DrGrBrHt","dragonbreath",black05)
END

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	!CheckStatGT(scstarget,65,RESISTPOISON)
THEN DO
	Action(FastDragonBreath2,GREEN_DRAGON_BREATH,"DrGrBrHt","dragonbreath",black05)
END

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
THEN DO
	Action(FastDragonBreath2,GREEN_DRAGON_BREATH,"DrGrBrHt","dragonbreath",black05)
END


//////////////////////////////////////////////////////////////////////////
///	Use wing buffet to drive away anything at all dangerous
//////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%MOD_FOLDER%/dragon/ssl/wingbuffet.ssl)

//////////////////////////////////////////////////////////////////////////
///	Try a dispel magic
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!GlobalTimerNotExpired("firstdispel","LOCALS")
	TargetBlock(PCsInOrderShort)
	TriggerBlock(DispelTarget)
THEN DO
	Combine()
	SetGlobalTimer("firstdispel","LOCALS",1000)
	Action(Spell,WIZARD_DISPEL_MAGIC|100|50)
	OnContinue(SetGlobalTimer("firstdispel","LOCALS",1000))
END

///////////////////////////////////////////////////////////////
///	Renew defences
////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(AbazigalEnclave)
	HPPercentLT(Myself,40)
THEN DO
	Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
END

IF TRIGGER
        RequireBlock(AbazigalEnclave)
	TriggerBlock(StoneskinFailing)
	GlobalTimerExpired("redefend","LOCALS")
	Target(Myself)
THEN DO
        Action(Sequencer,GREEN_DRAGON_DEFENSIVE_SEQUENCER)
END

IF TRIGGER
	TriggerBlock(StoneskinFailing)
	GlobalTimerExpired("redefend","LOCALS")
	Target(Myself)
THEN DO
	Action(Spell,DRAGON_STONE_SKIN|100|100)
END

IF TRIGGER
        RequireBlock(AbazigalEnclave)
	HPPercentLT(Myself,75)
THEN DO
	Action(SpellMyself,CLERIC_RIGHTEOUS_MAGIC)
END

IF TRIGGER
        TriggerBlock(ILackDefensiveHarmony)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellMyself,CLERIC_DEFENSIVE_HARMONY|100|100)
END

/////////////////////////////////////////////////////////////////////////////
///	Breach enemy defences
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless|PriorityBreach)
	!GlobalTimerNotExpired("breach","LOCALS")
THEN DO
	Combine()
	SetGlobalTimer("breach","LOCALS",12)
	Action(Spell,WIZARD_BREACH|200|100)
END

	
/////////////////////////////////////////////////////////////////
///	High-powered offensive sequencer
/////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(AbazigalEnclave)
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SINecromancy|ResistMagicDamage|Enemy|NotEvil)
THEN DO
	Action(SequencerManualUnabbreviated,GREEN_DRAGON_OFFENSIVE_SEQUENCER,CLERIC_UNHOLY_BLIGHT,CLERIC_UNHOLY_BLIGHT,CLERIC_UNHOLY_BLIGHT)
END

//////////////////////////////////////////////////////////////////////////////
///	Insects
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(AbazigalEnclave)
	TargetBlock(PCsPreferringWeak)
	!GlobalTimerNotExpired("insects","LOCALS")
THEN DO
	SetGlobalTimer("insects","LOCALS",18)
	Action(Spell,CLERIC_INSECT_PLAGUE|100|100)
END

//////////////////////////////////////////////////////////////////////////
/////	True Sight
//////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(TruesightAlreadyCast|IllusionTarget|Enemy|SIDivination)
THEN DO
	Action(SpellMyself,WIZARD_TRUE_SIGHT)
END

///////////////////////////////////////////////////////////////////////////////
///	Unholy Blight
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR100|SINecromancy|ResistMagicDamage|Enemy|NotEvil)
THEN DO
	Action(Spell,CLERIC_UNHOLY_BLIGHT|200|100)
END


////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless|SpellProtectionTarget)
THEN DO
	Combine()
	Action(Spell,WIZARD_WARDING_WHIP|100|100)
	Action(Spell,WIZARD_SPELL_THRUST|100|100)
END

/////////////////////////////////////////////////////////////////
////	Power Words
//////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
///// Power Word: Silence
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(AbazigalEnclave)
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SpellTurn|SIConjuration)
	TriggerBlock(Enemy|Helpless|Silence|SRPowerWord)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_SILENCE|150|50)
END

////////////////////////////////////////////////////////////////////////
////  Power Word: Stun
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	TriggerBlock(MR100|SpellTurn|Stun|Enemy|Helpless|SIConjuration|SRPowerWord)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|150|50)
END

/////////////////////////////////////////////////////////////////
///	Domination
//////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR|Charm|SIEnchantment|SpellTurn|Enemy|Helpless)
	!CheckStatLT(scstarget,0,SAVEVSSPELL)
THEN DO
	Action(Spell,WIZARD_DOMINATION|150|50)
END




//////////////////////////////////////////////////////////////////////////
/// Kill swords
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([GOODCUTOFF.0.SWORD])
THEN DO
	Action(Spell,WIZARD_DEATH_SPELL)
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIEvocation|Enemy|MinorGlobe|SpellTurn)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

///////////////////////////////////////////////////////////////////////////
// Hit spellcasters with Melf / sequencer
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(AbazigalEnclave)
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIConjuration|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|Enemy)
THEN DO
	Combine()
	Action(Sequencer,DRAGON_MAGIC_MISSILE_SEQUENCER)
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END

IF TRIGGER
        RequireBlock(AbazigalEnclave)
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIConjuration|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|Enemy)
THEN DO
	Combine()
	Action(Sequencer,DRAGON_ACID_ARROW_SEQUENCER)
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END


//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs and acid arrows
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;HPPercentLT(scstarget,40))
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(MR50|SIEvocation|MinorGlobe|SpellTurn|Enemy)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(MR50|SIConjuration|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn)
THEN DO
	Combine()
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END

//////////////////////////////////////////////////////////////////////////////////
/////	Melee phase
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/dragon/ssl/melee.ssl)

////////////////////////////////////////////////////////
///	Closedown: avoid interaction with other scripts
////////////////////////////////////////////////////////////

IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END
