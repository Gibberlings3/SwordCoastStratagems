///////////////////////////////////////////////////////////////////////
///	Usual pre-battle stuff
///////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/dragon/ssl/dragondef.ssl)
INCLUDE FILE(%scsroot%/dragon/ssl/dragonsetup.ssl)




////////////////////////////////////////////////////////////////////////////
////	initial Spell Sequencer, plus dragonfear
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(Myself)
	Detect(NearestEnemyOf(Myself))
THEN DO
	Action(Sequencer,BROWN_DRAGON_TRIGGER)
	ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
	SetGlobalTimer("redefend","LOCALS",18)
END

///////////////////////////////////////////////////////////////////
///	Do clever-clever invisibility stuff
///////////////////////////////////////////////////////////////////

IF
	StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("RunInvisible","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("RunInvisible","LOCALS",4)
		SetInterrupt(FALSE)
		ApplySpellRES("runrun",Myself) // No such index
		RunAwayFromNoLeaveArea(NearestEnemyOf(Myself),45)
		SetInterrupt(TRUE)
		Continue()
END

IF
	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("castspell","LOCALS")
	Range(NearestEnemyOf(Myself),10)
	GlobalLT("InvisibleCount","LOCALS",6)
	!GlobalTimerNotExpired("Invisibility","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("Invisibility","LOCALS",12)
		IncrementGlobal("InvisibleCount","LOCALS",1)
		ApplySpell(Myself,WIZARD_NON_DETECTION)
		SpellNoDec(Myself,WIZARD_INVISIBILITY)
END



//////////////////////////////////////////////////////////
///	Healing
//////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,50)
THEN DO
	Action(SpellMyself,CLERIC_HEAL)
END

IF TRIGGER
	HPPercentLT(Myself,75)
	StateCheck(Myself,STATE_INVISIBLE)
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)
END

//////////////////////////////////////////////////////////////////////////
///	Create invisible stalkers
//////////////////////////////////////////////////////////////////////////

IF
	RequireBlock(False) // for reasons I'm unsure about, this block was marked False()
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("stalker","LOCALS")
	OR(2)
		!See(NearestEnemyOf(Myself))
		StateCheck(Myself,STATE_INVISIBLE)
THEN 
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("stalker","LOCALS",15)
		ReallyForceSpell(Myself,WIZARD_INVISIBLE_STALKER)
	RESPONSE #100
		Continue()
END




//////////////////////////////////////////////////////////////////////////
///  Pursue!
///////////////////////////////////////////////////////////////////

BEGIN LOOP("chasetimer","LOCALS",8||"chasetimer","LOCALS",100)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
END LOOP



//////////////////////////////////////////////////////////////////////////
///	Use breath weapon as often as possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	TargetBlock(EnemiesInReverseOrder)
	!CheckStatGT(scstarget,65,RESISTACID)
	TriggerBlock(AoEAcid)
THEN DO
	Action(FastDragonBreath,BROWN_DRAGON_BREATH,"dragonbreath",tra_09)
END

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	!CheckStatGT(scstarget,65,RESISTACID)
THEN DO
	Action(FastDragonBreath,BROWN_DRAGON_BREATH,"dragonbreath",tra_09)
END

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
THEN DO
	Action(FastDragonBreath,BROWN_DRAGON_BREATH,"dragonbreath",tra_09)
END

//////////////////////////////////////////////////////////////////////////
///	Use wing buffet to drive away anything at all dangerous
//////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%/dragon/ssl/wingbuffet.ssl)

///////////////////////////////////////////////////////////////////////////
///	Malison is pretty high priority - we'll
///	cast it fairly blind, but not till after we've done Remove Magic
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder) 
        !HaveSpellRES("%BROWN_DRAGON_TRIGGER%")
	Global("FiredSequencer","LOCALS",1)
THEN DO
	Action(Spell,WIZARD_GREATER_MALISON)
END

////////////////////////////////////////////////////////////
////	Renew defences
////////////////////////////////////////////////////////////

IF TRIGGER
	OR(2)
		!CheckStatGT(Myself,0,STONESKINS)
		!StateCheck(Myself,STATE_HASTED)
THEN DO
	Action(SequencerMyself,BROWN_DRAGON_SEQUENCER)
END

IF TRIGGER
	TriggerBlock(StoneskinFailing)
	!GlobalTimerNotExpired("stoneskins","LOCALS")
THEN DO
	Action(ForceSpellMyself,DRAGON_STONE_SKIN)
	SetGlobalTimer("stoneskins","LOCALS",30)
END

IF TRIGGER
	!CheckStatGT(Myself,0,WIZARD_SPELL_TURNING)
	!CheckStatGT(Myself,0,WIZARD_SPELL_DEFLECTION)
	!GlobalTimerNotExpired("turning","LOCALS")
THEN DO
	Action(SpellMyself,BEHOLDER_SPELL_TURNING)
	SetGlobalTimer("turning","LOCALS",18)
END

//////////////////////////////////////////////////////////////////////////
/////	True Sight
//////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(TruesightAlreadyCast|IllusionTarget|Enemy|SIDivination)
	!GlobalTimerNotExpired("truesight","LOCALS")
THEN DO
	Action(SpellMyself,WIZARD_TRUE_SIGHT)
	SetGlobalTimer("truesight","LOCALS",18)
END


///////////////////////////////////////////////////////////////////////////
////	Kill swords (don't worry about other summons)
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([GOODCUTOFF.0.SWORD])
	Gender(scstarget,SUMMONED)
	TriggerBlock(Enemy|Helpless)
	HasItem("morsword",scstarget)
THEN DO
	Combine()
	Action(Spell,WIZARD_DEATH_SPELL|150|50)
	Action(Sequencer,DRAGON_MAGIC_MISSILE_SEQUENCER)
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

/////////////////////////////////////////////////////////////////////////////
///	Breach enemy defences
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
	CheckStatGT(scstarget,50,RESISTACID) // we're assuming that anything this high probably comes from a spell
THEN DO
	Combine()
	Action(Spell,WIZARD_BREACH|200|100)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless|PriorityBreach)
THEN DO
	Combine()
	Action(Spell,WIZARD_BREACH|200|100)
END

////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Helpless|SpellShieldTarget)
THEN DO
	Combine()
	Action(Spell,WIZARD_SECRET_WORD|100|100)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless|SpellProtectionTarget)
THEN DO
	Combine()
	Action(Spell,WIZARD_PIERCE_MAGIC|100|100)
	Action(Spell,WIZARD_SECRET_WORD|100|100)
END


//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIEvocation|Enemy|MinorGlobe|SpellTurn)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs 
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellDeflect|Enemy)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END


//////////////////////////////////////////////////////////////////////////////////
/////	Melee phase
//////////////////////////////////////////////////////////////////////////////////


DEFAULT TRIGGER(OR(2)&!StateCheck(Myself,STATE_INVISIBLE)&HPPercentGT(Myself,80))
INCLUDE FILE(%scsroot%/dragon/ssl/melee.ssl)
DEFAULT TRIGGER()

////////////////////////////////////////////////////////
///	Closedown: avoid interaction with other scripts
////////////////////////////////////////////////////////////

IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END

