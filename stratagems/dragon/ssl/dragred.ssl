///////////////////////////////////////////////////////////////////////
///	Usual pre-battle stuff
///////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/dragon/ssl/dragondef.ssl)

IF	
	!GlobalTimerNotExpired("initialstoneskin","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("initialstoneskin","LOCALS",1000)
		ApplySpell(Myself,DRAGON_STONE_SKIN)
END


INCLUDE FILE(%scsroot%/dragon/ssl/dragonsetup.ssl)

///////////////////////////////////////////////////////////////////////
///Initial defensive sequencer - Haste, Remove Magic, Minor Spell Deflection -
/// plus dragonfear
///////////////////////////////////////////////////////////////////////

IF TRIGGER // note we don't get here unless in a fight
	Target(Myself)
THEN DO
	Action(Sequencer,FIRKRAAG_SEQUENCER)
	ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
	SetGlobalTimer("dragonbreath","LOCALS",1)
	SetGlobalTimer("redefend","LOCALS",3)
	Continue()
END

/////////////////////////////////////////////////////////////////////////
///Contingency stoneskin
/////////////////////////////////////////////////////////////////////////
IF
	Global("contingency","LOCALS",0)
	!CheckStatGT(Myself,0,STONESKINS)
	See(NearestEnemyOf(Myself))
	GlobalTimerExpired("redefend","LOCALS")
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25942)
		DisplayString(Myself,25942)
		SetGlobal("contingency","LOCALS",1)
		ReallyForceSpell(Myself,DRAGON_STONE_SKIN)
END

/////////////////////////////////////////////////////////////////////////
/// Renew defences
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(HardPlus)
        OR(2)
       	   !CheckStatGT(Myself,0,STONESKINS)
       	   !CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
THEN DO
       Action(SpellMyself,WIZARD_IMPROVED_INVISIBILITY)
       SetGlobalTimer("invisible","LOCALS",12)

IF TRIGGER
           See(NearestEnemyOf(Myself))
       	   !CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
       	   TriggerBlock(HardPlus)
THEN DO
       Action(SpellMyself,SPELL_DEFLECTION_NO_VISUAL)
END

IF TRIGGER
          See(NearestEnemyOf(Myself))
          !StateCheck(Myself,STATE_HASTED)
          TriggerBlock(Hard)
THEN DO
         Action(SpellMyself,WIZARD_HASTE)
END

IF TRIGGER
          See(NearestEnemyOf(Myself))
          !StateCheck(Myself,STATE_HASTED)
          TriggerBlock(Insane)
THEN DO
         Action(SpellMyself,WIZARD_IMPROVED_HASTE)
END

/////////////////////////////////////////////////////////////////////////
///Ordinary stoneskin
/////////////////////////////////////////////////////////////////////////
IF TRIGGER
        TriggerBlock(StoneskinFailing)
	See(NearestEnemyOf(Myself))
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
		Action(SpellMyself,DRAGON_STONE_SKIN)
END

/////////////////////////////////////////////////////////////////////////
///Stay hidden if buffing
/////////////////////////////////////////////////////////////////////////

IF                                                                       
       StateCheck(Myself,STATE_INVISIBLE)
       !GlobalTimerNotExpired("invisible","LOCALS")
THEN
    RESPONSE #100
             RandomWalkContinuous()
END

//////////////////////////////////////////////////////////////////////////
///	Top priority
////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
THEN DO
	Action(Spell,DRAGON_LOWER_FIRE)
END


//////////////////////////////////////////////////////////////////////////
///	Use breath weapon as often as possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	TargetBlock(EnemiesInReverseOrder)
	TriggerBlock(AoEFire)
	!CheckStatGT(scstarget,65,RESISTFIRE)
THEN DO
	Action(DragonBreath2,RED_DRAGON_BREATH,RED_DRAGON_HIT,"dragonbreath",red05)
END

IF TRIGGER
	TargetBlock(EnemiesInReverseOrder)
	!CheckStatGT(scstarget,65,RESISTFIRE)
THEN DO
	Action(DragonBreath2,RED_DRAGON_BREATH,RED_DRAGON_HIT,"dragonbreath",red05)
END

//////////////////////////////////////////////////////////////////////////
///	Use even if it won't work
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	TargetBlock(EnemiesInReverseOrder)
THEN DO
	Action(DragonBreath2,RED_DRAGON_BREATH,RED_DRAGON_HIT,"dragonbreath",red05)
END



//////////////////////////////////////////////////////////////////////////
///	Use wing buffet to drive away anything at all dangerous
//////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%/dragon/ssl/wingbuffet.ssl)

/////////////////////////////////////////////////////////////////////////
/// try a dispel magic
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	TargetBlock(PCsInOrder)
	TriggerBlock(DispelTarget)
THEN DO
	Action(Spell,WIZARD_DISPEL_MAGIC|100|100)
END


/////////////////////////////////////////////////////////////////////////////
///	Renew stoneskin
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        TriggerBlock(StoneskinFailing)
	GlobalTimerExpired("redefend","LOCALS")
	Target(Myself)
THEN DO
	Action(Spell,DRAGON_STONE_SKIN|100|100)
END

////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Helpless|SpellShieldTarget|CorePlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_SECRET_WORD|100|100)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless|SpellProtectionTarget|CorePlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_WARDING_WHIP|100|100)
	Action(Spell,WIZARD_SECRET_WORD|100|100)
END



/////////////////////////////////////////////////////////////////////////////
///	Breach enemy defences
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
	CheckStatGT(scstarget,50,RESISTFIRE)
THEN DO
	Action(Spell,WIZARD_BREACH|200|100)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless|PriorityBreach)
THEN DO
	Action(Spell,WIZARD_BREACH|200|100)
END

////////////////////////////////////////////////////////////////////////////
////	Silence
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|Silence)
	!StateCheck(scstarget,STATE_SILENCED)
THEN DO
	Action(Spell,DRAGON_SILENCE|150|50)
END

IF TRIGGER
        ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SpellTurn|SIConjuration|Silence|SRPowerWord|HardPlus) 
	TriggerBlock(Enemy|Helpless)
	!StateCheck(scstarget,STATE_SILENCED)

THEN DO
	Action(Spell,WIZARD_POWER_WORD_SILENCE|150|50)
END



/////////////////////////////////////////////////////////////////////////////
///	Kill summons
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(Summons)
	Gender(scstarget,SUMMONED)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_DEATH_SPELL|150|50)
END

//////////////////////////////////////////////////////////////////////////
/// Kill swords
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([GOODCUTOFF.0.SWORD])
THEN DO
	Action(Spell,WIZARD_MAGIC_MISSILE)
END


///////////////////////////////////////////////////////////////////////////
////	Slow
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR100|Slow|SIAlteration|Enemy|MinorGlobe|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_SLOW|150|50)
END



//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIEvocation|Enemy|MinorGlobe|SpellTurn)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

///////////////////////////////////////////////////////////////////////////
// Hit spellcasters with Melf
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIConjuration|Stoneskin|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|Enemy|DirectDamageSafe)
THEN DO
	Combine()
	Action(Sequencer,DRAGON_MAGIC_MISSILE_SEQUENCER)
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END

//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs and acid arrows
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|Enemy|DirectDamageSafe)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TriggerBlock(MR100|SIConjuration|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|DirectDamageSafe)
THEN DO
	Combine()
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END


//////////////////////////////////////////////////////////////////////////////////
/////	Melee phase
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/dragon/ssl/melee.ssl)

////////////////////////////////////////////////////////
///	Closedown: avoid interaction with other scripts
////////////////////////////////////////////////////////////

IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END
