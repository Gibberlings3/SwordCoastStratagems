///////////////////////////////////////////////////////////////////////
///	Cutscenes et al
///////////////////////////////////////////////////////////////////////

IF
	Global("GEM_ACTIVE","GLOBAL",0)
	HP(Myself,1)
THEN
	RESPONSE #100
		StartCutSceneMode()
		HideGUI()
		StartCutScene("ILDRGDIE")
END

IF
	Global("killme","LOCALS",0)
	!Global("GEM_ACTIVE","GLOBAL",0)
THEN
	RESPONSE #100
		StartCutSceneMode()
		ActionOverride("CUTSPY",DestroySelf())
		MoveViewObject([PC],INSTANT)
		Wait(1)
		EndCutSceneMode()
		SetGlobal("killme","LOCALS",1)
END

IF
	Global("killme","LOCALS",1)
	HPGT(Myself,0)
THEN
	RESPONSE #100
		DestroyItem("minhp1")  // No such index
		Kill(Myself)
END

///////////////////////////////////////////////////////////////////////
///	Usual pre-battle stuff
///////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/dragon/ssl/dragondef.ssl)

IF	
	!GlobalTimerNotExpired("initialstoneskin","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("initialstoneskin","LOCALS",1000)
		ApplySpell(Myself,DRAGON_STONE_SKIN)
END


INCLUDE FILE(%MOD_FOLDER%/dragon/ssl/dragonsetup.ssl)

///////////////////////////////////////////////////////////////////////
///Initial defensive sequencer - Haste, Fire shield:cold, Minor Spell Deflection -
/// plus dragonfear
///////////////////////////////////////////////////////////////////////

IF TRIGGER // note we don't get here unless in a fight
	Target(Myself)
	TriggerBlock(HardPlus)
THEN DO
	Action(Sequencer,ICAS_SEQUENCER)
	ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
	SetGlobalTimer("dragonbreath","LOCALS",1)
	SetGlobalTimer("redefend","LOCALS",3)
	Continue()
END

/////////////////////////////////////////////////////////////////////////
///Contingency stoneskin
/////////////////////////////////////////////////////////////////////////
IF TRIGGER
	Global("contingency","LOCALS",0)
	!CheckStatGT(Myself,0,STONESKINS)
	See(NearestEnemyOf(Myself))
	GlobalTimerExpired("redefend","LOCALS")
	TriggerBlock(CorePlus)
THEN DO
                Action(Literal) 
		DisplayStringHead(Myself,25942)
		DisplayString(Myself,25942)
		SetGlobal("contingency","LOCALS",1)
		ReallyForceSpell(Myself,DRAGON_STONE_SKIN)
END

//////////////////////////////////////////////////////////////////////////
///	Top priority
////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	TriggerBlock(EasyPlus)
THEN DO
	Action(Spell,DRAGON_LOWER_COLD)
END


//////////////////////////////////////////////////////////////////////////
///	Use breath weapon as often as possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	TargetBlock(EnemiesInReverseOrder)
	TriggerBlock(AoECold)
	!CheckStatGT(scstarget,65,RESISTCOLD)
THEN DO
	Action(DragonBreath,INNATE_DRAGON_BREATH,"dragonbreath",dragon01)
END

IF TRIGGER
	TargetBlock(EnemiesInReverseOrder)
	!CheckStatGT(scstarget,65,RESISTCOLD)
THEN DO
	Action(DragonBreath,INNATE_DRAGON_BREATH,"dragonbreath",dragon01)
END

//////////////////////////////////////////////////////////////////////////
///	Use even if it won't work
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	TargetBlock(EnemiesInReverseOrder)
THEN DO
	Action(DragonBreath,INNATE_DRAGON_BREATH,"dragonbreath",dragon01)
END



//////////////////////////////////////////////////////////////////////////
///	Use wing buffet to drive away anything at all dangerous
//////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%MOD_FOLDER%/dragon/ssl/wingbuffet_iwd.ssl)

/////////////////////////////////////////////////////////////////////////
/// try a dispel magic
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TriggerBlock(EasyPlus)
	TargetBlock(PCsInOrder)
	TriggerBlock(DispelTarget)
THEN DO
	Action(Spell,WIZARD_DISPEL_MAGIC|100|100)
END


/////////////////////////////////////////////////////////////////////////////
///	Renew stoneskin
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        TriggerBlock(StoneskinFailing)
	GlobalTimerExpired("redefend","LOCALS")
	TriggerBlock(EasyPlus)
	Target(Myself)
THEN DO
	Action(Spell,DRAGON_STONE_SKIN|100|100)
END

/////////////////////////////////////////////////////////////////////////////
///	Breach enemy defences
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
	CheckStatGT(scstarget,50,RESISTCOLD)
	TriggerBlock(CorePlus)
THEN DO
	Action(Spell,WIZARD_BREACH|200|100)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless|PriorityBreach)
THEN DO
	Action(Spell,WIZARD_BREACH|200|100)
END

////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Helpless|SpellShieldTarget)
	TriggerBlock(CorePlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_SECRET_WORD|100|100)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless|SpellProtectionTarget)
	TriggerBlock(CorePlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_PIERCE_MAGIC|100|100)
	Action(Spell,WIZARD_SECRET_WORD|100|100)
END


/////////////////////////////////////////////////////////////////////////////
///	Kill summons
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(Summons)
	Gender(scstarget,SUMMONED)
	TriggerBlock(Enemy|Helpless)
	TriggerBlock(CorePlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_DEATH_SPELL|150|50)
END

//////////////////////////////////////////////////////////////////////////
/// Kill swords
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([GOODCUTOFF.0.SWORD])
	TriggerBlock(EasyPlus)
THEN DO
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

//////////////////////////////////////////////////////////////////////////
/// Blinding sequencer
//////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(PCFighters)
	TriggerBlock(Enemy|Helpless|MR|MinorGlobe)
	CheckStatGT(scstarget,0,SAVEVSSPELL)
	TriggerBlock(HardPlus)
THEN DO
	Combine()
	Action(MinorSequencerManual,ICAS_MINOR_SEQUENCER,BLINDNESS,BLINDNESS|100|100)
END

//////////////////////////////////////////////////////////////////////////
/// Direct-damage cold spells
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	TargetBlock(DistantEnemies)
	TriggerBlock(MR|Enemy|SIEvocation|AoEIceStorm)
	TriggerBlock(EasyPlus)
THEN DO
	Action(Spell,WIZARD_ICE_STORM|80|20)
END

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MR|SpellTurn|SIAlteration|Enemy|ResistCold)
	TriggerBlock(DirectDamageSafe)
	CheckStatGT(scstarget,6,SAVEVSSPELL)
	TriggerBlock(CorePlus)
THEN DO
	Action(Spell,WIZARD_OTILUKES_FREEZING_SPHERE|150|50)
END

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;HPPercentLT(scstarget,40))
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(MR|ResistCold|Enemy|ArrowSafe|SpellTurn|MinorGlobe|Helpless|SIEvocation|MirrorImage)
	TriggerBlock(EasyPlus)
THEN DO
	Action(Spell,WIZARD_ICELANCE|150|50)
END

//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIEvocation|Enemy|MinorGlobe|SpellTurn)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
	TriggerBlock(EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs and acid arrows
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|Enemy|DirectDamageSafe)
	TriggerBlock(EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END



//////////////////////////////////////////////////////////////////////////////////
/////	Melee phase
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/dragon/ssl/melee.ssl)

////////////////////////////////////////////////////////
///	Closedown: avoid interaction with other scripts
////////////////////////////////////////////////////////////

IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END
