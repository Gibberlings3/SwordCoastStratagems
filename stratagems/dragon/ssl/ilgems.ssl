BEGIN_ACTION_DEFINITION
	Name(Spell)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!CheckSpellState(scstarget,DO_NOT_TARGET_SPELLS)
		HaveSpell(scsargument1)
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		Spell(scstarget,scsargument1)
END

IF TRIGGER
   TriggerBlock(CorePlus)
   Global("innate_casting","LOCALS",0)
THEN DO
     Action(Literal)
     ApplySpellRES("dw#innat",Myself)
     SetGlobal("innate_casting","LOCALS",1)
     Continue()
END

IF TRIGGER
   TriggerBlock(EasyMinus)
   Global("innate_casting","LOCALS",1)
THEN DO
     Action(Literal)
     ApplySpellRES("dw#inrem",Myself)
     SetGlobal("innate_casting","LOCALS",0)
     Continue()
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//// Summon umber hulks
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!GlobalTimerNotExpired("SUMMON_UMBER","LOCALS")
	TriggerBlock(EasyMinus)
	!See(NearestEnemyOf(Myself))
	!Exists([0.0.SAHUAGIN])
	!Exists([0.0.UMBERHULK])
THEN DO
                Action(Literal)
		SetGlobalTimer("SUMMON_UMBER","LOCALS",THREE_MINUTES)
		ForceSpellPoint([1245.1162],INNATE_DRAGON_GEM_SUMMONING)  // SPIN135.SPL (No such index)
		Wait(1)
		ForceSpellPoint([1248.806],INNATE_DRAGON_GEM_SUMMONING)  // SPIN135.SPL (No such index)
		Wait(4)
END

IF TRIGGER
	!Global("first_summon","LOCALS",1)
	TriggerBlock(CorePlus)
	!See(NearestEnemyOf(Myself))
	!Exists([0.0.SAHUAGIN])
	!Exists([0.0.UMBERHULK])
THEN DO
                Action(Literal)
                SetGlobal("first_summon","LOCALS",1)
		SetGlobalTimer("SUMMON_UMBER","LOCALS",THREE_MINUTES)
		ForceSpellPoint([1245.1162],INNATE_DRAGON_GEM_SUMMONING)  // SPIN135.SPL (No such index)
		Wait(1)
		ForceSpellPoint([1248.806],INNATE_DRAGON_GEM_SUMMONING)  // SPIN135.SPL (No such index)
		Wait(4)
END

IF TRIGGER
	!GlobalTimerNotExpired("SUMMON_UMBER","LOCALS")
	TriggerBlock(Core)
	!Exists([0.0.UMBERHULK])
THEN DO
                Action(Literal)
		SetGlobalTimer("SUMMON_UMBER","LOCALS",FOUR_MINUTES)
		ForceSpellPoint([1208.903],INNATE_DRAGON_GEM_SUMMONING)  // SPIN135.SPL (No such index)
		Wait(4)
END

IF TRIGGER
	!GlobalTimerNotExpired("SUMMON_UMBER","LOCALS")
	TriggerBlock(Hard)
	!Exists([0.0.UMBERHULK])
THEN DO
                Action(Literal)
		SetGlobalTimer("SUMMON_UMBER","LOCALS",THREE_MINUTES)
		ForceSpellPoint([1208.903],INNATE_DRAGON_GEM_SUMMONING)  // SPIN135.SPL (No such index)
		Wait(4)
END

IF TRIGGER
	!GlobalTimerNotExpired("SUMMON_UMBER","LOCALS")
	TriggerBlock(Insane)
	!Exists([0.0.UMBERHULK])
THEN DO
                Action(Literal)
		SetGlobalTimer("SUMMON_UMBER","LOCALS",THREE_MINUTES)
		ForceSpellPoint([1208.903],INNATE_DRAGON_GEM_SUMMONING)  // SPIN135.SPL (No such index)
		Wait(4)
END

////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Helpless|SpellShieldTarget)
	TriggerBlock(CorePlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_SECRET_WORD|100|100)
END

///////////////////////////////////////////////////////////////////////////
////	Kill swords (don't worry about other summons)
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([GOODCUTOFF.0.SWORD])
	Gender(scstarget,SUMMONED)
	TriggerBlock(Enemy|Helpless)
	HasItem("morsword",scstarget)
THEN DO
	Action(Spell,WIZARD_MAGIC_MISSILE)
END



/////////////////////////////////////////////////////////////////////////////
/////  Finger of Death
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SpellDeflect|Slay|SINecromancy|Enemy)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Spell,WIZARD_FINGER_OF_DEATH|150|50)
END

//////////////////////////////////////////////////////////////////////////
/// Direct-damage cold spells
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR|Enemy|SIEvocation)
	Range(scstarget,10)
THEN DO
	Action(Spell,WIZARD_CONE_OF_COLD|80|20)
END

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MR|SpellTurn|SIAlteration|Enemy|ResistCold)
	TriggerBlock(DirectDamageSafe)
	CheckStatGT(scstarget,6,SAVEVSSPELL)
	TriggerBlock(CorePlus)
THEN DO
	Action(Spell,WIZARD_OTILUKES_FREEZING_SPHERE|150|50)
END

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;HPPercentLT(scstarget,40))
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(MR|ResistCold|Enemy|ArrowSafe|SpellTurn|MinorGlobe|Helpless|SIEvocation|MirrorImage)
	TriggerBlock(EasyPlus)
THEN DO
	Action(Spell,WIZARD_ICELANCE|150|50)
END

//////////////////////////////////////////////////////////////////////////
///  Blind warriors
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCFighters)
	TriggerBlock(Enemy|Helpless|MR|MinorGlobe|SpellTurn)
	CheckStatGT(scstarget,0,SAVEVSSPELL)
	!StateCheck(scstarget,STATE_BLIND)
THEN DO
	Combine()
	Action(Spell,WIZARD_BLINDNESS|150|50)
END


//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIEvocation|Enemy|MinorGlobe|SpellTurn)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

///////////////////////////////////////////////////////////////////////////
// Hit spellcasters with Melf
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIConjuration|Stoneskin|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|Enemy|DirectDamageSafe)
THEN DO
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END

//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs and acid arrows
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|Enemy|DirectDamageSafe)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TriggerBlock(MR100|SIConjuration|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|DirectDamageSafe)
THEN DO
	Combine()
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END

