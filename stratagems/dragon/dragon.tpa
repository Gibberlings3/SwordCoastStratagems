LAF include STR_VAR file=dragon_shared.tph END
LAF include STR_VAR file=caster_shared.tph locbase=caster_shared END

DEFINE_ACTION_FUNCTION dragon
BEGIN



 LAF dragon_resources_shared END
 ACTION_IF is_iwd BEGIN
   LAF define_difficulty STR_VAR type=dragon_iwd RET difficulty_variable END
   LAM read_in_spell_trigger_data // do it after creating dragon sequencers
   LAF dragon_iwd END
 END ELSE BEGIN
   LAF define_difficulty STR_VAR type=dragon RET difficulty_variable END
   ACTION_IF is_bg2 && enhanced_edition BEGIN
		LAF dragon_strip_regeneration END
   END
   LAF dragon_resources END
   LAM read_in_spell_trigger_data // do it after creating dragon sequencers
   LAF dragon_scripts END
   LAF dragon_core END
   LAF dragon_other END
   LAF dragon_compatibility END

   LAF check_label STR_VAR label=dw#ascension_abazigal RET value END
   ACTION_IF value BEGIN
         OUTER_SPRINT component_loc ascension
         LAF run STR_VAR file=abazigal_dragon END
   END
 END

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////// Build needed resources
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION dragon_resources BEGIN

   // weapons

   LAF edit_item STR_VAR item=~dragred1 dragsil1 draggre1 dragblaw dragbrow dragblue~ editstring=~dragon_weapon=>null~ END

   // breath weapons

   LAF edit_spell STR_VAR spell=~SHADOW_DRAGON_BREATH SILVER_DRAGON_BREATH_COLD RED_DRAGON_HIT BLACK_DRAGON_BREATH BLUE_DRAGON_BREATH BROWN_DRAGON_BREATH GREEN_DRAGON_BREATH drgrbrht~
                          editstring=~dragon_breath=>null~
   END

   // projectiles

   ACTION_FOR_EACH pro IN dragblck dragsilv BEGIN
     COPY_EXISTING ~%pro%.pro~ ~override~
        WRITE_SHORT 0x206 450
     BUT_ONLY
   END


   // exits from dragon areas shouldn't be passable by NPCs

    MAKE_PATCH
       match=>"trigger_type=2"
       impassable_by_NPC=>1
    END
    LAF edit_area STR_VAR area=~ar1203 ar1402 ar2102 ar2807 ar2902 ar3018 ar3024 ar6004 ar6005~ editstring=~patch_trigger=>patch_data~ END


   // fix the bug in hdragsil from QP

   COPY_EXISTING ~hdragsil.cre~ ~override~
		READ_LONG 0x2c4 ~eff_off~
		PATCH_IF ~%eff_off%~=0 THEN BEGIN
			READ_LONG 0x2bc ~itm_off~
			READ_LONG 0x2c0 ~itm_num~
			WRITE_LONG 0x2c4 ~itm_num~*0x14+~itm_off~
		END
    BUT_ONLY

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////// Compile needed scripts
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION dragon_scripts BEGIN

    LAF ssl_to_bcs STR_VAR script=~dragblac dragbrow draggre2 draggree dragred dragshad gorsal dragsilv~ location=~ssl~ END
    LAF clone_script STR_VAR script=~dragshad=>shadra01 dragblac=>draghell~ END

    ACTION_IF !FILE_EXISTS_IN_GAME ~abazdg02.cre~ BEGIN
       LAF ssl_to_bcs STR_VAR script=abazdrag location=~ssl~ END
    END

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////// Main modifications
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION dragon_core BEGIN

    //////////////////
    /// red dragons
    //////////////////
    
    /// Firkraag

    MAKE_PATCH
      dragon_general=>null
      level_all=>23
      immunity=>fire
      remove_spells=>all
      add_spells=>~MAGIC_MISSILE(4) MELF_ACID_ARROW(3) WIZARD_DISPEL_MAGIC(2) SLOW HASTE
                   GREATER_MALISON SECRET_WORD IMPROVED_INVISIBILITY BREACH(2) DEATH_SPELL POWER_WORD_SILENCE 
                   DRAGON_STONE_SKIN DRAGON_LOWER_FIRE IMPROVED_HASTE
                   DRAGON_SILENCE FIRKRAAG_SEQUENCER DRAGON_MAGIC_MISSILE_SEQUENCER WARDING_WHIP~
      enforce_script_order=>~shoutdlg above dragred~
    END
    LAF edit_creature STR_VAR creature="dragred firkra02" edits=patch_data END // do generic dragred because we clone it in Fire Giants to make Brimstone

    // Saladrex

    MAKE_PATCH
      dragon_general=>null
      level_all=>26
      immunity=>fire
      remove_spells=>all
      add_spells=>~MAGIC_MISSILE(5) MELF_ACID_ARROW(3) WIZARD_DISPEL_MAGIC(2) SLOW
                   GREATER_MALISON BREACH(3) DEATH_SPELL DRAGON_STONE_SKIN(3)
                   DRAGON_SILENCE PROTECTION_MAGIC_WEAPON_NOEFF(2) POWER_WORD_BLIND DRAGON_LOWER_FIRE
                   RUBY_RAY_OF_REVERSAL(2) SALADREX_TRIGGER DRAGON_MAGIC_MISSILE_SEQUENCER SALADREX_SEQUENCER~
    END
    LAF edit_creature STR_VAR creature=gorsal edits=patch_data END

    ///////////////////////////
    // green dragons
    ///////////////////////////

    // WK dragon guardian and Fll'yesitat (sp?)

    MAKE_PATCH
        dragon_general=>null
        level_all=>24
        immunity=>poison
        immunity'=>insects
        remove_spells=>all
        add_spells=>~UNHOLY_BLIGHT(2) DEFENSIVE_HARMONY(2) CLERIC_TRUE_SIGHT HEAL
                     MAGIC_MISSILE(4) MELF_ACID_ARROW(4) WIZARD_DISPEL_MAGIC(2)
                     SPELL_THRUST(2) BREACH(2) DOMINATION DEATH_SPELL POWER_WORD_SILENCE
                     WARDING_WHIP POWER_WORD_STUN DRAGON_STONE_SKIN(3) BLACK_DRAGON_PLANT
                     INSECT_PLAGUE GREEN_DRAGON_OFFENSIVE_SEQUENCER DRAGON_MAGIC_MISSILE_SEQUENCER~
    END
    LAF edit_creature STR_VAR creature=~fsdragon bazdra03~ edits=patch_data END
    LAF edit_creature STR_VAR creature=bazdra03 editstring="add_spells=>~%FLLYISSETAT_TRIGGER%~" END

    ///////////////////////////
    // black dragons
    ///////////////////////////

    // Nizi and the Hell Dragons

    MAKE_PATCH
       dragon_general=>null
       level_all=>23
       immunity=>acid
       immunity'=>insects
       immunity''=>poison
       remove_spells=>all
       add_spells=>~MAGIC_MISSILE(2) MELF_ACID_ARROW(2) WIZARD_DISPEL_MAGIC WIZARD_HASTE
                    WIZARD_DIRE_CHARM(2) DRAGON_STONE_SKIN DRAGON_ACID_ARROW_SEQUENCER~
       strip_script=>hdragred
    END
    LAF edit_creature STR_VAR creature=~dragblac hdragred hdragsil~ edits=patch_data END

    // Nizi needs a script rearrangement and an insect swarm
    
    LAF edit_creature STR_VAR creature=dragblac 
                              editstring=~strip_script=>"dragblac shoutdlg" insert_script_high=>dragblac insert_script_high=>shoutdlg add_spells=>"BLACK_DRAGON_INSECT BLACK_DRAGON_PLANT"~
    END

    //////////////////////////////
    // silver dragons
    //////////////////////////////
    
    // Adalon

    MAKE_PATCH
       dragon_general=>null
       level_all=>23
       immunity=>cold
       remove_spells=>all
       add_spells=>~CLERIC_HOLD_PERSON(2) CURE_SERIOUS_WOUNDS(2) WIZARD_CHROMATIC_ORB(3)
                    RAY_OF_ENFEEBLEMENT(3) FLAME_ARROW(3) HASTE ICE_STORM BREACH(2)
                    DEATH_SPELL WIZARD_CHAIN_LIGHTNING WIZARD_FINGER_OF_DEATH DRAGON_STONE_SKIN
                    DRAGON_SILENCE SILVER_DRAGON_SEQUENCER~
    END
    LAF edit_creature STR_VAR creature=udsilver edits=patch_data END

    ////////////////////////////
    // Special
    ////////////////////////////
    
    // Thax - shadow dragon
    
    MAKE_PATCH
       dragon_general=>null
       level_all=>23
       immunity=>cold
       remove_spells=>all
       add_spells=>~WIZARD_CHROMATIC_ORB WIZARD_DISPEL_MAGIC HASTE GREATER_MALISON
                    WIZARD_CHAOS BREACH(2) DEATH_SPELL PROTECTION_MAGIC_WEAPON_NOEFF DRAGON_STONE_SKIN~
    END
    LAF edit_creature STR_VAR creature=shadra01 edits=patch_data END
	ACTION_IF enhanced_edition BEGIN
		COPY_EXISTING "shadra01.cre" override
			WRITE_BYTE 0x273 IDS_OF_SYMBOL (class SHADOW_DRAGON)
	END

    // Draconis - brown dragon

    MAKE_PATCH
       dragon_general=>null
       level_all=>30
       immunity=>acid
       remove_spells=>all
       add_spells=>~MAGIC_MISSILE(5) WIZARD_DISPEL_MAGIC SLOW GREATER_MALISON SECRET_WORD HEAL CURE_CRITICAL_WOUNDS(5) BEHOLDER_SPELL_TURNING(3) WIZARD_TRUE_SIGHT(2)
                    BREACH(2) DEATH_SPELL PIERCE_MAGIC(2) DRAGON_MAGIC_MISSILE_SEQUENCER BROWN_DRAGON_SEQUENCER BROWN_DRAGON_TRIGGER~
    END
    LAF edit_creature STR_VAR creature=bazdra02 edits=patch_data END

    // Abazigal - blue dragon - ONLY IF ASCENSION IS NOT INSTALLED

    ACTION_IF !FILE_EXISTS_IN_GAME ~abazdg02.cre~ BEGIN
     MAKE_PATCH
       dragon_general=>null
       level_all=>32
       immunity=>electricity
       remove_spells=>all
       add_spells=>~MAGIC_MISSILE(5) HASTE WIZARD_LIGHTNING_BOLT SLOW(2) SECRET_WORD(2) GREATER_MALISON
                    BREACH(3) SHADOW_DOOR WIZARD_CHAOS DEATH_SPELL WIZARD_TRUE_SIGHT
                    PROTECTION_MAGIC_WEAPON_NOEFF WARDING_WHIP RUBY_RAY_OF_REVERSAL
                    WIZARD_FINGER_OF_DEATH MAZE POWER_WORD_BLIND DRAGON_STONE_SKIN(3)
                    ABAZIGAL_TRIGGER ABAZIGAL_SEQUENCER DRAGON_LOWER_ELECTRICAL DRAGON_MAGIC_MISSILE_SEQUENCER~
     END
     LAF edit_creature STR_VAR creature=dragblue edits=patch_data END


    END

END


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////// Dragons added by other components (corrects for kooky install order)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION dragon_other BEGIN

    MAKE_PATCH
       dragon_general=>null
    END
    LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=~dw#abbl1 dw#abbl2 dw#abgre dw#abred dw#abdgy dw#ysdra~  edits=patch_data END

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////// Compatibility with other mods
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION dragon_compatibility BEGIN


END


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////// IWD - just Icasaracht
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION dragon_iwd BEGIN

   // icasaracht cre file

    MAKE_PATCH
       dragon_general=>null
       immunity=>cold
       remove_spells=>all
       add_spells=>~MAGIC_MISSILE(3) WIZARD_DISPEL_MAGIC ICELANCE DRAGON_STONE_SKIN ICE_STORM(2) SECRET_WORD BREACH(3) OTILUKES_FREEZING_SPHERE
                    DEATH_SPELL PIERCE_MAGIC DRAGON_LOWER_COLD ICAS_SEQUENCER ICAS_MINOR_SEQUENCER~
       add_effect_inline=>~opcode=>193 parameter2=>1~  // detect invisibility by script
    END
    LAF edit_creature STR_VAR creature=icasa edits=patch_data END

	// icasa breath - doesn't need cold-resistance immunity any more and it glitches for CR<0.
	
	COPY_EXISTING "%INNATE_DRAGON_BREATH%.spl" override
		LPF DELETE_EFFECT INT_VAR match_opcode=324 match_parameter2=25 END

    // icasa weapon

    LAF edit_item STR_VAR item=icasa editstring=~dragon_weapon=>null~ END

    // icasa script

    LAF ssl_to_bcs STR_VAR script=~ildragon~ location=~ssl~ END

    // icasa soul gem

    MAKE_PATCH
       immunity=>cold
       remove_spells=>all
       add_spells=>~MAGIC_MISSILE(3) BLINDNESS(2) MELF_ACID_ARROW(3) ICELANCE(3) SECRET_WORD CONE_OF_COLD(2) OTILUKES_FREEZING_SPHERE(2) WIZARD_FINGER_OF_DEATH~
       add_effect_inline=>~opcode=>193 parameter2=>1~  // detect invisibility by script
       strip_script=>"ilgemn ilgemu"
    END
    LAF edit_creature STR_VAR creature=dlsgem edits=patch_data END
    
    // soul gem script
    
    LAF ssl_to_bcs STR_VAR script=ilgems location="ssl" END




END


////////////

DEFINE_ACTION_FUNCTION dragon_strip_regeneration BEGIN

	
	ACTION_FOR_EACH creature IN
		BAZDRA02
		DRAGBLUE
		GORSAL		
		BEGIN
			COPY_EXISTING "%creature%.cre" override
				LPF DELETE_EFFECT INT_VAR match_opcode=98 END
		END


END

