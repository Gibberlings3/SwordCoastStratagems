LAF include STR_VAR file=caster_shared.tph locbase=caster_shared END


//////////////////////////////////////////////////////////////////
////   Dragon weapons are at least +4 and have range 5
//////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION dragon_weapon BEGIN
   PATCH_MAKE_PATCH
      enchantmentGT=>4
      patch_ability_inline=>~ability_rangeGT=>5~
   END
   LPF apply_patches STR_VAR file_ext=ITM edits=patch_data END
END

//////////////////////////////////////////////////////////////////
////   Dragon breath bypasses MR, has long range, level 0
//////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION dragon_breath BEGIN
   PATCH_MAKE_PATCH
      secondary=>0 // not "offensive damage", so bypasses Cloak of Mirroring
      patch_ability_inline=>~ability_range=>320~
      patch_effect_inline'=>~resist_dispel=>0 power=>0~
   END
   LPF apply_patches STR_VAR file_ext=SPL edits=patch_data END
END

//////////////////////////////////////////////////////////////////
////   Baseline patch for dragons
//////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION dragon_general BEGIN
 LPF check_ini STR_VAR ini=bypass_dragon_immunity_changes RET value END
 PATCH_IF value=0 BEGIN
   PATCH_MAKE_PATCH
      delete_effect=>~opcode=193~
      add_effect_inline=>~opcode=>193 parameter2=>1~
      immunity_to_opcode=>~74 13 55 211 19~//immunity to blindness/slay/death/imprisonment/Mind Flayer
      immunity_to_string=>~1474 32089 14674~ // blindness, devour brain
      immunity_to_spell=>~%WIZARD_IMPRISONMENT%~
   END
   PATCH_IF !is_iwd BEGIN
      SPRINT $patch_data("immunity_to_spell'") "%DRAGON_WING_BUFFET%"
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END
 END
 PATCH_MAKE_PATCH
      class=>"if enhanced_edition then no_change else CLERIC_MAGE"
      strip_scs_scripts=>null
 END
 LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END
 LPF ini_check STR_VAR ini=dragons_get_plus_one_apr RET value END
 PATCH_IF value BEGIN
	WRITE_BYTE 0x53 (THIS < 5) ? THIS + 1 : THIS
 END
END

//////////////////
// sequencers  and other shared resources
//////////////////

DEFINE_ACTION_FUNCTION dragon_resources_shared BEGIN

   LAF check_label STR_VAR label=dw#dragon_resources RET value END
   ACTION_IF !value BEGIN

       LAF make_label STR_VAR label=dw#dragon_resources END
       LAF include STR_VAR file=caster_shared.tph locbase=caster_shared END

       ////////////////
       // sequencer data
       ////////////////

       MAKE_PATCH
          minor_sequencer=>spwi420b
          sequencer=>spwi710b
          trigger=>spwi809b
       END
       ACTION_PHP_EACH patch_data AS entry=>resref BEGIN
           OUTER_SPRINT "%entry%_icon" "%resref%"
       END


    ////////////////
    // difficulty
    ////////////////

    LAF innate_casting_effects END
    LAF check_ini STR_VAR ini=dragon_staying_power RET mult=value END
    LAF hitpoint_boost_effects INT_VAR mult STR_VAR spell=dw#drahp END

   /////////////////
   // versions of SI:Abj, SI:Div without the animation, which looks silly on dragons
   // (may also be built by Anadramatis)
   /////////////////

   ACTION_IF !is_iwd BEGIN
      LAF clone_spell STR_VAR spell=~%WIZARD_SPELL_IMMUNITY_ABJURATION%=>dw#0d590 %WIZARD_SPELL_IMMUNITY_DIVINATION%=>dw#0d592~
                           editstring=~add_effect_inline=>"opcode=>291 target=>1 parameter2=>1 timing=>1"~
      END
   END

   //////////////////////
   //  dragon shapeshift (to revert Polymorph Other)
   //////////////////////

   COPY_EXISTING spwi489.spl "override/dwplydr.spl"
      LPF DELETE_EFFECT INT_VAR match_opcode=172 END
      LPF DELETE_EFFECT INT_VAR match_opcode=135 END
	  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=112 STR_VAR resource=squirp END // only needed on pre-2.6 versions


       // add missing innates on IWD
       ACTION_IF is_iwd BEGIN
          LAF add_spell_to_ids STR_VAR typename=innate idsname=DRAGON_FEAR RET newname END
          COPY "%MOD_FOLDER%/%component_loc%/resource/spin895.spl" "override/%newname%.spl"
			SAY 0x8 @1
          LAF add_spell_to_ids STR_VAR typename=innate idsname=DRAGON_STONE_SKIN RET newname END
          COPY "%MOD_FOLDER%/%component_loc%/resource/spin694.spl" "override/%newname%.spl"
          LAF add_spell_to_ids STR_VAR typename=innate idsname=SPELL_DEFLECTION_NO_VISUAL RET newname END
          COPY
             "%MOD_FOLDER%/%component_loc%/resource/spin710.spl" "override/%newname%.spl"
             "%MOD_FOLDER%/%component_loc%/resource/spin710b.spl" "override/%newname%b.spl"
              REPLACE_TEXTUALLY spin710 "%newname%"
          LAF add_spell_to_ids STR_VAR typename=innate idsname=DRAGON_LOWER_COLD RET newname END
          COPY "%MOD_FOLDER%/%component_loc%/resource/spin682.spl" "override/%newname%.spl"
			SAY 0x8 @2
          OUTER_SPRINT DRAGON_LOWER_COLD "%newname%"
          MAKE_PATCH
             match=>"opcode=30"
             opcode=>28
          END
          LAF edit_spell STR_VAR spell=DRAGON_LOWER_COLD editstring="patch_effect=>patch_data" END
       END

       // innate spells need to properly be innate
       MAKE_PATCH
            level=>1
            spell_type=>innate
            patch_ability_inline=>"ability_icon_loc=>4"
       END
       ACTION_IF is_iwd BEGIN
          OUTER_SPRINT spell "DRAGON_FEAR DRAGON_STONE_SKIN DRAGON_LOWER_COLD"
       END ELSE BEGIN
          OUTER_SPRINT spell "PROTECTION_MAGIC_WEAPON_NOEFF DRAGON_LOWER_ELECTRICAL DRAGON_LOWER_FIRE BLACK_DRAGON_PLANT BLACK_DRAGON_INSECT DRAGON_SILENCE DRAGON_STONE_SKIN"
       END
       LAF edit_spell STR_VAR spell edits=patch_data END

       ACTION_IF is_iwd BEGIN
          OUTER_SPRINT table dragon_triggers_iwd.2da
       END ELSE BEGIN
          OUTER_SPRINT table dragon_triggers.2da
       END
       LAF process_table STR_VAR table locbase=dragon function=build_dragon_trigger END

   END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////// helper function for building triggers
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION build_dragon_trigger
     STR_VAR name=""
             type=""
             spell1=""
             spell2=""
             spell3=""
             category=""
BEGIN
     OUTER_SET strref=EVALUATE_BUFFER "%%type%_string%"
     OUTER_SPRINT icon EVALUATE_BUFFER "%%type%_icon%"
     ACTION_IF "%category%" STRING_EQUAL o BEGIN
        OUTER_SET remover=1
     END ELSE BEGIN
        OUTER_SET remover=0
     END
     LAF make_spell_trigger INT_VAR remover STR_VAR name strref icon spell1 spell2 spell3 prefix=dt END
END

