IF
	OnCreation()
	PartyHasItem("clck26") //guard against multiple copies of the Cloak of Mirroring - relevant if SCS is installed midgame
THEN
	RESPONSE #100
		DestroyItem("clck26")
		Continue()
END


/////////////////////////////////////////////////////////////////
/////	Block pro/undead scrolls
/////////////////////////////////////////////////////////////////

IF                                                               
       !GlobalTimerNotExpired("blockproundead","LOCALS")
       See(NearestEnemyOf(Myself))
THEN
       RESPONSE #100
           ApplySpellRES("dw#nprun",Player1)
           ApplySpellRES("dw#nprun",Player2)
           ApplySpellRES("dw#nprun",Player3)
           ApplySpellRES("dw#nprun",Player4)
           ApplySpellRES("dw#nprun",Player5)
           ApplySpellRES("dw#nprun",Player6)
           SetGlobalTimer("blockproundead","LOCALS",300)
           Continue()
END




//////////////////////////////////////////////////////////////////////////////////////
///	I'm a lich
///////////////////////////////////////////////////////////////////////////////////

VARIABLE(IsLich=True)
VARIABLE(HasL1=True)
VARIABLE(HasL2=True)
VARIABLE(HasL3=True)
VARIABLE(HasL4=True)
VARIABLE(HasL5=True)
VARIABLE(HasL6=True)
VARIABLE(HasL7=True)
VARIABLE(HasL8=True)
VARIABLE(HasL9=True)
VARIABLE(slowspell= )
VARIABLE(IsConjurer=True)

//////////////////////////////////////////////////////////////////////////////////
///
///   Define actions
///
////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsspellsubstitute1|| )
BEGIN LOOP(scsspellsubstitute2|| )
	INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)
END LOOP
END LOOP

///////////////////////////////////////////////////////////////////////////////////////
////	Setup
///////////////////////////////////////////////////////////////////////////////////////



INCLUDE FILE (%scsroot%/mage/ssl/bg2/generalblocks/rest.ssl)

IF
	!Global("chaincontingencyfired","LOCALS",1)
	OR(7)
		Detect(NearestEnemyOf(Myself))
		BEGIN LOOP(scsplayer||1;2;3;4;5;6)
			Range(Playerscsplayer,15)
		END LOOP
THEN
	RESPONSE #100
		SetGlobal("chaincontingencyfired","LOCALS",1)
		SetGlobalTimer("redefend","LOCALS",0)
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
		DisplayStringHead(Myself,26328)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_THE_ELEMENTS)
		ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
		ReallyForceSpell(Myself,WIZARD_SPELL_TURNING)
		DisplayString(Myself,25942)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobal("spelltrigger","LOCALS",1)
		SetGlobal("spellsequencer","LOCALS",3)
		SetGlobal("melfnum","LOCALS",10)
		ReallyForceSpell(Myself,DEMILICH_DEATH)
		SetGlobal("seeabjuration","LOCALS",25) // this may seem long, but the demilich is likely to get a timestop off
END

///////////////////////////////////////////////////////////////////////////
//	Melee
///////////////////////////////////////////////////////////////////////////

IF
	GlobalGT("melfnum","LOCALS",0)
	!HasItem("dw#melf",Myself)
THEN
	RESPONSE #100
		IncrementGlobal("melfnum","LOCALS",-1)
		ApplySpellRes("dw#melf",Myself)
		Continue()
END

INCLUDE FILE(%scsroot%/mage/ssl/bg2/meleeblocks/demilich.ssl)

/////////////////////////////////////////////////////////////////////////////////////
////	Demilich starts with a timestop/multisummon combo
/////////////////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

INCLUDE FILE(%scsroot%/mage/ssl/bg2/combatblocks/timestop.ssl)
INCLUDE FILE(%scsroot%/mage/ssl/bg2/combatblocks/planetar.ssl)
INCLUDE FILE(%scsroot%/mage/ssl/bg2/combatblocks/gate.ssl)

//////////////////////////////////////////////////////////////////////////
// Renew and retreat
//////////////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/renew.ssl)


//////////////////////////////////////////////////////////////////////////
// Look for PCs
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER()

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/mill.ssl)

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))


////////////////////////////////////////////////////////////////////////////
// Core magic block
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg2/magetypes/demilich.ssl)

////////////////////////////////////////////////////////////////////////////
///// 	Chase PCs
////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/chase.ssl)


//////////////////////////////////////////////////////
///	If everyone is helpless, kill'em.
//////////////////////////////////////////////////////
	
IF
	See(NearestEnemyOf(Myself))
	BEGIN LOOP(scsloopvar||1;2;3;4;5;6)
		OR(3)
			!See(Playerscsloopvar)
			StateCheck(Playerscsloopvar,STATE_IMMOBILE)
			CheckStatGT(Playerscsloopvar,0,HELD)
	END LOOP
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/closedown.ssl)
////////////////////////////


