////////////////////////////////////////////////////////
////////////////////////////////////////////////////////
//// 	Things to do only once
////////////////////////////////////////////////////////
////////////////////////////////////////////////////////

ACTION_IF !VARIABLE_IS_SET ~initialise~ BEGIN

  OUTER_SET initialise=1

////////////////////////////////////////////////////////
//// general variables, macros and functions
////////////////////////////////////////////////////////

  INCLUDE ~%scsroot%/lib/install_sfo.tpa~

////////////////////////////////////////////////////////
//// stratagems-specific macros and functions
////////////////////////////////////////////////////////

  INCLUDE ~%scsroot%/lib_stratagems/stratagems_shared.tph~ 

////////////////////////////////////////////////////////
//// tutu/BGT variables
////////////////////////////////////////////////////////
  // we'll have set the variables in component 0 already, but it's harmless to repeat, and makes using force-install a bit more (though not totally) safe
  ACTION_IF (GAME_IS ~tutu tutu_totsc bgt~ || FILE_EXISTS_IN_GAME ~dorn.cre~) BEGIN
     INCLUDE ~%scsroot%/lib_stratagems/crossplatform.tph~
     OUTER_SET ~is_bg1~=1
  END ELSE BEGIN
     OUTER_SPRINT ~tutu_var~ ~~
     OUTER_SET ~is_bg1~=0
  END

  ACTION_IF !GAME_IS ~tutu tutu_totsc~ BEGIN
     OUTER_SET ~is_bg2~=1
  END ELSE BEGIN
     OUTER_SET ~is_bg2~=0
  END

  ACTION_IF FILE_EXISTS_IN_GAME ~dorn.cre~ BEGIN
     OUTER_SET enhanced_edition=1
     OUTER_SET is_bg1=1
     OUTER_SET is_bg2=0
  END ELSE BEGIN
     OUTER_SET enhanced_edition=0
  END



////////////////////////////////////////////////////
//  get ToBEx status
////////////////////////////////////////////////////

  ACTION_IF !(ENGINE_IS ~tob~ && FILE_EXISTS ~bgmain.exe~) BEGIN
     OUTER_SET ~skip_tobex~=1
  END ELSE BEGIN
     LAF action_check_ini STR_VAR ini=~Disable_ToBEx~ RET skip_tobex=value END
  END

  ACTION_IF !(ENGINE_IS ~tob~ && FILE_EXISTS ~bgmain.exe~) BEGIN
     OUTER_SET ~skip_hacks~=1
  END ELSE BEGIN
     LAF action_check_ini STR_VAR ini=~Disable_Hacks_If_ToBEx_Skipped~ RET skip_hacks=value END
  END

  ACTION_IF enhanced_edition=1 BEGIN
     OUTER_SET skip_tobex=1
     OUTER_SET skip_hacks=1
  END

///////////////////////////////////////////////////////////////
///	Array to identify PCs
///////////////////////////////////////////////////////////////

  COPY_EXISTING - ~pdialog.2da~ ~.../scs-inline/pdialog.2da~
	COUNT_2DA_ROWS 3 ~rowcount~
	FOR (i=0;i<~%rowcount%~;i=i+1) BEGIN
		READ_2DA_ENTRY ~%i%~ 0 3 ~entry~
		TO_LOWER ~entry~
		SPRINT $script_name_pdialog(EVALUATE_BUFFER ~%entry%~) ~~
	END
	//: add candlekeep training group names
	SPRINT $script_name_pdialog(~arkanis~) ~~
	SPRINT $script_name_pdialog(~canderous~) ~~
	SPRINT $script_name_pdialog(~deder~) ~~
	SPRINT $script_name_pdialog(~mordaine~) ~~
	SPRINT $script_name_pdialog(~osprey~) ~~
  BUT_ONLY_IF_IT_CHANGES

///////////////////////////////////////////////////////////////
///	Possibly-missing IDS entries
///////////////////////////////////////////////////////////////


  COPY_EXISTING ~SPELL.IDS~ ~override~
    REPLACE_TEXTUALLY ~PSIONIC _SUPERIOR_INVISIBILITY~ ~PSIONIC_SUPERIOR_INVISIBILITY~
  BUT_ONLY

  APPEND ~STATE.IDS~ ~0x8010202D STATE_DISABLED~
  UNLESS ~0x8010202D STATE_DISABLED~  /// includes Helpless, Stunned, Panic, Sleeping, Charmed, Feebleminded, Confused

  APPEND ~STATE.IDS~ ~0x00000029 STATE_IMMOBILE~
  UNLESS ~0x00000029 STATE_IMMOBILE~  /// includes Helpless, Stunned, Sleeping

  APPEND ~STATE.IDS~ ~0x00400010 STATE_NOT_TARGETABLE~
  UNLESS ~0x00400010 STATE_NOT_TARGETABLE~  /// includes Improved Invisibility and plain Invisibility

  APPEND ~STATE.IDS~ ~0x00000FC0 STATE_REALLY_DEAD~
  UNLESS ~0x00000FC0 STATE_REALLY_DEAD~

///////////////////////////////////////////////////
// String localisation
///////////////////////////////////////////////////

ACTION_IF enhanced_edition BEGIN
   OUTER_SPRINT trigger_string 25101
   OUTER_SPRINT minor_sequencer_string 26469
   OUTER_SPRINT sequencer_string 24618
END ELSE BEGIN
   OUTER_SPRINT trigger_string 26423
   OUTER_SPRINT minor_sequencer_string 5013
   OUTER_SPRINT sequencer_string 25951
END


///////////////////////////////////////////////////
// Initialise SSL variables
///////////////////////////////////////////////////

  OUTER_SPRINT ~sslvariables~ ~%percentage%scsroot%percentage%=%scsroot%&%percentage%tutu_var%percentage%=%tutu_var%~
  ACTION_IF GAME_IS bgt BEGIN
     OUTER_SPRINT sslvariables ~%sslvariables%&IsTrilogy=True~
  END
  ACTION_IF enhanced_edition BEGIN
     OUTER_SPRINT sslvariables ~%sslvariables%&EnhancedEdition=True~
  END
  LAF action_check_ini STR_VAR ini=Enable_Easy_Option RET value=value END
  ACTION_IF value BEGIN
     OUTER_SPRINT sslvariables ~%sslvariables%&EasyOptionAvailable=True&EasyMinusLine=TriggerBlock(EasyMinus)&CoreMinusLine=TriggerBlock(CoreMinus)&CorePlusLine=TriggerBlock(CorePlus)&HardPlusLine=TriggerBlock(HardPlus)~
  END ELSE BEGIN
     OUTER_SPRINT sslvariables ~%sslvariables%&EasyMinusLine=&CoreMinusLine=&CorePlusLine=&HardPlusLine=~
  END
  LAF action_check_ini STR_VAR ini=Run_Silent RET value=value END
  ACTION_IF value BEGIN
     OUTER_SPRINT sslvariables ~%sslvariables%&RunningSilent=&RunningNoisy=UNUSED&StringsAreDisabled=True~
  END ELSE BEGIN
     OUTER_SPRINT sslvariables ~%sslvariables%&RunningSilent=UNUSED&RunningNoisy=&StringsAreDisabled=False~
  END
  ACTION_IF is_bg2=0 BEGIN
     OUTER_SPRINT sslvariables ~%sslvariables%&PlusUnknownSafe=PlusNullSafe&MultipleDamageTypeSafe=NullSafe&IsBG1Only=True~
  END

///////////////////////////////////////////////////////////////
///	Third-party compatibility
///////////////////////////////////////////////////////////////


        ACTION_IF FILE_EXISTS_IN_GAME ~dvimhere.mrk~ THEN BEGIN
           OUTER_SET ~demivrgvs~=1
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Demivrgvs=True~
           APPEND ~SPELL.IDS~ ~1116 CLERIC_SUNSCORCH~ UNLESS ~1116 CLERIC_SUNSCORCH~
        END ELSE BEGIN
           OUTER_SET ~demivrgvs~=0
        END

        ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ THEN BEGIN
           OUTER_SET ~demivrgvs_v4~=1
        END ELSE BEGIN
           OUTER_SET ~demivrgvs_v4~=0
        END

        // check for safe version of imprisonment

        ACTION_IF FILE_EXISTS_IN_GAME ~tb#impri.spl~ OR FILE_EXISTS_IN_GAME ~dvimhere.mrk~ BEGIN
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&SafeImprisonment=True~
        END


	OUTER_SET ~galactygon_swarm~=0
        OUTER_SET ~galactygon_armor~=0
        OUTER_SET ~galactygon_color~=0
        OUTER_SET ~galactygon_web~=0
        OUTER_SET ~galactygon_mentaldomination~=0
        OUTER_SET ~galactygon_flamestrike~=0
        OUTER_SET ~galactygon_shillelagh~=0
        OUTER_SET ~galactygon_alicorn~=0
        OUTER_SET ~galactygon_static~=0


        ACTION_IF FILE_EXISTS_IN_GAME ~LCPACKB5.xxx~ THEN BEGIN
           ACTION_IF FILE_EXISTS_IN_GAME ~LC_SWARM.SPL~ THEN BEGIN
                     OUTER_SET ~galactygon_swarm~=1
                     OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Galactygon_Swarm=True~
           END
           ACTION_IF FILE_EXISTS_IN_GAME ~SPWI117F.SPL~ THEN BEGIN
                     OUTER_SET ~galactygon_armor~=1
                     OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Galactygon_Armor=True~
                     OUTER_SET ~galactygon_color~=1
                     OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Galactygon_Color=True~
           END
           ACTION_IF FILE_EXISTS_IN_GAME ~LC_WEBA1.VVC~ THEN BEGIN
                     OUTER_SET ~galactygon_web~=1
                     OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Galactygon_Web=True~
           END
           ACTION_IF FILE_EXISTS_IN_GAME ~LCMENDOM.SPL~ THEN BEGIN
                     OUTER_SET ~galactygon_mentaldomination~=1
                     OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Galactygon_MentalDomination=True~
           END
           ACTION_IF FILE_EXISTS_IN_GAME ~LCFLSRIN.SPL~ THEN BEGIN
                     OUTER_SET ~galactygon_flamestrike~=1
                     OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Galactygon_FlameStrike=True~
           END
           ACTION_IF FILE_EXISTS_IN_GAME ~SPPR120A.SPL~ THEN BEGIN
                     OUTER_SET ~galactygon_shillelagh~=1
                     OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Galactygon_Shillelagh=True~
           END
           ACTION_IF FILE_EXISTS_IN_GAME ~FBLADE1.SPL~ THEN BEGIN
                     OUTER_SET ~galactygon_alicorn~=1
                     OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Galactygon_Alicorn=True~
           END
           ACTION_IF FILE_EXISTS_IN_GAME ~LC_RECI1.VVC~ THEN BEGIN
                     OUTER_SET ~galactygon_static~=1
                     OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Galactygon_Static=True~
           END
        END

        // detect Refinements HLAs

        ACTION_IF FILE_EXISTS_IN_GAME ~tg#come.spl~ BEGIN
           OUTER_SET ~refinements~=1
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Refinements=True~
        END ELSE BEGIN
           OUTER_SET ~refinements~=0
        END



END   // end of do-once part

////////////////////////////////////////////////////////
////////////////////////////////////////////////////////
//// 	Things to do every time
////////////////////////////////////////////////////////
////////////////////////////////////////////////////////

  LAF action_check_ini STR_VAR ini=God_Does_Not_Play_Dice RET value=value END
  
  ACTION_IF value BEGIN
     RANDOM_SEED 3
  END ELSE BEGIN
     RANDOM_SEED 3.14
  END

///////////////////////////////////////////////////
// detect whether Truesight cures blindness
///////////////////////////////////////////////////

    ACTION_IF FILE_EXISTS_IN_GAME ~dw#truesight_blindness.mrk~ BEGIN
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&TruesightCuresBlindness=True~
    END
