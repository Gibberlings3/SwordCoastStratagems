
INCLUDE FILE(%scsroot%/vampire/ssl/vampire_definitions.ssl)

VARIABLE(IsNotMoron=True)

/////////////////////////////////////////////////////////////////
/////	fire shield
/////////////////////////////////////////////////////////////////

IF TRIGGER
        TriggerBlock(Insane)
	Global("difficulty","LOCALS",0)
THEN DO
                Action(Literal)
		SetGlobal("difficulty","LOCALS",1)
		ApplySpellRES("ibodhi7",Myself) // No such index
		Continue()
END

IF TRIGGER
	TriggerBlock(HardMinus)
	Global("difficulty","LOCALS",1)
THEN DO
		SetGlobal("difficulty","LOCALS",0)
		ApplySpellRES("dw#hprem",Myself) // No such index
		Continue()
END

/////////////////////////////////////////////////////////////////
/////	Block pro/undead scrolls
/////////////////////////////////////////////////////////////////

IF                                                               
       !GlobalTimerNotExpired("blockproundead","LOCALS")
       See(NearestEnemyOf(Myself))
THEN
       RESPONSE #100
           ApplySpellRES("dw#nprun",Player1)
           ApplySpellRES("dw#nprun",Player2)
           ApplySpellRES("dw#nprun",Player3)
           ApplySpellRES("dw#nprun",Player4)
           ApplySpellRES("dw#nprun",Player5)
           ApplySpellRES("dw#nprun",Player6)
           SetGlobalTimer("blockproundead","LOCALS",300)
           Continue()
END


IF TRIGGER
	TriggerBlock(HardMinus)
	HPPercentLT(Myself,10)
	Global("Death","LOCALS",0)
THEN DO
                Action(Literal)
	        StartCutSceneMode()
	        StartDialogueNoSet(Player1)
       	 	EndCutSceneMode()
END

IF TRIGGER
	TriggerBlock(Insane)
	HPLT(Myself,10)
	Global("Death","LOCALS",0)
THEN DO
                Action(Literal)
	        StartCutSceneMode()
	        StartDialogueNoSet(Player1)
       	 	ReallyForceSpell(Myself,CLERIC_HEAL)
       	 	EndCutSceneMode()
END

IF
	Die()
THEN
	RESPONSE #50
		ReallyForceSpellDead(Myself,BODHI_MIST_FORM_CHANGE)
END

/////////////////////////////////////////////////////////////////
/////	Extra allies
/////////////////////////////////////////////////////////////////

IF TRIGGER
  TriggerBlock(CoreMinus)
  Global("DMWW_Bodhi_Init","AR0809",0)
  See([PC])
THEN DO
     Action(Literal)
     SetGlobal("DMWW_Bodhi_Init","AR0809",1)
     Continue()
END

IF TRIGGER
  TriggerBlock(Hard)
  Global("DMWW_Bodhi_Init","AR0809",0)
  See([PC])
THEN DO
     Action(Literal)
     SetGlobal("DMWW_Bodhi_Init","AR0809",1)
     CreateCreature("bodfgt02",[1054.251],0)
     CreateCreature("bodfgt02",[1659.249],0)
     Continue()
END

IF TRIGGER
  TriggerBlock(Insane)
  Global("DMWW_Bodhi_Init","AR0809",0)
  See([PC])
THEN DO
     Action(Literal)
     SetGlobal("DMWW_Bodhi_Init","AR0809",1)
     CreateCreature("bodfgt02",[1054.251],0)
     CreateCreature("bodfgt02",[1659.249],0)
     CreateCreature("bodfgt02",[1056.253],0)
     CreateCreature("bodfgt02",[1657.247],0)
     Continue()
END


IF
    Global("DMWW_Bodhi_Init","AR0809",0)
THEN
  RESPONSE #100
    // stall
END

IF
	StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		NoAction() // block random post-death utterances
END


INCLUDE FILE (%scsroot%/vampire/ssl/vampire_flight.ssl)
INCLUDE FILE (%scsroot%/bodhi/ssl/bodhi_bats.ssl)
INCLUDE FILE (%scsroot%/bodhi/ssl/bodhi_touch_summons.ssl)
INCLUDE FILE (%scsroot%/bodhi/ssl/bodhi_dark.ssl)
INCLUDE FILE (%scsroot%/bodhi/ssl/bodhi_sleep.ssl)
INCLUDE FILE (%scsroot%/bodhi/ssl/bodhi_chill.ssl)
INCLUDE FILE (%scsroot%/bodhi/ssl/bodhi_touch.ssl)
INCLUDE FILE (%scsroot%/vampire/ssl/vampire_melee.ssl)
