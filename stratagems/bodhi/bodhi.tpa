INCLUDE ~%scsroot%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION bodhi
BEGIN

    LAF define_difficulty STR_VAR type=bodhi RET difficulty_variable END
    LOAD_TRA "%scs_tra_loc%/english/vampire.tra" "%scs_tra_loc%/%LANGUAGE%/vampire.tra"
        // pro/undead scrolls

        LAF run STR_VAR file=undeadscroll locbase=tactical_bg2 tra=tactical_bg2 END

        // creature file

	ACTION_IF !(FILE_EXISTS_IN_GAME ~ibodhi1.spl~) THEN BEGIN // officially we're incompatible with the Tactics version; unofficially, we try
		LAF bodhi_CRE END
	END

        // spells

        LAF bodhi_spells END

        // improved weapons

        LAF install STR_VAR files="bodhi.itm" location=resource END
        LAF edit_creature STR_VAR creature=~bodhi2 ppbodhi4~ editstring=~replace_items=>"vamp1(WEAPON1)"~ END

        // challenge version

        MAKE_PATCH
           replace_items=>~bodhi(WEAPON1)~
           script_class=>dw#chbod
        END
        LAF edit_creature STR_VAR creature=chevil08 edits=patch_data END

        // scripts

        LAF ssl_to_bcs STR_VAR script=~bodhi2 c6bodhi dw#chbod~ location=ssl END

        // mages
        
        LAF bodhi_mages END
        
        // bodhi's vampire allies
        
        LAF bodhi_vampires END
        
        // bodhi's grimwards
        
        LAF install STR_VAR location=resource files="dw#bodfg.baf" END
        LAF edit_creature STR_VAR creature="bodfgt01 bodfgt02" editstring="insert_script_high=>dw#bodfg" END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_CRE INT_VAR version=0 BEGIN


      MAKE_PATCH
         strip_script=>~wtasight vampir01~
         strip_scs_scripts=>null
         make_casting_innate=>null
         add_effect_inline=>~opcode=>193 parameter2=>1~  // detect invisibility by script
         add_effect_inline'=>~opcode=>31 parameter1=>50 parameter2=>1~ // 50% MDR
         add_effect_inline''=>~opcode=>297~ // immunity to turning
         immunity_to_opcode=>13 // protection from Azuredge et al
         immunity_to_string=>10554
         movement=>12
         ac_natural=>~-4~
         ac_effective=>~-4~
         hitpoints=>183
         resist_slashing=>25
         resist_crushing=>25
         resist_piercing=>25
         resist_missile=>25
      END
      LAF edit_creature STR_VAR creature=~c6bodhi bodhi2 chevil08~ edits=patch_data END
      LAF edit_creature STR_VAR creature=finbodh allow_missing=1 edits=patch_data END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_spells 
INT_VAR version=1
BEGIN


        // many are made from hot-modifying existing spells. For safety (and especially for SR) in most cases we use a pristine copy


        ACTION_IF enhanced_edition BEGIN
              OUTER_SPRINT copy_loc "%scsroot%/%component_loc%/resource/unmodified/ee"
           END ELSE BEGIN
              OUTER_SPRINT copy_loc "%scsroot%/%component_loc%/resource/unmodified/vanilla"
        END


        // Cloud of Bats - just a renamed clone of Creeping Doom
        
        LAF clone_spell STR_VAR spell="%CLERIC_CREEPING_DOOM%=>ibodhi1" editstring="say_name=>2400 spell_type=>innate patch_ability_inline=>~casting_time=>0~" END

        // Abyssal Darkness, Buried Alive - fine, but reduce the save penalty to -4 from -10, and Buried Alive
        // needs to remove the effects of a Slow spell and vice versa
        
        LAF install STR_VAR file="ibodhi2.spl ibodhi3.spl" location=resource END
        LAF edit_spell STR_VAR spell=ibodhi2 editstring="say_name=>2502 patch_effect_inline=>~savebonus=>-4~" END
        LAF edit_spell STR_VAR spell=ibodhi3 editstring="say_name=>2503 patch_effect_inline=>~savebonus=>-4~" END

        ACTION_IF enhanced_edition BEGIN
           LAF edit_spell STR_VAR spell=ibodhi3 editstring="add_effect_inline=>~opcode=>321 power=>3 timing=>1 target=>2 save_vs_spell=>1 savebonus=>-4 resource=>%WIZARD_SLOW%~" END
           LAF edit_spell STR_VAR spell="%WIZARD_SLOW%" editstring="add_effect_inline=>~opcode=>321 power=>3 timing=>1 target=>2 save_vs_spell=>1 savebonus=>-4 resource=>ibodhi3~" END
        END ELSE BEGIN
           LAF edit_spell STR_VAR spell=ibodhi3 editstring="add_effect_inline=>~opcode=>206 power=>3 timing=>0 duration=>24 target=>2 save_vs_spell=>1 savebonus=>-4 resource=>%WIZARD_SLOW% at_end=>1~" END
           LAF edit_spell STR_VAR spell="%WIZARD_SLOW%" editstring="add_effect_inline=>~opcode=>206 power=>3 timing=>0 duration=>60 target=>2 save_vs_spell=>1 savebonus=>-4 resource=>ibodhi3 at_end=>1~" END
        END
        
        //  Chill of the Grave - based on Horrid Wilting, lowest-level (L16) effect, with cold damage in place of magic
        

        COPY "%copy_loc%/horrid_wilting.spl" "override/ibodhi4.spl"
        MAKE_PATCH
           say_name=>2504
           spell_type=>innate
           delete_ability=>"ability_min_level>1"
           patch_ability_inline=>"casting_time=>0"
           patch_effect_inline=>"match=>opcode=12 parameter2b=>2"
        END
        LAF edit_spell STR_VAR spell=ibodhi4 edits=patch_data END

        // touch of death - variant Death Spell that also does 36 hp magic damage to all in area, and heals caster of 36 hp

        COPY "%copy_loc%/death_spell.spl" "override/ibodhi5.spl"
        MAKE_PATCH
           say_name=>2505
           spell_type=>innate
           patch_ability_inline=>"casting_time=>0"
           add_effect_inline=>"power=>6 opcode=>12 target=>2 timing=>1 parameter2b=>64 parameter1=>36"
           add_effect_inline'=>"power=>6 opcode=>141 target=>2 timing=>1 parameter2=>2"
           add_effect_inline''=>"power=>6 opcode=>18 target=>1 timing=>0 duration=>300 parameter1=>36"
        END
        LAF edit_spell STR_VAR spell=ibodhi5 edits=patch_data END

        // Sepulchral Sleep - clone of Greater Command with cosmetic changes

        COPY "%copy_loc%/greater_command.spl" "override/ibodhi6.spl"
        MAKE_PATCH
           say_name=>2506
           spell_type=>innate
           delete_ability=>"ability_min_level>1"
           patch_ability_inline=>"casting_time=>0 projectile=>172"
           delete_effect=>"resource=spcomend"
           patch_effect_inline=>"match=>opcode=215 resource=>spnwchrm"
        END
        LAF edit_spell STR_VAR spell=ibodhi6 edits=patch_data END

        // Bodhi's insane-only fire shield effect, which also buffs her -
        // based on ordinary fire shield, but permanent and applies a minor Malison effect

        LAF hitpoint_boost_effects INT_VAR remove_only=1 RET sectype END
        COPY "%copy_loc%/fire_shield_blue.spl" "override/ibodhi7.spl"
        MAKE_PATCH
           say_name=>2507
           secondary=>"%sectype%"
           delete_ability=>"ability_min_level>1"
           patch_effect_inline=>"duration=>60000 resist_dispel=>0"
           patch_effect_inline'=>"match=>opcode=232 resource=>ibodhi7d"
           add_effect_inline=>"opcode=>215 target=>1 parameter2=>1 duration=>6000 resource=>spfiresb"
           add_effect_inline'=>"opcode=>215 target=>1 parameter2=>1 duration=>6000 resource=>spfiresa"
           add_effect_inline'2=>~opcode=>102 target=>1 timing=>1 parameter1=>1~ // immune to L1 spells
           add_effect_inline'3=>~opcode=>102 target=>1 timing=>1 parameter1=>2~ // immune to L2 spells
           add_effect_inline'4=>~opcode=>1 target=>1 duration=>60000 parameter1=>4 parameter2=>1~ // 4 attacks

        END
        LAF edit_spell STR_VAR spell=ibodhi7 edits=patch_data END

        MAKE_PATCH
           say_name=>2507
           add_effect_inline=>"power=>4 opcode=>142 target=>2 parameter2=>45 resist_dispel=>3 duration=>36"
           add_effect_inline'=>"power=>4 opcode=>139 target=>2 parameter1=>44916 timing=>1"
           add_effect_inline''=>"number_to_add=>5 power=>4 opcode=>entry_index+33 target=>2 parameter1=>-1 duration=>36 resist_dispel=>3"
        END
        LAF clone_spell STR_VAR spell="%WIZARD_FIRE_SHIELD_BLUE%d=>ibodhi7d" edits=patch_data END

        // Bodhi's undead-control power - fine

        LAF install STR_VAR file="ibodhi8.spl" location=resource END
        LAF edit_spell STR_VAR spell=ibodhi8 editstring="say_name=>2510 spell_type=>innate" END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_mages BEGIN

        // script

        LAF install STR_VAR file=dw#bodmg.baf location=resource END

        MAKE_PATCH
           level=>16
           dialog=>""
           dv=>""
           strip_script=>all
           script_override=>dw#bodmg
           script_class=>mage14a
           class=>mage
           allegiance=>enemy
           enforce_charclass=>null
        END
        LAF clone_creature STR_VAR creature=~c6bguard=>impbod1 bodtan=>impbod2~ edits=patch_data END
        LAF edit_creature STR_VAR creature=impbod1 editstring=~say_both_names=>2508~ END
        LAF edit_creature STR_VAR creature=impbod2 editstring=~say_both_names=>2509~ END

        LAF check_label STR_VAR label=dw#mage RET value END
        ACTION_IF value BEGIN
           LAF edit_creature STR_VAR creature="impbod1 impbod2" editstring="enforce_mage=>accept_level" END
        END ELSE BEGIN
           LAF edit_creature STR_VAR creature="impbod1 impbod2" editstring="script_class=>mage14a" END
        END

END



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

<<<<<<<< .../stratagems-inline/bodhi_vampires.2da
script_new     easiest         easy          core         hard         insane
dw#c6bt1       c6vamp01        vampif01      vampif01     impbod1      impbod1
dw#c6bt2       c6vamp01        vampim01      vampim01     impbod2      impbod2
dw#c6bt3       c6vamp01        c6vamp01      vampif01     vampif01     vamold01
dw#c6bt4       c6vamp01        c6vamp01      vampim01     vampim01     vamold01
>>>>>>>>


DEFINE_ACTION_FUNCTION bodhi_vampires
BEGIN
     MAKE_PATCH
          dw#bods1=>vampim01
          dw#bods2=>vampif01
          dw#bods3=>vamold01
          dw#bods4=>impbod1
          dw#bods5=>impbod2
     END
     OUTER_SPRINT c6vamp01 spin811
     ACTION_PHP_EACH patch_data AS resref=>vamp BEGIN
        LAF clone_spell STR_VAR spell="%VAMPIRE_FORM_CHANGE%=>%resref%" END
        LAF swap_text STR_VAR files="%resref%.spl" swaps="c6vamp01=>%vamp%" END
        OUTER_SPRINT "%vamp%" "%resref%"
     END
     LAF process_table STR_VAR table="bodhi_vampires.2da" function=bodhi_vampires_helper inline=yes END

     MAKE_PATCH
        clone_actor_inline=>"match=>~actor_x_coord_start=1261~ actor_x_coord=>1169 actor_y_coord=>407 script_override=>dw#c6bt1"
        patch_actor_inline'1=>"match=>~actor_x_coord_start=1261~ script_override=>dw#c6bt2"
        patch_actor_inline'2=>"match=>~actor_x_coord_start=1540~ script_override=>dw#c6bt3"
        patch_actor_inline'3=>"match=>~actor_x_coord_start=1255~ script_override=>dw#c6bt4"
     END
     LAF edit_area STR_VAR area=ar0809 edits=patch_data END


END


DEFINE_ACTION_FUNCTION bodhi_vampires_helper
   STR_VAR script_new=""
           easiest=""
           easy=""
           core=""
           hard=""
           insane=""
BEGIN
          OUTER_SPRINT vampire_easiest EVALUATE_BUFFER "%%easiest%%"
          OUTER_SPRINT vampire_easy EVALUATE_BUFFER "%%easy%%"
          OUTER_SPRINT vampire_core EVALUATE_BUFFER "%%core%%"
          OUTER_SPRINT vampire_hard EVALUATE_BUFFER "%%hard%%"
          OUTER_SPRINT vampire_insane EVALUATE_BUFFER "%%insane%%"
          LAF ssl_to_bcs STR_VAR script=bat_template location=ssl rename_to="%script_new%" END
END




