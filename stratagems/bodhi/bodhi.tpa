INCLUDE ~%scsroot%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION bodhi
BEGIN

    LAF define_difficulty STR_VAR type=bodhi RET difficulty_variable END
    LOAD_TRA "%scs_tra_loc%/english/vampire.tra" "%scs_tra_loc%/%LANGUAGE%/vampire.tra"
        // pro/undead scrolls

        LAF run STR_VAR file=undeadscroll locbase=tactical_bg2 tra=tactical_bg2 END

        // creature file

	ACTION_IF !(FILE_EXISTS_IN_GAME ~ibodhi1.spl~) THEN BEGIN // officially we're incompatible with the Tactics version; unofficially, we try
		LAF bodhi_CRE END
	END

        // spells

        LAF bodhi_spells END

        // improved weapons

        LAF install STR_VAR files="bodhi.itm" location=resource END
        LAF edit_creature STR_VAR creature=~bodhi2 ppbodhi4~ editstring=~replace_items=>"vamp1(WEAPON1)"~ END

        // challenge version

        MAKE_PATCH
           replace_items=>~bodhi(WEAPON1)~
           script_class=>dw#chbod
        END
        LAF edit_creature STR_VAR creature=chevil08 edits=patch_data END

        // scripts

        LAF ssl_to_bcs STR_VAR script=~bodhi2 c6bodhi dw#chbod finbodhi~ variables location=ssl END

        // ascension
        
        ACTION_IF FILE_EXISTS_IN_GAME finbodhi.cre BEGIN 
           LAF bodhi_ascension STR_VAR variables END
        END

        // mages
        
        LAF bodhi_mages END
        
        // bodhi's vampire allies
        
        LAF bodhi_vampires END
        
        // bodhi's grimwards
        
        LAF install STR_VAR location=resource files="dw#bodfg.baf" END
        LAF edit_creature STR_VAR creature="bodfgt01 bodfgt02" editstring="insert_script_high=>dw#bodfg" END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_CRE INT_VAR version=0 BEGIN


      MAKE_PATCH
         strip_script=>~wtasight vampir01~
         strip_scs_scripts=>null
         make_casting_innate=>null
         add_effect_inline=>~opcode=>193 parameter2=>1~  // detect invisibility by script
         add_effect_inline'=>~opcode=>31 parameter1=>50 parameter2=>1~ // 50% MDR
         add_effect_inline''=>~opcode=>297~ // immunity to turning
         immunity_to_opcode=>13 // protection from Azuredge et al
         immunity_to_string=>10554
         movement=>12
         ac_natural=>~-4~
         ac_effective=>~-4~
         hitpoints=>183
         resist_slashing=>25
         resist_crushing=>25
         resist_piercing=>25
         resist_missile=>25
      END
      LAF edit_creature STR_VAR creature=~c6bodhi bodhi2 chevil08~ edits=patch_data END
      LAF edit_creature STR_VAR creature=finbodh allow_missing=1 edits=patch_data END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_spells 
INT_VAR version=1
BEGIN

        // labelling
        
        MAKE_PATCH
           1=>2400
           2=>2502
           3=>2503
           4=>2504
           5=>2505
           6=>2506
           7=>2507
           7d=>2507
           8=>2510
        END
        ACTION_PHP_EACH patch_data AS code=>string BEGIN
           LAF install STR_VAR file=~ibodhi%code%.spl~ location=resource END
           LAF edit_spell STR_VAR spell=~ibodhi%code%~ editstring=~say_name=>%string%~ END
        END
        
        // tweaks relative to Tactics

        // Cloud of Bats

        LAF edit_spell STR_VAR spell=ibodhi1 editstring=~patch_effect_inline=>"duration=>18"~ END

        // Abyssal Darkness, Buried Alive, Sepulchral sleep

        LAF edit_spell STR_VAR spell=~ibodhi2 ibodhi3 ibodhi6~ editstring=~patch_effect_inline=>"savebonus=>-4"~ END  // reduced from -10
        
        // bodhi's insane-only fire shield, which also buffs her
        
         LAF hitpoint_boost_effects INT_VAR remove_only=1 RET sectype END
         MAKE_PATCH
            secondary=>%sectype%
            add_effect_inline=>"opcode=>215 target=>1 parameter2=>1 duration=>6000 resource=>spfiresb"
            add_effect_inline'=>"opcode=>215 target=>1 parameter2=>1 duration=>6000 resource=>spfiresa" 
            add_effect_inline'2=>~opcode=>102 target=>1 timing=>1 parameter1=>1~ // immune to L1 spells
            add_effect_inline'3=>~opcode=>102 target=>1 timing=>1 parameter1=>2~ // immune to L2 spells
            add_effect_inline'4=>~opcode=>1 target=>1 duration=>60000 parameter1=>4 parameter2=>1~ // 4 attacks
         END

         LAF edit_spell STR_VAR spell=ibodhi7 edits=patch_data END


END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_mages BEGIN

        // script

        LAF install STR_VAR file=dw#bodmg.baf location=resource END

        MAKE_PATCH
           level=>16
           dialog=>""
           dv=>""
           strip_script=>all
           script_override=>dw#bodmg
           script_class=>mage14a
           class=>mage
           allegiance=>enemy
           enforce_charclass=>null
        END
        LAF clone_creature STR_VAR creature=~c6bguard=>impbod1 bodtan=>impbod2~ edits=patch_data END
        LAF edit_creature STR_VAR creature=impbod1 editstring=~say_both_names=>2508~ END
        LAF edit_creature STR_VAR creature=impbod2 editstring=~say_both_names=>2509~ END

        LAF check_label STR_VAR label=dw#mage RET value END
        ACTION_IF value BEGIN
           LAF edit_creature STR_VAR creature="impbod1 impbod2" editstring="enforce_mage=>accept_level" END
        END ELSE BEGIN
           LAF edit_creature STR_VAR creature="impbod1 impbod2" editstring="script_class=>mage14a" END
        END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_ascension
STR_VAR variables=""
BEGIN

               MAKE_PATCH
                  strip_script=>all
                  script_override=>finbodhi
                  dv=>finbodh
                  dialog=>finbodh
                  remove_items=>minhp1
                  xp_value=>26000
               END
               LAF clone_creature STR_VAR creature=~c6bodhi=>finbodh~ edits=patch_data END

               LAF ssl_to_bcs STR_VAR script=finbodhi variables location=~ssl~ END


END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

<<<<<<<< .../stratagems-inline/bodhi_vampires.2da
script_new     easiest         easy          core         hard         insane
dw#c6bt1       c6vamp01        vampif01      vampif01     impbod1      impbod1
dw#c6bt2       c6vamp01        vampim01      vampim01     impbod2      impbod2
dw#c6bt3       c6vamp01        c6vamp01      vampif01     vampif01     vamold01
dw#c6bt4       c6vamp01        c6vamp01      vampim01     vampim01     vamold01
>>>>>>>>


DEFINE_ACTION_FUNCTION bodhi_vampires
BEGIN
     MAKE_PATCH
          dw#bods1=>vampim01
          dw#bods2=>vampif01
          dw#bods3=>vamold01
          dw#bods4=>impbod1
          dw#bods5=>impbod2
     END
     OUTER_SPRINT c6vamp01 spin811
     ACTION_PHP_EACH patch_data AS resref=>vamp BEGIN
        LAF clone_spell STR_VAR spell="%VAMPIRE_FORM_CHANGE%=>%resref%" END
        LAF swap_text STR_VAR files="%resref%.spl" swaps="c6vamp01=>%vamp%" END
        OUTER_SPRINT "%vamp%" "%resref%"
     END
     LAF process_table STR_VAR table="bodhi_vampires.2da" function=bodhi_vampires_helper inline=yes END

     MAKE_PATCH
        clone_actor_inline=>"match=>~actor_x_coord_start=1261~ actor_x_coord=>1169 actor_y_coord=>407 script_override=>dw#c6bt1"
        patch_actor_inline'1=>"match=>~actor_x_coord_start=1261~ script_override=>dw#c6bt2"
        patch_actor_inline'2=>"match=>~actor_x_coord_start=1540~ script_override=>dw#c6bt3"
        patch_actor_inline'3=>"match=>~actor_x_coord_start=1255~ script_override=>dw#c6bt4"
     END
     LAF edit_area STR_VAR area=ar0809 edits=patch_data END


END


DEFINE_ACTION_FUNCTION bodhi_vampires_helper
   STR_VAR script_new=""
           easiest=""
           easy=""
           core=""
           hard=""
           insane=""
BEGIN
          OUTER_SPRINT vampire_easiest EVALUATE_BUFFER "%%easiest%%"
          OUTER_SPRINT vampire_easy EVALUATE_BUFFER "%%easy%%"
          OUTER_SPRINT vampire_core EVALUATE_BUFFER "%%core%%"
          OUTER_SPRINT vampire_hard EVALUATE_BUFFER "%%hard%%"
          OUTER_SPRINT vampire_insane EVALUATE_BUFFER "%%insane%%"
          LAF ssl_to_bcs STR_VAR script=bat_template location=ssl rename_to="%script_new%" END
END




