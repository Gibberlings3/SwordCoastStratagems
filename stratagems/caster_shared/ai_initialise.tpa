LAF include STR_VAR file=caster_shared.tph END
DEFINE_ACTION_FUNCTION ai_initialise BEGIN

    // make sure this is in the ids

    LAF return_detectable_string STR_VAR type=CONTINGENCY_TRIGGERED RET detection_string=string END

    // install SCS-AI bits of DS

    LAF run STR_VAR file=ds_scs locbase=ds END
    ACTION_IF (iwd_divine || iwd_arcane) BEGIN
       LAF run STR_VAR file=ds_iwd locbase=ds END
    END

    LAF detectable_spells INT_VAR fix_khelben=0 fix_old_ds=0 STR_VAR table = "%scsroot%/ds/ds_scs_ai.2da" END  // additional SCS entries

    // bespoke detection of timestop
    
    ACTION_IF is_bg2 BEGIN
       LAF run STR_VAR function=detect_timestop locbase=initial END
    END

    /// make animations disappear under invisibility (EE only)

    ACTION_IF enhanced_edition BEGIN
      LAF include STR_VAR file=invisibility_animations.tpa locbase=spell END
      LAF invisibility_animations END
    END

    /// build prebuff spells

    LAF build_prebuff_spells END

    /// make curse spell
      ACTION_IF is_bg2 BEGIN
         LAF cursed_wound END
      END
      
    // build spells to label casters

    LAF build_caster_label END  
    ACTION_IF demivrgvs BEGIN
       LAF build_detect_invisibility_label END
    END

    // build marker spells

    // the ITEM_xx_SEEN are used to mark creatures as having already been targetted by the effect

    ACTION_FOR_EACH splstate IN ITEM_MM_SEEN ITEM_PSIONIC_SEEN ITEM_ANTIWEB_SEEN BEGIN
       LAF build_marker_spell INT_VAR duration=144 STR_VAR splstate marker_name=EVAL "%%splstate%%" END
    END
    
    // build the difficulty widget

    LAF run STR_VAR file=difficulty_controls locbase=lib tra=difficulty END

    // on IWD, rewire the SPECIFICS field
    
    ACTION_IF is_iwd BEGIN
      LAF run STR_VAR file=iwd_specifics locbase=lib END
    END

END