DEFINE_ACTION_FUNCTION clericmage BEGIN

   LAF make_label STR_VAR label=dw#clericmage END

   // include resources

   OUTER_SPRINT component_loc "caster_shared/clericmage"
   LAF include STR_VAR file="clericmage_shared.tph" END

   ACTION_IF is_bg1 BEGIN
       LAF clericmage_bg1 END
   END

   ACTION_IF is_bg2 BEGIN
       LAF clericmage_bg2 END
   END

   ACTION_IF is_iwd BEGIN
       LAF clericmage_iwd END
   END


END

DEFINE_ACTION_FUNCTION clericmage_bg1 BEGIN

    // currently none

END

DEFINE_ACTION_FUNCTION clericmage_bg2 BEGIN

   // pre-install edits
   
   LAF edit_creature STR_VAR creature=sendai editstring="level1=>22 level2=>22" END

   // compile scripts

   LAF edit_creature STR_VAR creature=sendai8 editstring="strip_script=>cmage20a enforce_clericmage=>accept_level" END
   LAF edit_creature STR_VAR creature="gaal01 gaal02" editstring="enforce_clericmage=>null" END
   LAF edit_creature STR_VAR creature=shupol01 editstring="strip_script=>cmage20a enforce_clericmage=>null" END


   OUTER_SET sendai_xloc_1=647
   OUTER_SET sendai_yloc_1=361
   OUTER_SET sendai_xloc_2=1010
   OUTER_SET sendai_yloc_2=616
   OUTER_SET sendai_xloc_3=642
   OUTER_SET sendai_yloc_3=869
   LAF edit_creature STR_VAR creature=sendai editstring="strip_script=>senbattl enforce_clericmage=>accept_level" END
   LAF edit_creature STR_VAR creature=vampaer editstring="class=>cleric_mage level1=>13 level2=>13 enforce_clericmage=>accept_level" END
   LAF DELETE_SCRIPT_BLOCK STR_VAR script=vampaer match=DOMINATION END

   // do HLAs

   LAF edit_creature STR_VAR creature=sendai editstring="add_items=>~dw#hlamg dw#hlapr~" END

   // extra edits
   
   LAF install STR_VAR file="gaal.baf" location=script END



END

DEFINE_ACTION_FUNCTION clericmage_iwd BEGIN

   // spells for presio duel
   
   // Spell 1: gives party the DO_NOT_TARGET_SPELLS spellstate for 1 hour

   LAF ds_resolve_stat STR_VAR id=DO_NOT_TARGET_SPELLS RET ds_value=stat_ind END
   MAKE_PATCH
      add_basic_ability=>null
      ability_target=>2
      add_effect_inline=>"opcode=>328 target=>2 timing=>0 parameter2=>%ds_value% duration=>1000 mode=>1"
   END
   LAF make_spell STR_VAR spell=dw#pres1 edits=patch_data END
   
   // Spell 2: gives immunity to and removes Spell 1
   
   MAKE_PATCH
      add_basic_ability=>null
      ability_target=>2
      add_effect_inline=>"opcode=>318 target=>2 timing=>0 duration=>10 resource=>dw#pres1"
      add_effect_inline'=>"opcode=>321 target=>2 timing=>1 resource=>dw#pres1"
   END
   LAF make_spell STR_VAR spell=dw#pres2 edits=patch_data END
   
   // apply Presio spells in area script
   
   <<<<<<<< .../stratagems-inline/ar4003add.baf
   IF
     !Global("dw_presio_duel","MYAREA",1)
     GlobalGT("CDPresioDuel","GLOBAL",0)
     GlobalLT("CDPresioDuel","GLOBAL",3)
   THEN
      RESPONSE #100
            SetGlobal("dw_presio_duel","MYAREA",1)
            ApplySpellRES("dw#pres1",Player1)
            ApplySpellRES("dw#pres1",Player2)
            ApplySpellRES("dw#pres1",Player3)
            ApplySpellRES("dw#pres1",Player4)
            ApplySpellRES("dw#pres1",Player5)
            ApplySpellRES("dw#pres1",Player6)
            Continue()
   END

   IF
     Global("dw_presio_duel","MYAREA",1)
     OR(2)
          Global("CDPresioDuel","GLOBAL",7)
          Dead("presio")
   THEN
       RESPONSE #100
            SetGlobal("dw_presio_duel","MYAREA",0)
            ApplySpellRES("dw#pres2",Player1)
            ApplySpellRES("dw#pres2",Player2)
            ApplySpellRES("dw#pres2",Player3)
            ApplySpellRES("dw#pres2",Player4)
            ApplySpellRES("dw#pres2",Player5)
            ApplySpellRES("dw#pres2",Player6)
            Continue()
   END
   >>>>>>>>
   
   LAF extend_area_script STR_VAR area=ar4003 top=ar4003add inline=yes END

   // apply Presio spells in dialog

   <<<<<<<< .../stratagems-inline/presioadd.d
   ADD_TRANS_ACTION dundlt2 BEGIN 1 2 END BEGIN END ~ApplySpellRES("dw#pres2",LastTalkedToBy)~
   >>>>>>>>
   COMPILE ".../stratagems-inline/presioadd.d"

    // presio herself
    
   COPY_EXISTING "presio.cre" override
		SPRINT filename "%SOURCE_RES%"
	   LPF enforce_clericmage STR_VAR arguments=accept_level script=d3pres END
   BUT_ONLY
   COPY_EXISTING "presio.cre" "override/cdpresio.cre" 
       WRITE_ASCII 0x248 "cdpresio" (8)
       WRITE_ASCII 0x250 "d3pres" (8)

   LAF ssl_to_bcs STR_VAR script=cdpresio location=ssl END
   
   //// Keep rakshasas

   COPY_EXISTING ar9711.bcs override
           LPF unique_iwd_script_summons INT_VAR counter_start=1 STR_VAR original=rakshinv new_prefix=rakshin  END
   BUT_ONLY
   COPY_EXISTING 
    rakshin1.cre override
    rakshin2.cre override
     WRITE_ASCII 0x260 effolpty (8)
     WRITE_ASCII 0x268 "%SOURCE_RES%" (8)
		SPRINT filename "%SOURCE_RES%"
     LPF enforce_clericmage STR_VAR arguments=accept_level script="%SOURCE_RES%" END
   BUT_ONLY
   COPY_EXISTING "rakshasa.cre" override
     WRITE_ASCII 0x258 effolpty (8)
     WRITE_ASCII 0x268 keraksha (8)
		SPRINT filename "%SOURCE_RES%"
     LPF enforce_clericmage STR_VAR arguments=accept_level script=keraksha END

   // Ilmadia's drow mage/priests
   
   MAKE_PATCH
      match=>"actor_x_coord_start=1942 and actor_y_coord_start=2172"
      actor_resource=>dw#drpm2
      script_class=>dw#lddr2
   END
   LAF edit_area STR_VAR area=ar8011 editstring="patch_actor=>patch_data" END
   

   COPY_EXISTING drowpm.cre "override/dw#drpm2.cre"
		SPRINT filename "%SOURCE_RES%"
       LPF enforce_clericmage STR_VAR arguments=accept_level script=dw#lddr2 END
   BUT_ONLY
   COPY_EXISTING drowpm.cre override
		SPRINT filename "%SOURCE_RES%"
       LPF enforce_clericmage STR_VAR arguments=accept_level script=lddrowcm END
   BUT_ONLY

   


END