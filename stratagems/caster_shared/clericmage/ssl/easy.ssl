/// attack defenses

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest|Enemy)
   StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
THEN DO
     Action(SpellMyself,WIZARD_TRUE_SIGHT|100|50)
     Action(SpellMyself,WIZARD_ORACLE|100|50)
END

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   !GlobalTimerNotExpired("easy_malison","LOCALS")
   TriggerBlock(Easiest|Enemy)
THEN DO
     Action(Spell,WIZARD_GREATER_MALISON)
     SetGlobalTimer("easy_malison","LOCALS",120)
END

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest|Enemy)
   OR(3)
      CheckSpellState(scstarget,BUFF_PRO_DAMAGE)
      CheckSpellState(scstarget,BUFF_PRO_SPELLS)
      CheckSpellState(scstarget,BUFF_ENHANCEMENT)
   !GlobalTimerNotExpired("easy_dispel","LOCALS")
THEN DO
     Combine()
     SetGlobalTimer("easy_dispel","LOCALS",20)
     SetGlobalTimer("east_antimagic","LOCALS",10)
     Action(Spell,WIZARD_DISPEL_MAGIC|100|50)
END

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest|Enemy)
   CheckSpellState(scstarget,BUFF_PRO_SPELLS)
   !GlobalTimerNotExpired("easy_antimagic","LOCALS")
THEN DO
     Combine()
     SetGlobalTimer("easy_antimagic","LOCALS",16)
     Action(Spell,WIZARD_SPELL_STRIKE|100|50)
     Action(Spell,WIZARD_PIERCE_SHIELD|100|50)
     Action(Spell,WIZARD_WARDING_WHIP|100|50)
     Action(Spell,WIZARD_RUBY_RAY_OF_REVERSAL|100|50)
     Action(Spell,WIZARD_PIERCE_MAGIC|100|50)
     Action(Spell,WIZARD_SECRET_WORD|100|50)
     Action(Spell,WIZARD_SPELL_THRUST|100|50)
END

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest|Enemy|SpellTurn)
   OR(2)
      CheckSpellState(scstarget,BUFF_PRO_DAMAGE)
      CheckSpellState(scstarget,BUFF_ENHANCEMENT)
   !GlobalTimerNotExpired("easy_breach","LOCALS")
THEN DO
   SetGlobalTimer("easy_breach","LOCALS",16)
   Action(Spell,WIZARD_BREACH|100|100)
END

// kill summons

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
	OR(2)
		Gender(scstarget,SUMMONED)
		Gender(scstarget,20)
   TriggerBlock(Enemy|Easiest)
THEN DO
     Action(Spell,WIZARD_DEATH_SPELL|100|100)
END

// basic buffs

IF TRIGGER
   Range(NearestEnemyOf(Myself),10)
   !GlobalTimerNotExpired("easybuff","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     SetGlobalTimer("easybuff","LOCALS",12)
     Action(SpellMyself,CLERIC_SHIELD_OF_THE_ARCHONS|100|100)
     Action(SpellMyself,CLERIC_REGENERATE|100|100)
     Action(SpellMyself,CLERIC_BLADE_BARRIER|100|100)
     Action(SpellMyself,CLERIC_RIGHTEOUS_MAGIC|100|100)
     Action(SpellMyself,CLERIC_HOLY_POWER|100|100)
     Action(SpellMyself,CLERIC_DRAW_UPON_HOLY_MIGHT|100|100)
END

// close range spells, L7-9

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   Range(scstarget,6)
   TriggerBlock(Easiest)
THEN DO
     Action(SpellL9Myself,WIZARD_WAIL_OF_THE_BANSHEE)
     Action(Spell,WIZARD_MAZE)
     Action(Spell,WIZARD_PRISMATIC_SPRAY)
     Action(SpellL7Myself,CLERIC_NATURE_BEAUTY)
     Action(SpellL7Myself,CLERIC_HOLY_WORD)
     Action(SpellL7Myself,CLERIC_UNHOLY_WORD)
     Action(Spell,CLERIC_DOLOROUS_DECAY)
     Action(SpellMyself,CLERIC_HARM)
END

/// area effect, L7-9

IF TRIGGER
   TargetBlock(DistantEnemies)
   TriggerBlock(Enemy|Easiest)
   !Range(scstarget,20)
THEN DO
     Action(SpellL9,WIZARD_METEOR_SWARM)
     Action(Spell,WIZARD_INCENDIARY_CLOUD)
END

IF TRIGGER
   IgnoreBlock(IsRakshasa)
   TargetBlock(DistantEnemies)
   TriggerBlock(Enemy|Easiest)
   !Range(scstarget,20)
THEN DO
     Action(Spell,WIZARD_DELAYED_BLAST_FIREBALL)
     Action(Spell,WIZARD_DEATH_FOG)
END

IF TRIGGER
   RequireBlock(IsRakshasa)
   TargetBlock(DistantEnemies)
   TriggerBlock(Enemy|Easiest)
THEN DO
     Action(Spell,WIZARD_DELAYED_BLAST_FIREBALL)
     Action(Spell,WIZARD_DEATH_FOG)
END

/// summoning spells, L7-9

IF TRIGGER
   See(NearestEnemyOf(Myself))
   !GlobalTimerNotExpired("easy_summon","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     Action(SpellMyself,WIZARD_MORDENKAINENS_SWORD|100|50)
     Action(SpellMyself,WIZARD_SUMMON_EFREET|100|50)
     Action(SpellMyself,WIZARD_SUMMON_DJINNI|100|50)
     Action(SpellMyself,WIZARD_SUMMON_HAKEASHAR|100|50) 
          Action(SpellMyself,CLERIC_CONJURE_EARTH_ELEMENTAL|100|50)
       Action(SpellMyself,CLERIC_CONJURE_FIRE_ELEMENTAL|100|50)
       Action(SpellMyself,CLERIC_CONJURE_ANIMALS|100|50)
       Action(SpellMyself,CLERIC_AERIAL_SERVANT|100|50)
     SetGlobalTimer("easy_summon","LOCALS",12)
END

/// Single-target or party-friendly spells, L7-9

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   TriggerBlock(Easiest|Enemy)
THEN DO
     Combine()
     Action(SpellL9,WIZARD_POWER_WORD_KILL)
     Action(SpellL9,WIZARD_BIGBYS_CRUSHING_HAND)
     Action(Spell,WIZARD_ABI_DALZIMS_HORRID_WILTING)
      Action(SpellL7,CLERIC_CREEPING_DOOM)
     Action(Spell,WIZARD_NPC_SYMBOL_FEAR)
     Action(Spell,WIZARD_NPC_SYMBOL_STUN)
     Action(Spell,WIZARD_NPC_SYMBOL_DEATH)
     Action(Spell,WIZARD_POWER_WORD_BLIND)
     Action(Spell,WIZARD_BIGBYS_CLENCHED_FIST)
     Action(Spell,WIZARD_SPHERE_OF_CHAOS)
     Action(Spell,WIZARD_POWER_WORD_STUN)
     Action(SpellL7,WIZARD_NPC_SYMBOL_STUN)
     Action(SpellL7,WIZARD_NPC_SYMBOL_DEATH)
     Action(SpellL7,CLERIC_CONFUSION)
     Action(SpellL7,CLERIC_FINGER_OF_DEATH)
END

// close range spells, L3-6

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   Range(scstarget,6)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,WIZARD_CONE_OF_COLD) 
     Action(SpellMyself,CLERIC_SLAY_LIVING)
     Action(Spell,CLERIC_POISON)
     Action(SpellMyself,CLERIC_CLOAK_OF_FEAR)
     Action(Spell,WIZARD_VAMPIRIC_TOUCH)
END


// area effect L4-5

IF TRIGGER
   IgnoreBlock(IsLich|IsRakshasa|IsVampire)
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest|Enemy)
   !Range(scstarget,20)
THEN DO
     Combine()
     Action(Spell,WIZARD_CLOUDKILL)
     Action(Spell,WIZARD_ICE_STORM)
END

IF TRIGGER
   RequireBlock(IsLich|IsRakshasa|IsVampire)
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest|Enemy)
THEN DO
     Combine()
     Action(Spell,WIZARD_CLOUDKILL)
     Action(Spell,WIZARD_ICE_STORM)
END

// summoning spells, L4-6

IF TRIGGER
   See(NearestEnemyOf(Myself))
   !GlobalTimerNotExpired("easy_summon","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     Action(SpellMyself,WIZARD_INVISIBLE_STALKER|100|50)
     Action(SpellMyself,WIZARD_CONJURE_FIRE_ELEMENTAL|100|50)
     Action(SpellMyself,WIZARD_CONJURE_AIR_ELEMENTAL|100|50)
     Action(SpellMyself,WIZARD_CONJURE_EARTH_ELEMENTAL|100|50)
     Action(SpellMyself,WIZARD_SUMMON_NISHRUU|100|50)
     Action(SpellMyself,WIZARD_ANIMATE_DEAD|100|50)
     Action(SpellMyself,WIZARD_CONJURE_LESSER_FIRE_ELEMENTAL|100|50)
     Action(SpellMyself,WIZARD_CONJURE_LESSER_AIR_ELEMENTAL|100|50)
     Action(SpellMyself,WIZARD_CONJURE_LESSER_EARTH_ELEMENTAL|100|50)
     Action(SpellMyself,WIZARD_MONSTER_SUMMONING_3|100|50)
     Action(SpellMyself,WIZARD_SPIDER_SPAWN|100|50)
     Action(SpellMyself,WIZARD_MONSTER_SUMMONING_2|100|50)
     Action(SpellMyself,CLERIC_ANIMATE_DEAD|100|50)
       Action(SpellMyself,CLERIC_CALL_WOODLAND_BEINGS|100|50)
       Action(SpellMyself,CLERIC_ANIMAL_SUMMONING_2|100|50)
       Action(SpellMyself,CLERIC_ANIMAL_SUMMONING_1|100|50)
     SetGlobalTimer("easy_summon","LOCALS",12)
END

// Single-target or party-friendly spells, L4-6


IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest|Enemy)
   OR(2)
	        GlobalGT("DMWWPetrifyPlayer","GLOBAL",0)
		!InPartySlot(scstarget,0) // isn't Player 1

THEN DO
     Action(Spell,WIZARD_FLESH_TO_STONE)
END

IF TRIGGER
   RequireBlock(Demivrgvs)
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest|Enemy) 	
   OR(2)
	        GlobalGT("DMWWDisintegratePlayer","GLOBAL",0)
		!InPartySlot(scstarget,0) // isn't Player 1

THEN DO
     Action(Spell,WIZARD_DISINTEGRATE)
END

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest|Enemy)
THEN DO
     Combine()
     Action(Spell,WIZARD_POWER_WORD_SILENCE)
     Action(Spell,WIZARD_CHAIN_LIGHTNING) 
     Action(Spell,CLERIC_INSECT_PLAGUE)
     Action(Spell,CLERIC_FLAME_STRIKE)
     Action(Spell,CLERIC_MENTAL_DOMINATION)
     Action(Spell,WIZARD_DOMINATION)
     Action(Spell,WIZARD_HOLD_MONSTER)
     Action(Spell,WIZARD_CHAOS)
     Action(Spell,WIZARD_SUN_FIRE)
     Action(Spell,WIZARD_TELEPORT_FIELD)
     Action(Spell,WIZARD_CONFUSION)
     Action(Spell,WIZARD_EMOTION_HOPELESSNESS)
END

     // area effect L1-3

IF TRIGGER
   IgnoreBlock(IsLich|IsRakshasa)
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest|Enemy)
   OR(2)
        !Range(scstarget,20)
        CheckStatGT(Myself,0,MINORGLOBE)
THEN DO
     Combine()
     Action(Spell,WIZARD_FIREBALL)
     Action(Spell,WIZARD_LIGHTNING_BOLT) 
     Action(Spell,CLERIC_HOLY_SMITE)
     Action(Spell,CLERIC_UNHOLY_BLIGHT)
     Action(Spell,CLERIC_RIGID_THINKING)
     Action(Spell,CLERIC_HOLD_PERSON)
     Action(Spell,CLERIC_SUMMON_INSECTS)
     Action(Spell,CLERIC_COMMAND)
     Action(Spell,CLERIC_DOOM)
     Action(Spell,WIZARD_SKULL_TRAP)
     Action(Spell,WIZARD_STINKING_CLOUD)
     Action(Spell,WIZARD_WEB)
END

IF TRIGGER
   RequireBlock(IsLich|IsRakshasa)
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest|Enemy)
THEN DO
     Combine()
     Action(Spell,WIZARD_FIREBALL)
     Action(Spell,WIZARD_LIGHTNING_BOLT)
     Action(Spell,WIZARD_SKULL_TRAP)
     Action(Spell,WIZARD_ICE_STORM)
     Action(Spell,WIZARD_STINKING_CLOUD)
     Action(Spell,WIZARD_WEB)
END

// summoning spells, L3

IF TRIGGER
   See(NearestEnemyOf(Myself))
   !GlobalTimerNotExpired("easy_summon","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     Action(SpellMyself,WIZARD_MONSTER_SUMMONING_1|100|50)
     SetGlobalTimer("easy_summon","LOCALS",12)
END


// close range spells, L1-3

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   Range(scstarget,6)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,WIZARD_BURNING_HANDS)
     Action(Spell,WIZARD_COLOR_SPRAY)
END
 // Single-target or party-friendly spells, L1-3

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest|Enemy)
THEN DO
     Combine()
     Action(Spell,WIZARD_FLAME_ARROW)
     Action(Spell,WIZARD_HOLD_PERSON)
     Action(Spell,WIZARD_SLOW)
     Action(Spell,WIZARD_DIRE_CHARM)
     Action(Spell,WIZARD_HORROR)
     Action(Spell,WIZARD_MELF_ACID_ARROW)
     Action(Spell,WIZARD_GLITTERDUST)
     Action(Spell,WIZARD_RAY_OF_ENFEEBLEMENT)
     Action(Spell,WIZARD_POWER_WORD_SLEEP)
     Action(Spell,WIZARD_CHARM_PERSON)
     Action(Spell,WIZARD_BLINDNESS)
     Action(Spell,WIZARD_MAGIC_MISSILE)
     Action(Spell,WIZARD_CHROMATIC_ORB)
     Action(Spell,WIZARD_SLEEP)
     Action(Spell,WIZARD_SPOOK)
END

// basic buffs

IF TRIGGER
   Range(NearestEnemyOf(Myself),10)
   !GlobalTimerNotExpired("easybuff","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     SetGlobalTimer("easybuff","LOCALS",12)
     Action(SpellMyself,CLERIC_SPIRITUAL_HAMMER|100|100)
     Action(SpellMyself,CLERIC_FLAME_BLADE|100|100)
END



// melee

IF TRIGGER
   !GlobalTimerNotExpired("castspell","LOCALS")
   Target(NearestEnemyOf(Myself))
   TriggerBlock(Easiest)
THEN DO
     Action(Literal)
     Attack(scstarget)
END

// closedown

IF TRIGGER
   TriggerBlock(Easiest)
THEN DO
     Action(Literal)
END
