/// shared code for mage and priest missiles

DEFINE_ACTION_FUNCTION handle_missiles BEGIN

	ACTION_IF !FILE_EXISTS_IN_GAME "dw#missl.itm" BEGIN // idempotency

		
		ACTION_DEFINE_ASSOCIATIVE_ARRAY missile_array BEGIN
			melfmet=>"dw#melf"
			eneblade=>"dw#eneb"
			"%itm_starlight_blade%"=>"dw#starb"
			cdshar2=>"dw#sharb"		
		END
		
		ACTION_PHP_EACH missile_array AS old=>new BEGIN
			ACTION_IF FILE_EXISTS_IN_GAME "%old%.itm" BEGIN
				// make new missile
				COPY_EXISTING "%old%.itm" "override/%new%.itm"
				// make new spell
				COPY "%MOD_FOLDER%/mage/resource/dw#melf.spl" "override/%new%.spl"
					WRITE_ASCII 0xae "%new%" (8)
			END		
		END
		
		// make marker
		
		COPY_EXISTING "dw#marker_file.itm" "override/dw#missl.itm" 
			WRITE_BYTE 0x18 0 // undroppable

		// get dispel to remove dw#missl and missiles
		
		ACTION_FOR_EACH spell IN "%WIZARD_REMOVE_MAGIC%" "%WIZARD_TRUE_DISPEL_MAGIC%" "%CLERIC_DISPEL_MAGIC%" "%INQUIS_DISPEL%" "%INNATE_WIZARD_REMOVE_MAGIC%" BEGIN
			COPY_EXISTING "%spell%.spl" override
				LPF ADD_SPELL_EFFECT INT_VAR opcode=123 target=2 STR_VAR resource="dw#missl" END	
				PHP_EACH missile_array AS old=>new BEGIN
					PATCH_IF FILE_EXISTS_IN_GAME "%old%.itm" BEGIN
						LPF ADD_SPELL_EFFECT INT_VAR opcode=123 target=2 STR_VAR resource="%new%" END					
					END
				END
			BUT_ONLY
			IF_EXISTS
		END







	END






END
