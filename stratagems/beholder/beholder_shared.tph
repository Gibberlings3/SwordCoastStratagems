DEFINE_PATCH_FUNCTION beholder_immunities BEGIN

   // beholders can fly, so they're immune to web, entangle, grease;
   // they're immune to most of their own eyestalks; they have free
   // action and are unaffected by slow, stun, hold; they have cleansed
   // aura (we impose 1 spell/rd by script); they can't be backstabbed; they don't need WTASIGHT
   
   PATCH_MAKE_PATCH
      remove_items=>beholder
      delete_opcodes=>292
      immunity_to_opcode=>~40 45 109 175 126 154 157 158~
      add_effect_inline=>~opcode=>163~
      add_effect_inline'=>~opcode=>188 parameter2=>1~
      add_effect_inline''=>~opcode=>292 parameter2=>1~
      immunity_to_spell=>~%BEHOLDER_CHARM_PERSON% %BEHOLDER_FEAR% %BEHOLDER_SLOW% %BEHOLDER_FLESH_TO_STONE% %BEHOLDER_DOMINATION%
                      %BEHOLDER_PARALYZATION% %BEHOLDER_DEATH_RAY% %BEHOLDER_FINGER_OF_DEATH%~
      strip_scs_scripts=>null

   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END

DEFINE_PATCH_FUNCTION make_beholder BEGIN

   LPF beholder_immunities END
   PATCH_MAKE_PATCH
         add_spells=>"BEHOLDER_CHARM_PERSON BEHOLDER_FEAR BEHOLDER_DISINTEGRATE BEHOLDER_SLOW BEHOLDER_FLESH_TO_STONE BEHOLDER_DOMINATION BEHOLDER_CAUSE_SERIOUS_WOUNDS BEHOLDER_DEATH_RAY BEHOLDER_PARALYZATION BEHOLDER_TELEKINESIS BEHOLDER_ANTIMAGIC_RAY"
         strip_scs_scripts=>null
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END




END

DEFINE_PATCH_FUNCTION make_gauth BEGIN

   LPF beholder_immunities END
   PATCH_MAKE_PATCH
         add_spells=>"BEHOLDER_CHARM_PERSON BEHOLDER_SLOW BEHOLDER_CAUSE_SERIOUS_WOUNDS BEHOLDER_PARALYZATION BEHOLDER_LIGHTNING_BOLT"
         strip_scs_scripts=>null
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END


END

DEFINE_PATCH_FUNCTION make_director BEGIN

   LPF color INT_VAR red=2 RET color END
   LPF beholder_immunities END
   PATCH_MAKE_PATCH
      add_spells=>"BEHOLDER_MAGIC_MISSILE BEHOLDER_AGANNAZAR_SCORCHER BEHOLDER_CONE_OF_COLD BEHOLDER_ENERVATION BEHOLDER_SLOW BEHOLDER_PARALYZATION"
      strip_scs_scripts=>null
      add_effect_inline=>"opcode=>52 parameter1=>%color% parameter2=>2"
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END

DEFINE_PATCH_FUNCTION make_hive_mother BEGIN
   LPF color INT_VAR blue=5 RET color END
   PATCH_MAKE_PATCH
         beholder_immunities=>null
         class=>MAGE
         remove_spells=>all
         add_spells=>"MAGIC_MISSILE(5) MAZE(3) CHAIN_LIGHTNING(3) PRISMATIC_SPRAY(3) DISPEL_MAGIC HIVE_MOTHER_ANTIMAGIC_RAY HIVE_MOTHER_DEATH_RAY HIVE_MOTHER_DISINTEGRATE
         HIVE_MOTHER_CAUSE_SERIOUS_WOUNDS BEHOLDER_PARALYZATION BEHOLDER_CHARM_PERSON BEHOLDER_DOMINATION BEHOLDER_SLOW BEHOLDER_TELEKINESIS BEHOLDER_FEAR BEHOLDER_FLESH_TO_STONE"
         add_effect_inline=>"opcode=>52 parameter1=>%color% parameter2=>2"
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END

DEFINE_PATCH_FUNCTION make_elder_orb BEGIN
   LPF color INT_VAR red=3 green=3 blue=3 RET color END
   PATCH_MAKE_PATCH
         beholder_immunities=>null
         level=>20
         remove_spells=>all
         add_spells=>"BEHOLDER_CHARM_PERSON BEHOLDER_FEAR BEHOLDER_DISINTEGRATE BEHOLDER_SLOW BEHOLDER_FLESH_TO_STONE BEHOLDER_DOMINATION BEHOLDER_CAUSE_SERIOUS_WOUNDS BEHOLDER_DEATH_RAY BEHOLDER_PARALYZATION BEHOLDER_TELEKINESIS BEHOLDER_ANTIMAGIC_RAY"
         add_effect_inline=>"opcode=>52 parameter1=>%color% parameter2=>2"
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END

DEFINE_PATCH_FUNCTION make_undead_beholder BEGIN

   LPF color INT_VAR green=4 blue=3 RET color END
   PATCH_MAKE_PATCH
         beholder_immunities=>null
         add_items=>ring95 // undead immunities
         add_spells=>"BEHOLDER_FEAR BEHOLDER_DISINTEGRATE BEHOLDER_SLOW BEHOLDER_FLESH_TO_STONE BEHOLDER_CAUSE_SERIOUS_WOUNDS BEHOLDER_DEATH_RAY BEHOLDER_PARALYZATION BEHOLDER_TELEKINESIS BEHOLDER_ANTIMAGIC_RAY"
         strip_scs_scripts=>null
         add_effect_inline=>"opcode=>52 parameter1=>%color% parameter2=>2"
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END
