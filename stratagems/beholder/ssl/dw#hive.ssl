///////////////////////////////////////////////////////////////
/// HIVE MOTHER
//////////////////////////////////////////////////////////////

// The vanilla Hive Mother has the following eyestalk powers:
//
// 1. Death ray
// 2. Disintegrate
// 3. Upgraded Wounding (5d8 magic damage - average 22.5 -  vs. a fixed 17)
// 4. Domination
// 5. Fear
// 6. Slow
// 7. Upgraded Anti-Magic (penetrates magic defences, drains wizard spells, removes spellcasting for longer)
// 8. Hold Person
// 9. Finger of Death (cast as ForceSpell, not as eyestalk power, though)
// 
// and the following spells, which are selected at random, inexhaustible, and disruptable:
//
// Maze
// Chain Lightning
// Lightning Bolt
// Prismatic Spray
// Dispel Magic
// Magic Missile
//
// They have 180 hp, 50% immunity to combat damage and 75% immunity to fire, acid etc; they're 75% MR.
//
// They start with Improved Mantle and Stoneskin, and can re-buff with Stoneskin and Cure Critical Wounds every 5 rounds.
//
//
// We reinterpret as follows:
//
// Eyestalks: the standard 10 (i.e., lose Disintegrate. since it's too save-or-reload; lost FoD as an eyestalk power;
// add Charm, Petrify, Telekinesis). But we upgrade the death ray to a FoD-style effect (-2 save penalty; 2d8+1 damage if
// save made).
//
// Spells: we take them to be:
//
// Maze: 3 per day
// Chain Lightning: 3 per day
// Prismatic Spray: 3 per day
// Dispel Magic: 1 per day
// Magic Missile: 5 per day - EDIT: for now we just skip it
//
// We leave the buffing alone
//



/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Hit nearby targets with a Maze
//
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!Global("maze","LOCALS",3)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|Maze|SpellTurn)
	Range(scstarget,10)
THEN DO
	Action(SpellNoDec,WIZARD_MAZE|100|50)
	IncrementGlobal("maze","LOCALS",1)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Lay waste with a Prismatic Spray
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!Global("spray","LOCALS",3)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled)
	Range(scstarget,5)
THEN DO
	Action(SpellNoDec,WIZARD_PRISMATIC_SPRAY|100|50)
	IncrementGlobal("spray","LOCALS",1)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Find someone to electrocute
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!Global("lightning","LOCALS",3)
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled|MR100|ResistElectric)
THEN DO
	Action(SpellNoDec,WIZARD_CHAIN_LIGHTNING|100|50)
	IncrementGlobal("lightning","LOCALS",1)
END
