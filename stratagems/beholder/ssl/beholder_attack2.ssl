

//////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// PARALYSATION EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled|Hold)
THEN DO
	Action(Eyestalk,BEHOLDER_PARALYZATION,paralyse|100|100)
END

IF TRIGGER
        RequireBlock(Indiscriminate)
	TargetBlock(AllTargets)
	!Race(scstarget,BEHOLDER)
	TriggerBlock(Disabled|Hold)
THEN DO
	Action(Eyestalk,BEHOLDER_PARALYZATION,paralyse|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// TELEKINESIS EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////

// steal stuff

INCLUDE FILE(%MOD_FOLDER%/beholder/ssl/beholder_steal.ssl)

// push back

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|MR100|CorePlus)
THEN DO
	Action(Eyestalk,BEHOLDER_TELEKINESIS|100|100)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// DOMINATION EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsUndead)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|Charm)
THEN DO
	Action(Eyestalk,BEHOLDER_DOMINATION|100|100)
END

IF TRIGGER
	IgnoreBlock(IsUndead)
	RequireBlock(Indiscriminate)
	TargetBlock(AllTargets)
	!Race(scstarget,BEHOLDER)
	TriggerBlock(Disabled|Charm)
THEN DO
	Action(Eyestalk,BEHOLDER_DOMINATION|100|100)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// FEAR EYE 
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|Panic)
THEN DO
	Action(Eyestalk,BEHOLDER_FEAR|100|100)
END

IF TRIGGER
        RequireBlock(Indiscriminate)
	TargetBlock(AllTargets)
	!Race(scstarget,BEHOLDER)
	TriggerBlock(Disabled|Panic)
THEN DO
	Action(Eyestalk,BEHOLDER_FEAR|100|100)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// CHARM EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsUndead)
	General(scstarget,HUMANOID)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|Charm)
THEN DO
	Action(Eyestalk,BEHOLDER_CHARM_PERSON|100|100)
END

IF TRIGGER
        RequireBlock(Indiscriminate)
	IgnoreBlock(IsUndead)
	General(scstarget,HUMANOID)
	TargetBlock(AllTargets)
	TriggerBlock(Enemy|Disabled|Charm)
THEN DO
	Action(Eyestalk,BEHOLDER_CHARM_PERSON|100|100)
END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  SLOW EYE
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|Slow)
THEN DO
	Action(Eyestalk,BEHOLDER_SLOW|100|100)
END

IF TRIGGER
        RequireBlock(Indiscriminate)
	TargetBlock(AllTargets)
	!Race(scstarget,BEHOLDER)
	TriggerBlock(Enemy|Disabled|Slow)
THEN DO
	Action(Eyestalk,BEHOLDER_SLOW|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Block, to prevent us firing everything at once
//////////////////////////////////////////////////////////////////////////////////////////////////////////

/*
IF
	See(NearestEnemyOf(Myself))
	Global("BeholderBehavior","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("BeholderBehavior","LOCALS",2)
END
*/

///////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Now re-use lethal eyestalks on helpless foes (but not, at this stage, charmed ones)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TriggerBlock(Slay|Enemy|DirectDamageSafe|CorePlus)
	TargetBlock(PlayersInRandomOrder)
	TargetBlock(EnemiesInOrder)
THEN DO
	Action(Eyestalk,BEHOLDER_DEATH_RAY)
END

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy)
	TriggerBlock(DirectDamageSafe)
	!StateCheck(scstarget,STATE_STONE_DEATH)
THEN DO
	Action(Eyestalk,BEHOLDER_CAUSE_SERIOUS_WOUNDS)
END
	
IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Petrify|DirectDamageSafe) 
	OR(2)
	        GlobalGT("DMWWPetrifyPlayer","GLOBAL",0)
		!InPartySlot(scstarget,0) // isn't Player 1
THEN DO
	Action(Eyestalk,BEHOLDER_FLESH_TO_STONE)
END

//////////////////////////////////////////////////////////////////////////////////////////
// Finish off weak summons
//////////////////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TriggerBlock(Slay|Enemy)
	TargetBlock(Summons)
	Gender(scstarget,SUMMONED)
THEN DO
	Action(EyestalkRES,dw#besud,BEHOLDER_DEATH_RAY|100|100)
	Continue()
END

////////////////////////////////////////////////////////////////////////////////////////////////////
//	Kill charmed allies if they're all that's left standing
////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PlayersInRandomOrder)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_BLIND)
	!GlobalTimerNotExpired("BEHOLDER_DEATH_RAY","LOCALS")
	!GlobalTimerNotExpired("BEHOLDER_CAUSE_SERIOUS_WOUNDS","LOCALS")
	!GlobalTimerNotExpired("BEHOLDER_FLESH_TO_STONE","LOCALS")
	!CheckStatGT(scstarget,0,SANCTUARY)
	OR(2)
		!GlobalTimerExpired("SeeBounce","LOCALS")
		!HasItem("wa2shiel",scstarget)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Literal)
        SetGlobalTimer("BEHOLDER_CAUSE_SERIOUS_WOUNDS","LOCALS",6)
        SetGlobalTimer("BEHOLDER_DEATH_RAY","LOCALS",6)
        SetGlobalTimer("BEHOLDER_FLESH_TO_STONE","LOCALS",6)
        ForceSpell(scstarget,BEHOLDER_CAUSE_SERIOUS_WOUNDS)
        ForceSpell(scstarget,BEHOLDER_FLESH_TO_STONE)
        ForceSpell(scstarget,BEHOLDER_DEATH_RAY)
END


///////////////////////////////////////////////////////////////////////////////////////////////////////////
////	If we get to here with unused eyestalks, it's reasonable to infer that we're avoiding targetting people
////	who are magic-resistant. Stop being fastidious for a round or two and see 
////	if it helps
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("IgnoreResistance","LOCALS")
	OR(3)
		!GlobalTimerNotExpired("BEHOLDER_DEATH_RAY","LOCALS")
		!GlobalTimerNotExpired("BEHOLDER_FLESH_TO_STONE","LOCALS")
		!GlobalTimerNotExpired("BEHOLDER_CAUSE_SERIOUS_WOUNDS","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("IgnoreResistance","LOCALS",12)
END




/////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Check if we have any lethal eyestalks unused. If not, and no enemies are in strike range, stop, instead
///	of going into melee - it means that there are lethal things we can still do from a distance.
/////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself)) // if not we want combat block to kick in
 	!GlobalTimerNotExpired("used_lethal_eyestalk","LOCALS")
	!Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		NoAction()
END
/////////////////////////////////////////////////////////////////////
// Combat block kicks in here
/////////////////////////////////////////////////////////////////////



IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Plus3Safe|PiercingSafe)
	Range(scstarget,10)
THEN DO
	Action(Attack)
END


IF
	!Range(NearestEnemyOf(Myself),30)
	See(NearestEnemyOf(Myself))
	NumCreatureGT([0.0.BEHOLDER],1)
THEN
	RESPONSE #100
		MoveToObject(LastSeenBy(Myself))
END


INCLUDE FILE(%MOD_FOLDER%\genai\ssl\chase.ssl)
