////////////////////////////////////////////////////////////
//
// ANTI-MAGIC EYE
//
////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////
// Help other beholders
////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
	TargetBlock(Beholders)
	!Range(scstarget,2) // avoids targetting myself
	TriggerBlock(BeholderNeedingHelp)
THEN DO
	Combine()
	Action(EyestalkFriendly,BEHOLDER_ANTIMAGIC_RAY)
END	

///////////////////////////////////////////////////
// Destroy illusions
////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(NoAntimagicEye)
	TargetBlock(EnemiesInOrder)
    TriggerBlock(EasyPlus)
	Gender(scstarget,ILLUSIONARY)
THEN DO
          Action(Eyestalk,BEHOLDER_ANTIMAGIC_RAY|100|100)
END

///////////////////////////////////////////////////
// Disable spellcasters
////////////////////////////////////////////////

IF TRIGGER
          IgnoreBlock(NoAntimagicEye)
          TargetBlock(PCsInOrder)
          TriggerBlock(Enemy|Disabled|Balduran)
          TriggerBlock(EasyPlus)
          OR(6)
                Class(scstarget,MAGE)
                Class(scstarget,CLERIC_MAGE)
                Class(scstarget,MAGE_THIEF)
                Class(scstarget,CLERIC)
                Class(scstarget,CLERIC_THIEF)
                Class(scstarget,DRUID)
	  !CheckStat(scstarget,100,SPELLFAILUREMAGE)
	  !CheckStat(scstarget,100,SPELLFAILUREPRIEST)
THEN DO
          Action(Eyestalk,BEHOLDER_ANTIMAGIC_RAY|100|100)
END

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
          TargetBlock(PCsInOrder)
          TriggerBlock(Enemy|Disabled|Balduran)
          TriggerBlock(EasyPlus)
          OR(7)
                Class(scstarget,FIGHTER_MAGE)
                Class(scstarget,FIGHTER_MAGE_CLERIC)
                Class(scstarget,FIGHTER_MAGE_THIEF)
                Class(scstarget,FIGHTER_CLERIC)
                Class(scstarget,FIGHTER_DRUID)
                Class(scstarget,CLERIC_RANGER)
                Class(scstarget,BARD)
	  !CheckStat(scstarget,100,SPELLFAILUREMAGE)
	  !CheckStat(scstarget,100,SPELLFAILUREPRIEST)
THEN DO
          Action(Eyestalk,BEHOLDER_ANTIMAGIC_RAY|100|100)
END

////////////////////////////////////////////////////////////
//Eliminate defences against beholder eyestalks
/////////////////////////////////////////////////////////////

IF TRIGGER
          IgnoreBlock(NoAntimagicEye)
          TargetBlock(PCsInOrder)
          TriggerBlock(Enemy|Disabled|BeholderEyestalkDefences)
          !Class(scstarget,CLERIC_ALL)
          !Class(scstarget,MAGE_ALL)
          !Class(scstarget,BARD)
          !Class(scstarget,DRUID_ALL)
          TriggerBlock(EasyPlus)
THEN DO
          Action(Eyestalk,BEHOLDER_ANTIMAGIC_RAY|100|100)
          Wait(1)
END

////////////////////////////////////////////////////////////////////
//Now bring down beneficial magic
////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
	  IgnoreBlock(IsHiveMother)
          TargetBlock(PCsInOrder)
          TriggerBlock(EasyPlus)
          TriggerBlock(Enemy|Disabled|BeholderAntimagicTarget)
          !Class(scstarget,CLERIC_ALL)
          !Class(scstarget,MAGE_ALL)
          !Class(scstarget,BARD)
          !Class(scstarget,DRUID_ALL)
THEN DO
          Action(Eyestalk,BEHOLDER_ANTIMAGIC_RAY|100|100)
END

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
	  RequireBlock(IsHiveMother)
          TargetBlock(PCsInOrder)
          TriggerBlock(EasyPlus)
          TriggerBlock(Enemy|Disabled|HiveMotherAntimagicTarget)
          !Class(scstarget,CLERIC_ALL)
          !Class(scstarget,MAGE_ALL)
          !Class(scstarget,BARD)
          !Class(scstarget,DRUID_ALL)
THEN DO
          Action(Eyestalk,BEHOLDER_ANTIMAGIC_RAY|100|100)
          Wait(1)
END

