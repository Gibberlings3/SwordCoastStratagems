////////////////////////////////////////////////////////////
//
// ANTI-MAGIC EYE
//
////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////
// Help other beholders
////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
	TargetBlock(Beholders)
	!Range(scstarget,2) // avoids targetting myself
	OR(4)
		StateCheck(scstarget,STATE_BLIND)
		StateCheck(scstarget,STATE_CONFUSED)
		StateCheck(scstarget,STATE_CHARMED)
		CheckStatGT(scstarget,0,CLERIC_INSECT_PLAGUE)
	Race(scstarget,BEHOLDER)
THEN DO
	Combine()
	Action(Eyestalk2,BEHOLDER_ANTIMAGIC_RAY,antimagicray)
END	

///////////////////////////////////////////////////
// Disable spellcasters
////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
        IgnoreBlock(NewBeholderAntimagic)
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Disabled)
	OR(2)
		!GlobalTimerExpired("SeeBounce","LOCALS")
		!HasItem("wa2shiel",scstarget)
	!CheckStat(scstarget,100,SPELLFAILUREMAGE)
	!CheckStat(scstarget,100,SPELLFAILUREPRIEST)
THEN DO
	Action(Eyestalk2,BEHOLDER_ANTIMAGIC_RAY,antimagicray|100|100)
END

BEGIN LOOP(scsplayer||2;4;1;5;3;6)

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
          RequireBlock(NewBeholderAntimagic)
          !GlobalTimerNotExpired("DMWWBeholderEyescsplayer","GLOBAL")
          Target(Playerscsplayer)
          TriggerBlock(Enemy|Disabled)
          OR(6)
                Class(scstarget,MAGE)
                Class(scstarget,CLERIC_MAGE)
                Class(scstarget,MAGE_THIEF)
                Class(scstarget,CLERIC)
                Class(scstarget,CLERIC_THIEF)
                Class(scstarget,DRUID)
	  OR(2)
		!GlobalTimerExpired("SeeBounce","LOCALS")
		!HasItem("wa2shiel",scstarget)
	  !CheckStat(scstarget,100,SPELLFAILUREMAGE)
	  !CheckStat(scstarget,100,SPELLFAILUREPRIEST)
THEN DO
          Action(Eyestalk3RES,dw#bhan,antimagicray,DMWWBeholderEyescsplayer,9,11,15|100|100)
END

END LOOP


BEGIN LOOP(scsplayer||2;1;5;4;6;3)

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
          RequireBlock(NewBeholderAntimagic)
          !GlobalTimerNotExpired("DMWWBeholderEyescsplayer","GLOBAL")
          Target(Playerscsplayer)
          TriggerBlock(Enemy|Disabled)
          OR(7)
                Class(scstarget,FIGHTER_MAGE)
                Class(scstarget,FIGHTER_MAGE_CLERIC)
                Class(scstarget,FIGHTER_MAGE_THIEF)
                Class(scstarget,FIGHTER_CLERIC)
                Class(scstarget,FIGHTER_DRUID)
                Class(scstarget,CLERIC_RANGER)
                Class(scstarget,BARD)
	  OR(2)
		!GlobalTimerExpired("SeeBounce","LOCALS")
		!HasItem("wa2shiel",scstarget)
	  !CheckStat(scstarget,100,SPELLFAILUREMAGE)
	  !CheckStat(scstarget,100,SPELLFAILUREPRIEST)
THEN DO
          Action(Eyestalk3RES,dw#bhan,antimagicray,DMWWBeholderEyescsplayer,10,15,20|100|100)
END

END LOOP


////////////////////////////////////////////////////////////
//Eliminate defences against beholder eyestalks
/////////////////////////////////////////////////////////////


IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
        IgnoreBlock(NewBeholderAntimagic)
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled)
	OR(4)
		CheckStatGT(scstarget,0,CLERIC_PHYSICAL_MIRROR)
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_PETRIFICATION)
		CheckStatGT(scstarget,0,SCRIPTINGSTATE2)
THEN DO
	Combine()
	Action(Eyestalk2,BEHOLDER_ANTIMAGIC_RAY,antimagicray|100|50)
END

BEGIN LOOP(scsplayer||3;5;1;2;6;4)

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
          RequireBlock(NewBeholderAntimagic)
          !GlobalTimerNotExpired("DMWWBeholderEyescsplayer","GLOBAL")
          Target(Playerscsplayer)
          TriggerBlock(Enemy|Disabled)
          !Class(scstarget,CLERIC_ALL)
          !Class(scstarget,MAGE_ALL)
          !Class(scstarget,BARD)
          !Class(scstarget,DRUID_ALL)
	OR(4)
		CheckStatGT(scstarget,0,CLERIC_PHYSICAL_MIRROR)
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_PETRIFICATION)
		CheckStatGT(scstarget,0,SCRIPTINGSTATE2)
THEN DO
          Action(Eyestalk3RES,dw#bhan,antimagicray,DMWWBeholderEyescsplayer,10,15,20|100|100)
          Wait(1)
END

END LOOP

////////////////////////////////////////////////////////////////////
//Now bring down beneficial magic
////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
        IgnoreBlock(NewBeholderAntimagic)
	IgnoreBlock(IsHiveMother)
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled)
	OR(8)
		CheckStatGT(scstarget,0,STONESKINS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		StateCheck(scstarget,STATE_INVISIBLE)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_BLESS)
		StateCheck(scstarget,STATE_CHANT)
		StateCheck(scstarget,STATE_HASTED)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Action(Eyestalk2,BEHOLDER_ANTIMAGIC_RAY,antimagicray|100|100)
END

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
        IgnoreBlock(NewBeholderAntimagic)
	RequireBlock(IsHiveMother)
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled)
	OR(12)
		CheckStatGT(scstarget,0,STONESKINS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,0,MINORGLOBE)
		StateCheck(scstarget,STATE_INVISIBLE)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_BLESS)
		StateCheck(scstarget,STATE_CHANT)
		StateCheck(scstarget,STATE_HASTED)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
		CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TRAP)
THEN DO
	Action(Eyestalk2,BEHOLDER_ANTIMAGIC_RAY,antimagicray|100|100)
END

BEGIN LOOP(scsplayer||2;4;6;3;5;1)

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
          RequireBlock(NewBeholderAntimagic)
	  IgnoreBlock(IsHiveMother)
          !GlobalTimerNotExpired("DMWWBeholderEyescsplayer","GLOBAL")
          Target(Playerscsplayer)
          TriggerBlock(Enemy|Disabled)
          !Class(scstarget,CLERIC_ALL)
          !Class(scstarget,MAGE_ALL)
          !Class(scstarget,BARD)
          !Class(scstarget,DRUID_ALL)
	OR(8)
		CheckStatGT(scstarget,0,STONESKINS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		StateCheck(scstarget,STATE_INVISIBLE)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_BLESS)
		StateCheck(scstarget,STATE_CHANT)
		StateCheck(scstarget,STATE_HASTED)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
          Action(Eyestalk3RES,dw#bhan,antimagicray,DMWWBeholderEyescsplayer,10,15,20|100|100)
          Wait(1)
END

IF TRIGGER
        IgnoreBlock(NoAntimagicEye)
          RequireBlock(NewBeholderAntimagic)
	  RequireBlock(IsHiveMother)
          !GlobalTimerNotExpired("DMWWBeholderEyescsplayer","GLOBAL")
          Target(Playerscsplayer)
          TriggerBlock(Enemy|Disabled)
          !Class(scstarget,CLERIC_ALL)
          !Class(scstarget,MAGE_ALL)
          !Class(scstarget,BARD)
          !Class(scstarget,DRUID_ALL)
	OR(12)
		CheckStatGT(scstarget,0,STONESKINS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,0,MINORGLOBE)
		StateCheck(scstarget,STATE_INVISIBLE)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_BLESS)
		StateCheck(scstarget,STATE_CHANT)
		StateCheck(scstarget,STATE_HASTED)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
		CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
		OR(2)
                   CheckStat(scstarget,1,WIZARD_SPELL_TRAP)
                   CheckStat(scstarget,2,WIZARD_SPELL_TRAP)
THEN DO
          Action(Eyestalk3RES,dw#bhan,antimagicray,DMWWBeholderEyescsplayer,10,15,20|100|100)
          Wait(1)
END


END LOOP
