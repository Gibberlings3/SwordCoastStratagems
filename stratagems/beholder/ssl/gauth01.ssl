////////////////////////////////////////////////////////////////////
//	Define beholder eyestalk power
/////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%\beholder\ssl\beholder_definitions.ssl)

//////////////////////////////////////////////////////////////////////
//	Initialise
//////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%\beholder\ssl\initialise.ssl)


//////////////////////////////////////////////////////////////////////
//	Rest and recover
//////////////////////////////////////////////////////////////////////

IF
	Global("initialise","LOCALS",1)
	GlobalTimerExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("initialise","LOCALS",0)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		Rest()
END
/////////////////////////////////////////////////////////////////////////////
///	Conserve CPU cycles
//////////////////////////////////////////////////////////////////////////////

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END

///////////////////////////////////////////////////////////////////////
///	Damaged eyestalks (originally borrowed from SimDing0)
///////////////////////////////////////////////////////////////////////

IF
    IgnoreBlock(IsUndead)
    HitBy([ANYONE],CRUSHING)
    TookDamage()
    DamageTakenGT(15)
    HPPercentLT(Myself,75)
    !StateCheck(Myself,STATE_REALLY_DEAD)
THEN
    RESPONSE #100
        ApplySpellRES("dw#degau",Myself)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  WOUNDING EYE
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	HPPercentLT(scstarget,40)
	TriggerBlock(Enemy|Disabled|MR100|DirectDamageSafe)
	!StateCheck(scstarget,STATE_STONE_DEATH)
THEN DO
	Action(Eyestalk,BEHOLDER_CAUSE_SERIOUS_WOUNDS|100|100)
	SetGlobalTimer("used_lethal_eyestalk","GLOBAL",12)
	Wait(1)
END

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled|MR100|DirectDamageSafe)
THEN DO
	Action(Eyestalk,BEHOLDER_CAUSE_SERIOUS_WOUNDS|100|100)
	SetGlobalTimer("used_lethal_eyestalk","GLOBAL",12)
	Wait(1)
END
	




//////////////////////////////////////////////////////////////////////////////////////////////////////
//
// LIGHTNING EYE
//
//////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	TargetBlock(EnemiesInReverseOrder)
	TriggerBlock(Enemy|Disabled|ResistElectric)
THEN DO
	Action(Eyestalk,BEHOLDER_LIGHTNING_BOLT|100|100)
	SetGlobalTimer("used_lethal_eyestalk","GLOBAL",12)
	Wait(1)
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// PARALYSATION EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled|Hold)
THEN DO
	Action(Eyestalk,BEHOLDER_PARALYZATION,paralyse|100|100)
	Wait(1)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// CHARM EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	General(scstarget,HUMANOID)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|Charm)
THEN DO
	Action(Eyestalk,BEHOLDER_CHARM_PERSON|100|100)
	Wait(1)
END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  SLOW EYE
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|Slow)
THEN DO
	Action(Eyestalk,BEHOLDER_SLOW|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Block, to prevent us firing everything at once
//////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("BeholderBehavior","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("BeholderBehavior","LOCALS",2)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Now re-use lethal eyestalks on helpless foes (but not, at this stage, charmed ones)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR100)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Eyestalk,BEHOLDER_CAUSE_SERIOUS_WOUNDS)
	Wait(1)
END


IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR100|ResistElectric)
THEN DO
	Action(Eyestalk,BEHOLDER_LIGHTNING_BOLT)
	Wait(1)
END
	
////////////////////////////////////////////////////////////////////////////////////////////////////
//	Kill charmed allies if they're all that's left standing
////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PlayersInRandomOrder)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_BLIND)
	!GlobalTimerNotExpired("BEHOLDER_LIGHTNING_BOLT","LOCALS")
	!GlobalTimerNotExpired("BEHOLDER_CAUSE_SERIOUS_WOUNDS","LOCALS")
	!CheckStatGT(scstarget,0,SANCTUARY)
	OR(2)
		!GlobalTimerExpired("SeeBounce","LOCALS")
		!HasItem("wa2shiel",scstarget)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Literal)
        SetGlobalTimer("BEHOLDER_CAUSE_SERIOUS_WOUNDS","LOCALS",6)
        SetGlobalTimer("BEHOLDER_LIGHTNING_BOLT","LOCALS",6)
        ForceSpell(scstarget,BEHOLDER_CAUSE_SERIOUS_WOUNDS)
        Wait(1)
        ForceSpell(scstarget,BEHOLDER_LIGHTNING_BOLT)
        Wait(1)
END


///////////////////////////////////////////////////////////////////////////////////////////////////////////
////	If we get to here with unused eyestalks, it's reasonable to infer that we're avoiding targetting people
////	who are magic-resistant. Stop being fastidious for a round or two and see 
////	if it helps
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("IgnoreResistance","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("BEHOLDER_LIGHTNING_BOLT","LOCALS")
		!GlobalTimerNotExpired("BEHOLDER_CAUSE_SERIOUS_WOUNDS","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("IgnoreResistance","LOCALS",12)
END




/////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Check if we have any lethal eyestalks unused. If not, and no enemies are in strike range, stop, instead
///	of going into melee - it means that there are lethal things we can still do from a distance.
/////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself)) // if not we want combat block to kick in
 	!GlobalTimerNotExpired("used_lethal_eyestalk","LOCALS")
	!Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		NoAction()
END

/////////////////////////////////////////////////////////////////////
// Combat block kicks in here
/////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Plus3Safe)
	Range(scstarget,10)
THEN DO
	Action(Attack)
END

IF
	!Range(NearestEnemyOf(Myself),30)
	See(NearestEnemyOf(Myself))
	NumCreatureGT([0.0.BEHOLDER],1)
THEN
	RESPONSE #100
		MoveToObject(LastSeenBy(Myself))
END

INCLUDE FILE(%scsroot%\genai\ssl\chase.ssl)
