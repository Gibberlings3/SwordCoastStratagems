///////////////////////////////////////////////////////////////////////////
////    Data points:
////
////	1) Directors in PnP get six eyestalk powers: Burning Hands, MM, Enervation,
////	Wall of Ice, Phantasmal Force and Slow. 
////
////    2) There are unused AGANNAZAR_SCORCHER and MAGIC_MISSILE beholder eyestalks
////
////    3) The actual-in-game Director is scripted to cast ICE_STORM, MAGIC_MISSILE, BURNING_HANDS
////     (and has terrible, appalling AI) 
////
////     We keep MM and Slow, and introduce Enervation (3 levels).  Wall of Ice 
////     goes to Cone of Cold, Burning Hands goes to Agannazar's scorcher (which we
////     rename "Scorching Ray". Phantasmal Force becomes Paralyzation
///////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////
//	Define beholder eyestalk powers
/////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%\beholder\ssl\beholder_definitions.ssl)

//////////////////////////////////////////////////////////////////////
//	Initialise
//////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%\beholder\ssl\initialise.ssl)

////////////////////////////////////////////////////////////
////	This is a rest-and-recover step for when
////	enemies leave you alone...
////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%\lib\ssl\common_rest.ssl)

/////////////////////////////////////////////////////////////////////////////
///	Conserve CPU cycles
//////////////////////////////////////////////////////////////////////////////

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END


///////////////////////////////////////////////////////////////////////
///	Damaged eyestalks (originally borrowed from SimDing0)
///////////////////////////////////////////////////////////////////////

IF
    HitBy([ANYONE],CRUSHING)
    TookDamage()
    DamageTakenGT(15)
    HPPercentLT(Myself,75)
    !StateCheck(Myself,STATE_REALLY_DEAD)
THEN
    RESPONSE #100
        ApplySpellRES("dw#dedir",Myself)
END
///////////////////////////////////////////////////////////////////////////////
//	Attack section
/////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  SLOW EYE
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Global("BeholderBehavior","LOCALS",0)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|Slow)
THEN DO
	Action(Eyestalk,BEHOLDER_SLOW|100|100)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	ENERVATION EYE
/////////////////////////////////////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	  Global("BeholderBehavior","LOCALS",0)
          TargetBlock(EnemiesInOrder)
          TriggerBlock(Enemy|Disabled|DirectDamageSafe|LevelDrain)
THEN DO
          Action(Eyestalk,BEHOLDER_ENERVATION|100|100)
          SetGlobalTimer("used_lethal_eyestalk","LOCALS",12)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	CONE OF COLD EYE
/////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
          TargetBlock(EnemiesInOrder)
          TriggerBlock(Enemy|Disabled)
          Range(scstarget,15)
THEN DO
          Action(Eyestalk,BEHOLDER_CONE_OF_COLD|100|50)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	SCORCHING RAY EYE
/////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Global("BeholderBehavior","LOCALS",1)
          TargetBlock(PCsPreferringWeak)
          TriggerBlock(Enemy|Disabled|MinorGlobe|ResistFire|DirectDamageSafe)
THEN DO
          Action(Eyestalk,BEHOLDER_AGANNAZAR_SCORCHER,burning|100|50)
          SetGlobalTimer("used_lethal_eyestalk","LOCALS",12)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	MAGIC MISSILE EYE
/////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
          TargetBlock(PCsPreferringWeak)
          TriggerBlock(Enemy|Disabled|MinorGlobe|MagicMissile|DirectDamageSafe)
THEN DO
          Action(EyestalkMarker,BEHOLDER_MAGIC_MISSILE,ITEM_MM_SEEN|100|50)
          SetGlobalTimer("used_lethal_eyestalk","LOCALS",12)
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// PARALYSATION EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled|MR100|Hold)
	
THEN DO
	Action(Eyestalk,BEHOLDER_PARALYZATION,paralyse|100|100)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Now re-use lethal eyestalks on helpless foes
///////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TriggerBlock(MR100|LevelDrain|Enemy|DirectDamageSafe)
	TargetBlock(PlayersInRandomOrder)
	TargetBlock(EnemiesInOrder)
THEN DO
	Action(Eyestalk,BEHOLDER_ENERVATION)
END

IF TRIGGER
	TriggerBlock(MR100|Enemy|DirectDamageSafe)
	TargetBlock(PlayersInRandomOrder)
	TargetBlock(EnemiesInOrder)
THEN DO
	Action(Eyestalk,BEHOLDER_AGANNAZAR_SCORCHER)
END

IF TRIGGER
	TriggerBlock(MR100|Enemy|DirectDamageSafe)
	TargetBlock(PlayersInRandomOrder)
	TargetBlock(EnemiesInOrder)
THEN DO
	Action(Eyestalk,BEHOLDER_MAGIC_MISSILE,MM)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////
////	If we get to here with unused eyestalks, it's reasonable to infer that we're avoiding targetting people
////	who are magic-resistant. Stop being fastidious for a round or two and see 
////	if it helps
//////////////////////////////////////////////////////////////////////////////////////////////////////////////


IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("IgnoreResistance","LOCALS")
	OR(3)
		!GlobalTimerNotExpired("BEHOLDER_ENERVATION","LOCALS")
		!GlobalTimerNotExpired("BEHOLDER_AGANNAZAR_SCORCHER","LOCALS")
		!GlobalTimerNotExpired("BEHOLDER_MAGIC_MISSILE","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("IgnoreResistance","LOCALS",12)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Check if we have any lethal eyestalks unused. If not, and no enemies are in strike range, stop, instead
///	of going into melee - it means that there are lethal things we can still do from a distance.
/////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself)) // if not we want combat block to kick in
 	!GlobalTimerNotExpired("used_lethal_eyestalk","LOCALS")
	!Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		NoAction()
END
/////////////////////////////////////////////////////////////////////
// Combat block kicks in here
/////////////////////////////////////////////////////////////////////



IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Plus3Safe)
	Range(scstarget,10)
THEN DO
	Action(Attack)
END


IF
	!Range(NearestEnemyOf(Myself),30)
	See(NearestEnemyOf(Myself))
	NumCreatureGT([0.0.BEHOLDER],1)
THEN
	RESPONSE #100
		MoveToObject(LastSeenBy(Myself))
END


INCLUDE FILE(%scsroot%\genai\ssl\chase.ssl)

