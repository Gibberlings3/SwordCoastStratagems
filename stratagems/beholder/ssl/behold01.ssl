
////////////////////////////////////////////////////////////////////
//	Define beholder powers
/////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%\beholder\ssl\beholder_definitions.ssl)


//////////////////////////////////////////////////////////////////////
//	Initialise
//////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%\beholder\ssl\initialise.ssl)

////////////////////////////////////////////////////////////
////	This is a rest-and-recover step for when
////	enemies leave you alone...
////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%\lib\ssl\common_rest.ssl)

/////////////////////////////////////////////////////////////////////////////
///	Conserve CPU cycles
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(AlwaysActive)
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN DO
     Action(EndScript)
END


///////////////////////////////////////////////////////////////////////
///	NB: as per AD&D 2e, hive mother eyestalks can't be damaged
///////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
//	Antimagic ray
/////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/beholder/ssl/beholder_antimagic.ssl)

///////////////////////////////////////////////////////////////////////////////
//	First attack section
/////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/beholder/ssl/beholder_attack1.ssl)

////////////////////////////////////////////////////////////////////////////
///   Hive mother section
/////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Hit nearby targets with a Maze
//
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsHiveMother)
          TriggerBlock(EasyPlus)
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	TriggerBlock(Maze|SpellTurn|MR|Enemy|Helpless|SIConjuration)
	Range(scstarget,6)
	OR(2)
		InPartySlot(scstarget,1) // isn't Player 1
		Global("DMWWMazePlayer","GLOBAL",0)
THEN DO
	Action(Spell,WIZARD_MAZE|150|50)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Lay waste with a Prismatic Spray
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
   IgnoreBlock(Demivrgvs)
          TriggerBlock(EasyPlus)
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   TriggerBlock(MR|SIConjuration|Helpless)
   CheckStatGT(scstarget,4,SAVEVSSPELL)
THEN DO
   Action(SpellArea,WIZARD_PRISMATIC_SPRAY|200|100)
END


IF TRIGGER
        RequireBlock(IsHiveMother)
          TriggerBlock(EasyPlus)
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   RequireBlock(Demivrgvs)
   TriggerBlock(MR|SIAlteration|Helpless)
THEN DO
   Action(SpellArea,WIZARD_PRISMATIC_SPRAY|200|100)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Find someone to electrocute
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(IsHiveMother)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR|ResistElectric|EasyPlus|SIEvocation)
THEN DO
	Combine()
	Action(Spell,WIZARD_CHAIN_LIGHTNING|150|50)
END

//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(IsHiveMother)
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|Enemy|MagicMissile)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
	Allegiance(Myself,ENEMY)
THEN DO
	Action(SpellMarker,WIZARD_MAGIC_MISSILE,ITEM_MM_SEEN)
END

///////////////////////////////////////////////////////////////////////////
/// Now hit basically anyone with MMs
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(IsHiveMother)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MinorGlobe|SpellTurn|Enemy|MagicMissile)
THEN DO
	Action(SpellMarker,WIZARD_MAGIC_MISSILE,ITEM_MM_SEEN)
END

//// End of Hive Mother section ////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
//	Remaining attack section
/////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/beholder/ssl/beholder_attack2.ssl)


