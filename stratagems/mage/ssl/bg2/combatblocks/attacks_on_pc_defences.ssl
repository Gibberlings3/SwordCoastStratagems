///////////////////////////////////////////////////////////////////////
////	MAGIC-STRIKE PHASE 1: Remove Magic, Breach, anti-magic sequencers
////
///////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////////////
////	Hit people with Stoneskin, Chaotic Commands, Protection from Magic
////	Energy, Death Ward or Protection from Magic Weapons with a Breach
///////////////////////////////////////////////////////////////////////////

// Prioritising: we *really* care about pro/m/energy if we have ADHW

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellTurn|SIAbjuration|Enemy|Helpless)
	CheckStatGT(scstarget,99,MAGICDAMAGERESISTANCE)
	HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)
	RequireBlock(HasL8)
	!GlobalTimerNotExpired("priority_breach","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
		!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
	slowspell
THEN DO
	SetGlobalTimer("priority_breach","LOCALS",12)
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("DMWW_breach","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Spell,WIZARD_BREACH|150|100)
	Action(Wand,"wand18",777718|150|100)
END


IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellTurn|SIAbjuration|Enemy|Helpless)
	OR(4)
		!CheckStatGT(scstarget,-10,SAVEVSSPELL) // a rough indicator of a potion of Magic Shielding
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,2,STONESKINS)
	!GlobalTimerNotExpired("DMWW_breach","LOCALS")
	RequireBlock(HasL5)
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
		!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
	slowspell
THEN DO
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("DMWW_breach","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Spell,WIZARD_BREACH|100|150)
	Action(Wand,"wand18",777718|100|150)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(SpellTurn|SIAbjuration|Helpless)
	OR(4)
		!CheckStatGT(scstarget,-10,SAVEVSSPELL) // a rough indicator of a potion of Magic Shielding
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,2,STONESKINS)
	!GlobalTimerNotExpired("DMWW_breach","LOCALS")
	RequireBlock(HasL5)
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
		!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
	slowspell
THEN DO
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("DMWW_breach","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Spell,WIZARD_BREACH|100|150)
	Action(Wand,"wand18",777718|100|150)
END


//// Lower Resistance sequencer

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	!CheckStatLT(scstarget,40,RESISTMAGIC)
	TriggerBlock(Enemy|Helpless)
	RequireBlock(HasL8)
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks

THEN DO
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(Trigger,6,WIZARD_LOWER_RESISTANCE,WIZARD_LOWER_RESISTANCE,WIZARD_LOWER_RESISTANCE)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrderShort)
	!CheckStatLT(scstarget,40,RESISTMAGIC)
	TriggerBlock(Helpless)
	RequireBlock(HasL8)
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks

THEN DO
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(Trigger,6,WIZARD_LOWER_RESISTANCE,WIZARD_LOWER_RESISTANCE,WIZARD_LOWER_RESISTANCE)
END

///////////////////////////////////////////////////////////////////////////////
////	Multistrike sequencer (which we'll use timer or no timer)
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCSpellcasters)
	OR(5)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
		CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
		CheckStatGT(scstarget,0,WIZARD_SPELL_IMMUNITY)
		CheckStatGT(scstarget,0,MINORGLOBE)
		CheckStatGT(scstarget,0,CLERIC_SHIELD_OF_THE_ARCHONS)
	TriggerBlock(Enemy|Helpless)
	RequireBlock(HasL7)
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("malison","LOCALS")

THEN DO
	Combine()
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("malison","LOCALS",24)
	Action(Trigger,3,WIZARD_SECRET_WORD,WIZARD_BREACH,WIZARD_POWER_WORD_SILENCE)
END

////////////////////////////////////////////////////////////////////////////
/////	With dispel magic, our priorities are   (i) Pro/M/Energy
/////					 	(ii) Chaotic Commands/ProMW/Potions
/////						(iii) other stuff
/////
/////						We'll ignore Malison for the first two	
////////////////////////////////////////////////////////////////////////////

/// initialise

IF
  See(Myself)
  False()
THEN
    RESPONSE #100
    NoAction()
END


IF TRIGGER
	IgnoreBlock(Indiscriminate)
	OR(2)
		!GlobalTimerNotExpired("DMWW_dispel","LOCALS")
		HasItem("dw#dispe",Myself)
	TargetBlock(PCsInOrder)
	RequireBlock(HasL3)
	CheckStatGT(scstarget,99,MAGICDAMAGERESISTANCE)
	BEGIN LOOP(scsvar||1;2;3;4;5;6)
		!StateCheck(Playerscsvar,STATE_DISABLED)
		!CheckStatGT(Playerscsvar,0,CLERIC_INSECT_PLAGUE)
		!Allegiance(Playerscsvar,ENEMY)
	END LOOP
	TriggerBlock(SIAbjuration|Enemy)
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
		!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks

THEN DO
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Trigger,8,WIZARD_BREACH,WIZARD_CHAIN_LIGHTNING,WIZARD_DEATH_SPELL)
	Action(RMSequencer)
	Action(SpellArea,WIZARD_DISPEL_MAGIC|100|150)
END

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	OR(2)
		!GlobalTimerNotExpired("DMWW_dispel","LOCALS")
		HasItem("dw#dispe",Myself)
	TargetBlock(PCsInOrder)
	RequireBlock(HasL3)
	OR(4)
		!CheckStatGT(scstarget,-10,SAVEVSSPELL) // a rough indicator of a potion of Magic Shielding
		CheckStatGT(scstarget,0,STONESKINS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
	BEGIN LOOP(scsvar||1;2;3;4;5;6)
		!StateCheck(Playerscsvar,STATE_DISABLED)
		!CheckStatGT(Playerscsvar,0,CLERIC_INSECT_PLAGUE)
		!CheckStatGT(Playerscsvar,0,WIZARD_GREATER_MALISON)
		!Allegiance(Playerscsvar,ENEMY)
	END LOOP
	TriggerBlock(SIAbjuration|Enemy)
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
		!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks

THEN DO
	Combine()
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Trigger,8,WIZARD_BREACH,WIZARD_CHAIN_LIGHTNING,WIZARD_DEATH_SPELL)
	Action(RMSequencer)
	Action(SpellArea,WIZARD_DISPEL_MAGIC|100|100)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	OR(2)
		!GlobalTimerNotExpired("DMWW_dispel","LOCALS")
		HasItem("dw#dispe",Myself)
	TargetBlock(EnemiesInOrder)
	RequireBlock(HasL3)
	OR(5)
	     	CheckStatGT(scstarget,99,MAGICDAMAGERESISTANCE)
		!CheckStatGT(scstarget,-10,SAVEVSSPELL) // a rough indicator of a potion of Magic Shielding
		CheckStatGT(scstarget,0,STONESKINS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
	TriggerBlock(SIAbjuration)
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
		!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks

THEN DO
	Combine()
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Trigger,8,WIZARD_BREACH,WIZARD_CHAIN_LIGHTNING,WIZARD_DEATH_SPELL)
	Action(RMSequencer)
	Action(SpellArea,WIZARD_DISPEL_MAGIC|100|100)
END


////////////////////////////////////////////////////////////////////////////
///	Interrupt the dispel magic to have a go at people's defences if they
///	have either Spell Trap or Spell Immunity
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	IgnoreBlock(Indiscriminate)
	RequireBlock(HasL8)
	CheckStat(scstarget,1,WIZARD_SPELL_TRAP)
	TriggerBlock(Enemy|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
		!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks

	slowspell
THEN DO
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",24)
	Action(Trigger,3,WIZARD_SECRET_WORD,WIZARD_BREACH,WIZARD_POWER_WORD_SILENCE)
	Action(SpellArea,WIZARD_SPELL_STRIKE|100|100)
END

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	RequireBlock(HasL3)
	CheckStatGT(scstarget,0,WIZARD_SPELL_IMMUNITY)
	TriggerBlock(Enemy|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
		!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
	slowspell
THEN DO
        Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Spell,WIZARD_SPELL_STRIKE|100|100)
//	Action(SpellArea,WIZARD_PIERCE_SHIELD|100|100)
	Action(SpellArea,WIZARD_RUBY_RAY_OF_REVERSAL|100|100)
	Action(Spell,WIZARD_WARDING_WHIP|100|100)
///	Action(SpellArea,WIZARD_PIERCE_MAGIC|100|100)
	Action(SpellArea,WIZARD_SECRET_WORD|100|100)
	Action(SpellArea,WIZARD_SPELL_THRUST|100|100)
END


////////////////////////////////////////////////////////////////////////////
///	Normal antimagic services resume
////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	IgnoreBlock(Indiscriminate)
	OR(2)
		!GlobalTimerNotExpired("DMWW_dispel","LOCALS")
		HasItem("dw#dispe",Myself)
	TargetBlock(PCsInOrder)
	RequireBlock(HasL3)
	OR(14)
		!CheckStatGT(scstarget,-10,SAVEVSSPELL) // a rough indicator of a potion of Magic Shielding
		CheckStatGT(scstarget,0,STONESKINS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_BLESS)
		StateCheck(scstarget,STATE_CHANT)
		StateCheck(scstarget,STATE_HASTED)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
		CheckStatGT(scstarget,0,CLERIC_DEFENSIVE_HARMONY)
		CheckStatGT(scstarget,0,PROTECTION_FROM_EVIL)
		CheckStatGT(scstarget,0,CLERIC_PHYSICAL_MIRROR)
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_PETRIFICATION)
		CheckStatGT(scstarget,0,SCRIPTINGSTATE2)
	BEGIN LOOP(scsvar||1;2;3;4;5;6)
		!StateCheck(Playerscsvar,STATE_DISABLED)
		!CheckStatGT(Playerscsvar,0,CLERIC_INSECT_PLAGUE)
		!CheckStatGT(Playerscsvar,0,WIZARD_GREATER_MALISON)
		!Allegiance(Playerscsvar,ENEMY)
	END LOOP
	TriggerBlock(SIAbjuration|Enemy)
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
		!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks

THEN DO
	Combine()
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	Action(Trigger,8,WIZARD_BREACH,WIZARD_CHAIN_LIGHTNING,WIZARD_DEATH_SPELL)
	Action(RMSequencer)
	Action(SpellArea,WIZARD_DISPEL_MAGIC|100|100)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	OR(2)
		!GlobalTimerNotExpired("DMWW_dispel","LOCALS")
		HasItem("dw#dispe",Myself)
	TargetBlock(EnemiesInOrder)
	RequireBlock(HasL3)
	OR(14)
		!CheckStatGT(scstarget,-10,SAVEVSSPELL) // a rough indicator of a potion of Magic Shielding
		CheckStatGT(scstarget,0,STONESKINS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_BLESS)
		StateCheck(scstarget,STATE_CHANT)
		StateCheck(scstarget,STATE_HASTED)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
		CheckStatGT(scstarget,0,CLERIC_DEFENSIVE_HARMONY)
		CheckStatGT(scstarget,0,PROTECTION_FROM_EVIL)
		CheckStatGT(scstarget,0,CLERIC_PHYSICAL_MIRROR)
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_PETRIFICATION)
		CheckStatGT(scstarget,0,SCRIPTINGSTATE2)
	TriggerBlock(SIAbjuration)
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
		!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks

THEN DO
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	Action(Trigger,8,WIZARD_BREACH,WIZARD_CHAIN_LIGHTNING,WIZARD_DEATH_SPELL)
	Action(RMSequencer)
	Action(SpellArea,WIZARD_DISPEL_MAGIC|100|100)
END

IF TRIGGER
	IgnoreBlock(Indiscriminate)
        RequireBlock(HasL4)
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy)
	!GlobalTimerNotExpired("malison","GLOBAL")
	!CheckStatGT(scstarget,0,WIZARD_GREATER_MALISON)
THEN DO
	SetGlobalTimer("malison","GLOBAL",60)
	Action(Spell,WIZARD_GREATER_MALISON|100|200)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
        RequireBlock(HasL4)
	TargetBlock(EnemiesInOrderShort)
	!GlobalTimerNotExpired("malison","GLOBAL")
	!CheckStatGT(scstarget,0,WIZARD_GREATER_MALISON)
THEN DO
	SetGlobalTimer("malison","GLOBAL",60)
	Action(Spell,WIZARD_GREATER_MALISON|100|200)
END

