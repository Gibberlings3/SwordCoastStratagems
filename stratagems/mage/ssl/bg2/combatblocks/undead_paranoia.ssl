



////////////////////////////////////////////////////////////////////
/// Use this block when you fear that Protected-from-undead foes
/// are close
////////////////////////////////////////////////////////////////////

IF TRIGGER
	!Detect(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	OR(6)
	   BEGIN LOOP(scsplayer||1;2;3;4;5;6)
		Range(Playerscsplayer,20)
	   END LOOP
	Target([ENEMY.0.SWORD])
	Target([ENEMY.0.DEMONIC])
	!CombatCounter(0)
THEN DO
	Action(RMSequencer)
	Action(Spell,WIZARD_DISPEL_MAGIC)
END	

IF TRIGGER
	!See(NearestEnemyOf(Myself))
	OR(6)
	   BEGIN LOOP(scsplayer||1;2;3;4;5;6)
		Range(Playerscsplayer,20)
	   END LOOP
	Global("ChainContingencyFired","LOCALS",1)
THEN DO
	Action(SpellMyselfRESReplace,"dw#plane",WIZARD_SUMMON_PLANATAR_EVIL|150|50)
END

IF TRIGGER
	!See(NearestEnemyOf(Myself))
	OR(6)
	   BEGIN LOOP(scsplayer||1;2;3;4;5;6)
		Range(Playerscsplayer,20)
	   END LOOP
	!See([ENEMY.0.DEMONIC.0.0.0.CHAOTIC_EVIL])
	!See([ENEMY.0.0.TANARI.0.0.CHAOTIC_EVIL])
	!See([ENEMY.0.0.0.0.SUMMONED_DEMON.CHAOTIC_EVIL])
THEN DO
	Action(SpellMyselfRESReplace,"dw#gatem",WIZARD_GATE|200|50)
END

IF TRIGGER
	!See(NearestEnemyOf(Myself))
	OR(6)
	   BEGIN LOOP(scsplayer||1;2;3;4;5;6)
		Range(Playerscsplayer,20)
	   END LOOP
	!See([ENEMY.0.DEMONIC.0.0.0.LAWFUL_EVIL])
	!See([ENEMY.0.0.TANARI.0.0.LAWFUL_EVIL])
	!See([ENEMY.0.0.0.0.SUMMONED_DEMON.LAWFUL_EVIL])
THEN DO
	Action(SpellMyselfRESReplace,"dw#sumgl",WIZARD_SUMMON_FIEND|200|50)
	Action(SpellMyselfRESReplace,"dw#sumgl",WIZARD_MAZE|100|100)
END

IF TRIGGER
	!See(NearestEnemyOf(Myself))
	OR(6)
	   BEGIN LOOP(scsplayer||1;2;3;4;5;6)
		Range(Playerscsplayer,20)
	   END LOOP
THEN DO
	Action(SpellMyself,WIZARD_MORDENKAINENS_SWORD|200|50)
	Action(SpellMyselfRESReplace,"dw#sumef",WIZARD_SUMMON_EFREET|200|50)
END

IF TRIGGER
	!Detect(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
        OR(6)
		BEGIN LOOP(scsplayer||1;2;3;4;5;6)
			Range(Playerscsplayer,20)
		END LOOP		
	Target([ENEMY])
	!CombatCounter(0)
THEN DO
	Action(Spell,WIZARD_DRAGONS_BREATH)
	Action(Spell,WIZARD_COMET)
	Action(Spell,WIZARD_ABI_DALZIMS_HORRID_WILTING)
	Action(Spell,WIZARD_CHAIN_LIGHTNING)
END

IF TRIGGER
	!See(NearestEnemyOf(Myself))
        OR(6)
		BEGIN LOOP(scsplayer||1;2;3;4;5;6)
			Range(Playerscsplayer,20)
		END LOOP
THEN DO
	Action(SpellMyself,WIZARD_DISPEL_MAGIC|200|50)
END


IF 
	!Detect(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END
