BEGIN_ACTION_DEFINITION
	Name(FakePrebuff)
	TRIGGER
	        OnCreation()
	ACTION
		RESPONSE #scsprob1
		ApplySpellRES("%WIZARD_scsargument1_VISUALS_ONLY%",Myself)
		Continue()
END

BEGIN_ACTION_DEFINITION
	Name(FakeBuff)
	TRIGGER
	        GlobalTimerExpired("castspell","LOCALS")
	        !GlobalTimerNotExpired("rebuff","LOCALS")
	ACTION
		RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("rebuff","LOCALS",18)
		ForceSpellRES("%WIZARD_scsargument1_VISUALS_ONLY%",Myself)
		RESPONSE #200
		Continue()
END

BEGIN_ACTION_DEFINITION
	Name(FakeSpell)
	TRIGGER
	        !GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpellRES("%WIZARD_scsargument1_VISUALS_ONLY%",scstarget)
END

DEFAULT TRIGGER(Allegiance(Myself,ENEMY))

IF
  OnCreation()
THEN
    RESPONSE #100
             SetGlobalTimer("rebuff","LOCALS",18)
             Continue()
    RESPONSE #100
             SetGlobalTimer("rebuff","LOCALS",12)
             Continue()
    RESPONSE #100
             SetGlobalTimer("rebuff","LOCALS",24)
             Continue()
END

IF TRIGGER
THEN DO
        Action(FakePrebuff,SPELL_IMMUNITY_ABJURATION)
        Action(FakePrebuff,SPELL_TURNING|100|100)
        Action(FakePrebuff,MINOR_GLOBE_OF_INVULNERABILITY|100|100)
        Action(FakePrebuff,FIRE_SHIELD_RED|100|100)
        Action(FakePrebuff,GHOST_ARMOR|100|100)
END

IF TRIGGER
       CheckStat(Myself,0,MINORGLOBE)
THEN DO
       Action(FakeBuff,MINOR_GLOBE_OF_INVULNERABILITY|100|100)
END

IF TRIGGER
THEN DO
     Action(FakeBuff,PROTECTION_FROM_MAGIC_WEAPONS|100|200)
END

IF TRIGGER
THEN DO
     Action(FakeBuff,IMPROVED_MANTLE|100|200)
END

IF TRIGGER
THEN DO
       Action(FakeBuff,FIRE_SHIELD_BLUE|100|100)
END

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   Range(scstarget,15)
THEN DO
     Action(FakeSpell,SUN_FIRE|100|100)
     Action(FakeSpell,CONE_OF_COLD|100|100)
END

IF TRIGGER
   Target(NearestEnemyOf(Myself))
THEN DO
     Action(FakeSpell,ABI_DALZIMS_HORRID_WILTING|100|400)
     Action(FakeSpell,SYMBOL_STUN|100|400)
     Action(FakeSpell,SYMBOL_DEATH|100|400)
     Action(FakeSpell,DISPEL_MAGIC|100|400)
     Action(FakeSpell,FINGER_OF_DEATH|100|400)
     Action(FakeSpell,DOMINATION|100|400)
     Action(FakeSpell,CHAOS|100|400)
     Action(FakeSpell,POWER_WORD_BLIND|100|400)
END

IF
  Range(NearestEnemyOf(Myself),10)
THEN
    RESPONSE #100
       RunAwayFrom(LastSeenBy(Myself),15)
END

IF
  True()
THEN
    RESPONSE #100
             RandomWalk()
END


