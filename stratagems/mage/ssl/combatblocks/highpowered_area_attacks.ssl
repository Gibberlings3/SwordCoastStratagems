
///////////////////////////////////////////////////////////////////////
////   Wail of the Banshee
///////////////////////////////////////////////////////////////////////


IF TRIGGER
        IgnoreBlock(IsBG1Only)
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	Range(scstarget,10)
	TriggerBlock(Slay|MR100|Enemy|Helpless|SINecromancy|DirectDamageSafe)
	/*slowspell*/
THEN DO
	Action(SpellL9Myself,WIZARD_WAIL_OF_THE_BANSHEE|150|50)
END


IF TRIGGER
        IgnoreBlock(IsBG1Only)
	IgnoreBlock(Indiscriminate)
	StateCheck(Myself,STATE_BLIND)
	TriggerBlock(PlayersInRange)
THEN DO
	Action(SpellL9Myself,WIZARD_WAIL_OF_THE_BANSHEE|150|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrderShort)
	Range(scstarget,10)
	TriggerBlock(Slay|MR100|Enemy|Helpless|SINecromancy|DirectDamageSafe)
	/*slowspell*/
THEN DO
	Action(SpellL9Myself,WIZARD_WAIL_OF_THE_BANSHEE|150|50)
END

///////////////////////////////////////////////////////////////////////
////   Summon Fiend
///////////////////////////////////////////////////////////////////////


IF TRIGGER
        IgnoreBlock(IsBG1Only)
        IgnoreBlock(IsCopy)
	/*slowspell*/
	!See([ENEMY.0.DEMONIC.0.0.0.LAWFUL_EVIL])
	!See([ENEMY.0.0.TANARI.0.0.LAWFUL_EVIL])
	!See([ENEMY.0.0.0.0.SUMMONED_DEMON.LAWFUL_EVIL])
	!NumCreatureGT([ENEMY.0.DEMONIC],2)
	Allegiance(Myself,ENEMY)
	IgnoreBlock(Indiscriminate)
	OR(2)
	   Alignment(Myself,CHAOTIC_NEUTRAL)
	   Alignment(Myself,CHAOTIC_EVIL)
THEN DO
	Action(SpellRESReplaceMyself,"%WIZARD_SUMMON_FIEND_SCS%",WIZARD_SUMMON_FIEND|100|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        IgnoreBlock(IsCopy)
	/*slowspell*/
	!Allegiance(Myself,ENEMY)
	RequireBlock(Indiscriminate)
	!NumCreatureGT([ENEMY.0.DEMONIC],2)
	OR(2)
	   Alignment(Myself,CHAOTIC_NEUTRAL)
	   Alignment(Myself,CHAOTIC_EVIL)
THEN DO
	Action(SpellRESReplaceMyself,"%WIZARD_SUMMON_FIEND_SCS%",WIZARD_SUMMON_FIEND|100|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        IgnoreBlock(IsCopy)
	/*slowspell*/
	!See([ENEMY.0.DEMONIC.0.0.0.CHAOTIC_EVIL])
	!See([ENEMY.0.0.TANARI.0.0.CHAOTIC_EVIL])
	!See([ENEMY.0.0.0.0.SUMMONED_DEMON.CHAOTIC_EVIL])
	!NumCreatureGT([ENEMY.0.DEMONIC],2)
	Allegiance(Myself,ENEMY)
	IgnoreBlock(Indiscriminate)
	OR(2)
	   Alignment(Myself,LAWFUL_NEUTRAL)
	   Alignment(Myself,LAWFUL_EVIL)
THEN DO
	Action(SpellRESReplaceMyself,"%WIZARD_SUMMON_FIEND_SCS%",WIZARD_SUMMON_FIEND|100|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        IgnoreBlock(IsCopy)
	/*slowspell*/
	!Allegiance(Myself,ENEMY)
	RequireBlock(Indiscriminate)
	!NumCreatureGT([ENEMY.0.DEMONIC],2)
	OR(2)
	   Alignment(Myself,LAWFUL_NEUTRAL)
	   Alignment(Myself,LAWFUL_EVIL)
THEN DO
	Action(SpellRESReplaceMyself,"%WIZARD_SUMMON_FIEND_SCS%",WIZARD_SUMMON_FIEND|100|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        IgnoreBlock(IsCopy)
	/*slowspell*/
	Allegiance(Myself,ENEMY)
	IgnoreBlock(Indiscriminate)
	!NumCreatureGT([ENEMY.0.DEMONIC],2)
	OR(2)
	   Alignment(Myself,NEUTRAL)
	   Alignment(Myself,NEUTRAL_EVIL)
THEN DO
	Action(SpellRESReplaceMyself,"%WIZARD_SUMMON_FIEND_SCS%",WIZARD_SUMMON_FIEND|100|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        IgnoreBlock(IsCopy)
	/*slowspell*/
	!Allegiance(Myself,ENEMY)
	RequireBlock(Indiscriminate)
	!NumCreatureGT([ENEMY.0.DEMONIC],2)
	OR(2)
	   Alignment(Myself,NEUTRAL)
	   Alignment(Myself,NEUTRAL_EVIL)
THEN DO
	Action(SpellRESReplaceMyself,"%WIZARD_SUMMON_FIEND_SCS%",WIZARD_SUMMON_FIEND|100|50)
END

//////////////////////////////
///   Incendiary Cloud and Meteor Swarm
//////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	  IgnoreBlock(Indiscriminate)
          TargetBlock(EnemiesInOrderShort)
          TriggerBlock(MR100|SIEvocation|Enemy|AoEFire)
	!NumCreatureGT([ENEMY.HUMANOID],1)
	!NumCreatureGT([ENEMY.GIANTHUMANOID],1)
	!NumCreatureGT([ENEMY.MONSTER],1)
          CheckStatGT(Myself,99,RESISTFIRE)
          /*slowspell*/
THEN DO
          Combine()
          Action(SpellL9Area,WIZARD_METEOR_SWARM|150|50)
          Action(SpellArea,WIZARD_INCENDIARY_CLOUD|150|50)
END


//////////////////////////////
///   Symbol: Stun
//////////////////////////////



IF TRIGGER
        IgnoreBlock(IsBG1Only)
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Stun|MR100|SIConjuration|Helpless|AoEHelpless)
	/*slowspell*/
THEN DO
	Action(SpellArea,WIZARD_SYMBOL_STUN|100|300)
END


IF TRIGGER
        IgnoreBlock(IsBG1Only)
          StateCheck(Myself,STATE_BLIND)
          TriggerBlock(PlayersInShortRange)
THEN DO
	Action(SpellMyself,WIZARD_SYMBOL_STUN|100|300)
END

///////////////////////////////
////	Symbol: Death
///////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Slay|MR|SIConjuration|DirectDamageSafe|AoE)
	/*slowspell*/
THEN DO
	Action(SpellArea,WIZARD_SYMBOL_DEATH|100|300)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
          StateCheck(Myself,STATE_BLIND)
          TriggerBlock(PlayersInShortRange)
THEN DO
	Action(SpellMyself,WIZARD_SYMBOL_DEATH|100|100)
END

////////////////////////////////////////////////////////////////////////
////  Symbol: Fear
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	IgnoreBlock(Demivrgvs)
	TriggerBlock(MR100|Panic|Helpless|SIConjuration|AoEHelpless)
	/*slowspell*/
THEN DO
	Action(Spell,WIZARD_SYMBOL_FEAR|100|300)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        StateCheck(Myself,STATE_BLIND)
        TriggerBlock(PlayersInShortRange)
	IgnoreBlock(Demivrgvs)
THEN DO
	Action(SpellMyself,WIZARD_SYMBOL_FEAR|100|300)
END


////////////////////////////////////////////////////////////////////////
////  Symbol: Weakness (SR)
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	RequireBlock(Demivrgvs)
	TriggerBlock(MR100|Panic|Helpless|SIConjuration)
	/*slowspell*/
THEN DO
	Action(Spell,WIZARD_SYMBOL_WEAKNESS|100|300)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
          StateCheck(Myself,STATE_BLIND)
          TriggerBlock(PlayersInShortRange)
	RequireBlock(Demivrgvs)
THEN DO
	Action(SpellMyself,WIZARD_SYMBOL_WEAKNESS|100|300)
END

///////////////////////////////////
////	Wilting
///////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	TargetBlock(EnemiesInOrder)
	OR(3)
             !Gender(scstarget,SUMMONED)
             !CheckSpellState(Myself,TIME_STOP)
             !GlobalTimerNotExpired("adhw","LOCALS")
        OR(3)
             !Gender(scstarget,SUMMONED)
             !CheckSpellState(Myself,TIME_STOP)
             HasItem("dw#morhw",scstarget)
	TriggerBlock(ResistMagicDamage|MR|SINecromancy|Helpless|AoEMagicDamage|DirectDamageSafe)
	/*slowspell*/
THEN DO
        SetGlobalTimer("adhw","LOCALS",24)
	Action(SpellArea,WIZARD_ABI_DALZIMS_HORRID_WILTING|100|100)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(MR|SINecromancy|Helpless|DirectDamageSafe)
	!CheckStatGT(scstarget,99,MAGICDAMAGERESISTANCE)
	TriggerBlock(DirectDamageSafe)
	/*slowspell*/
THEN DO
	Action(SpellArea,WIZARD_ABI_DALZIMS_HORRID_WILTING|100|100)
END



IF TRIGGER
        IgnoreBlock(IsBG1Only)
          StateCheck(Myself,STATE_BLIND)
          TriggerBlock(PlayersInShortRange)
THEN DO
	Action(SpellMyself,WIZARD_ABI_DALZIMS_HORRID_WILTING|200|100)
END

///////////////////////////////////////////////////////////////////////
////   Acid Storm
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        RequireBlock(IsIWDArcane)
        IgnoreBlock(IsRakshasa)
	TargetBlock(PCsInReverseOrder)
	TargetBlock(DistantEnemies)
	TriggerBlock(MR|Enemy|AoEAcidStorm)
	/*slowspell*/
	OR(2)
	     !Range(scstarget,20)
	     CheckStatGT(Myself,74,RESISTACID)
THEN DO
	Action(SpellArea,WIZARD_ACID_STORM|80|20)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        RequireBlock(IsIWDArcane)
        RequireBlock(IsRakshasa)
	TargetBlock(PCsInReverseOrder)
	TargetBlock(DistantEnemies)
	TriggerBlock(MR|Enemy|AoEAcidStorm)
	/*slowspell*/
THEN DO
	Action(SpellArea,WIZARD_ACID_STORM|80|20)
END

///////////////////////////////////////////////////////////////////////
////   Malavon's Rage
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        RequireBlock(IsIWDArcane)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR|SIEvocation|AoEMalavon)
THEN DO
	Combine()
	Action(SpellMyself,WIZARD_MALAVONS_RAGE|150|50)
END


///////////////////////////////////////////////////
///   Prismatic Spray
///////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
   IgnoreBlock(Demivrgvs)
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   TriggerBlock(MR|SIConjuration|Helpless)
   CheckStatGT(scstarget,4,SAVEVSSPELL)
   /*slowspell*/
THEN DO
   Action(SpellArea,WIZARD_PRISMATIC_SPRAY|200|100)
END


IF TRIGGER
        IgnoreBlock(IsBG1Only)
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   RequireBlock(Demivrgvs)
   TriggerBlock(MR|SIAlteration|Helpless)
   /*slowspell*/
THEN DO
   Action(SpellArea,WIZARD_PRISMATIC_SPRAY|200|100)
END

///////////////////////////////////////////////////
///   Sequencers
///////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(AoE|MR100|SINecromancy|ResistMagicDamage|Enemy|NotEvil)
THEN DO
	Action(SequencerManualUnabbreviated,3X_UNHOLY_BLIGHT,CLERIC_UNHOLY_BLIGHT,CLERIC_UNHOLY_BLIGHT,CLERIC_UNHOLY_BLIGHT|300|100)
END

IF TRIGGER
      TargetBlock(EnemiesInOrderShort)
      Range(scstarget,7)
      TriggerBlock(MR|Helpless)
      !CheckStatGT(scstarget,79,RESISTCOLD)
THEN DO
      Action(SequencerManual,3X_CONE_OF_COLD,CONE_OF_COLD,CONE_OF_COLD,CONE_OF_COLD|200|100)
END

IF TRIGGER
      TargetBlock(EnemiesInOrderShort)
      Range(scstarget,7)
      !Range(NearestAllyOf(Myself),15)
      TriggerBlock(MR|Helpless)
THEN DO
      Action(SequencerManual,CHAIN_LIGHTNING_CONE_OF_COLD_SUN_FIRE,CHAIN_LIGHTNING,CONE_OF_COLD,SUN_FIRE|200|100)
END

IF TRIGGER
	TargetBlock(PCsInReverseOrder)
	TargetBlock(DistantEnemies)
	TriggerBlock(MR|Enemy|MinorGlobe|AoEIceStorm)
THEN DO
	Action(Sequencer,3X_ICE_STORM|80|20)
END

///////////////////////////////////////////////////
///   Specifically for finale Abazigal: shockwave
///////////////////////////////////////////////////


IF TRIGGER
        RequireBlock(IsAbazigalFinal)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR|ResistElectric|SIEvocation|HardPlus)
	!GlobalTimerNotExpired("shockwave","LOCALS")
THEN DO
	Combine()
	SetGlobalTimer("shockwave","LOCALS",18)
	Action(SpellRES,"abaz01"|100|50)
END


///////////////////////////////////////////////////
///   Delayed blast fireball
///////////////////////////////////////////////////


IF TRIGGER
        IgnoreBlock(IsRakshasa)
	TargetBlock(PCsInReverseOrder)
	TargetBlock(DistantEnemies)
	TriggerBlock(MR|Enemy|AoEFire)
	/*slowspell*/
	OR(2)
	     !Range(scstarget,20)
	     CheckStatGT(Myself,74,RESISTFIRE)
THEN DO
	Action(SpellArea,WIZARD_DELAYED_BLAST_FIREBALL|80|20)
END

IF TRIGGER
        RequireBlock(IsRakshasa)
	TargetBlock(PCsInReverseOrder)
	TargetBlock(DistantEnemies)
	TriggerBlock(MR|Enemy|AoEFire)
	/*slowspell*/
THEN DO
	Action(SpellArea,WIZARD_DELAYED_BLAST_FIREBALL|80|20)
END
