/////////////////////////////////////////////////////////////////////////
///// Power Word: Kill
////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	IgnoreBlock(Indiscriminate)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	OR(2)
		HPPercentLT(scstarget,75)
		!CheckStatGT(scstarget,12,Level)
	OR(3)
		HPPercentLT(scstarget,50)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	TriggerBlock(MR100|SpellTurn|Slay|SIConjuration|Enemy|SRPowerWord)
	TriggerBlock(DirectDamageSafe)
	!CheckSpellState(Myself,TIME_STOP)
THEN DO
	Combine()
	Action(SpellL9,WIZARD_POWER_WORD_KILL|150|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrderShort)
	OR(2)
		HPPercentLT(scstarget,75)
		!CheckStatGT(scstarget,12,Level)
	OR(3)
		HPPercentLT(scstarget,50)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	TriggerBlock(MR100|SpellTurn|Slay|SIConjuration|SRPowerWord)
	TriggerBlock(DirectDamageSafe)
	!CheckSpellState(Myself,TIME_STOP)
THEN DO
	Combine()
	Action(SpellL9,WIZARD_POWER_WORD_KILL|150|50)
END


/////////////////////////////////////////////////////////////////////////
////  Maze
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	TriggerBlock(Maze|SpellTurn|MR|Enemy|Helpless|SIConjuration)
	Range(scstarget,6)
	OR(2)
		!InPartySlot(scstarget,1) // isn't Player 1
		Global("DMWWMazePlayer","GLOBAL",0)
THEN DO
	Action(Spell,WIZARD_MAZE|150|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Maze|SpellTurn|MR|Helpless|SIConjuration)
	Range(scstarget,6)
	OR(2)
		!InPartySlot(scstarget,1) // isn't Player 1
		Global("DMWWMazePlayer","GLOBAL",0)
THEN DO
	Action(Spell,WIZARD_MAZE|150|50)
END

////////////////////////////////////////////////////////////////////////
////  Power Word: Stun
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	IgnoreBlock(Indiscriminate)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	TriggerBlock(MR100|SpellTurn|Stun|Enemy|Helpless|SIConjuration|SRPowerWord)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	!CheckSpellState(Myself,TIME_STOP)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|150|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|SpellTurn|Stun|Helpless|SIConjuration|SRPowerWord)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	!CheckSpellState(Myself,TIME_STOP)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|150|50)
END

////////////////////////////////////////////////////////////////////////
////  Bigby's Hand spells
/////////////////////////////////////////////////////////////////////////


IF TRIGGER
        IgnoreBlock(IsBG1Only)
	IgnoreBlock(Indiscriminate)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	TriggerBlock(MR100|SpellTurn|Enemy|Helpless|SIEvocation)
	/*slowspell*/
	OR(2)
            CheckStatGT(scstarget,4,SAVEVSDEATH)
            GlobalTimerNotExpired("targetcompromise","LOCALS")
THEN DO
        Combine()
	Action(SpellL9,WIZARD_BIGBYS_CRUSHING_HAND|150|50)
	Action(Spell,WIZARD_BIGBYS_CLENCHED_FIST|150|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|SpellTurn|Helpless|SIEvocation)
	/*slowspell*/
	OR(2)
            CheckStatGT(scstarget,4,SAVEVSDEATH)
            GlobalTimerNotExpired("targetcompromise","LOCALS")
THEN DO
	Action(SpellL9,WIZARD_BIGBYS_CRUSHING_HAND|150|50)
	Action(Spell,WIZARD_BIGBYS_CLENCHED_FIST|150|50)
END

////////////////////////////////////////////////////////////////////////
////  Disintegrate
/////////////////////////////////////////////////////////////////////////

  IF TRIGGER
	IgnoreBlock(Indiscriminate)
	RequireBlock(Demivrgvs)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	/*slowspell*/
	TargetBlock(PCsInOrderShort)
	TriggerBlock(MR100|SpellTurn|Disintegrate|SIEvocation|ResistMagicDamage100|Enemy)
	TriggerBlock(DirectDamageSafe)
	OR(2)
	        GlobalGT("DMWWDisintegratePlayer","GLOBAL",0)
		!InPartySlot(scstarget,0) // isn't Player 1
THEN DO
	Combine()
	Action(Spell,WIZARD_DISINTEGRATE|150|50)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	RequireBlock(Demivrgvs)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SpellTurn|Disintegrate|SIEvocation)
	/*slowspell*/
	TriggerBlock(DirectDamageSafe)
	OR(2)
	        GlobalGT("DMWWDisintegratePlayer","GLOBAL",0)
		!InPartySlot(scstarget,0) // isn't Player 1
THEN DO
	Action(Spell,WIZARD_DISINTEGRATE|150|50)
END

////////////////////////////////////////////////////////////////////////
////  Control undead PCs
/////////////////////////////////////////////////////////////////////////


IF TRIGGER
        TargetBlock(PCsInOrder)
        General(scstarget,UNDEAD)
	CheckStatGT(scstarget,0,SAVEVSSPELL)
	/*slowspell*/
THEN DO
        Action(Spell,WIZARD_CONTROL_UNDEAD|100|50)
END