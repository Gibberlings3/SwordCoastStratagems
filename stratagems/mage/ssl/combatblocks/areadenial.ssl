//////////////////////////////////////////////////////////////////////////
///	Area-denial phase: Liches and Rakshasas use webs, stinking clouds, death fogs
///	and cloudkill. Others use some of the same.
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	!GlobalTimerNotExpired("areadenial","LOCALS")
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|AoEDisabled|Enemy)
	/*slowspell*/
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",18)
	Action(SpellArea,WIZARD_SPHERE_OF_CHAOS|100|100)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        RequireBlock(IsIWDArcane)
	!GlobalTimerNotExpired("areadenial","LOCALS")
	RequireBlock(IsRakshasa|IsLich|IsVampire)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|AoESuffocate|DirectDamageSafe|Enemy)
	/*slowspell*/
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",18)
	Action(SpellArea,WIZARD_SUFFOCATE|100|100)
END  

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        RequireBlock(IsIWDArcane)
	!GlobalTimerNotExpired("areadenial","LOCALS")
	IgnoreBlock(IsRakshasa|IsLich|IsVampire)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|AoESuffocate|DirectDamageSafe|Enemy)
	/*slowspell*/
	!Range(scstarget,10)
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",18)
	Action(SpellArea,WIZARD_SUFFOCATE|100|100)
END

IF TRIGGER
	!GlobalTimerNotExpired("areadenial","LOCALS")
	RequireBlock(IsLich|IsVampire|IsRakshasa)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|Poison|AoEPoison|DirectDamageSafe|Enemy)
	/*slowspell*/
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",18)
	Action(SpellArea,WIZARD_DEATH_FOG|100|100)
END

IF TRIGGER
	!GlobalTimerNotExpired("areadenial","LOCALS")
	RequireBlock(IsRakshasa)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|Poison|AoEPoison|DirectDamageSafe|Enemy)
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",18)
	Action(SpellArea,WIZARD_CLOUDKILL|100|100)
END

IF TRIGGER
	!GlobalTimerNotExpired("areadenial","LOCALS")
	IgnoreBlock(IsRakshasa)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|Poison|AoEPoison|DirectDamageSafe|Enemy)
	/*slowspell*/
	OR(2)
	     !Range(scstarget,20)
	     CheckStatGT(Myself,99,RESISTACID)
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",18)
	Action(SpellArea,WIZARD_DEATH_FOG|100|100)
END

IF TRIGGER
	!GlobalTimerNotExpired("areadenial","LOCALS")
	RequireBlock(IsLich|IsVampire)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|Poison|AoEPoison|DirectDamageSafe|Enemy)
	!NumCreatureGT([ENEMY.HUMANOID],1)
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",18)
	Action(SpellArea,WIZARD_CLOUDKILL|100|100)
END

IF TRIGGER
	!GlobalTimerNotExpired("areadenial","LOCALS")
	RequireBlock(IsRakshasa)
	Target(NearestEnemyOf(Myself))
	!See([ENEMY.WEAPON])
          TriggerBlock(AoEHelpless|Enemy)
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",18)
	SetGlobalTimer("webcast","LOCALS",30)
	Action(SequencerMarker,STINKING_CLOUD_WEB,ITEM_ANTIWEB_SEEN) // Stinking Cloud and Web
	Action(SpellAreaMarker,WIZARD_WEB,ITEM_ANTIWEB_SEEN|150|50)
END

IF TRIGGER
	!GlobalTimerNotExpired("areadenial","LOCALS")
	Target(NearestEnemyOf(Myself))
          TriggerBlock(AoEHelpless)
THEN DO
	SetGlobalTimer("areadenial","LOCALS",18)
	SetGlobalTimer("webcast","LOCALS",30)
	Action(SequencerMarker,2X_WIZARD_WEB,ITEM_ANTIWEB_SEEN|150|50)
END

IF TRIGGER
   	IgnoreBlock(IsIndiscriminate)
	!GlobalTimerNotExpired("areadenial","LOCALS")
	RequireBlock(IsLich)
	!NumCreatureGT([ENEMY.HUMANOID],1)
	!See([ENEMY.WEAPON])
	Target(NearestEnemyOf(Myself))
        TriggerBlock(AoEHelpless|Enemy)
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",18)
	SetGlobalTimer("webcast","LOCALS",30)
	Action(SequencerMarker,STINKING_CLOUD_WEB,ITEM_ANTIWEB_SEEN) // Stinking Cloud and Web
	Action(SpellAreaMarker,WIZARD_WEB,ITEM_ANTIWEB_SEEN|150|100)
END

IF TRIGGER
	!GlobalTimerNotExpired("teleportfield","LOCALS")
	Target(NearestEnemyOf(Myself))
	RequireBlock(HasL4)
	!AreaCheck("ar1516")  // spellhold (causes problems)
	!AreaCheck("ar2300") // Sahuagin city (ditto)
	!AreaCheck("ar2202") // drow pitfight (ditto)
	!GlobalTimerNotExpired("redefend","LOCALS")
THEN DO
        Combine()
	SetGlobalTimer("teleportfield","LOCALS",30)
	Action(SpellMyself,WIZARD_TELEPORT_FIELD|100|100)
END

IF TRIGGER
	  IgnoreBlock(Indiscriminate)
          !GlobalTimerNotExpired("areadenial","LOCALS")
          IgnoreBlock(IsLich|IsRakshasa)
          !CheckStat(Myself,0,MINORGLOBE)
          !NumCreatureGT([ENEMY.HUMANOID],1)
	!See([ENEMY.WEAPON])
          Target(NearestEnemyOf(Myself))
          TriggerBlock(AoEHelpless|Enemy)
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",30)
	Action(SpellAreaMarker,WIZARD_WEB,ITEM_ANTIWEB_SEEN|150|100)
	SetGlobalTimer("webcast","LOCALS",30)
END

IF TRIGGER
	  IgnoreBlock(Indiscriminate)
          !GlobalTimerNotExpired("areadenial","LOCALS")
          IgnoreBlock(IsLich|IsRakshasa)
          !CheckStat(Myself,0,MINORGLOBE)
          !NumCreatureGT([ENEMY.HUMANOID],1)
	!See([ENEMY.WEAPON])
          Target(NearestEnemyOf(Myself))
          TriggerBlock(AoEHelpless|Enemy)
THEN DO
        Combine()
	SetGlobalTimer("areadenial","LOCALS",30)
	Action(SpellArea,WIZARD_STINKING_CLOUD|150|100)
END

