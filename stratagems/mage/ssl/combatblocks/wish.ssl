/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
/// Wish
///
/// Roughly based on this baseline (from 25 tests, assuming max Wisdom - we assume
/// mages who use Wish have arranged this through potions/natural ability/
/// Wishcraft HLA, though we don't systematically simulate this
///
///  Improved Haste on party (6/25)
///  All party abilities set to 25 for 4 rounds (6/25)
///  Breach all enemies (9/25)
///  Resurrect all dead party members (5/25) - can't simulate this, but 
///     we increase chance of Mass Restoration in lieu
///  Restore all spells (7/25) - SPWISH16
///  Mass restoration (5/25) - SPWISH07
///  Improved Alacrity + Timestop (5/25)
///  All party get Greater Deathblow (6/25)
///  All party get Hardiness (4/25)
///
///  Actual key is:
///  1 - breach
///  2 - imp haste
///  3 - TS + IA
///  4 - Hardiness
///  5 - restoration
///  6 - rest
///  7 - abilities to 25 for 4 rds
//////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
///     Scenario: enemies worth breaching, start of combat
///////////////////////////////////////////////////////////////////////////

IF
        IgnoreBlock(Indiscriminate)
        IgnoreBlock(IsBG1Only)
        !GlobalTimerNotExpired("first_round_wish_considered","LOCALS")
        !GlobalTimerNotExpired("castspell","LOCALS")
        SSLBoolean(WIZARD_WISH)
	HaveSpell(WIZARD_WISH)
	!CheckSpellState(Myself,TIME_STOP)
	/*slowspell*/
	OR(12)
	      CheckSpellState(Player1,PRIORITY_BREACH)
	      CheckSpellState(Player2,PRIORITY_BREACH)
	      CheckSpellState(Player3,PRIORITY_BREACH)
	      CheckSpellState(Player4,PRIORITY_BREACH)
	      CheckSpellState(Player5,PRIORITY_BREACH)
	      CheckSpellState(Player6,PRIORITY_BREACH)
	      CheckSpellState(Player1,BUFF_PRO_SPELLS)
	      CheckSpellState(Player2,BUFF_PRO_SPELLS)
	      CheckSpellState(Player3,BUFF_PRO_SPELLS)
	      CheckSpellState(Player4,BUFF_PRO_SPELLS)
	      CheckSpellState(Player5,BUFF_PRO_SPELLS)
	      CheckSpellState(Player6,BUFF_PRO_SPELLS)
THEN
	RESPONSE #400
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg1",Myself) //breach
		SetGlobalTimer("first_round_wish_considered","LOCALS",1000)
		SetGlobalTimer("later_wish","LOCALS",18)
	RESPONSE #150
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg2",Myself) // improved haste
		SetGlobalTimer("first_round_wish_considered","LOCALS",1000)
		SetGlobalTimer("later_wish","LOCALS",18)
	RESPONSE #200
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg3",Myself)  // TS + IA
		SetGlobalTimer("first_round_wish_considered","LOCALS",1000)
		SetGlobalTimer("later_wish","LOCALS",18)
	RESPONSE #150
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg4",Myself) //hardiness
		SetGlobalTimer("first_round_wish_considered","LOCALS",1000)
		SetGlobalTimer("later_wish","LOCALS",18)
	RESPONSE #1000
		SetGlobalTimer("first_round_wish_considered","LOCALS",1000)
		SetGlobalTimer("later_wish","LOCALS",18)
		Continue()
END

///////////////////////////////////////////////////////////////////////////
///     Scenario: enemies worth breaching, no injuries, not start of combat
///////////////////////////////////////////////////////////////////////////

IF
        IgnoreBlock(Indiscriminate)
        IgnoreBlock(IsBG1Only)
        GlobalTimerNotExpired("first_round_wish_considered","LOCALS")
        !GlobalTimerNotExpired("later_wish","LOCALS")
        !GlobalTimerNotExpired("castspell","LOCALS")
        SSLBoolean(WIZARD_WISH)
	HaveSpell(WIZARD_WISH)
	/*slowspell*/
	!CheckSpellState(Myself,TIME_STOP)
	!StateCheck(NearestAllyOf(Myself),STATE_DISABLED)
	!StateCheck(SecondNearestAllyOf(Myself),STATE_DISABLED)
	!StateCheck(ThirdNearestAllyOf(Myself),STATE_DISABLED)
	HPPercentGT(Myself,75)
	HPPercentGT(NearestAllyOf(Myself),75)
	HPPercentGT(SecondNearestAllyOf(Myself),75)
	HPPercentGT(ThirdNearestAllyOf(Myself),75)
	OR(12)
	      CheckSpellState(Player1,PRIORITY_BREACH)
	      CheckSpellState(Player2,PRIORITY_BREACH)
	      CheckSpellState(Player3,PRIORITY_BREACH)
	      CheckSpellState(Player4,PRIORITY_BREACH)
	      CheckSpellState(Player5,PRIORITY_BREACH)
	      CheckSpellState(Player6,PRIORITY_BREACH)
	      CheckSpellState(Player1,BUFF_PRO_SPELLS)
	      CheckSpellState(Player2,BUFF_PRO_SPELLS)
	      CheckSpellState(Player3,BUFF_PRO_SPELLS)
	      CheckSpellState(Player4,BUFF_PRO_SPELLS)
	      CheckSpellState(Player5,BUFF_PRO_SPELLS)
	      CheckSpellState(Player6,BUFF_PRO_SPELLS)
THEN
	RESPONSE #400
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg1",Myself) //breach
	RESPONSE #200
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg3",Myself)  // TS + IA
	RESPONSE #250
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg6",Myself)  // rest
	RESPONSE #150
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg4",Myself) //hardiness
	RESPONSE #500
		Continue()
END

///////////////////////////////////////////////////////////////////////////
///     Scenario: no enemies worth breaching, no injuries, not start of combat
///////////////////////////////////////////////////////////////////////////

IF
        IgnoreBlock(Indiscriminate)
        IgnoreBlock(IsBG1Only)
        GlobalTimerNotExpired("first_round_wish_considered","LOCALS")
        !GlobalTimerNotExpired("later_wish","LOCALS")
        !GlobalTimerNotExpired("castspell","LOCALS")
        SSLBoolean(WIZARD_WISH)
	HaveSpell(WIZARD_WISH)
	/*slowspell*/
	!CheckSpellState(Myself,TIME_STOP)
	!StateCheck(NearestAllyOf(Myself),STATE_DISABLED)
	!StateCheck(SecondNearestAllyOf(Myself),STATE_DISABLED)
	!StateCheck(ThirdNearestAllyOf(Myself),STATE_DISABLED)
	HPPercentGT(Myself,75)
	HPPercentGT(NearestAllyOf(Myself),75)
	HPPercentGT(SecondNearestAllyOf(Myself),75)
	HPPercentGT(ThirdNearestAllyOf(Myself),75)
        !CheckSpellState(Player1,PRIORITY_BREACH)
        !CheckSpellState(Player2,PRIORITY_BREACH)
        !CheckSpellState(Player3,PRIORITY_BREACH)
        !CheckSpellState(Player4,PRIORITY_BREACH)
        !CheckSpellState(Player5,PRIORITY_BREACH)
        !CheckSpellState(Player6,PRIORITY_BREACH)
        !CheckSpellState(Player1,BUFF_PRO_SPELLS)
        !CheckSpellState(Player2,BUFF_PRO_SPELLS)
        !CheckSpellState(Player3,BUFF_PRO_SPELLS)
        !CheckSpellState(Player4,BUFF_PRO_SPELLS)
        !CheckSpellState(Player5,BUFF_PRO_SPELLS)
        !CheckSpellState(Player6,BUFF_PRO_SPELLS)
THEN
	RESPONSE #200
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg3",Myself) //TS+IA
	RESPONSE #250
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg6",Myself)  // rest
	RESPONSE #300
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg7",Myself)  // all abilities to 25
	RESPONSE #150
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg2",Myself)  // haste
	RESPONSE #100
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg4",Myself) //hardiness
	RESPONSE #1000
		Continue()
END

///////////////////////////////////////////////////////////////////////////
///     Scenario: enemies worth breaching, some injuries, not start of combat
///////////////////////////////////////////////////////////////////////////

IF
        IgnoreBlock(Indiscriminate)
        IgnoreBlock(IsBG1Only)
        GlobalTimerNotExpired("first_round_wish_considered","LOCALS")
        !GlobalTimerNotExpired("later_wish","LOCALS")
        !GlobalTimerNotExpired("castspell","LOCALS")
        SSLBoolean(WIZARD_WISH)
	HaveSpell(WIZARD_WISH)
	/*slowspell*/
	!CheckSpellState(Myself,TIME_STOP)
	OR(7)
              StateCheck(NearestAllyOf(Myself),STATE_DISABLED)
	      StateCheck(SecondNearestAllyOf(Myself),STATE_DISABLED)
	      StateCheck(ThirdNearestAllyOf(Myself),STATE_DISABLED)
	      !HPPercentGT(Myself,75)
	      !HPPercentGT(NearestAllyOf(Myself),75)
	      !HPPercentGT(SecondNearestAllyOf(Myself),75)
	      !HPPercentGT(ThirdNearestAllyOf(Myself),75)
	OR(12)
	      CheckSpellState(Player1,PRIORITY_BREACH)
	      CheckSpellState(Player2,PRIORITY_BREACH)
	      CheckSpellState(Player3,PRIORITY_BREACH)
	      CheckSpellState(Player4,PRIORITY_BREACH)
	      CheckSpellState(Player5,PRIORITY_BREACH)
	      CheckSpellState(Player6,PRIORITY_BREACH)
	      CheckSpellState(Player1,BUFF_PRO_SPELLS)
	      CheckSpellState(Player2,BUFF_PRO_SPELLS)
	      CheckSpellState(Player3,BUFF_PRO_SPELLS)
	      CheckSpellState(Player4,BUFF_PRO_SPELLS)
	      CheckSpellState(Player5,BUFF_PRO_SPELLS)
	      CheckSpellState(Player6,BUFF_PRO_SPELLS)
THEN
	RESPONSE #200
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg5",Myself) //restoration
	RESPONSE #350
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg1",Myself) //breach
	RESPONSE #200
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg3",Myself)  // TS + IA
	RESPONSE #250
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg6",Myself)  // rest
END

///////////////////////////////////////////////////////////////////////////
///     Scenario: no-one worth breaching, some injuries, not start of combat
///////////////////////////////////////////////////////////////////////////

IF
        IgnoreBlock(Indiscriminate)
        IgnoreBlock(IsBG1Only)
        GlobalTimerNotExpired("first_round_wish_considered","LOCALS")
        !GlobalTimerNotExpired("later_wish","LOCALS")
        !GlobalTimerNotExpired("castspell","LOCALS")
        SSLBoolean(WIZARD_WISH)
	HaveSpell(WIZARD_WISH)
	/*slowspell*/
	!CheckSpellState(Myself,TIME_STOP)
	OR(7)
              StateCheck(NearestAllyOf(Myself),STATE_DISABLED)
	      StateCheck(SecondNearestAllyOf(Myself),STATE_DISABLED)
	      StateCheck(ThirdNearestAllyOf(Myself),STATE_DISABLED)
	      !HPPercentGT(Myself,75)
	      !HPPercentGT(NearestAllyOf(Myself),75)
	      !HPPercentGT(SecondNearestAllyOf(Myself),75)
	      !HPPercentGT(ThirdNearestAllyOf(Myself),75)
        !CheckSpellState(Player1,PRIORITY_BREACH)
        !CheckSpellState(Player2,PRIORITY_BREACH)
        !CheckSpellState(Player3,PRIORITY_BREACH)
        !CheckSpellState(Player4,PRIORITY_BREACH)
        !CheckSpellState(Player5,PRIORITY_BREACH)
        !CheckSpellState(Player6,PRIORITY_BREACH)
        !CheckSpellState(Player1,BUFF_PRO_SPELLS)
        !CheckSpellState(Player2,BUFF_PRO_SPELLS)
        !CheckSpellState(Player3,BUFF_PRO_SPELLS)
        !CheckSpellState(Player4,BUFF_PRO_SPELLS)
        !CheckSpellState(Player5,BUFF_PRO_SPELLS)
        !CheckSpellState(Player6,BUFF_PRO_SPELLS)
THEN
	RESPONSE #400
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg5",Myself) //restoration
	RESPONSE #150
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg1",Myself) //breach
	RESPONSE #200
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg4",Myself)  // hardiness
	RESPONSE #250
		RemoveSpell(WIZARD_WISH)
		SpellNoDecRES("dw#wisg6",Myself)  // rest
	RESPONSE #500
	         Continue()
END