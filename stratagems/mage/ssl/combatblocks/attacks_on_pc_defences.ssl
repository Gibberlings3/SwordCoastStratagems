///////////////////////////////////////////////////////////////////////
////	MAGIC-STRIKE PHASE 1: Remove Magic, Breach, anti-magic sequencers
////
///////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////////////
////	Hit people with Stoneskin, Chaotic Commands, Protection from Magic
////	Energy, Death Ward or Protection from Magic Weapons with a Breach
///////////////////////////////////////////////////////////////////////////

// Prioritising: we *really* care about pro/m/energy if we have ADHW

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|SIAbjuration|Enemy|Helpless)
	TriggerBlock(ProtectionFromMagicEnergy)
	SSLBoolean(WIZARD_ABI_DALZIMS_HORRID_WILTING)
	HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)
	!GlobalTimerNotExpired("priority_breach","LOCALS")
	!CheckSpellState(Myself,TIME_STOP)
	/*slowspell*/
THEN DO
	SetGlobalTimer("priority_breach","LOCALS",12)
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("DMWW_breach","LOCALS",18)
	Action(Spell,WIZARD_BREACH|150|100)
	Action(Wand,wand18|150|100)
END


IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|SIAbjuration|Enemy|Helpless)
        TriggerBlock(PriorityBreach)
	!GlobalTimerNotExpired("DMWW_breach","LOCALS")
	!CheckSpellState(Myself,TIME_STOP)
	/*slowspell*/
THEN DO
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("DMWW_breach","LOCALS",18)
	Action(Spell,WIZARD_BREACH|100|150)
	Action(Wand,wand18|100|150)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(SpellDeflect|SIAbjuration|Helpless)
        TriggerBlock(PriorityBreach)
        !GlobalTimerNotExpired("DMWW_breach","LOCALS")
	!CheckSpellState(Myself,TIME_STOP)
	/*slowspell*/
THEN DO
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("DMWW_breach","LOCALS",18)
	Action(Spell,WIZARD_BREACH|100|150)
	Action(Wand,wand18|100|150)
END


//// Lower Resistance sequencer

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TriggerBlock(ResistMagicTarget)
	TriggerBlock(Enemy|Helpless)
	!CheckSpellState(Myself,TIME_STOP)

THEN DO
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(Sequencer,3X_LOWER_RESISTANCE)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(ResistMagicTarget)
	TriggerBlock(Helpless)
	!CheckSpellState(Myself,TIME_STOP)

THEN DO
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(Sequencer,3X_LOWER_RESISTANCE)
END

///////////////////////////////////////////////////////////////////////////////
////	Multistrike sequencer (which we'll use timer or no timer)
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Helpless)
	TriggerBlock(SpellProtectionTarget)
	!CheckSpellState(Myself,TIME_STOP)

THEN DO
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(Sequencer,BREACH_SECRET_WORD_POWER_WORD_SILENCE)
END

////////////////////////////////////////////////////////////////////////////
/////	Top-priority dispel magic. We'll ignore Malison for these
////////////////////////////////////////////////////////////////////////////

/// initialise

IF
  See(Myself)
  False()
THEN
    RESPONSE #100
    NoAction()
END


IF TRIGGER
	IgnoreBlock(Indiscriminate)
	!SSLBoolean(WIZARD_TIME_STOP) // this block is duplicated in the Time Stop section
	OR(2)
		!GlobalTimerNotExpired("DMWW_dispel","LOCALS")
		HasItem("dw#dispe",Myself)
	TargetBlock(PCsInOrder)
	TriggerBlock(PriorityDispel|AvoidDispel|SIAbjuration|Enemy)
	!CheckSpellState(Myself,TIME_STOP)
 THEN DO
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(Sequencer,DEATH_SPELL_DISPEL_MAGIC_GREATER_MALISON)
	Action(SequencerManual,3X_DISPEL_MAGIC,DISPEL_MAGIC,DISPEL_MAGIC,DISPEL_MAGIC)
	Action(SequencerManual,2X_DISPEL_MAGIC_GREATER_MALISON,DISPEL_MAGIC,DISPEL_MAGIC,GREATER_MALISON)
	Action(SpellArea,WIZARD_DISPEL_MAGIC|100|150)
END

////////////////////////////////////////////////////////////////////////////
///	Interrupt the dispel magic to have a go at people's defences if they
///	have either Spell Trap or Spell Immunity
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	IgnoreBlock(Indiscriminate)
	TriggerBlock(Enemy|Helpless)
	TriggerBlock(SpellTrap)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!CheckSpellState(Myself,TIME_STOP)
        /*slowspell*/
THEN DO
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(SpellArea,WIZARD_SPELL_STRIKE|100|100)
END

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	RequireBlock(HasL3)
        TriggerBlock(SpellImmunity)
	TriggerBlock(Enemy|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!CheckSpellState(Myself,TIME_STOP)
       	/*slowspell*/
THEN DO
        Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(SpellL9,WIZARD_SPELL_STRIKE|100|100)
	Action(SpellArea,WIZARD_RUBY_RAY_OF_REVERSAL|100|100)
	Action(Spell,WIZARD_WARDING_WHIP|100|100)
	Action(SpellArea,WIZARD_SECRET_WORD|100|100)
	Action(SpellArea,WIZARD_SPELL_THRUST|100|100)
END


////////////////////////////////////////////////////////////////////////////
///	Normal antimagic services resume
////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	IgnoreBlock(Indiscriminate)
	OR(2)
		!GlobalTimerNotExpired("DMWW_dispel","LOCALS")
		HasItem("dw#dispe",Myself)
	TargetBlock(PCsInOrder)
	RequireBlock(HasL3)
	TriggerBlock(DispelTarget|AvoidDispel)
	TriggerBlock(SIAbjuration|Enemy)
	!CheckSpellState(Myself,TIME_STOP)
 THEN DO
	Combine()
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	Action(Sequencer,DEATH_SPELL_DISPEL_MAGIC_GREATER_MALISON)
	Action(SequencerManual,3X_DISPEL_MAGIC,DISPEL_MAGIC,DISPEL_MAGIC,DISPEL_MAGIC)
	Action(SequencerManual,2X_DISPEL_MAGIC_GREATER_MALISON,DISPEL_MAGIC,DISPEL_MAGIC,GREATER_MALISON)
	Action(SpellArea,WIZARD_DISPEL_MAGIC|100|100)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	OR(2)
		!GlobalTimerNotExpired("DMWW_dispel","LOCALS")
		HasItem("dw#dispe",Myself)
	TargetBlock(EnemiesInOrder)
	RequireBlock(HasL3)
	TriggerBlock(DispelTarget|AvoidDispel|AvoidDispellingMalison|SIAbjuration)
	!CheckSpellState(Myself,TIME_STOP)
THEN DO
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	Action(Sequencer,DEATH_SPELL_DISPEL_MAGIC_GREATER_MALISON)
	Action(SequencerManual,3X_DISPEL_MAGIC,DISPEL_MAGIC,DISPEL_MAGIC,DISPEL_MAGIC)
	Action(SequencerManual,2X_DISPEL_MAGIC_GREATER_MALISON,DISPEL_MAGIC,DISPEL_MAGIC,GREATER_MALISON)
	Action(SpellArea,WIZARD_DISPEL_MAGIC|100|100)
END


IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|NotMalisoned|AoEHelpless)
	!GlobalTimerNotExpired("malison","GLOBAL")
THEN DO
	SetGlobalTimer("malison","GLOBAL",60)
	Action(Spell,WIZARD_GREATER_MALISON|100|200)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrderShort)
	!GlobalTimerNotExpired("malison","GLOBAL")
	TriggerBlock(NotMalisoned|AoEHelpless)
THEN DO
	SetGlobalTimer("malison","GLOBAL",60)
	Action(Spell,WIZARD_GREATER_MALISON|100|200)
END

