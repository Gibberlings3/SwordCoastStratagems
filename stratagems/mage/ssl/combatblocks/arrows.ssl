//////////////////////////////////////////////////////////////////////////
///  ATTACK PHASE 6: Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|Enemy|MagicMissile)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
	Allegiance(Myself,ENEMY)
THEN DO
	Action(SpellMarker,WIZARD_MAGIC_MISSILE,ITEM_MM_SEEN)
END

///////////////////////////////////////////////////////////////////////////
///  ATTACK PHASE 7: Melfs and MMs & similar single-strike spells
///
///  Begin by hitting spellcasters with Melf
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(Indiscriminate)
	Allegiance(Myself,ENEMY)
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|Enemy)
THEN DO
	Action(Spell,WIZARD_MELF_ACID_ARROW|100|50)
END

//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs and Chromatic Orbs
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
    IgnoreBlock(Indiscriminate)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MinorGlobe|SpellTurn|Enemy|ResistElectric|SIEvocation)
THEN DO
	Action(SpellRESBoolean,WIZARD_MINOR_LIGHTNING_BOLT)
END
IF TRIGGER
    IgnoreBlock(Indiscriminate)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MinorGlobe|SpellTurn|Enemy|ResistCold|SIEvocation)
THEN DO
	Action(SpellRESBoolean,WIZARD_ICE_KNIFE)
END

IF TRIGGER
        IgnoreBlock(Indiscriminate)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MinorGlobe|SpellTurn|Enemy)
THEN DO
	Action(Spell,WIZARD_CHROMATIC_ORB)
	Action(Spell,WIZARD_LARLOCH_MINOR_DRAIN)
END

IF TRIGGER
        RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MinorGlobe|SpellTurn)
THEN DO
	Action(SpellRESBoolean,WIZARD_MINOR_LIGHTNING_BOLT)
	Action(Spell,WIZARD_CHROMATIC_ORB)
	Action(Spell,WIZARD_LARLOCH_MINOR_DRAIN)
	Action(Spell,WIZARD_ICE_KNIFE)
END

IF TRIGGER
        IgnoreBlock(Indiscriminate)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MinorGlobe|SpellTurn|Enemy|MagicMissile)
THEN DO
	Action(SpellMarker,WIZARD_MAGIC_MISSILE,ITEM_MM_SEEN)
END

IF TRIGGER
        RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MinorGlobe|SpellTurn|MagicMissile)
THEN DO
	Action(SpellMarker,WIZARD_MAGIC_MISSILE,ITEM_MM_SEEN)
END

IF TRIGGER
        IgnoreBlock(Indiscriminate)
	ConditionalTargetBlock(EnemiesInOrderShort;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|MR100|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn)
THEN DO
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END



IF TRIGGER
        RequireBlock(Indiscriminate)
	ConditionalTargetBlock(EnemiesInOrderShort;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn)
THEN DO
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END


