///////////////////////////////////////////////////////////////////////
////	MAGIC-STRIKE PHASE 2: attack specific magical defences
////
/////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////
////	Attack PM scrolls
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TriggerBlock(ProtectionFromMagicScroll|Enemy|Helpless)
THEN DO
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(vsPMscroll|200|50)
END

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(ProtectionFromMagicScroll|Helpless)
THEN DO
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(vsPMscroll|200|50)
END

////////////////////////////////////////////////////////////////////////////
////	Attack Spell Shield
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
       IgnoreBlock(Indiscriminate)
       TargetBlock(PCsInOrder)
       TriggerBlock(SpellShieldTarget|Enemy|Helpless|SRDetectInvisible)
       !GlobalTimerNotExpired("dropped_spell_shield","LOCALS")  // I'm not sure what this is doing here, but Chesterton's fence...
THEN DO
       Combine()
       SetGlobalTimer("dropped_spell_shield","LOCALS",12)
       Action(SpellArea,WIZARD_WARDING_WHIP)
       Action(SpellArea,WIZARD_SECRET_WORD)
       Action(SpellArea,WIZARD_SPELL_THRUST)
       Action(SpellArea,WIZARD_PIERCE_MAGIC)
       Action(SpellArea,WIZARD_RUBY_RAY_OF_REVERSAL)
END


////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(Indiscriminate)
        TargetBlock(PCsInOrder)
        TriggerBlock(SpellProtectionTargetSecretWord|Enemy|Helpless|SRDetectInvisible)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS") // the timestop timer is doing double duty here: it's also ensuring that we don't use "minor" antimagic more often than every four rounds
	/*slowspell*/
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",12)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Sequencer,BREACH_POWER_WORD_SILENCE_SECRET_WORD)
	Action(SpellArea,WIZARD_SECRET_WORD|100|100)
END

IF TRIGGER
        IgnoreBlock(Indiscriminate)
        TargetBlock(PCsInOrder)
	TriggerBlock(SpellProtectionTarget|Enemy|Helpless|SRDetectInvisible)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS") // the timestop timer is doing double duty here: it's also ensuring that we don't use "minor" antimagic more often than every four rounds
	/*slowspell*/
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",12)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(SpellArea,WIZARD_SPELL_STRIKE|100|100)
	Action(SpellArea,WIZARD_RUBY_RAY_OF_REVERSAL|100|100)
	Action(SpellArea,WIZARD_WARDING_WHIP|100|100)
END

IF TRIGGER
        IgnoreBlock(Indiscriminate)
        TargetBlock(PCsInOrder)
	TriggerBlock(SpellProtectionTargetSpellThrust|Enemy|Helpless|SRDetectInvisible)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS") // the timestop timer is doing double duty here: it's also ensuring that we don't use "minor" antimagic more often than every four rounds
	/*slowspell*/
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",12)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(SpellArea,WIZARD_SPELL_THRUST|100|100)
END

IF TRIGGER
        RequireBlock(Indiscriminate)
        TargetBlock(EnemiesInOrderShort)
        TriggerBlock(SpellProtectionTargetSecretWord|Enemy|Helpless|SRDetectInvisible)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS") // the timestop timer is doing double duty here: it's also ensuring that we don't use "minor" antimagic more often than every four rounds
	/*slowspell*/
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",12)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Sequencer,BREACH_POWER_WORD_SILENCE_SECRET_WORD)
	Action(SpellArea,WIZARD_SECRET_WORD|100|100)
END

IF TRIGGER
        RequireBlock(Indiscriminate)
        TargetBlock(EnemiesInOrderShort)
	TriggerBlock(SpellProtectionTarget|Enemy|Helpless|SRDetectInvisible)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS") // the timestop timer is doing double duty here: it's also ensuring that we don't use "minor" antimagic more often than every four rounds
	/*slowspell*/
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",12)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(SpellL9Area,WIZARD_SPELL_STRIKE|100|100)
	Action(SpellArea,WIZARD_RUBY_RAY_OF_REVERSAL|100|100)
	Action(SpellArea,WIZARD_WARDING_WHIP|100|100)
END

IF TRIGGER
        RequireBlock(Indiscriminate)
        TargetBlock(EnemiesInOrderShort)
	TriggerBlock(SpellProtectionTargetSpellThrust|Enemy|Helpless|SRDetectInvisible)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS") // the timestop timer is doing double duty here: it's also ensuring that we don't use "minor" antimagic more often than every four rounds
	/*slowspell*/
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",12)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(SpellArea,WIZARD_SPELL_THRUST|100|100)
END



/////////////////////////////////////////////////////////////////////////
////	Attack magic resistance
/////////////////////////////////////////////////////////////////////////


IF TRIGGER
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TargetBlock(Celestials)
	TriggerBlock(LowerMRTarget|Enemy|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	/*slowspell*/
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",12)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Sequencer,3X_LOWER_RESISTANCE)
	Action(Spell,WIZARD_LOWER_RESISTANCE|150|50)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(LowerMRTarget|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	/*slowspell*/
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",12)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Sequencer,3X_LOWER_RESISTANCE)
	Action(Spell,WIZARD_LOWER_RESISTANCE|150|50)
END
