////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
///  Priority 0: deal with things that make it impossible to function,
///  i.e. silence, spell failure, blindness
////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////



//////////////////////////////////////////////////////////////////////
///  Vocalize
////////////////////////////////////////////////////////////////////

IF TRIGGER
	StateCheck(Myself,STATE_SILENCED)
THEN DO
	Action(SpellMyself,WIZARD_VOCALIZE)
END

//////////////////////////////////////////////////////////////////////
///  Clear Insect Plague
////////////////////////////////////////////////////////////////////

IF TRIGGER
       CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
       CheckStat(Myself,0,WIZARD_FIRE_SHIELD)
       TriggerBlock(EasyPlus)
THEN DO
	Action(SequencerMyself,FIRE_SHIELD_BLUE_MINOR_SPELL_DEFLECTION_STONE_SKIN)
	Action(SequencerMyself,FIRE_SHIELD_RED_MINOR_GLOBE_OF_INVULNERABILITY_STONE_SKIN)
	Action(SequencerMyself,FIRE_SHIELD_RED_IMPROVED_INVISIBILITY_MINOR_SPELL_DEFLECTION)
        Action(SequencerMyself,FIRE_SHIELD_RED_MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_MINOR_SPELL_DEFLECTION_SPIDER_SPAWN)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION)
        Action(SequencerMyself,FIRE_SHIELD_RED_GLOBE_OF_INVULNERABILITY_SPELL_DEFLECTION)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_SPELL_DEFLECTION_SPELL_SHIELD)
        Action(SequencerMyself,FIRE_SHIELD_RED_SPELL_DEFLECTION_SPELL_IMMUNITY_ABJURATION)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_SPELL_DEFLECTION_SPELL_IMMUNITY_ABJURATION)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_FIRE_SHIELD_RED_PROTECTION_FROM_MAGIC_WEAPONS)
        Action(SequencerMyself,FIRE_SHIELD_RED_GLOBE_OF_INVULNERABILITY_PROTECTION_FROM_MAGIC_WEAPONS)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_IMMUNITY_ABJURATION)
        Action(SequencerMyself,FIRE_SHIELD_RED_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_TURNING)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_TURNING)
        SetGlobal("firedsequencer","LOCALS",1)
END

IF TRIGGER
       CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
       CheckStat(Myself,0,WIZARD_FIRE_SHIELD)
       TriggerBlock(EasyPlus)
THEN DO
        Action(SpellMyself,WIZARD_FIRE_SHIELD_RED)
	Action(SpellMyself,WIZARD_FIRE_SHIELD_BLUE)
END

IF TRIGGER
       RequireBlock(Demivrgvs)
       CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
       CheckStat(Myself,0,WIZARD_FIRE_SHIELD)
       TriggerBlock(EasyPlus)
THEN DO
        Action(SpellMyself,WIZARD_MESTILS_ACID_SHEATH)
END

//////////////////////////////////////////////////////////////////////
///  Clear Blindness
////////////////////////////////////////////////////////////////////

IF TRIGGER
	StateCheck(Myself,STATE_BLIND)
	RequireBlock(TruesightCuresBlindness)
THEN DO
	Action(SpellMyself,WIZARD_TRUE_SIGHT)
END

////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
///  Priority 1: consider Time Stop!
////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////
///  If you have timestop, consider a dispel very early in the sequence
////////////////////////////////////////////////////////////////////


IF TRIGGER
	SSLBoolean(WIZARD_TIME_STOP)
        IgnoreBlock(IsBG1Only)
        IgnoreBlock(Indiscriminate)
	OR(2)
		!GlobalTimerNotExpired("DMWW_dispel","LOCALS")
		HasItem("dw#dispe",Myself)
	TargetBlock(PCsInOrder)
	TriggerBlock(ProtectionFromMagicEnergy)
	TriggerBlock(AvoidDispel)
	TriggerBlock(SIAbjuration|Enemy)
	!CheckSpellState(Myself,TIME_STOP)
	TriggerBlock(CorePlus)
THEN DO
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(Sequencer,DEATH_SPELL_DISPEL_MAGIC_GREATER_MALISON)
	Action(SequencerManual,3X_DISPEL_MAGIC,DISPEL_MAGIC,DISPEL_MAGIC,DISPEL_MAGIC)
	Action(SequencerManual,2X_DISPEL_MAGIC_GREATER_MALISON,DISPEL_MAGIC,DISPEL_MAGIC,GREATER_MALISON)
	Action(SpellArea,WIZARD_DISPEL_MAGIC|100|150)
END

//////////////////////////////////////////////////////////////////////
///  main TS
///
///  the reason we check prep difficulty is: only fairly-well-protected
///  people can stand around waiting to raise TS
////////////////////////////////////////////////////////////////////


IF TRIGGER
	IgnoreBlock(IsLich)
        IgnoreBlock(IsBG1Only)
	TriggerBlock(HardPlusPrep)
	!CheckSpellState(Myself,TIME_STOP)
	OR(7)
		See(Player1) // i.e. don't waste timestop on riff-raff
		See(Player2)
		See(Player3)
		See(Player4)
		See(Player5)
		See(Player6)
		See([GOODCUTOFF.0.0.0.182])
	/*slowspell*/
THEN DO
	Action(SpellL9Myself,WIZARD_TIME_STOP|100|50)
	Wait(1)
END


IF TRIGGER
	RequireBlock(IsLich)
        IgnoreBlock(IsBG1Only)
	TriggerBlock(HardPlusPrep)
	!CheckSpellState(Myself,TIME_STOP)
	See(NearestEnemyOf(Myself)) // this isn't redundant: liches can See Players even when protected by Pro/Undead
	OR(7)
		See(Player1) // i.e. don't waste timestop on riff-raff
		See(Player2)
		See(Player3)
		See(Player4)
		See(Player5)
		See(Player6)
		See([GOODCUTOFF.0.0.0.182])
	/*slowspell*/
THEN DO
	Action(SpellL9Myself,WIZARD_TIME_STOP|100|50)
	Wait(1)
END

IF TRIGGER
	IgnoreBlock(IsLich)
        IgnoreBlock(IsBG1Only)
	TriggerBlock(CoreMinusPrep)
	!CheckSpellState(Myself,TIME_STOP)
	OR(7)
		See(Player1) // i.e. don't waste timestop on riff-raff
		See(Player2)
		See(Player3)
		See(Player4)
		See(Player5)
		See(Player6)
		See([GOODCUTOFF.0.0.0.182])
	/*slowspell*/
THEN DO
	Action(SpellL9Myself,WIZARD_TIME_STOP)
	Wait(1)
END


IF TRIGGER
	RequireBlock(IsLich)
        IgnoreBlock(IsBG1Only)
	TriggerBlock(CoreMinusPrep)
	!CheckSpellState(Myself,TIME_STOP)
	See(NearestEnemyOf(Myself)) // this isn't redundant: liches can See Players even when protected by Pro/Undead
	OR(7)
		See(Player1) // i.e. don't waste timestop on riff-raff
		See(Player2)
		See(Player3)
		See(Player4)
		See(Player5)
		See(Player6)
		See([GOODCUTOFF.0.0.0.182])
	/*slowspell*/
THEN DO
	Action(SpellL9Myself,WIZARD_TIME_STOP)
	Wait(1)
END

////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
///  Priority 2: keep up at least one attack-blocking defense. Wizards
///  are paper, and AC alone doesn't really cut it. Priority order
///  is pro/weapons > stoneskin > mirror image > invisibility, though
///  MI+blur sequencer trumps stoneskin alone
///
/// firstly use triggers, when we benefit from at least one of the other effects
/// then cast a spell
/// then if necessary use a trigger even when another part is redundant.
///
///
/// at this stage, fighter-mages with a Tenser's Transformation trigger should trigger it if
/// they don't have a better defensive plan
///
/// Don't do any of this in round 1
////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////

//// tenser

IF TRIGGER
        TriggerBlock(ILackWeaponProtection)
        !CheckSpellState(Myself,TIME_STOP)
        See(NearestEnemyOf(Myself))
        Global("instantprep","LOCALS",1)
        OR(2)
             HPPercentLT(Myself,50)
             !HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
        OR(2)
             HPPercentLT(Myself,50)
             !HaveSpell(WIZARD_STONE_SKIN)
THEN DO
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SPELL_DEFLECTION_TENSERS_TRANSFORMATION)
     Action(SequencerMyself,IMPROVED_HASTE_STONE_SKIN_TENSERS_TRANSFORMATION)
END


/// pro/mw, spell deflection

IF TRIGGER
        TriggerBlock(ILackWeaponProtection)
        !CheckSpellState(Myself,TIME_STOP)
        !CheckSpellState(Myself,SPELL_PROTECTION)
        !StateCheck(Myself,STATE_INVISIBLE)
        See(NearestEnemyOf(Myself))
        !GlobalTimerNotExpired("justdonepmw","LOCALS")
        Global("instantprep","LOCALS",1)
THEN DO
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SPELL_DEFLECTION_SPELL_SHIELD)
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SPELL_DEFLECTION_STONE_SKIN)
     Action(SequencerMyself,FIRE_SHIELD_RED_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_TURNING)
     Action(SequencerMyself,FIRE_SHIELD_BLUE_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_TURNING)
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SPELL_SHIELD_SPELL_TURNING)
     SetGlobal("firedsequencer","LOCALS",1)
     SetGlobalTimer("redefend","LOCALS",7)
     SetGlobalTimer("justdonepmw","LOCALS",7)
END

// pro/mw, improved invisibility

IF TRIGGER
        TriggerBlock(ILackWeaponProtection)
        !CheckSpellState(Myself,TIME_STOP)
        !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
        !StateCheck(Myself,STATE_INVISIBLE)
        See(NearestEnemyOf(Myself))
        !GlobalTimerNotExpired("justdonepmw","LOCALS")
        Global("instantprep","LOCALS",1)
THEN DO
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SHADOW_DOOR_SPELL_IMMUNITY_DIVINATION)
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SHADOW_DOOR_STONE_SKIN)
     Action(SequencerMyself,FIRE_SHIELD_RED_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_TURNING)
     SetGlobal("firedsequencer","LOCALS",1)
     SetGlobalTimer("redefend","LOCALS",7)
     SetGlobalTimer("justdonepmw","LOCALS",7)
END

// pro/mw, fire shield

IF TRIGGER
        TriggerBlock(ILackWeaponProtection)
        !CheckSpellState(Myself,TIME_STOP)
          CheckStat(Myself,0,WIZARD_FIRE_SHIELD)
        !StateCheck(Myself,STATE_INVISIBLE)
        See(NearestEnemyOf(Myself))
        !GlobalTimerNotExpired("justdonepmw","LOCALS")
        Global("instantprep","LOCALS",1)
THEN DO
     Action(SequencerMyself,FIRE_SHIELD_BLUE_FIRE_SHIELD_RED_PROTECTION_FROM_MAGIC_WEAPONS)
     Action(SequencerMyself,FIRE_SHIELD_RED_GLOBE_OF_INVULNERABILITY_PROTECTION_FROM_MAGIC_WEAPONS)
     Action(SequencerMyself,FIRE_SHIELD_BLUE_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_IMMUNITY_ABJURATION)
     SetGlobal("firedsequencer","LOCALS",1)
     SetGlobalTimer("redefend","LOCALS",7)
     SetGlobalTimer("justdonepmw","LOCALS",7)
END

// pro/mw spells, then triggers again

IF TRIGGER
        TriggerBlock(ILackWeaponProtection)
        !CheckSpellState(Myself,TIME_STOP)
        !StateCheck(Myself,STATE_INVISIBLE)
        See(NearestEnemyOf(Myself))
        !GlobalTimerNotExpired("justdonepmw","LOCALS")
        Global("instantprep","LOCALS",1)
THEN DO
        Action(SpellL9Myself,WIZARD_ABSOLUTE_IMMUNITY)
        Action(SpellMyself,WIZARD_IMPROVED_MANTLE)
        Action(SpellMyself,WIZARD_MANTLE)
        Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
        SetGlobalTimer("redefend","LOCALS",7)
        SetGlobalTimer("justdonepmw","LOCALS",7)
END


IF TRIGGER
        TriggerBlock(ILackWeaponProtection)
        !CheckSpellState(Myself,TIME_STOP)
        !StateCheck(Myself,STATE_INVISIBLE)
        See(NearestEnemyOf(Myself))
        !GlobalTimerNotExpired("justdonepmw","LOCALS")
        Global("instantprep","LOCALS",1)
THEN DO
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SPELL_DEFLECTION_SPELL_SHIELD)
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SPELL_DEFLECTION_STONE_SKIN)
     Action(SequencerMyself,FIRE_SHIELD_RED_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_TURNING)
     Action(SequencerMyself,FIRE_SHIELD_BLUE_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_TURNING)
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SPELL_SHIELD_SPELL_TURNING)
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SHADOW_DOOR_SPELL_IMMUNITY_DIVINATION)
     Action(SequencerMyself,PROTECTION_FROM_MAGIC_WEAPONS_SHADOW_DOOR_STONE_SKIN)
     Action(SequencerMyself,FIRE_SHIELD_RED_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_TURNING)
     Action(SequencerMyself,FIRE_SHIELD_BLUE_FIRE_SHIELD_RED_PROTECTION_FROM_MAGIC_WEAPONS)
     Action(SequencerMyself,FIRE_SHIELD_RED_GLOBE_OF_INVULNERABILITY_PROTECTION_FROM_MAGIC_WEAPONS)
     Action(SequencerMyself,FIRE_SHIELD_BLUE_PROTECTION_FROM_MAGIC_WEAPONS_SPELL_IMMUNITY_ABJURATION)
     SetGlobal("firedsequencer","LOCALS",1)
     SetGlobalTimer("redefend","LOCALS",7)
     SetGlobalTimer("justdonepmw","LOCALS",7)
END

// now onto Stoneskin

/// stoneskin, improved invisibility, spell deflection

IF TRIGGER
        See(NearestEnemyOf(Myself))
        OR(2)
             CheckStatLT(Myself,2,STONESKINS)
              !CheckSpellState(Myself,SPELL_PROTECTION)
        OR(2)
              CheckStatLT(Myself,2,STONESKINS)
              !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
        OR(2)
              !CheckSpellState(Myself,SPELL_PROTECTION)
              !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
        Global("instantprep","LOCALS",1)
THEN DO
        Action(SequencerMyself,IMPROVED_INVISIBILITY_MINOR_SPELL_DEFLECTION_STONE_SKIN|200|100)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
END

/// stoneskin, MGI, spell deflection

IF TRIGGER
        See(NearestEnemyOf(Myself))
        OR(2)
              CheckStatLT(Myself,2,STONESKINS)
              !CheckSpellState(Myself,SPELL_PROTECTION)
        OR(2)
              CheckStatLT(Myself,2,STONESKINS)
              CheckStat(Myself,0,MINORGLOBE)
        OR(2)
              !CheckSpellState(Myself,SPELL_PROTECTION)
              CheckStat(Myself,0,MINORGLOBE)
        Global("instantprep","LOCALS",1)
THEN DO
        Action(SequencerMyself,MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION_STONE_SKIN|200|100)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
END


// stoneskin, spell deflection

IF TRIGGER
        See(NearestEnemyOf(Myself))
        CheckStatLT(Myself,2,STONESKINS)
        !CheckSpellState(Myself,SPELL_PROTECTION)
        Global("instantprep","LOCALS",1)
THEN DO
        Action(SequencerMyself,GHOST_ARMOR_MINOR_SPELL_DEFLECTION_STONE_SKIN|200|100)
        Action(SequencerMyself,MINOR_SPELL_DEFLECTION_STONE_SKIN_TELEPORT_FIELD|200|100)
        Action(SequencerMyself,MINOR_SPELL_DEFLECTION_SHIELD_STONE_SKIN|200|100)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_MINOR_SPELL_DEFLECTION_STONE_SKIN|200|100)
        Action(SequencerMyself,MINOR_SPELL_DEFLECTION_SPIRIT_ARMOR_STONE_SKIN|200|100)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
END

// stoneskin, improved invisibility

IF TRIGGER
        See(NearestEnemyOf(Myself))
        CheckStatLT(Myself,2,STONESKINS)
        !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
        Global("instantprep","LOCALS",1)
THEN DO
        Action(SequencerMyself,IMPROVED_INVISIBILITY_MIRROR_IMAGE_STONE_SKIN|200|100)
        Action(SequencerMyself,IMPROVED_INVISIBILITY_STONE_SKIN_TELEPORT_FIELD|200|100)
        Action(SequencerMyself,IMPROVED_INVISIBILITY_SHIELD_STONE_SKIN|200|100)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
END

// now spells, and triggers again

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(ILackWeaponProtection) // I don't think there's really time to renew SS if you've already got strong defenses
        CheckStatLT(Myself,2,STONESKINS)
        !GlobalTimerNotExpired("redefend","LOCALS")
        Global("instantprep","LOCALS",1)
THEN DO
        Action(SequencerMyself,IMPROVED_INVISIBILITY_MINOR_SPELL_DEFLECTION_STONE_SKIN|200|100)
        Action(SequencerMyself,MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION_STONE_SKIN|200|100)
        Action(SequencerMyself,GHOST_ARMOR_MINOR_SPELL_DEFLECTION_STONE_SKIN|200|100)
        Action(SequencerMyself,MINOR_SPELL_DEFLECTION_STONE_SKIN_TELEPORT_FIELD|200|100)
        Action(SequencerMyself,MINOR_SPELL_DEFLECTION_SHIELD_STONE_SKIN|200|100)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_MINOR_SPELL_DEFLECTION_STONE_SKIN|200|100)
        Action(SequencerMyself,MINOR_SPELL_DEFLECTION_SPIRIT_ARMOR_STONE_SKIN|200|100)
        Action(SequencerMyself,IMPROVED_INVISIBILITY_MIRROR_IMAGE_STONE_SKIN|200|100)
        Action(SequencerMyself,IMPROVED_INVISIBILITY_STONE_SKIN_TELEPORT_FIELD|200|100)
        Action(SequencerMyself,IMPROVED_INVISIBILITY_SHIELD_STONE_SKIN|200|100)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
END

// on to mirror image

// MI + II

IF TRIGGER
        See(NearestEnemyOf(Myself))
        !StateCheck(Myself,STATE_MIRRORIMAGE)
        !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
        Global("instantprep","LOCALS",1)
THEN DO
        Action(SequencerMyself,BLUR_IMPROVED_INVISIBILITY_MIRROR_IMAGE)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
END



// MI-based sequencer

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(ILackWeaponProtection)
        CheckStatLT(Myself,2,STONESKINS)
        !StateCheck(Myself,STATE_MIRRORIMAGE)
        Global("instantprep","LOCALS",1)
THEN DO
        Action(SequencerMyself,INVISIBILITY_MIRROR_IMAGE)
        Action(SequencerMyself,BLUR_MIRROR_IMAGE)
        Action(SequencerMyself,MIRROR_IMAGE_SHIELD)
        Action(SequencerMyself,BLUR_IMPROVED_INVISIBILITY_MIRROR_IMAGE)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
END

// stoneskin only

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(ILackWeaponProtection) // I don't think there's really time to renew SS if you've already got strong defenses
        Range(NearestEnemyOf(Myself),5)
        CheckStatLT(Myself,2,STONESKINS)
        !GlobalTimerNotExpired("redefend","LOCALS")
        Global("instantprep","LOCALS",1)
THEN DO
        Action(SpellMyself,WIZARD_STONE_SKIN)
        SetGlobalTimer("redefend","LOCALS",7)
END

// MI only

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(ILackWeaponProtection) // I don't think there's really time to renew MI if you've already got strong defenses
        CheckStatLT(Myself,2,STONESKINS)
        !StateCheck(Myself,STATE_MIRRORIMAGE)
        Global("instantprep","LOCALS",1)
THEN DO
        Action(SpellMyself,WIZARD_MIRROR_IMAGE)
        SetGlobalTimer("redefend","LOCALS",7)
END

//// Full-on Invisibility in case I'm in big trouble and need to pull off

IF TRIGGER
        IgnoreBlock(Demivrgvs)
        TriggerBlock(CorePlus)
        IgnoreBlock(IsLich|IsVampire)
	HPPercentLT(Myself,75)
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(TruesightSafe)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellRESReplaceMyself,"dw#misld",WIZARD_MISLEAD)
	Action(SpellMyself,WIZARD_SHADOW_DOOR)
	Action(SequencerMyself,INVISIBILITY_MIRROR_IMAGE)
	Action(Potion,"potn10",@101010)
	Action(SpellMyself,WIZARD_INVISIBILITY)
END



IF TRIGGER
        RequireBlock(Demivrgvs)
        TriggerBlock(CorePlus)
        IgnoreBlock(IsLich|IsVampire)
        !Global("dvgldust","LOCALS",1)
	HPPercentLT(Myself,75)
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(TruesightSafe)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellRESReplaceMyself,"dw#misld",WIZARD_MISLEAD)
	Action(SpellMyself,WIZARD_SHADOW_DOOR)
	Action(SequencerMyself,INVISIBILITY_MIRROR_IMAGE)
	Action(Potion,"potn10",@101010)
	Action(SpellMyself,WIZARD_INVISIBILITY)
END

////////////////////////////////////////////////////////////////////////
/////	Interlude: if foes are close then definitely drink
/////	the damn defence potion!
////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
        TriggerBlock(CorePlus)
	Range(LastSeenBy(Myself),5)	
	!StateCheck(Myself,STATE_INVISIBLE)
	GlobalTimerExpired("redefend","LOCALS")
	Global("magedrinks","LOCALS",0)
THEN DO
	ActionCondition(Potion,"%tutu_var%potn11",@101011;CheckStatGT(Myself,0,ARMORCLASS)&Class(Myself,FIGHTER_MAGE))
	ActionCondition(Potion,"%tutu_var%potn24",@101024;CheckStatGT(Myself,0,ARMORCLASS))
        ActionCondition(Potion,"dw#ptn11",@101011;CheckStatGT(Myself,0,ARMORCLASS)&Class(Myself,FIGHTER_MAGE))
	ActionCondition(Potion,"dw#ptn24",@101024;CheckStatGT(Myself,0,ARMORCLASS))
	SetGlobal("magedrinks","LOCALS",1)
	SetGlobalTimer("redefend","LOCALS",7)
END

////////////////////////////////////////////////////////////////////////////
//// Interlude: Healing potions, but only if I'm safe
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsLich|IsVampire)
        TriggerBlock(CorePlus)
	HPPercentLT(Myself,100)
	OR(2)
		StateCheck(Myself,STATE_INVISIBLE)
		!See(NearestEnemyOf(Myself))
	OR(2)
		HPPercentLT(Myself,75)
		!See(NearestEnemyOf(Myself))
THEN DO
	Action(Potion,"potn55",@101055)
	Action(Potion,"potn52",@101052)
	Action(Potion,"extheal",@101052)
	ActionCondition(Potion,"potn08",@101008;OR(2)&!See(NearestEnemyOf(Myself))&CheckStatLT(Myself,11,Level))
END

////////////////////////////////////////////////////////////////////////////
//// Interlude: fighter types use improved haste
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
   See(NearestEnemyOf(Myself))
   GlobalTimerExpired("redefend","LOCALS")
   !StateCheck(Myself,STATE_HASTED)
        Global("instantprep","LOCALS",1)
THEN DO
     Action(SequencerMyself,GLOBE_OF_INVULNERABILITY_IMPROVED_HASTE_SPELL_SHIELD)
     Action(SequencerMyself,GLOBE_OF_INVULNERABILITY_IMPROVED_HASTE_SPELL_IMMUNITY_ABJURATION)
     Action(SequencerMyself,FIRE_SHIELD_BLUE_FIRE_SHIELD_RED_IMPROVED_HASTE)
     Action(SequencerMyself,IMPROVED_HASTE_SHADOW_DOOR_SPELL_DEFLECTION)
     Action(SequencerMyself,IMPROVED_HASTE_SHADOW_DOOR_SPELL_IMMUNITY_DIVINATION)
     SetGlobal("firedsequencer","LOCALS",1)
     SetGlobalTimer("redefend","LOCALS",7)
END


IF TRIGGER
   See(NearestEnemyOf(Myself))
   GlobalTimerExpired("redefend","LOCALS")
   !StateCheck(Myself,STATE_HASTED)
        Global("instantprep","LOCALS",1)
THEN DO
     Action(SpellMyself,WIZARD_IMPROVED_HASTE)
END

////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
///  Priority 3: keep up at least one protection against personal spell
///  attacks, i.e. *either* have Improved Invisibility, *or* Spell
///  Deflection / Spell Turning, running at all times
///
///  (though take more chances here)
////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////

// improved invisibility, spell deflection

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(TruesightSafe|EnemySpellcasterPresent)
	GlobalTimerExpired("redefend","LOCALS")
	!GlobalTimerNotExpired("impinv","LOCALS")
        !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
        !CheckSpellState(Myself,SPELL_PROTECTION)
THEN DO
        Action(SequencerMyself,IMPROVED_INVISIBILITY_MINOR_SPELL_DEFLECTION_SPIDER_SPAWN|200|100)
        Action(SequencerMyself,FIRE_SHIELD_RED_IMPROVED_INVISIBILITY_MINOR_SPELL_DEFLECTION|200|100)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
        SetGlobalTimer("impinv","LOCALS",13)
END

/// improved invisibility, (M)GI

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(TruesightSafe|EnemySpellcasterPresent)
	GlobalTimerExpired("redefend","LOCALS")
	!GlobalTimerNotExpired("impinv","LOCALS")
        !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
        CheckStat(Myself,0,MINORGLOBE)
THEN DO
        Action(SequencerMyself,GLOBE_OF_INVULNERABILITY_SHADOW_DOOR_SPELL_IMMUNITY_DIVINATION|200|100)
        Action(SequencerMyself,GLOBE_OF_INVULNERABILITY_SHADOW_DOOR_SPELL_SHIELD|200|100)
        Action(SequencerMyself,GLOBE_OF_INVULNERABILITY_SPELL_DEFLECTION_SPELL_SHIELD|200|100)
        Action(SequencerMyself,IMPROVED_INVISIBILITY_MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION|200|100)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
        SetGlobalTimer("impinv","LOCALS",13)
END

/// (M)GI, spell deflection

IF TRIGGER
        See(NearestEnemyOf(Myself))
	GlobalTimerExpired("redefend","LOCALS")
        !CheckSpellState(Myself,SPELL_PROTECTION)
        CheckStat(Myself,0,MINORGLOBE)
THEN DO
        Action(SequencerMyself,FIRE_SHIELD_RED_GLOBE_OF_INVULNERABILITY_SPELL_DEFLECTION|200|100)
        Action(SequencerMyself,BLADE_BARRIER_GLOBE_OF_INVULNERABILITY_SPELL_DEFLECTION|200|100)
        Action(SequencerMyself,GLOBE_OF_INVULNERABILITY_SPELL_DEFLECTION_SPELL_IMMUNITY_ABJURATION|200|100)
        Action(SequencerMyself,IMPROVED_INVISIBILITY_MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION|200|100)
        Action(SequencerMyself,FIRE_SHIELD_RED_MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION|200|100)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
        SetGlobalTimer("impinv","LOCALS",13)
END

// improved invisibility alone

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(TruesightSafe|EnemySpellcasterPresent)
	GlobalTimerExpired("redefend","LOCALS")
	!GlobalTimerNotExpired("impinv","LOCALS")
        !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN DO
         Action(SpellRESReplaceMyself,"dw#misld",WIZARD_MISLEAD|200|100)
         Action(SpellMyself,WIZARD_SHADOW_DOOR|200|100)
         Action(SpellMyself,WIZARD_IMPROVED_INVISIBILITY|200|100)
        SetGlobalTimer("redefend","LOCALS",7)
        SetGlobalTimer("impinv","LOCALS",13)
END

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(TruesightSafe|EnemySpellcasterPresent)
	GlobalTimerExpired("redefend","LOCALS")
	!GlobalTimerNotExpired("impinv","LOCALS")
        !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN DO
        Action(SequencerMyself,GLOBE_OF_INVULNERABILITY_SHADOW_DOOR_SPELL_IMMUNITY_DIVINATION|200|100)
        Action(SequencerMyself,GLOBE_OF_INVULNERABILITY_SHADOW_DOOR_SPELL_SHIELD|200|100)
        Action(SequencerMyself,GLOBE_OF_INVULNERABILITY_SPELL_DEFLECTION_SPELL_SHIELD|200|100)
        Action(SequencerMyself,IMPROVED_INVISIBILITY_MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION|200|100)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
        SetGlobalTimer("impinv","LOCALS",13)
END


// spell deflection alone

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(EnemySpellcasterPresent)
        GlobalTimerExpired("redefend","LOCALS")
	!GlobalTimerNotExpired("impinv","LOCALS")
	!CheckSpellState(Myself,SPELL_PROTECTION)
THEN DO
         Action(SequencerMyself,FIRE_SHIELD_BLUE_MINOR_SPELL_DEFLECTION_SPIDER_SPAWN|200|100)
         Action(SequencerMyself,FIRE_SHIELD_BLUE_SPELL_DEFLECTION_SPELL_SHIELD|200|100)
         Action(SequencerMyself,FIRE_SHIELD_RED_SPELL_DEFLECTION_SPELL_IMMUNITY_ABJURATION|200|100)
         Action(SequencerMyself,FIRE_SHIELD_BLUE_SPELL_DEFLECTION_SPELL_IMMUNITY_ABJURATION|200|100)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
END


IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(EnemySpellcasterPresent)
        GlobalTimerExpired("redefend","LOCALS")
	!GlobalTimerNotExpired("impinv","LOCALS")
	!CheckSpellState(Myself,SPELL_PROTECTION)
THEN DO
         Action(SpellL9Myself,WIZARD_SPELL_TRAP|200|100)
         Action(SpellMyself,WIZARD_SPELL_TURNING|200|100)
         Action(SpellMyself,WIZARD_SPELL_DEFLECTION|200|100)
         Action(SpellMyself,WIZARD_MINOR_SPELL_TURNING|200|100)
         Action(SpellMyself,WIZARD_MINOR_SPELL_DEFLECTION|200|100)
        SetGlobalTimer("redefend","LOCALS",7)
END

// spell deflection alone

IF TRIGGER
        See(NearestEnemyOf(Myself))
        TriggerBlock(EnemySpellcasterPresent)
        GlobalTimerExpired("redefend","LOCALS")
	!GlobalTimerNotExpired("impinv","LOCALS")
	!CheckSpellState(Myself,SPELL_PROTECTION)
THEN DO
        Action(SequencerMyself,FIRE_SHIELD_RED_GLOBE_OF_INVULNERABILITY_SPELL_DEFLECTION|200|100)
        Action(SequencerMyself,GLOBE_OF_INVULNERABILITY_SPELL_DEFLECTION_SPELL_IMMUNITY_ABJURATION|200|100)
        Action(SequencerMyself,IMPROVED_INVISIBILITY_MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION|200|100)
        Action(SequencerMyself,FIRE_SHIELD_RED_MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION|200|100)
        Action(SequencerMyself,FIRE_SHIELD_BLUE_MINOR_GLOBE_OF_INVULNERABILITY_MINOR_SPELL_DEFLECTION)
     SetGlobal("firedsequencer","LOCALS",1)
        SetGlobalTimer("redefend","LOCALS",7)
END



////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
///  Priority 4: keep a (minor) globe running, if there's good reason to
///
////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////

IF TRIGGER
        See(NearestEnemyOf(Myself))
        GlobalTimerExpired("redefend","LOCALS")
        TriggerBlock(NeedMinorGlobe)
THEN DO
     Action(SpellMyself,WIZARD_GLOBE_OF_INVULNERABILITY|100|100)
     Action(SpellMyself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY|100|100)
     SetGlobalTimer("redefend","LOCALS",7)
END


////////////////////////////////////////////////////////////////////////
/// Priority 5: Use fire-shield-type and armour-type spells if not already in place
////////////////////////////////////////////////////////////////////////


IF TRIGGER
       CheckStat(Myself,0,WIZARD_FIRE_SHIELD)
       TriggerBlock(EasyPlus)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
        Action(SpellMyself,WIZARD_FIRE_SHIELD_RED|70|30)
	Action(SpellMyself,WIZARD_FIRE_SHIELD_BLUE|70|30)
	SetGlobalTimer("redefend","LOCALS",7)
END

IF TRIGGER
       RequireBlock(Demivrgvs)
       CheckStat(Myself,0,WIZARD_FIRE_SHIELD)
       TriggerBlock(EasyPlus)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
        Action(SpellMyself,WIZARD_MESTILS_ACID_SHEATH|70|30)
	SetGlobalTimer("redefend","LOCALS",7)
END

IF TRIGGER
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),8)
	CheckStatGT(Myself,1,ARMORCLASS)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellMyself,WIZARD_SPIRIT_ARMOR|70|30)
	SetGlobalTimer("redefend","LOCALS",7)
END

IF TRIGGER
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),8)
	CheckStatGT(Myself,2,ARMORCLASS)
	GlobalTimerExpired("redefend","LOCALS")
	!HaveSpell(WIZARD_SPIRIT_ARMOR)
THEN DO
	Action(SpellMyself,WIZARD_GHOST_ARMOR|70|30)
	SetGlobalTimer("redefend","LOCALS",7)
END

IF TRIGGER
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),8)
	CheckStatGT(Myself,4,ARMORCLASS)
	GlobalTimerExpired("redefend","LOCALS")
	!HaveSpell(WIZARD_SPIRIT_ARMOR)
	!HaveSpell(WIZARD_GHOST_ARMOR)
THEN DO
	Action(SpellMyself,WIZARD_SHIELD|70|30)
	SetGlobalTimer("redefend","LOCALS",7)
END

////////////////////////////////////////////////////////////////////////
/// Priority 6: True Strike if not active (DV only)
////////////////////////////////////////////////////////////////////////

IF TRIGGER
       RequireBlock(Demivrgvs)
       See(NearestEnemyOf(Myself))
       Range(NearestEnemyOf(Myself),10)
       !CheckSpellState(Myself,TRUE_STRIKE)
THEN DO
     Action(SpellMyself,WIZARD_TRUE_STRIKE|100|30)
END

