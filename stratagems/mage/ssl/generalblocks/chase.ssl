////////////////////////////////////////////////////////////////////////////////////////////////////
//	No sign of hostiles? If the fight is going on anyway, then it's fair enough to have a look
// 	for them.
/////////////////////////////////////////////////////////////////////////////////////////////////////

IF
  !Global("chasing","LOCALS",0)
  See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
    	     SetGlobal("chasing","LOCALS",0)
    	     Continue()
END


// if subsequently I can't see an enemy, chase previously targetted PC

IF
   SSLBoolean(WIZARD_DIMENSION_DOOR)
   ActionListEmpty()
   Global("inafight","LOCALS",1)
   Allegiance(Myself,ENEMY)
   !See(NearestEnemyOf(Myself))
   Global("chasing","LOCALS",0)
   !Global("chasePC","LOCALS",0)
THEN
    RESPONSE #100
             SetGlobalTimer("chasetimer","LOCALS",8)
             SetGlobal("chasing","LOCALS",1)
END

IF
   !SSLBoolean(WIZARD_DIMENSION_DOOR)
   ActionListEmpty()
   Global("inafight","LOCALS",1)
   Allegiance(Myself,ENEMY)
   !See(NearestEnemyOf(Myself))
   Global("chasing","LOCALS",0)
   !Global("chasePC","LOCALS",0)
THEN
    RESPONSE #100
             SetGlobalTimer("chasetimer","LOCALS",15)
             SetGlobal("chasing","LOCALS",1)
END

BEGIN LOOP(scsloopvar||1;2;3;4;5;6)


IF
	!GlobalTimerNotExpired("teleportpursuetimer","LOCALS")
	GlobalTimerExpired("chasetimer","LOCALS")
	Global("chasePC","LOCALS",scsloopvar)
	!Detect([PC])
	SSLBoolean(WIZARD_DIMENSION_DOOR)
	HaveSpell(WIZARD_DIMENSION_DOOR)
	!Range(Playerscsloopvar,30)
	!StateCheck(Myself,STATE_INVISIBLE)
	!HasItem("dw#nodor",Myself)

THEN
	RESPONSE #100
	    SetGlobalTimer("castspell","LOCALS",6)
		ReallyForceSpell(Playerscsloopvar,WIZARD_DIMENSION_DOOR)
		RemoveSpell(WIZARD_DIMENSION_DOOR)
		SetGlobalTimer("teleportpursuetimer","LOCALS",18)
		SetGlobal("chasePC","LOCALS",0)
END

IF TRIGGER
                !SSLBoolean(WIZARD_DIMENSION_DOOR)
                ActionListEmpty()
                !See(NearestEnemyOf(Myself))
		GlobalTimerNotExpired("chasetimer","LOCALS")
		Global("chasePC","LOCALS",scsloopvar)
  THEN DO
                Action(FindIfOffscreen,Playerscsloopvar)
END

IF
	!See(NearestEnemyOf(Myself))
	GlobalTimerExpired("chasetimer","LOCALS")
	!Global("inafight","LOCALS",0)
	!Global("chasePC","LOCALS",0)
	THEN
		RESPONSE #100
			SetGlobal("inafight","LOCALS",0)
			SetGlobal("chasePC","LOCALS",0)
END


IF TRIGGER
                SSLBoolean(WIZARD_DIMENSION_DOOR)
                ActionListEmpty()
                !See(NearestEnemyOf(Myself))
	        !GlobalTimerNotExpired("castspell","LOCALS")
		GlobalTimerNotExpired("chasetimer","LOCALS")
		Global("chasePC","LOCALS",scsloopvar)
		OR(5)
		     !HaveSpell(WIZARD_DIMENSION_DOOR)
		     Range(Playerscsloopvar,30)
		     StateCheck(Myself,STATE_INVISIBLE)
		     HasItem("dw#nodor",Myself)
	             GlobalTimerNotExpired("teleportpursuetimer","LOCALS")
  THEN DO
                Action(FindIfOffscreen,Playerscsloopvar)
END

END LOOP



// closedown on Easiest

IF TRIGGER
   TriggerBlock(Easiest)
THEN DO
     Action(Literal)
END
