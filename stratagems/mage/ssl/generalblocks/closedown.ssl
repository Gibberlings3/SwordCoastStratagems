//////////////////////////////////////////////////////////////////////////
///	At this point we put flame arrows, magic missiles and Melfs into PCs
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PlayersInOrder)
	Allegiance(Myself,ENEMY)
	TriggerBlock(MR100|MinorGlobe)
	OR(2)  // avoids edge cases where mage targets himself
	     !Allegiance(scstarget,ENEMY)
	     StateCheck(scstarget,STATE_CHARMED)
THEN DO
        Combine()
	Action(Spell,WIZARD_FLAME_ARROW)
	Action(Spell,WIZARD_MELF_ACID_ARROW)
	Action(Spell,WIZARD_CHROMATIC_ORB)
	Action(SpellMarker,WIZARD_MAGIC_MISSILE,ITEM_MM_SEEN)
END

//////////////////////////////////////////////////////
////	If we get this far, try being less picky for area-effect spells
//////////////////////////////////////////////////////

IF
!GlobalTimerNotExpired("castspell","LOCALS")
!GlobalTimerNotExpired("targetcompromise","LOCALS")
See(NearestEnemyOf(Myself))
	THEN
	RESPONSE #100
		SetGlobalTimer("targetcompromise","LOCALS",6)
END

//////////////////////////////////////////////////////
///	If there are no active players, and you don't have any spells to kill them with, kill things in melee
//////////////////////////////////////////////////////
	
IF
	!GlobalTimerNotExpired("castspell","LOCALS")
        See(NearestEnemyOf(Myself))
	OR(2)
		Class(Myself,MAGE)
		Class(Myself,MAGE_THIEF)
	BEGIN LOOP(scsloopvar||1;2;3;4;5;6)
		OR(3)
			!See(Playerscsloopvar)
			StateCheck(Playerscsloopvar,STATE_IMMOBILE)
			CheckStatGT(Playerscsloopvar,0,HELD)
	END LOOP
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

//////////////////////////////////////////////////////
////	If we get this far, we can't find anything to do, so
////	set the timer (this means we attack in the gap) 
//////////////////////////////////////////////////////

IF
!GlobalTimerNotExpired("castspell","LOCALS")
See(NearestEnemyOf(Myself))
	THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
END

//////////////////////////////////////////////////////
////	If we get THIS far, block other scripts (only
////    reason for this atm is mirrors of opposition)
//////////////////////////////////////////////////////

IF
  ActionListEmpty()
THEN
    RESPONSE #100
        NoAction()
END



