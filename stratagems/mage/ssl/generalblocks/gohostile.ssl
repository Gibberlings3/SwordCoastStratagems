/// strictly the "go hostile" block, but here we incorporate various
/// other combat-related things to be done before sighting the foe

//////////////////////////////////////////////////////////////////
///////   Top of chase block
//////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/genai/ssl/chase_top.ssl)

//////////////////////////////////////////////////////////////////
///////    Record created-in-sight status
//////////////////////////////////////////////////////////////////

IF TRIGGER
    IgnoreBlock(StartsInSight)
    Global("created_out_of_sight","LOCALS",0)
    OR(2)
        !Allegiance(Myself,ENEMY)
        !See(NearestEnemyOf(Myself))
THEN DO
      Action(Literal)
      SetGlobal("created_out_of_sight","LOCALS",1)
      Continue()
END

//////////////////////////////////////////////////////////////////
///////    Go hostile 
//////////////////////////////////////////////////////////////////

IF TRIGGER
    !Allegiance(Myself,ENEMY)
    TriggerBlock(AttackedByPlayer)
    IgnoreBlock(NeverGoHostile)
THEN DO
      Action(Literal)
      Enemy()
      Continue()
END

///////////////////////////////////////////////////
////	If attacked by an unseen foe, mill around
////	in confusion. You never know, you might
////	bump into them!
//////////////////////////////////////////////////

IF TRIGGER
      Allegiance(Myself,ENEMY)
      RequireBlock(EasyPlus)
    OR(12)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
      SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
      SpellCastOnMe([PC],0)
      SpellCastOnMe([FAMILIAR],0)
      SpellCastOnMe([ALLY],0)
      SpellCastOnMe([CONTROLLED],0)
      SpellCastOnMe([CHARMED],0) 
!GlobalTimerNotExpired("whatthe","LOCALS")
  !See(NearestEnemyOf(Myself))
THEN DO
    Action(Literal)
    SetGlobalTimer("whatthe","LOCALS",12)
    SetGlobal("inafight","LOCALS",1)
    Continue()
END

IF TRIGGER
  RequireBlock(EasyPlus)
  !See(NearestEnemyOf(Myself))
  !Heard([ANYONE],404)
  GlobalTimerNotExpired("whatthe","LOCALS")
THEN DO
    Action(Literal)
    RandomWalkContinuous()
END

/////////////////////////////////////////////////////////////////
///	Handle Cloud Kill spells
/////////////////////////////////////////////////////////////////

IF
	Heard([ANYONE],404)
	!General(Myself,UNDEAD)
	!Race(Myself,RAKSHASA)
	!Race(Myself,DEMONIC)
	!See([PC]) 
	Range(LastHeardBy(Myself),20)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		RunAwayFromNoLeaveArea(LastHeardBy(Myself),45)
		SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////
///	Block to prevent neutrals prebuffing
/////////////////////////////////////////////////////////////////

IF	
	!Allegiance(Myself,ENEMY)
	IgnoreBlock(IsPartyAlly)
	!Global("DMWWNeutralPrebuff","GLOBAL",1)
THEN
	RESPONSE #100
		NoAction()
END

