BEGIN_ACTION_DEFINITION
	Name(FakePrebuff)
	TRIGGER
	        OnCreation()
	ACTION
		RESPONSE #scsprob1
		ApplySpellRES("%WIZARD_scsargument1_VISUALS_ONLY%",Myself)
		Continue()
END

BEGIN_ACTION_DEFINITION
	Name(FakeBuff)
	TRIGGER
	        GlobalTimerExpired("castspell","LOCALS")
	        !GlobalTimerNotExpired("rebuff","LOCALS")
	ACTION
		RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("rebuff","LOCALS",18)
		ForceSpellRES("%WIZARD_scsargument1_VISUALS_ONLY%",Myself)
		RESPONSE #200
		Continue()
END

BEGIN_ACTION_DEFINITION
	Name(FakeSpell)
	TRIGGER
	        !GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpellRES("%WIZARD_scsargument1_VISUALS_ONLY%",scstarget)
END

DEFAULT TRIGGER(Allegiance(Myself,ENEMY))

IF
  OnCreation()
THEN
    RESPONSE #100
             SetGlobalTimer("rebuff","LOCALS",18)
             Continue()
    RESPONSE #100
             SetGlobalTimer("rebuff","LOCALS",12)
             Continue()
    RESPONSE #100
             SetGlobalTimer("rebuff","LOCALS",24)
             Continue()
END

IF
THEN
    RESPONSE #100
             ReallyForceSpellRES("dw#hdani",LastSummonerOf(Myself))
             Continue()
END

IF TRIGGER
THEN DO
        Action(FakePrebuff,SPELL_TURNING|100|100)
        Action(FakePrebuff,MINOR_GLOBE_OF_INVULNERABILITY|100|100)
        Action(FakePrebuff,GHOST_ARMOR|100|100)
END

IF TRIGGER
       CheckStat(Myself,0,MINORGLOBE)
THEN DO
       Action(FakeBuff,MINOR_GLOBE_OF_INVULNERABILITY|100|100)
END

IF TRIGGER
THEN DO
     Action(FakeBuff,PROTECTION_FROM_MAGIC_WEAPONS|100|200)
END

IF TRIGGER
THEN DO
     Action(FakeBuff,IMPROVED_MANTLE|100|200)
END

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   Range(scstarget,15)
THEN DO
     Action(FakeSpell,SUN_FIRE|100|200)
     Action(FakeSpell,CONE_OF_COLD|100|200)
END

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   !CheckStatGT(LastSummonerOf(Myself),15,LEVEL)
THEN DO
     Action(FakeSpell,DISPEL_MAGIC|100|300)
     Action(FakeSpell,DOMINATION|100|200)
     Action(FakeSpell,CHAOS|100|100)
     Action(FakeSpell,FIREBALL)
END

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   CheckStatGT(LastSummonerOf(Myself),15,LEVEL)
THEN DO
     Action(FakeSpell,ABI_DALZIMS_HORRID_WILTING|100|400)
     Action(FakeSpell,SYMBOL_STUN|100|350)
     Action(FakeSpell,SYMBOL_DEATH|100|300)
     Action(FakeSpell,DISPEL_MAGIC|100|250)
     Action(FakeSpell,FINGER_OF_DEATH|100|200)
     Action(FakeSpell,DOMINATION|100|150)
     Action(FakeSpell,CHAOS|100|100)
     Action(FakeSpell,POWER_WORD_BLIND)
END

IF
  Range(NearestEnemyOf(Myself),10)
THEN
    RESPONSE #100
       RandomWalkContinuous()
END

IF
  Range(NearestEnemyOf(Myself),20)
THEN
    RESPONSE #100
             RandomWalk()
END


