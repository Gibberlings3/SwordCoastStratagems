IF
	See([PC])
	NumTimesTalkedTo(0)
THEN
	RESPONSE #100
		StartDialog("%tutu_var%ANDRIS",[PC])
END

IF
	Global("DMWWAndris","LOCALS",0)
        !StateCheck(Myself,STATE_REALLY_DEAD)
	OR(4)
		Dead("beyn")
		HPPercentLT(Myself,90)
		StateCheck(Myself,STATE_DISABLED)
		StateCheck(Myself,STATE_SILENCED)
	OR(4)
		Dead("marcel")
		HPPercentLT(Myself,90)
		StateCheck(Myself,STATE_DISABLED)
		StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
		SetGlobal("DMWWAndris","LOCALS",1)
		ReallyForceSpellPointRES("dw#andri",[710.250])
END

IF
	Global("DMWWAndris","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobalTimer("AndrisTimer","LOCALS",36)
		SetGlobal("DMWWAndris","LOCALS",2)
END

IF
	GlobalTimerNotExpired("AndrisTimer","LOCALS")
	!Detect(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END

IF
	Global("DMWWAndris","LOCALS",2)
	GlobalTimerExpired("AndrisTimer","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("DMWWAndris","LOCALS",3)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		ApplySpell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
		ApplySpell(Myself,WIZARD_MIRROR_IMAGE)
		ApplySpell(Myself,WIZARD_SHIELD)
		ApplySpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
		DestroyItem("minhp1")
		Wait(1)
		ForceSpell(Player1,WIZARD_DIMENSION_DOOR)
END
