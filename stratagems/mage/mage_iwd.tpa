// this gets called from within mage.tpa

DEFINE_ACTION_FUNCTION mage_iwd STR_VAR version=0 BEGIN

   LAM read_in_iwd_mage_scripts
   ACTION_IF (version=0 || version=2) BEGIN
       LAF mage_edits_before_iwd END
   END
   ACTION_IF (version=0 || version=3) BEGIN
       LAF mage_edits_area_iwd END
       LAF mage_edits_creature_iwd END
   END
   ACTION_IF (version=0 || version=4) BEGIN
       LAF mage_edits_after_iwd END
   END




END

DEFINE_ACTION_FUNCTION mage_edits_before_iwd BEGIN

         // Malavon's attack scripting doesn't need to be broken into three pieces
         // Assume Malavon is immune to the effects of his Corrosive Fog spell

         LAF edit_creature STR_VAR creature=malavon editstring="strip_script=>~ldmalat1 ldmalat2~ immunity_to_spell=>MALAVONS_CORROSIVE_FOG" END

         // Alpheus's (broken) teleport routine is in any case a bad idea, since it wastes vital time

         LAF edit_creature STR_VAR creature=alpheus editstring="strip_script=>lwalpwg" END
         
         // ditto the Luremaster, I think

        LAF swap_are_cre_script STR_VAR area=ar9711 creature=trgbard script="dulurwrp=>null" END
        
        // skeletal mages should be straight mages, not f/m
        
        LAF edit_creature STR_VAR creature=ktsmg2 editstring="class=>mage" END
        
        // move some mage scripts to the bottom of the ordering

        MAKE_PATCH
            match=>"actor_resource=presapp"
            move_script_to_bottom=>d3preapp
        END
        LAF edit_area STR_VAR area=ar4003 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
            match=>"actor_resource=eldathl"
            move_script_to_bottom=>d4libr
        END
        LAF edit_area STR_VAR area=ar4004 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           match=>"actor_resource=sdelfwz1"
           move_script_to_bottom=>shshdwz1
        END
        LAF edit_area STR_VAR area=ar5302 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           match=>"actor_x_coord_start=1437"
            move_script_to_bottom=>uddrow3
        END
        LAF edit_area STR_VAR area=ar6002 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           match=>"actor_x_coord_start=1448 or actor_x_coord_start=1329 or actor_x_coord_start=1567"
           move_script_to_bottom=>uddrwsrd
        END
        LAF edit_area STR_VAR area=ar6002 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           match=>"actor_resource=kontik"
           move_script_to_bottom=>wtkontik
        END
        LAF edit_area STR_VAR area=ar7004 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           match=>"actor_resource=jklsum"
           swap_scripts=>"efrndw03=>null"
           move_script_to_bottom=>"cajsum1 cajsum2 cajsum3"
        END
        LAF edit_area STR_VAR area=ar9800 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           match=>"actor_resource=hrpinf"
           move_script_to_bottom=>"cainfhrp"
        END
        LAF edit_area STR_VAR area=ar9801 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           match=>"actor_resource=hrpmat"
           swap_script=>"dwmesvge=>null"
           move_script_to_bottom=>"neqhrpa"
        END
        LAF edit_area STR_VAR area=ar9800 editstring="patch_actor=>patch_data" END


        MAKE_PATCH
           sdelfwzh=>shshdwz1
           rcopper=>d4mage
           ilair=>ldilair
           tradran=>duadran
           trsph8=>duhero8
        END
        ACTION_PHP_EACH patch_data AS creature=>script BEGIN
           LAF edit_creature STR_VAR creature editstring="strip_script=>%script% insert_script_low=>%script%" END
        END

        MAKE_PATCH
           match=>"actor_resource=adran"
           move_script_to_bottom=>seadran
        END
        LAF edit_area STR_VAR area=ar9708 editstring="patch_actor=>patch_data" END

        // unneeded scripts
        MAKE_PATCH
           match=>"actor_resource=sdelfwz1"
           swap_scripts=>"shshdwz2=>null"
           swap_scripts'=>"shshdwz3=>null"
        END
        LAF edit_area STR_VAR area=ar5104 editstring="patch_actor=>patch_data" END

        // individuate the drow spellcasters (so we can give them individual instructions)

        MAKE_PATCH
           match=>"actor_x_coord_start is_in [2287 1437]"
           actor_resource=>DW2SOR
        END
        LAF edit_area STR_VAR area=ar6002 editstring="patch_actor=>patch_data" END
        COPY_EXISTING drowsor.cre "override/dw2sor.cre"
        MAKE_PATCH
           match=>"actor_x_coord_start is_in [730 823 2085 2407 1448 1567]"
           actor_resource=>DW2SSW
        END
        LAF edit_area STR_VAR area=ar6002 editstring="patch_actor=>patch_data" END
        COPY_EXISTING drowspe.cre "override/dw2ssw.cre"

        // individuate various script-summoned creatures
        
        // yuan-ti mages
        OUTER_SET yuan_counter=1
        COPY_EXISTING
         ar4004.bcs override
         ar4005.bcs override
           LPF unique_iwd_script_summons INT_VAR counter_start=yuan_counter STR_VAR original=yuanmag new_prefix=dwyumag RET yuan_counter=counter_end END
        BUT_ONLY
        // Kraken society mages
        COPY_EXISTING ar8007.bcs override
           LPF unique_iwd_script_summons INT_VAR counter_start=1 STR_VAR original=krakmag new_prefix=dwkrmag  END
        BUT_ONLY

        // largely-cosmetic tweaks to jackalwere fiend summoning
        
        MAKE_PATCH
           name1_string=>36712
           patch_ability_inline=>"casting_time=>8"
        END
        LAF edit_spell STR_VAR spell=INNATE_SUMMON_GLABREZU edits=patch_data END

        // misc
        
         LAF install STR_VAR file=d4maratk.baf location=resource END

END

DEFINE_ACTION_FUNCTION mage_edits_after_iwd BEGIN

END

DEFINE_ACTION_FUNCTION mage_edits_area_iwd BEGIN

        COPY_EXISTING_REGEXP GLOB ".*\.are" override
          SPRINT areaname "%SOURCE_RES%"
          GET_OFFSET_ARRAY act_arr ARE_V10_ACTORS
          PHP_EACH act_arr AS act_ind=>act_off BEGIN
             READ_ASCII (0x80+act_off) filename_old
             READ_ASCII act_off actor_name (0x20)
             SET match=0
             FOR (script_off=0x58+act_off;script_off<0x80+act_off;script_off+=8) BEGIN
                READ_ASCII script_off script_old
                TO_LOWER script_old
                PATCH_IF (match && "%script_old%" STRING_COMPARE "") BEGIN
                    LPF warning STR_VAR warning="script %script_old% below mage script in area %areaname%, actor %actor_name% (%filename_old%) " END
                END ELSE BEGIN
                PATCH_IF VARIABLE_IS_SET $mage_scripts_iwd("%script_old%") BEGIN
                  SET match=1
				  READ_ASCII act_off+0x80 actor_resref
				  LPF log_this INT_VAR repeat=0 STR_VAR file=creatures_with_potions.txt arguments="%actor_resref%" END
                  PATCH_PRINT "Determining spells for %actor_name% (area %areaname%)"
                  INNER_ACTION BEGIN
                   LAF mage_iwd_area_main STR_VAR script_old filename_old RET script filename END
                  END
                  PATCH_IF "%script%" STRING_COMPARE_CASE "" BEGIN
                   WRITE_ASCIIE script_off "%script%" (8)
                   WRITE_ASCIIE (0x80+act_off) "%filename%" (8)
                  END
                 END // end of VARIABLE_IS_SET loop
                END // end of match loop
             END
          END

        BUT_ONLY

END

DEFINE_ACTION_FUNCTION mage_edits_creature_iwd BEGIN

         COPY_EXISTING_REGEXP GLOB ".*\.cre" override
             SET match=0
             FOR (script_off=0x248;script_off<0x270;script_off+=8) BEGIN
                READ_ASCII script_off script_old
                TO_LOWER script_old
                PATCH_IF (match && "%script_old%" STRING_COMPARE "") BEGIN
                    LPF warning STR_VAR warning="script %script_old% below mage script in CRE file %SOURCE_RES%" END
                END ELSE BEGIN
                PATCH_IF VARIABLE_IS_SET $mage_scripts_iwd("%script_old%") BEGIN
                  SET match=1
				  LPF log_this INT_VAR repeat=0 STR_VAR file=creatures_with_potions.txt arguments="%SOURCE_RES%" END
                  PATCH_PRINT "Determining spells for creature file %SOURCE_RES%, script %script_old%"
                  PATCH_IF !FILE_EXISTS "%data_loc%/caster_scripts_built.txt" || !FILE_CONTAINS "%data_loc%/caster_scripts_built.txt" "%script_old%" BEGIN
                      SPRINT script "%script_old%"
                      LPF log_this STR_VAR file=caster_scripts_built.txt input="%script_old%" END
                  END ELSE BEGIN
                      LPF dw_unique_name STR_VAR old_name="%script_old%" suffix=bcs RET script=name END
					  WRITE_ASCII script_off "%script%" (8)
                  END
				  PATCH_WITH_SCOPE BEGIN
					SPRINT filename "%SOURCE_RES%"
					LPF enforce_mage STR_VAR script END
				  END
                  INNER_ACTION BEGIN
                      ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%component_loc%/iwd/top/%script_old%.baf" BEGIN
                           LAF extend STR_VAR script top="%script_old%" location="iwd/top" END
                      END
                      ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%component_loc%/iwd/top/%script_old%.ssl" BEGIN
                           LAF extend INT_VAR ssl=1 STR_VAR script top="%script_old%" location="iwd/top"  END
                      END
                  END
                 END // end of VARIABLE_IS_SET loop
                END // end of match loop
             END

         BUT_ONLY
END

DEFINE_ACTION_FUNCTION mage_iwd_area_main
   STR_VAR script_old=""
           filename_old=""
   RET script
       filename
BEGIN
   ACTION_TO_LOWER filename_old
   ACTION_TO_LOWER script_old
   ACTION_IF !FILE_EXISTS "%data_loc%/caster_scripts_built.txt" || !FILE_CONTAINS "%data_loc%/caster_scripts_built.txt" "%script_old%" BEGIN
      OUTER_SPRINT script "%script_old%"
      LAF log_this STR_VAR file=caster_scripts_built.txt input="%script_old%" END
   END ELSE BEGIN
      LAF dw_unique_name STR_VAR old_name="%script_old%" suffix=bcs RET script=name END
   END
   ACTION_IF FILE_EXISTS "%workspace%/%filename_old%_template.cre" BEGIN 
      LAF dw_unique_name STR_VAR old_name="%filename_old%" suffix=cre RET filename=name END
      COPY "%workspace%/%filename_old%_template.cre" "override/%filename%.cre"
   END ELSE BEGIN
      COPY_EXISTING "%filename_old%.cre" "%workspace%/%filename_old%_template.cre"
      OUTER_SPRINT filename "%filename_old%"
   END

   COPY_EXISTING "%filename%.cre" "override"
	     PATCH_WITH_SCOPE BEGIN
			SPRINT filename "%filename_old%"
			LPF enforce_mage STR_VAR script END
		 END
   BUT_ONLY
   ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%component_loc%/iwd/top/%script_old%.baf" BEGIN
      LAF extend STR_VAR script top="%script_old%" location="iwd/top" END
   END
   ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%component_loc%/iwd/top/%script_old%.ssl" BEGIN
      LAF extend INT_VAR ssl=1 STR_VAR script top="%script_old%" location="iwd/top" END
   END
END

/////////////////////////////////////////////////////////////////////////////
/// collect the lists of mage scripts
/////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_MACRO read_in_iwd_mage_scripts BEGIN

     ACTION_IF !VARIABLE_IS_SET iwd_mage_scripts_read_in BEGIN
           OUTER_SPRINT iwd_mage_scripts_read_in ""
           LAF read_in_list STR_VAR file=mage_scripts.txt location=iwd force_case=lower RET_ARRAY mage_scripts_iwd=array END
        //   LAF read_in_list STR_VAR file=genai_scripts_pure.txt force_case=lower locbase=genai location=iwd RET_ARRAY genai_scripts_pure=array END
     END
END

