DEFINE_ACTION_FUNCTION skip_candlekeep STR_VAR version=2 BEGIN
  ACTION_MATCH "%version%" WITH
  classic BEGIN
     LAF candlekeep_classic END
     LAF candlekeep_no_tutors END
  END
  notutors BEGIN
     LAF candlekeep_no_tutors END
  END
  DEFAULT
     FAIL "Unrecognised Candlekeep component"
  END
END

DEFINE_ACTION_FUNCTION candlekeep_no_tutors BEGIN

    LAF edit_area STR_VAR area=~%Candlekeep% %Candlekeep_Inn_L1%~ editstring=~delete_actor=>breaks_fourth_wall~ END
    LAF get_area_script STR_VAR area= ~%Candlekeep%~ RET script END

    ACTION_IF enhanced_edition BEGIN
      ACTION_IF is_eet BEGIN
       OUTER_SPRINT swapline ~DisplayString(Myself,231107)=>null DisplayString(Myself,216214)=>null~
      END ELSE BEGIN
       OUTER_SPRINT swapline ~DisplayString(Myself,31107)=>null DisplayString(Myself,16214)=>null~
      END
    END ELSE BEGIN
      ACTION_IF is_tutu BEGIN
       OUTER_SPRINT swapline ~DisplayStringHead(Myself,86017)=>null~
      END ELSE BEGIN
       OUTER_SPRINT swapline ~DisplayStringHead(Myself,16214)=>null~
      END
    END
    LAF swap_text STR_VAR files= ~%script%.bcs~ swaps=~%swapline%~ END
    ACTION_IF FILE_EXISTS_IN_GAME "macinit.bcs" BEGIN
       LAF swap_text STR_VAR files=macinit.bcs swaps=~SetGlobal("MACGameInit","GLOBAL",1)=>SetGlobal("MACGameInit","GLOBAL",1)EscapeArea()~ END
    END


END


DEFINE_PATCH_FUNCTION breaks_fourth_wall RET value BEGIN
   LPF ARE_read_actor_name RET actor=value END
   TO_UPPER actor
   PATCH_MATCH ~%actor%~ WITH
      TUTOR JONDALAR
   BEGIN
      LPF ARE_read_actor_resource RET resource=value END
      PATCH_IF "%resource%" STRING_EQUAL_CASE "_MONKTU1" BEGIN // used in TUTU for initialisation
         SET value=0
      END ELSE BEGIN
         SET value=1
      END
   END
   DEFAULT
      SET value=0
   END


END

DEFINE_ACTION_FUNCTION candlekeep_classic BEGIN

    OUTER_SPRINT local_loc candleskip
    LAF clone_creature STR_VAR creature=~%tutu_var%watch3 => dw#skcan~ editstring=~script_class=>dw#skcan dialog=>dw#skcan~ END

    LAF install STR_VAR files="dw#skcan.baf dw#skcan.d" END

    MAKE_PATCH
       add_actor_inline=>"actor_name=>~CandlekeepSkipper~ actor_resource=>DW#SKCAN actor_x_coord=>1178 actor_y_coord=>640"
    END
    LAF edit_area STR_VAR area="%Candlekeep%" edits=patch_data END
    LAF extend INT_VAR tv=1 STR_VAR file=gorion top=addtogorion END


END
