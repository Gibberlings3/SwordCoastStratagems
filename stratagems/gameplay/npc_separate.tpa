DEFINE_ACTION_FUNCTION npc_separate BEGIN
   ACTION_CLEAR_ARRAY npc_data
   ACTION_IF enhanced_edition BEGIN
      ACTION_DEFINE_ASSOCIATIVE_ARRAY npc_data BEGIN
         jaheira_loc=>5
         khalid_loc=>5
         minsc_loc=>4
         dynaheir_loc=>4
         montaron_loc=>5
         xzar_loc=>5
         eldoth_loc=>4
         skie_loc=>4
         jaheira_stay=>6
         khalid_stay=>6
         minsc_stay=>999
         dynaheir_stay=>6
         montaron_stay=>7
         xzar_stay=>6
         eldoth_stay=>8
         skie_stay=>6
         kicked_out_variable=>KickedOut
      END
   END ELSE BEGIN
     ACTION_IF GAME_IS bgt BEGIN
      ACTION_DEFINE_ASSOCIATIVE_ARRAY npc_data BEGIN
         jaheira_loc=>78
         khalid_loc=>5
         minsc_loc=>14
         dynaheir_loc=>4
         montaron_loc=>5
         xzar_loc=>5
         eldoth_loc=>4
         skie_loc=>4
         jaheira_stay=>999
         khalid_stay=>6
         minsc_stay=>13
         dynaheir_stay=>6
         montaron_stay=>7
         xzar_stay=>6
         eldoth_stay=>8
         skie_stay=>6
         kicked_out_variable=>IWasKickedOut
      END
     END ELSE BEGIN
      ACTION_DEFINE_ASSOCIATIVE_ARRAY npc_data BEGIN
         jaheira_loc=>0
         khalid_loc=>1
         minsc_loc=>0
         dynaheir_loc=>0
         montaron_loc=>1
         xzar_loc=>1
         eldoth_loc=>0
         skie_loc=>0
         jaheira_stay=>6
         khalid_stay=>6
         minsc_stay=>999
         dynaheir_stay=>6
         montaron_stay=>7
         xzar_stay=>6
         eldoth_stay=>8
         skie_stay=>6
         kicked_out_variable=>KickedOut
      END
     END
   END
   ACTION_PHP_EACH npc_data AS var=>val BEGIN
      OUTER_SPRINT  ~%var%~ ~%val%~
   END
   LAM read_in_NPC_dialogs // line added by BiG World Fixpack
   ACTION_IF (GAME_IS ~EET~) THEN BEGIN
     INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~ // import DLG variables for BG1 NPCs in EET because they aren't added to pdialog.2da (for above macro to find) until the EET_End step
     OUTER_SPRINT KHALID_POSTJOIN ~%KHALID_POST%~
     OUTER_SPRINT JAHEIRA_POSTJOIN ~%JAHEIRA_POST%~
     OUTER_SPRINT MINSC_POSTJOIN ~%MINSC_POST%~
     OUTER_SPRINT DYNAHEIR_POSTJOIN ~%DYNAHEIR_POST%~
     OUTER_SPRINT XZAR_POSTJOIN ~%XZAR_POST%~
     OUTER_SPRINT MONTARON_POSTJOIN ~%MONTARON_POST%~
     OUTER_SPRINT ELDOTH_POSTJOIN ~%ELDOTH_POST%~
     OUTER_SPRINT SKIE_POSTJOIN ~%SKIE_POST%~
   END
   COMPILE EVALUATE_BUFFER ~%scsroot%/%component_loc%/resource/pairsep.d~

   ACTION_CLEAR_ARRAY npc_data
   ACTION_DEFINE_ASSOCIATIVE_ARRAY npc_data BEGIN
      KHALID=>jaheira
      JAHEIRA=>khalid
      XZAR=>montaron
      MONTARON=>xzar
      ELDOTH=>skie
      SKIE=>eldoth
      MINSC=>dynaheir
      DYNAHEIR=>minsc
   END
   
   ACTION_PHP_EACH npc_data AS script=>ally BEGIN
      OUTER_SPRINT scriptfile EVALUATE_BUFFER ~%%script%_POSTJOIN%~
      ACTION_TO_LOWER script
      OUTER_SPRINT staynum EVALUATE_BUFFER ~%%script%_stay%~
      LAF swap_text STR_VAR files= ~%scriptfile%.dlg~ swaps= ~BeenInParty("%ally%")=>True() !Dead("%ally%")=>InMyArea("%ally%")!InParty("%ally%")NOTDEADMARKER Dead("%ally%")=>OR(3)Dead("%ally%")InParty("%ally%")!InMyArea("%ally%") NOTDEADMARKER=>!Dead("%ally%")~ END
      LAF swap_text STR_VAR files= ~%scriptfile%.dlg~ swaps= ~ActionOverride("%ally%",LeaveParty())=>ActionOverride("%ally%",LeaveParty())ActionOverride("%ally%",SetGlobal("%kicked_out_variable%","LOCALS",1))~ END
      ACTION_IF !(staynum=999) BEGIN
         PRINT "%staynum% %scriptfile%"
         <<<<<<<< pairsep2.d
         ALTER_TRANS ~%scriptfile%~ BEGIN ~%staynum%~ END BEGIN 0 END BEGIN "ACTION" "" END
         >>>>>>>>
         COPY pairsep2.d "%workspace%" EVALUATE_BUFFER
         COMPILE "%workspace%/pairsep2.d"
      END

   END




END





