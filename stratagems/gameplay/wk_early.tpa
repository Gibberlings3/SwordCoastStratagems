DEFINE_ACTION_FUNCTION wk_early BEGIN

// redirect copy-ground-piles at end of SoA from AR4000 to AR3000

   LAF swap_text STR_VAR files="cut59a.bcs" swaps="ar4000=>ar3000 1116=>1550 1805=>2550" END

// first time in AR4000 (i.e., immediately after expansion starts), jump to AR3000, applying AR4000's on-creation journal and chapter updates

   ACTION_IF enhanced_edition BEGIN
      OUTER_SPRINT journal_erase_string "EraseJournalEntry(97346)EraseJournalEntry(4352)"
      OUTER_SPRINT journal_add_string "AddJournalEntry(97347,QUEST)AddJournalEntry(73256,INFO)"
   END ELSE BEGIN
      OUTER_SPRINT journal_erase_string "EraseJournalEntry(4352)"
      OUTER_SPRINT journal_add_string "AddJournalEntry(73256,INFO)"
   END

<<<<<<<< .../stratagems-inline/ar4000add.baf
IF
   Global("DMWWWKEarly","GLOBAL",0)
THEN
    RESPONSE #100
		StartCutSceneMode()
		%journal_erase_string%
		LeaveAreaLUAPanic("AR3000","",[1300.1840],4)
		ActionOverride(Player1,LeaveAreaLUA("AR3000","",[1631.2548],4))
		ActionOverride(Player2,LeaveAreaLUA("AR3000","",[1717.2555],4))
		ActionOverride(Player3,LeaveAreaLUA("AR3000","",[1713.2490],4))
		ActionOverride(Player4,LeaveAreaLUA("AR3000","",[1712.2492],2))
		ActionOverride(Player5,LeaveAreaLUA("AR3000","",[1637.2579],0))
		ActionOverride(Player6,LeaveAreaLUA("AR3000","",[1642.2505],0))
		SetGlobal("DMWWWKEarly","GLOBAL",1)
		SetGlobalTimer("DMWWWKEarlyTimer","GLOBAL",3)
		FadeFromColor([20.0],0)
		EndCutSceneMode()
END

IF
   Global("DMWWWKEarly","GLOBAL",1) // i.e., upon returning
   !GlobalTimerNotExpired("DMWWWKEarlyTimer","GLOBAL")
THEN
    RESPONSE #100
               SetGlobal("DMWWWKEarly","GLOBAL",2)
	       SetGlobal("chapter","GLOBAL",%bg2_chapter_8%)
               %journal_add_string%
               Continue()
END
>>>>>>>>

    LAF extend_area_script INT_VAR inline=1 STR_VAR area=ar4000 top=ar4000add END


    /// add an entrance point in the Forest of Tethyr
    MAKE_PATCH
            type=>entrance
            entrance_name=>~dw#WKin~
            entrance_xloc=>~1311~
            entrance_yloc=>~2347~
    END
    LAF edit_area STR_VAR area=ar4000 editstring=~add_entry=>patch_data~ END

    // link that entrance point to Watchers' Keep (to the South exit block, to be specific)

    LAF edit_worldmap STR_VAR worldmap=worldm25 editstring="insert_link => ~from=>ar3000 to=>ar4000 dir_from=>S link_travtime_div_four=>9 entrance=>dw#WKin~" END

    // Make Tethyr inaccessible once the party have reached the Pocket Plane (not that it matters really)

    ACTION_IF !FILE_EXISTS_IN_GAME ~dw#teth.are~ BEGIN // Wheels of Prophecy not installed

<<<<<<<<.../stratagems-inline/notethyr.baf
IF
	!Global("DMWWBlockTethyrAccess","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("DMWWBlockTethyrAccess","GLOBAL",1)
		HideAreaOnMap("AR4000")
		RevealAreaOnMap("dw#teth")
		Continue()
END
>>>>>>>>

        LAF extend_area_script INT_VAR inline=1 STR_VAR area=ar4500 top=notethyr END
     END
     
     // Fake Tethyr  (nice to clone it, but adding new areas needs to be at the bottom)
     
     MAKE_PATCH
        type=>area
        short_name=>"dw#teth"
        resource=>ar4000
        long_name=>ar4000
        x=>397
        y=>56
        sequence=>6
        name_string=>61253
        tooltip_string=>61253
        reachable=>1
     END
     LAF edit_worldmap STR_VAR worldmap=worldm25 editstring="add_entry=>patch_data" END

     // postpone the start movie
     
     LAF swap_text STR_VAR files="cut100a.bcs cut100b.bcs" swaps=~StartMovie("INTRO") => SetGlobal("DMWWRequestIntroMovie","GLOBAL",1)~ END

		<<<<<<<< .../stratagems-inline/ar4000add2.baf
			IF
				Global("DMWWRequestIntroMovie","GLOBAL",1)
			THEN
				RESPONSE #100
					SetGlobal("DMWWRequestIntroMovie","GLOBAL",0)
					StartMovie("INTRO")
					Continue()
			END
		>>>>>>>>
      LAF extend_area_script INT_VAR inline=1 STR_VAR area=ar4000 top=ar4000add2 END

      // prevent NPCs being sent to the Pocket Plane before it's obtained

      ACTION_PHP_EACH npc_name_list AS npc=>null BEGIN
         OUTER_SPRINT dialog ~%%npc%_25POSTJOIN%~
         LAF swap_text INT_VAR allow_missing=1 STR_VAR files="%dialog%.dlg" swaps=~!AreaCheck("ar4000")=>!AreaCheck("ar4000")Global("DMWWBlockTethyrAccess","GLOBAL",1)~ END
      END
      
      // wipe unneeded content from ar4000 script
      
      LAF ALTER_SCRIPT_BLOCK
                  STR_VAR script=ar4000
                          match=~Global("TurnDay","AR4000",0)~ 
                          swap_out1=~SetGlobal("chapter","GLOBAL",[0-9]+)~
                          swap_in1=~~
                          swap_out2=~EraseJournalEntry([0-9]+)~
                          swap_in2=~~
                          swap_out3=~AddJournalEntry([^)]+)~
                          swap_in3=~~
      END
      LAF DELETE_SCRIPT_BLOCK
                  STR_VAR script=ar4000
                          match=FORCETOBPARTYMOVE4000
      END

      /// on EE, make sure Neera can't start her quest from here
	  
	  ACTION_IF enhanced_edition BEGIN
	  	LAF script_alter_block STR_VAR script=neer25 match=~AreaCheck("ar3000")~ function=~script_disjunctive_delete(AR3000)~ END
	  END

	// force use of ToB interjections (not doing so means no interjections in oBG2 and breaks some dialogs in EE)
	
	ACTION_FOR_EACH script IN 
	FSSPIR2 GORAPP1 GORAPR GORCAMB GORDEMO GORGIT GORIMP01 GORMAD1 GORODR1 GORPOL1 BEGIN
			COPY_EXISTING "%script%.dlg" override
				DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY ~!GlobalGT("Chapter","GLOBAL",%bg2_chapter_7%)~ ~False()~
					REPLACE_TEXTUALLY ~GlobalGT("Chapter","GLOBAL",%bg2_chapter_7%)~ ~~
					REPLACE_TEXTUALLY ~GlobalLT("Chapter","GLOBAL",%bg2_chapter_8%)~ ~False()~				
				END
			BUT_ONLY		
	END

END





