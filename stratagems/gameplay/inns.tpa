DEFINE_ACTION_FUNCTION inns BEGIN


	// UI setup
	
	LAF ui_inn_label END

	// rest spells
	
	LAF make_rest_spells END
	
	// rest desc
	
	LAF set_rest_desc END
	
	// room prices
	
	LAF adjust_room_prices END
	
	// worldscript
	LAF extend_worldscripts INT_VAR inline=1 STR_VAR top=global_inn_add END
		





END

DEFINE_ACTION_FUNCTION make_rest_spells BEGIN

	// make the 'rested' state
	
	OUTER_SPRINT string @24100
	LAF resolve_statdesc STR_VAR string bam=dw#rest location=resource RET stat_num END
	
	// make the spells that increase STR/DEX/CON
	
	OUTER_FOR (number=1;number<=3;++number) BEGIN
		spl.make[dw#ress%number%]
		[
			m.ab.add{}
			m.ab_fx.add{s_opcode=321 s_target=2 s_timing=1 s_resource:=dw#ress1}
			m.ab_fx.add{s_opcode=321 s_target=2 s_timing=1 s_resource:=dw#ress2}
			m.ab_fx.add{s_opcode=321 s_target=2 s_timing=1 s_resource:=dw#ress3}
			m.ab_fx.add{s_opcode=318 s_target=2 s_duration=1 s_parameter1=18 s_parameter2=124 s_resource:=%sfo_filename%}
			m.ab_fx.add{s_opcode=44 s_target=2 s_duration=2400*number s_parameter1=1}
		]

		spl.make[dw#resd%number%]
		[
			m.ab.add{}
			m.ab_fx.add{s_opcode=321 s_target=2 s_timing=1 s_resource:=dw#resd1}
			m.ab_fx.add{s_opcode=321 s_target=2 s_timing=1 s_resource:=dw#resd2}
			m.ab_fx.add{s_opcode=321 s_target=2 s_timing=1 s_resource:=dw#resd3}
			m.ab_fx.add{s_opcode=318 s_target=2 s_duration=1 s_parameter1=18 s_parameter2=122 s_resource:=%sfo_filename%}
			m.ab_fx.add{s_opcode=15 s_target=2 s_duration=2400*number s_parameter1=1}
		]

		spl.make[dw#resc%number%]
		[
			m.ab.add{}
			m.ab_fx.add{s_opcode=321 s_target=2 s_timing=1 s_resource:=dw#resc1}
			m.ab_fx.add{s_opcode=321 s_target=2 s_timing=1 s_resource:=dw#resc2}
			m.ab_fx.add{s_opcode=321 s_target=2 s_timing=1 s_resource:=dw#resc3}
			m.ab_fx.add{s_opcode=318 s_target=2 s_duration=1 s_parameter1=18 s_parameter2=126 s_resource:=%sfo_filename%}
			m.ab_fx.add{s_opcode=10 s_target=2 s_duration=2400*number s_parameter1=1}
		]
	END
	
	// make the main spells


	spl.make[dw#resm1]
	[
		m.ab.add{}
		m.ab_fx.add{s_opcode=321 s_target=3 s_timing=1 s_resource:=dw#resm1}
		m.ab_fx.add{s_opcode=321 s_target=3 s_timing=1 s_resource:=dw#resm3}
		m.ab_fx.add{s_opcode=321 s_target=3 s_timing=1 s_resource:=dw#resm2}
		m.ab_fx.add{s_opcode=18 s_target=3 s_duration=2400 s_parameter1=5}
		m.ab_fx.add{s_opcode=142 s_target=3 s_duration=2400 s_parameter2=stat_num}
	]
	
	spl.make[dw#resm2]
	[
		m.ab.add{}
		m.ab_fx.add{s_opcode=321 s_target=3 s_timing=1 s_resource:=dw#resm1}
		m.ab_fx.add{s_opcode=321 s_target=3 s_timing=1 s_resource:=dw#resm3}
		m.ab_fx.add{s_opcode=321 s_target=3 s_timing=1 s_resource:=dw#resm2}
		m.ab_fx.add{s_opcode=18 s_target=3 s_duration=2400*2 s_parameter1=5}
		m.ab_fx.add{s_opcode=142 s_target=3 s_duration=2400*2 s_parameter2=stat_num}
		m.ab_fx.add{s_opcode=326 s_target=3 s_timing=1 s_resource:=dw#ress2}
		m.ab_fx.add{s_opcode=326 s_target=3 s_timing=1 s_resource:=dw#resd2}
		m.ab_fx.add{s_opcode=326 s_target=3 s_timing=1 s_resource:=dw#resc2}
	]
	
	spl.make[dw#resm3]
	[
		m.ab.add{}
		m.ab_fx.add{s_opcode=321 s_target=3 s_timing=1 s_resource:=dw#resm1}
		m.ab_fx.add{s_opcode=321 s_target=3 s_timing=1 s_resource:=dw#resm3}
		m.ab_fx.add{s_opcode=321 s_target=3 s_timing=1 s_resource:=dw#resm2}
		m.ab_fx.add{s_opcode=18 s_target=3 s_duration=2400*3 s_parameter1=10}
		m.ab_fx.add{s_opcode=142 s_target=3 s_duration=2400*3 s_parameter2=stat_num}
		m.ab_fx.add{s_opcode=325 s_target=3 s_duration=2400*3 s_parameter1=1}
		m.ab_fx.add{s_opcode=326 s_target=3 s_timing=1 s_resource:=dw#ress3}
		m.ab_fx.add{s_opcode=326 s_target=3 s_timing=1 s_resource:=dw#resd3}
		m.ab_fx.add{s_opcode=326 s_target=3 s_timing=1 s_resource:=dw#resc3}
	]

END




DEFINE_ACTION_FUNCTION set_rest_desc BEGIN

	LAF 2da_read STR_VAR file=enginest.2da RET_ARRAY string_array=array END
	
	array.new[rest_desc_array]
	[
		STRREF_GUI_HELP_MERCHANT=>24101
		STRREF_GUI_HELP_NOBLE=>24102
		STRREF_GUI_HELP_ROYAL=>24103
	]
	
	ACTION_PHP_EACH rest_desc_array AS id=>tra_ref BEGIN
		OUTER_SET strref=$string_array("%id%" STRREF)
		ACTION_GET_STRREF strref desc
		OUTER_SPRINT desc_add (AT tra_ref)
		OUTER_SPRINT desc "%desc%%WNL%%WNL%%desc_add%"
		STRING_SET_EVALUATE strref "%desc%"
	END

	COPY_EXISTING "ui.menu" override
		REPLACE_TEXTUALLY ~\(.[ %TAB%]*\)text lua "store.roomText"~ ~\1text lua "store.roomText"%WNL%\1scrollbar 'GUISCRC'~

END

DEFINE_ACTION_FUNCTION adjust_room_prices BEGIN

	LAF 2da_read STR_VAR file=inn_price_mult.2da location=data RET_ARRAY price_rows=rows price_array=array END
	COPY_EXISTING_REGEXP GLOB ".*\.sto$" override
		PATCH_IF (LONG_AT 0x8) = 2 BEGIN // only patch inns
			PHP_EACH price_rows AS row=>discard BEGIN
				WRITE_LONG $price_array("%row%" OFFSET) THIS * $price_array("%row%" MULTIPLIER)
			END
		END
	BUT_ONLY
		

END


<<<<<<<<.../stratagems-inline/global_inn_add.baf

IF
	PartyRested()
	Global("dw_inn_roomtype","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("dw_inn_roomtype","GLOBAL",0) // no benefit in worst-quality room
		Continue()
END

IF
	PartyRested()
	Global("dw_inn_roomtype","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("dw_inn_roomtype","GLOBAL",0) 
		ApplySpellRES("dw#resm1",Player1)
		Continue()
END

IF
	PartyRested()
	Global("dw_inn_roomtype","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("dw_inn_roomtype","GLOBAL",0) 
		ApplySpellRES("dw#resm2",Player1)
		Continue()
END

IF
	PartyRested()
	Global("dw_inn_roomtype","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("dw_inn_roomtype","GLOBAL",0)
		ApplySpellRES("dw#resm3",Player1)
		Continue()
END
>>>>>>>>

