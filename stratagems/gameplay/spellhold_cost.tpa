DEFINE_ACTION_FUNCTION spellhold_cost
    STR_VAR version="0" // this is the cost in thousands of gp
BEGIN
    // work out variables
	OUTER_SET asking_price_thousands=version
    OUTER_SET asking_price=version *1000
    OUTER_SET asking_price_minus_one = asking_price - 1
    OUTER_SET actual_price_thousands=(version *3) / 4
    OUTER_SET actual_price=actual_price_thousands* 1000
    OUTER_SET actual_price_minus_one = actual_price - 1
    OUTER_SET threshold_price=asking_price / 4
    OUTER_SET threshold_price_minus_one=threshold_price - 1
    OUTER_SPRINT asking_price_string ~%version%,000~
    OUTER_SPRINT actual_price_string ~%temp%,000~

	// do the changes to scripting that actually implement the change
	// I don't know any mod that actually responds to this number
	
	ACTION_DEFINE_ASSOCIATIVE_ARRAY spellhold_text_swap BEGIN
		"PartyGoldLT(20000)" => "PartyGoldLT(%asking_price%)"
        "PartyGoldLT(50000)" => "PartyGoldLT(%asking_price%)"
		"PartyGoldLT(15000)" => "PartyGoldLT(%actual_price%)"
		"PartyGoldLT(5000)" => "PartyGoldLT(%threshold_price%)"
		"PartyGoldGT(49999)" => "PartyGoldGT(%asking_price_minus_one%)"
		"PartyGoldGT(19999)" => "PartyGoldGT(%asking_price_minus_one%)"
		"PartyGoldGT(14999)" => "PartyGoldGT(%actual_price_minus_one%)"
		"PartyGoldGT(4999)" => "PartyGoldGT(%threshold_price_minus_one%)"
		"TakePartyGold(20000)" => "TakePartyGold(%asking_price%)"
		"TakePartyGold(15000)" => "TakePartyGold(%actual_price%)"
		"DestroyGold(15000)" => "DestroyGold(%actual_price%)"
	END
	ACTION_FOR_EACH file IN "bodhi.dlg" "gaelan.dlg" "ar0800.bcs" "baldur.bcs" BEGIN
		COPY_EXISTING "%file%" override
			DECOMPILE_AND_PATCH BEGIN
				PHP_EACH spellhold_text_swap AS old=>new BEGIN
					REPLACE_TEXTUALLY "%old%" "%new%"
				END
			END
		BUT_ONLY
	END

	// set strings


	LAF 2da_read INT_VAR inline=1 STR_VAR file=cost_strings.2da RET_ARRAY cost_string_array=array END
	OUTER_SET actual_price_tra=$cost_string_array("%version%" ACTUAL_PRICE_TEXT)
	OUTER_SPRINT actual_price_text (AT actual_price_tra)
	OUTER_SET asking_price_tra=$cost_string_array("%version%" ASKING_PRICE_TEXT)
	OUTER_SPRINT asking_price_text (AT asking_price_tra)
	OUTER_SPRINT actual_price_text_old @1601
	OUTER_SPRINT asking_price_text_old @1602
	
	// these are the dialog files that mention the price
	
	ACTION_FOR_EACH dialog IN 
			bodhi 	// original game
			gaelan 
			bo#xan  // Xan mod
			o#xanj 
			bo#tiax  // Tiax mod (appears in crossmod I think)
		BEGIN
		COPY_EXISTING - "%dialog%.dlg" nowhere
			LPF strref_apply STR_VAR function=swap_strref_spellhold END
		IF_EXISTS
	END
END

DEFINE_PATCH_FUNCTION swap_strref_spellhold
	STR_VAR arguments=0
	RET value
BEGIN
	// return the unaltered strref
	value=arguments
	// make the swap
	PATCH_IF arguments>=0 BEGIN
		GET_STRREF arguments string
		GET_STRREF_S arguments sound
		INNER_PATCH_SAVE string "%string%" BEGIN
			
			REPLACE_TEXTUALLY "^20\(.?.?\)000" "%asking_price_thousands%\1000"
			REPLACE_TEXTUALLY "\([^1]\)20\(.?.?\)000" "\1%asking_price_thousands%\2000"
			REPLACE_TEXTUALLY "15\(.?.?\)000" "%actual_price_thousands%\1000"
			REPLACE_TEXTUALLY "%asking_price_text_old%" "%asking_price_text%"
			REPLACE_TEXTUALLY "%actual_price_text_old%" "%actual_price_text%"	
		END
		INNER_ACTION BEGIN
			ACTION_IF "%sound%" STR_EQ "" BEGIN
				STRING_SET_EVALUATE arguments "%string%"
			END ELSE BEGIN
				STRING_SET_EVALUATE arguments "%string%" [%sound%]			
			END
		END
	END
END


<<<<<<<<.../stratagems-inline/cost_strings.2da
2DA v1.0
*
			ACTUAL_PRICE_TEXT		ASKING_PRICE_TEXT
40			1603					1604
60			1605					1606
80			1606					1607
100			1608					1609
120			1610					1611
>>>>>>>>



