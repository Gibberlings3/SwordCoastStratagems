DEFINE_ACTION_FUNCTION realistic_wolves BEGIN

   LAF edit_creature STR_VAR creature=~%tutu_scriptbg%wolf~
                             editstring=~strip_script=>"wtasight _tasight wdasight _dasight" allegiance=>NEUTRAL~
   END
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=~dogwa dogwi~
                             editstring=~strip_script=>"wtasight _tasight wdasight _dasight wilddog" allegiance=>NEUTRAL script_race=>dw#wldog~
   END

   COPY ~%scsroot%/%component_loc%/resource/wolf.baf~ ~%workspace%/%tutu_var%wolf.baf~
   COMPILE ~%workspace%/%tutu_var%wolf.baf~

   COPY_EXISTING ~%tutu_var%wolf.bcs~ ~override/dw#wldog.bcs~
      REPLACE_TEXTUALLY 117 105
   LAF edit_creature STR_VAR creature="dogwa dogwi" tv=1 editstring="insert_script_high=>dw#wldog" END

<<<<<<<< .../scs-inline/wolftimer.baf

IF
  !GlobalTimerNotExpired("DMWWWolfControlTimer","GLOBAL")
THEN
    RESPONSE #400
             SetGlobalTimer("DMWWWolfControlTimer","GLOBAL",300)
             SetGlobal("DMWWWolvesHostile","GLOBAL",0)
             Continue()
    RESPONSE #100
             SetGlobalTimer("DMWWWolfControlTimer","GLOBAL",300)
             SetGlobal("DMWWWolvesHostile","GLOBAL",1)
             Continue()
END


>>>>>>>>

   EXTEND_TOP ~baldur.bcs~ ~.../scs-inline/wolftimer.baf~

// protect the Melicamp wolf!
    LAF edit_creature STR_VAR creature=wolfch tv=1 editstring=~script_race=>%tutu_scriptw%tasight~ END
// summoned wolves don't get wolf script
    LAF edit_creature STR_VAR creature=wolfsu editstring=~strip_script=>%tutu_var%wolf~ END
// protect the random wolves
    LAF random_wolves END


END

DEFINE_ACTION_FUNCTION random_wolves BEGIN

   LAF clone_creature STR_VAR creature= ~%tutu_scriptbg%wolf => dw#rnwlf
                                                     %tutu_var%dogwa => dw#rndwa
                                                     %tutu_var%dogwi => dw#rndwi~
                           editstring=~script_race => %tutu_scriptw%tasight~
   END


   ACTION_IF is_tutu BEGIN  // Tutu version
     ACTION_FOR_EACH scriptnum IN 5600 5601 5700 5701 5800 5801 5900 5901 6000 6001 6100 BEGIN
       LAF swap_text STR_VAR files= ~_ar%scriptnum%.bcs~
                          swaps=~_wolf=>dw#rnwlf _dogwa=>dw#rndwa _dogwi=>dw#rndwi~
       END
     END
   END ELSE BEGIN
     ACTION_FOR_EACH area IN Encounter_Plains1 Encounter_Plains2 Encounter_Canyon1 Encounter_Canyon2
                             Encounter_Cliff1 Encounter_Cliff2 Encounter_Grassland1 Encounter_Grassland2 
                             Encounter_Forest1 Encounter_Forest2 Encounter_Road BEGIN
       OUTER_SPRINT files EVALUATE_BUFFER ~%%area%%.are~
       LAF swap_text STR_VAR files
                             swaps= ~dogwi=>dw#rndwi dogwa=>dw#rndwa %tutu_scriptbg%wolf=>dw#rnwlf~
       END
     END
   END
END
