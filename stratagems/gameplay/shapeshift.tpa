DEFINE_ACTION_FUNCTION shapeshift BEGIN

// data

<<<<<<<< .../stratagems-inline/shapeshift_data
filename  name_code  desc_code  animation   icon     enchantment  to_hit  damage speed  dice_num  dice_size strength  str_ex   dexterity   AC    MR    attacks   movement_mod
dw#shwe1  13120      13140      32257       ibear    0            0       0      3      1         6         18        76       16          4     10    -1               100
dw#shwe2  13121      13141      32257       ibear    1            0       0      3      1         8         19        0        17          0     20    2               100
dw#shwe3  13122      13142      32257       ibear    3            0       0      3      2         6         21        0        20          -4    40    2               100
dw#shbrp  13123      13143      29185       ibear    2            0       0      3      1         6         18        100      12          7     0     2               200
dw#shwol  13124      13144      31488       iwolf    2            0       0      3      1         8         17        0        18          6     0     2               200
dw#shwyv  13125      13145      40960       iswospdr 2            0       0      3      2         4         18        76       17          3     0     2               100
dw#shsal  13126      13146      59648       iplysala 2            0       0      3      1         8         18        76       19          2     0     1               100
dw#shspi  13127      13147      31235       iswospdr 2            0       0      3      1         4         16        0        16          0     0     3               100
dw#shfir  13128      13148      29456       ishape07 4            0       0      0      1         10        22        0        24          -5    0     2               100
dw#shear  13129      13149      29440       ishape06 4            0       0      0      2         10        25        0        10          -5    0     2               75
>>>>>>>>

   <<<<<<<<  .../stratagems-inline/shapeshift_spells.2da
spellname    resource   icon
dw#shw2      dw#shwe2   spin155
dw#shw3      dw#shwe3   spin155
spcl611      dw#shbrp   spin107
spcl612      dw#shwol   spin110
spcl632      dw#shspi   plyspidr
spcl633      dw#shwyv   plywyvnb
spcl634      dw#shsal   plybassb
sppr731      dw#shfir   sppr731c
sppr732      dw#shear   sppr732c
>>>>>>>>


   LAF process_table STR_VAR table=shapeshift_data inline=yes function=token_core END
   LAF shapeshift_token_extras END // extra effects
   LAF shapeshift_spells END  // spells
   LAF shapeshift_classfiles END  // classfiles
   ACTION_IF FILE_EXISTS ~override/dw#insectplague.mrk~ BEGIN
       LAF shapeshift_insect_plague END
   END
   LAF shapeshift_token_enemies END // undroppable tokens for enemies
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  patch used by token-making block
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION token_core
                      STR_VAR 
                              filename=""
                              to_hit=0
                              damage=0
                              AC=10
                              strength=0
                              str_ex=0
                              dexterity=0
                              MR=0
                              damage_type=3 // slashing is default
                              dice_num=1
                              dice_size=0
                              attacks=~-1~
                              movement_mod=100
BEGIN
     ACTION_CLEAR_ARRAY core_ability_data
     ACTION_DEFINE_ASSOCIATIVE_ARRAY core_ability_data BEGIN
          ability_type=>1
          ability_icon=>~%icon%~
          ability_icon_loc=>1
          ability_target=>1
          ability_range=>1
          projectile=>1
          overhand=>50
          backhand=>50
          when_drained=>1
          strength_bonus=>1
          to_hit=>~%to_hit%~
          damage_bonus=>~%damage%~
          damage_type=>~%damage_type%~
          speed=>~%speed%~
          ability_numdice=>~%dice_num%~
          ability_dicesize=>~%dice_size%~
     END
     MAKE_PATCH
        icon=>~%icon%~
        say_both_names=> ~%name_code%~
        say_both_descriptions=>~%desc_code%~
        droppable=>1
        magical=>1
        not_copyable=>1
        displayable=>1
        category=>16
        unusable_cleric=>1
        unusable_fighter=>1
        unusable_paladin=>1
        unusable_ranger=>1
        unusable_mage=>1
        unusable_bard=>1
        unusable_thief=>1
        unusable_fighter_mage=>1
        unusable_fighter_cleric=>1
        unusable_fighter_thief=>1
        unusable_fighter_mage_thief=>1
        unusable_cleric_mage=>1
        unusable_cleric_thief=>1
        unusable_mage_thief=>1

        icon_ground=>gward01
        icon_carried=>cmisc6p
        enchantment=>~%enchantment%~
        add_ability => core_ability_data
        add_effect_global_inline=>~opcode=>233 parameter1=>1 parameter2=>96~
        add_effect_global_inline'=>~opcode=>0 parameter1=>"%AC%" parameter2=>0x10~
        add_effect_global_inline''=>~opcode=>53 parameter1=>%animation%~ // animation
        add_effect_global_inline'''=>~opcode=>145 parameter2=>0~ // no spells
        add_effect_global_inline''''=>~opcode=>145 parameter2=>1~ // no spells
        add_effect_global_inline'0=>~opcode=>215 target=>1 duration=>1 resource=>SPPOLYMP~ // swirly lights on start
     END
     ACTION_IF !(movement_mod=100) BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'1) ~opcode=>126 parameter1=>%movement_mod% parameter2=>2~
     END
     ACTION_IF ~dexterity~>0 BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'2) ~opcode=>15 parameter2=>1 parameter1=>%dexterity%~
     END
     ACTION_IF ~strength~>0 BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'3) ~opcode=>44 parameter2=>1 parameter1=>%strength%~
     END
     ACTION_IF str_ex>0 BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'4) ~opcode=>97 parameter2=>1 parameter1=>%str_ex%~
     END
     ACTION_IF ~MR~>0 BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'5) ~opcode=>166  parameter1=>%MR%~
         OUTER_SPRINT $patch_data(add_effect_global_inline'6) ~opcode=>142 parameter2=>63~
     END
     ACTION_IF !(attacks = ~-1~) BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'7) ~opcode=>1 parameter1=>%attacks% parameter2=>1~
     END
     LAF make_item STR_VAR item=~%filename%~ edits=patch_data END
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  Adds additional effects to the tokens
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION shapeshift_token_extras BEGIN
      // L2,L3 werewolf: haste
   LAF edit_item STR_VAR item=~dw#shwe2 dw#shwe3~
                      editstring=~add_effect_global_inline=>"opcode=>16"~
   END

   // L3 werewolf: regeneration, 50% resistances to elements
   LAF edit_item STR_VAR item=dw#shwe3
                         editstring=~add_effect_global_inline=>"opcode=>98 parameter1=>1 parameter2=>2"
                                     add_effect_global_inline=>"opcode=>142 parameter2=>87"
                                     add_effect_global_inline=>"opcode=>30 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>84 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>28 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>85 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>27 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>29 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>142 parameter2=>76"~
   END
   
   // wyvern:poisonous sting
   LAF edit_item STR_VAR item=dw#shwyv
                         editstring=~add_effect_inline=>"match=>ability_type=1 opcode=>25 target=>2 parameter1=>3 parameter2=>3 duration=>6 probability1=>25 save_vs_poison=>1"
                                     add_effect_inline=>"match=>ability_type=1 opcode=>142 target=>2 parameter2=>6 duration=>6 probability1=>25 save_vs_poison=>1"
                         ~
   END

   // salamander: fire shield, haste, fire resistance

   LAF edit_item STR_VAR item=dw#shsal
                         editstring=~add_effect_global_inline=>"opcode=>16"
                                     add_effect_global_inline=>"opcode=>30 parameter1=>100 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>215 parameter2=>1 resource=>spfiresr"
                                     add_effect_global_inline=>"opcode=>215 parameter2=>1 resource=>spfirest"
                                     add_effect_global_inline=>"opcode=>232 parameter1=>1 resource=>sppr952d"
                                     add_effect_global_inline=>"opcode=>142 parameter2=>68"
                                     ~
   END

   // sword spider: haste, immunity to web overlay,  protection from web spells, poison
   LAF edit_item STR_VAR item=dw#shspi
                         editstring= ~add_effect_global_inline=>"opcode=>16"
                                      add_effect_global_inline=>"opcode=>101 parameter2=>157"
                                      add_effect_global_inline=>"opcode=>206 resource=>spwi215"
                                      add_effect_global_inline=>"opcode=>206 resource=>spdr201"
                                      add_effect_global_inline=>"opcode=>206 resource=>spin575"
                                      add_effect_global_inline=>"opcode=>206 resource=>spin683"
                                      add_effect_inline=>"match=>ability_type check=>1 opcode=>25 target=>2 parameter1=>2 parameter2=>3 duration=>36 save_vs_poison=>1"
                                      add_effect_inline=>"match=>ability_type check=>1 opcode=>142 target=>2 parameter2=>6  duration=>36 save_vs_poison=>1"
                                      ~
   END
   
   // fire elemental: haste, THAC0=2, fire resistance, fire aura, immunity to weapons, fire damage
   LAF edit_item STR_VAR item=dw#shfir
                         editstring=~add_effect_global_inline=>"opcode=>16"
                                     add_effect_global_inline=>"opcode=>120 parameter2=>2"
                                     add_effect_global_inline=>"opcode=>54 parameter1=>2 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>30 parameter1=>100 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>142 parameter2=>164"
                                     add_effect_global_inline=>"opcode=>232 parameter1=>1 resource=>sppr952d"
                                     add_effect_inline=>"match=>ability_type check=>1 opcode=>12 target=>2 parameter2=>8 timing=>1 dicenum=>1 dicesize=>10"
                                     ~
   END
   // earth elemental: immunity to normal weapons, THAC0=2, damage resistance
   LAF edit_item STR_VAR item=dw#shear
                         editstring=~add_effect_global_inline=>"opcode=>120 parameter2=>2"
                                     add_effect_global_inline=>"opcode=>54 parameter1=>2 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>86 parameter1=>100 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>87 parameter1=>100 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>88 parameter1=>100 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>89 parameter1=>100 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>142 parameter2=>165"
                                     ~
    END

END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  Undroppable tokens for enemies
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION shapeshift_token_enemies BEGIN

   ACTION_DEFINE_ASSOCIATIVE_ARRAY shapeshift_enemy BEGIN
      ear=>nea
      fir=>nfi
      sal=>nsa
      wyv=>nwy
      spi=>nsp
      we1=>nw1
      we2=>nw2
      we3=>nw3
      brp=>nbr
      wol=>nwo
   END

   ACTION_PHP_EACH shapeshift_enemy AS old=>new BEGIN
     LAF clone_item STR_VAR item=~dw#sh%old% => dw#sh%new%~
                            editstring=~droppable=>0 delete_opcodes=>145~
     END
   END


END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  The main spell-making block
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION shapeshift_spells BEGIN


    OUTER_SPRINT name @13120
    LAF install_spell STR_VAR spell=dw#shw1 location=resource editstring=~set_name=>"%name%"~ END

   LAF process_table STR_VAR table=shapeshift_spells.2da inline=yes function=make_shapeshift_spell END

   LAF edit_spell STR_VAR spell=sppr731 editstring=~say_description=>13150~ allow_missing=1 END
   LAF edit_spell STR_VAR spell=sppr732 editstring=~say_description=>13151~ allow_missing=1 END


END


//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  used to make individual spell
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_shapeshift_spell
   STR_VAR spellname=""
           resource=""
           icon=""
BEGIN
   ACTION_IF FILE_EXISTS_IN_GAME ~%resource%.itm~ BEGIN
      COPY_EXISTING ~%resource%.itm~ ~override~    // use the token name, not the spell name (v24)
         LPF ITM_read_name1_string RET stringcode=value END
      BUT_ONLY
      LAF clone_spell STR_VAR spell=~dw#shw1=>%spellname%~
                          editstring=~name1_string=>%stringcode% literal=>"WRITE_ASCIIE 0xae %resource% (8) WRITE_ASCIIE 0x76 %icon%"~
      END

   END
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  used to make classfiles
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION shapeshift_classfiles BEGIN

    LAF edit_all_kits STR_VAR parent_class=druid editstring="remove_power=>spcl613" END  // redundant bear power
    MAKE_PATCH
       remove_power=>"spcl643"
       remove_power'=>"spcl644"
       grant_power=>"dw#shw1 1"
       grant_power'=>"dw#shw2 7"
       grant_power''=>"dw#shw3 13"
    END
    LAF edit_kit STR_VAR kit=shapeshifter edits=patch_data END

   COPY_EXISTING_REGEXP ~lu.*\.2da~ ~override~ // we allow for possible extra druid kits
     COUNT_2DA_ROWS 9 ~rowcount~
     FOR (i=0;i<~rowcount~;i=i+1) BEGIN
	  READ_2DA_ENTRY i 1 9 ~abcheck~
	  PATCH_IF ~%abcheck%~ STRING_EQUAL_CASE ~GA_SPPR731~ BEGIN
	     SET_2DA_ENTRY i 1 9 ~AP_SPPR731~
	     SET_2DA_ENTRY i 6 9 ~1~
	  END
	  PATCH_IF ~%abcheck%~ STRING_EQUAL_CASE ~GA_SPPR732~ BEGIN
	     SET_2DA_ENTRY i 1 9 ~AP_SPPR732~
	     SET_2DA_ENTRY i 6 9 ~1~
	  END
     END
   BUT_ONLY
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  insect plague compatibility
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION shapeshift_insect_plague BEGIN


   LAF include STR_VAR locbase=spell file=insect_plague.tpa END
   LAUNCH_ACTION_MACRO read_insect_plague_patch
   LAF edit_item STR_VAR item=~dw#shsal dw#shfir~ edits=insect_plague_patch END


END

