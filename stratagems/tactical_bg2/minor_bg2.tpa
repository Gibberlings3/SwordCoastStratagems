INCLUDE ~%scsroot%/lib/ai_wrap.tph~
DEFINE_ACTION_FUNCTION minor_bg2 BEGIN

      ACTION_FOR_EACH func IN compound grove cult talos elemental nyalee lava muggers BEGIN
         LAF ~minor_%func%~ END
      END

END

DEFINE_ACTION_FUNCTION minor_compound BEGIN
 // Traps in the Guarded compound need to target player only, and the nonfunctional True Sight traps become Breach traps.

 LAF install STR_VAR files=~dw#brtrp.baf~ inline=yes END
 COPY_EXISTING ~gslime.bcs~ ~override\dw#sltrp.bcs~
 LAF swap_text STR_VAR files="dw#sltrp.bcs" swaps="ANYONE=>GOODCUTOFF" END
 LAF swap_text STR_VAR files="ar0907.are" swaps="gtts=>dw#brtrp gslime=>dw#sltrp" END
 
END

<<<<<<<< .../stratagems-inline/dw#brtrp.baf
IF
	OR(2)
		Entered([GOODCUTOFF])
		Opened([GOODCUTOFF])
THEN
	RESPONSE #100
		DisplayString(LastTrigger,14381)
		ForceSpell(LastTrigger,WIZARD_BREACH)
END
>>>>>>>>

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION minor_grove BEGIN

// Mildly upgrade the Druid Grove, following the Tactics model

    LAF edit_creature STR_VAR creature=smound01 editstring=~xp_value=>8000~ END

    LAF extend_area_script STR_VAR area=ar1900 top=imp1900 location=resource END
    
    LAF clone_creature STR_VAR creature=~udelde=>dw#eldee~ editstring=~dv=>dw#eldee strip_script=>gensht01~ END

END

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION minor_cult BEGIN

     // Slightly improved Beholder Cult guards

     LAF edit_creature STR_VAR creature=~bhguard2 bhgaal01~ editstring=~str=>18 str_ex=>50~ END

END

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION minor_talos BEGIN

   // Talosar priests become fighter 10/cleric 12
   
   MAKE_PATCH
      class=>fighter_cleric
      str=>18
      str_ex=>76
      level1=>10
      level2=>12
      enforce_charclass=>null
      replace_items=>~plat04 blun05~
      resist_electricity=>120
   END
   LAF edit_creature STR_VAR creature=~talkni01 talkni02~ edits=patch_data END

   // Ada becomes L20 and gets a death-scream

   MAKE_PATCH
      level=>20
      insert_script_high=>dw#tlada
      replace_items=>~chan08 staf13~
      enforce_charclass=>null
   END
   LAF edit_creature STR_VAR creature=~talmiss~ edits=patch_data END
   LAF install STR_VAR file=~dw#tlada.baf~ location=resource END

   // more reinforcements, including a mage

   MAKE_PATCH
      say_both_names=>2800
      script_override=>sht0904
      add_items=>~brac14 dw#dispe~
   END
   LAF clone_creature STR_VAR creature=~mage16a=>dw#talmg~ edits=patch_data END
   LAF extend_area_script STR_VAR area=ar0904 top=ar0904add location=resource END


END

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION minor_elemental BEGIN

    // immunity to Earthquake
    
    LAF edit_creature STR_VAR creature=~druear01 elear01 elearg01 elearl01 elearpr elearpr2 elearpr3 elearsu 
                                                 elearsu1 elearsu2 elearsu3 elearsu4 elearsuw eleeel01 kearth mdearth
                                                 mekear01 shearth suearth sumelear swaear01 swaear02 udearth udelde~
                              editstring=~immunity_to_spell=>%CLERIC_EARTHQUAKE% immunity_to_spell=>spogre01~
    END

    // the elemental guardians whom Vithal summons

    LAF ssl_to_bcs STR_VAR script=~dw#udela dw#udele dw#udelf~ location=~ssl~ END

    // air

    LAF clone_spell STR_VAR spell=~%DRAGON_WING_BUFFET%=>dw#airbu~ editstring=~say_name=>3202~ END
    MAKE_PATCH
       strip_script=>airele01
       strip_scs_scripts=>null
       script_override=>dw#udela
       immunity_to_spell=>dw#airbu
       attacks=>3
       hitpoints=>180
       make_casting_innate=>null
    END
    LAF edit_creature STR_VAR creature=udelda edits=patch_data END
    LAF edit_creature STR_VAR creature=elair01 editstring=~immunity_to_spell=>dw#airbu~ END

    // fire

    MAKE_PATCH
       strip_scs_scripts=>null
       script_override=>dw#udelf
       attacks=>3
       hitpoints=>180
       make_casting_innate=>null
    END
    LAF edit_creature STR_VAR creature=udeldf edits=patch_data END


    // earth
    
    MAKE_PATCH
       strip_scs_scripts=>null
       script_override=>dw#udele
       attacks=>3
       hitpoints=>180
       make_casting_innate=>null
    END
    LAF edit_creature STR_VAR creature=udelde edits=patch_data END

    // vithal

    LAF extend STR_VAR script=dwvith top=udvithadd inline=yes END
END

<<<<<<<< .../stratagems-inline/udvithadd.baf
IF
  !Allegiance(Myself,ENEMY)
  HPPercentLT(Myself,100)
  OR(3)
     See("udelde")
     See("udeldf")
     See("udelda")
  !StateCheck(Myself,STATE_INVISIBLE)
THEN
   RESPONSE #100
      ForceSpell(Myself,WIZARD_INVISIBILITY)
END

IF
  !Allegiance(Myself,ENEMY)
  HPPercentLT(Myself,100)
  !CombatCounter(0)
  StateCheck(Myself,STATE_INVISIBLE)
THEN
   RESPONSE #100
      NoAction()
END

IF
  !Allegiance(Myself,ENEMY)
  StateCheck(Myself,STATE_INVISIBLE)
  CombatCounter(0)
THEN
   RESPONSE #100
      ApplySpell(Myself,RESTORE_FULL_HEALTH)
      ApplySpellRES("dw#brkin",Myself)
END

>>>>>>>>

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

//// Nyalee's mirror


DEFINE_ACTION_FUNCTION minor_nyalee BEGIN

      LAF extend STR_VAR script=hgnya01 top=nyalee_add location=ssl ssl=yes END
      LAF player_clones END
      LAF clone_spell STR_VAR spell=~dw#clone=>dw#nyopp~ editstring=~say_name=>3302~ END
      LAF clone_item STR_VAR item=~misc6h=>dw#nyopp~ editstring=~say_both_names=>3300 say_both_descriptions=>3301~ END


END

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

// fire giant lava shouldn't count as L1

DEFINE_ACTION_FUNCTION minor_lava BEGIN

        LAF edit_spell STR_VAR spell=FIRE_GIANT_LAVA editstring=~patch_effect_inline=>"power=>0"~ END

END

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

//// muggers in the docks shouldn't respawn endlessly & should be straight Thieves


DEFINE_ACTION_FUNCTION minor_muggers BEGIN

     LAF install STR_VAR files=~dw#dkmg1.baf dw#dkmg2.baf~ inline=yes END

     MAKE_PATCH
        script_class=>dw#dkmg1
        class=>thief
        enforce_charclass=>null
     END
     LAF edit_creature STR_VAR creature=spmugg2 edits=patch_data END

     MAKE_PATCH
        script_class=>dw#dkmg2
        class=>thief
        enforce_charclass=>null
     END
     LAF edit_creature STR_VAR creature=~spmugg spmugg3~ edits=patch_data END

END


<<<<<<<< .../stratagems-inline/dw#dkmg1.baf

IF
  GlobalGT("DMWWDockMuggers","GLOBAL",2)
  OR(2)
  AreaCheck("ar0300")
  AreaCheck("ar0400")
THEN
  RESPONSE #100
   DestroySelf()
END

IF
   OnCreation()
  OR(2)
  AreaCheck("ar0300")
  AreaCheck("ar0400")
THEN
  RESPONSE #100
    IncrementGlobal("DMWWDockMuggers","GLOBAL",1)
END

>>>>>>>>

<<<<<<<< .../stratagems-inline/dw#dkmg2.baf

IF
  GlobalGT("DMWWDockMuggers","GLOBAL",2)
  OR(2)
  AreaCheck("ar0300")
  AreaCheck("ar0400")
THEN
  RESPONSE #100
   DestroySelf()
END
>>>>>>>>

