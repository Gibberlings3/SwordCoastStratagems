//////////////////////////////////////////////////////////////////
/////		Flee if war is over
//////////////////////////////////////////////////////////////////

IF
	OR(2)
		GlobalGT("villynatyjob","GLOBAL",0)
		Dead("sahpr2") // Prince Villynaty
THEN
	RESPONSE #100
		EscapeArea()
END

////////////////////////////////////////////////////////////
////	This is a rest-and-recover step for when
////	enemies leave you alone...
////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%\lib\ssl\common_rest.ssl)

//////////////////////////////////////////////////////////////////////
/////	Hate PCs if appropriate
//////////////////////////////////////////////////////////////////////

IF
	!GlobalTimerNotExpired("hatePCs","LOCALS")
	OR(9)	
		Allegiance(LastAttackerOf(Myself),GOODCUTOFF)
		HitBy([GOODCUTOFF],CRUSHING)
		AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		SpellCastOnMe([GOODCUTOFF],0)
THEN
	RESPONSE #100
		SetGlobalTimer("hatePCs","LOCALS",60)
		SetGlobalTimer("DMWWHostileFish","GLOBAL",6)
		Continue()
END

IF
	!GlobalTimerNotExpired("hatePCs","LOCALS")
	See([PC])
	!See(scsrival)
THEN
	RESPONSE #100
		SetGlobalTimer("hatePCs","LOCALS",60)
END

IF
	!GlobalTimerNotExpired("hatePCs","LOCALS")
	GlobalTimerNotExpired("DMWWHostileFish","GLOBAL")
THEN
	RESPONSE #100
		SetGlobalTimer("hatePCs","LOCALS",60)
END

///////////////////////////////////////////////////////////////////////
////	Make noise
////////////////////////////////////////////////////////////////////////

IF
	See([PC])
	Range(LastSeenBy(Myself),12)
	!GlobalTimerNotExpired("Shout","LOCALS")
	!General(Myself,UNDEAD)
	!Gender(Myself,SUMMONED)
	!Gender(Myself,20)
THEN
	RESPONSE #30
		SetGlobalTimer("Shout","LOCALS",100)
		SetGlobalTimer("hatePCs","LOCALS",60)
		SetGlobalTimer("DMWWHostileFish","GLOBAL",6)
		DisplayStringHead(Myself,53784) // Surfacers! Feel our wrath!
		Continue()
	RESPONSE #30
		SetGlobalTimer("Shout","LOCALS",100)
		SetGlobalTimer("hatePCs","LOCALS",60)
		SetGlobalTimer("DMWWHostileFish","GLOBAL",6)
		DisplayStringHead(Myself,53785) // The Shark God take you!
		Continue()
	RESPONSE #30
		SetGlobalTimer("Shout","LOCALS",100)
		SetGlobalTimer("hatePCs","LOCALS",60)
		SetGlobalTimer("DMWWHostileFish","GLOBAL",6)
		DisplayStringHead(Myself,53786) // I shall taste your blood, manlings!
		Continue()
END

 
///////////////////////////////////////////////////
////	If attacked by an unseen foe, mill around
////	in confusion. You never know, you might
////	bump into them!
//////////////////////////////////////////////////

IF
	Allegiance(Myself,ENEMY)
    OR(12)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
      SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
      SpellCastOnMe([PC],0)
      SpellCastOnMe([FAMILIAR],0)
      SpellCastOnMe([ALLY],0)
      SpellCastOnMe([CONTROLLED],0)
      SpellCastOnMe([CHARMED],0) 
!GlobalTimerNotExpired("whatthe","LOCALS")
  !See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    SetGlobalTimer("whatthe","LOCALS",12)
    Continue()
END

IF
  !See(NearestEnemyOf(Myself))
  GlobalTimerNotExpired("whatthe","LOCALS")
THEN
  RESPONSE #100
    RandomWalkContinuous()
END

/////////////////////////////////////////////////////
///	Cancel the "chase" move so you actually stop and shoot
/////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!Global("chasePC","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("chasePC","LOCALS",0)
		ClearActions(Myself)
END

//////////////////////////////////////////////////////////////////////////////
/////	Call for help in a minimal way, if in hate-PC mode
//////////////////////////////////////////////////////////////////////////////


/// if I'm currently trying to find a PC and I've been looking for too long, give up.

IF
	GlobalGT("helptarget","LOCALS",0)
	GlobalTimerExpired("helpduration","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("helptarget","LOCALS",0)
END

/// if I'm currently trying to find a PC, set a time limit before I give up.

IF
	GlobalGT("helptarget","LOCALS",0)
	!See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("helpduration","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("helpduration","LOCALS",12)
		Continue()
END

BEGIN LOOP(scsplayer||1;2;3;4;5;6) // go through all the players

/// if I'm not currently chasing someone, am hostile, and see player X, give a Type 1 shout of X's number and set myself as chasing X

IF
	GlobalTimerNotExpired("hatePCs","LOCALS")
	!GlobalGT("helptarget","LOCALS",0)
	See(Playerscsplayer)
THEN
	RESPONSE #100
		Shout(31scsplayer)
		SetGlobal("helptarget","LOCALS",scsplayer)
		SetGlobalTimer("helpduration","LOCALS",12)
		Continue()
END

/// if I was chasing someone but aren't any more, and I hear a type 1 shout about player X, and if condition XXXXX is satisfied, give a type 2 shout of
/// X's number, go hostile, set myself as chasing X, and set a duration for how long I do it.
///

IF
	!GlobalGT("helptarget","LOCALS",0)
	Heard([ANYONE],31scsplayer)
	GlobalTimerExpired("helpduration","LOCALS")
	Range(LastHeardBy(Myself),10)
THEN
	RESPONSE #100
// 		Shout(888scsplayer) commented out: sahuagin shouts do not propagate
		SetGlobalTimer("hatePCs","LOCALS",60)
		SetGlobal("helptarget","LOCALS",scsplayer)
		SetGlobalTimer("helpduration","LOCALS",12)
		Continue()
END

/// if I'm not currently chasing someone and I hear a type 1 shout about player X, and if condition XXXXX is satisfied, give a type 2 shout of
/// X's number, go hostile, and set myself as chasing X

IF
	!GlobalGT("helptarget","LOCALS",0)
	Heard([ANYONE],31scsplayer)
	Range(LastHeardBy(Myself),10)
THEN
	RESPONSE #100
	//	Shout(888scsplayer)
		SetGlobalTimer("hatePCs","LOCALS",60)
		SetGlobal("helptarget","LOCALS",scsplayer)
		Continue()
END


/// if I can't see an enemy, am chasing player X, and am not innocent, then move towards player X


IF
	Global("helptarget","LOCALS",scsplayer)
	!Class(Myself,THIEF_ALL)
	!See(NearestEnemyOf(Myself))
	!Class(Myself,INNOCENT)
	!Detect(Playerscsplayer)
	!HasItem("dw#nohlp",Myself)
THEN
	RESPONSE #100
		MoveToObject(Playerscsplayer)
END

END LOOP

//////////////////////////////////////////////////////////////////////
/////	Attack rival sahuagin 
///////////////////////////////////////////////////////////////////////

IF
	!GlobalTimerNotExpired("hatePCs","LOCALS")
	OR(2)
		Detect([PC])
		Heard([ANYONE],310)
	See(scsrival)
THEN
	RESPONSE #100
		Shout(310) // "we're fighting!"
		AttackOneRound(LastSeenBy(Myself))
END

