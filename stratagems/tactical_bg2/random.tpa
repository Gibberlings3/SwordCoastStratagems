INCLUDE ~%MOD_FOLDER%/lib/ai_wrap.tph~
DEFINE_ACTION_FUNCTION random BEGIN

    // remove pointless trolls from ar0042

   LAF edit_area STR_VAR area=ar0042 editstring="delete_actor=>~actorname=troll~" END

   // scripts

   ACTION_IF FILE_EXISTS_IN_GAME ~ubsuna.xxx~ BEGIN
    	OUTER_SPRINT variables ~UBSuna=True~
    	OUTER_SPRINT variables_city ~UBSuna=True&IsInCity=True~
   END ELSE BEGIN
      OUTER_SPRINT variables ~~
      OUTER_SPRINT variables_city ~IsInCity=True~
   END

   // delete unneeded blocks from random encounters
   
   ACTION_FOR_EACH script IN ar0041 ar0042 ar0043 ar0044 ar0045 ar0046 BEGIN
    ACTION_FOR_EACH match IN reband01 orc05 rerak01 BEGIN
       LAF DELETE_SCRIPT_BLOCK STR_VAR script match END
    END
   END

   // add new blocks

   LAF extend STR_VAR script="ar0042 ar0043 ar0044" location=ssl bottom=dw#random ssl=yes END
   LAF extend STR_VAR script="ar0041 ar0045 ar0046" location=ssl bottom=dw#random ssl=yes variables="IsInCity=True" END

   // rakshasas

   LAF clone_creature STR_VAR creature=~raksha01=>dw#rrak1~ editstring=~strip_script=>raksha02~ END
   LAF clone_creature STR_VAR creature=~rakruh01=>dw#rrak2~ END
   LAF clone_creature STR_VAR creature=~rakraj01=>dw#rrak3~ editstring=~add_items=>misc42~ END
   LAF clone_creature STR_VAR creature=~rakmah01=>dw#rrak4~ editstring=~add_items=>misc44(x2)~ END

   // bandits

   LAF edit_creature STR_VAR creature=~reband01 reband02~ editstring=~str=>16 dex=>17 con=>14~ END
   MAKE_PATCH
      replace_items=>chan04
      add_items=>shld03
      str=>18
      str_ex=>50
      dex=>14
      con=>17
      genai=>profs
   END
   LAF edit_creature STR_VAR creature=reband03 edits=patch_data END

   // orog slavers

   LAF clone_creature STR_VAR creature=~refigh01=>dw#refg3~ END
   LAF clone_creature STR_VAR creature=~mage11=>dw#remg1~ editstring=~name1_string=>21939 name2_string=>21939 add_items=>staf01~ END

   ///	Tougher version of the first attack -
   ///	a L8 and a L10 priest, a L10 mage, two L9 fighters
   ///	and the same thieves from the previous encounter

   MAKE_PATCH
      add_items=>~staf01 rndtre04~
      genai=>profs
      name1_string=>13838
      name2_string=>13838
   END
   LAF clone_creature STR_VAR creature=~mage10c=>dw#remg2~ edits=patch_data END
   MAKE_PATCH
      add_items=>~chan04 blun02 shld03 rndtre03~
      genai=>profs
      name1_string=>13838
      name2_string=>13838
   END
   LAF clone_creature STR_VAR creature=~pries8a=>dw#repr1~ edits=patch_data END
   MAKE_PATCH
      add_items=>~plat01 blun03 shld03 rndtre03~
      genai=>profs
      name1_string=>13838
      name2_string=>13838
   END
   LAF clone_creature STR_VAR creature=~pries10c=>dw#repr2~ edits=patch_data END
   MAKE_PATCH
      level=>9
      replace_items=>plat01
      add_items=>rndtre02
      enforce_charclass=>null
   END
   LAF clone_creature STR_VAR creature=~reband03=>dw#refg1~ edits=patch_data END
   
   ///	Somewhat more stealth-style encounter - 4 REBAND01,
   ///	an 10th level and a 12th level mage, and 3 REFIGH01

   MAKE_PATCH
      add_items=>~dart01(x20) staf01 rndtre03~
      genai=>profs
      allegiance=>enemy
      name1_string=>13838
      name2_string=>13838
   END
   LAF clone_creature STR_VAR creature=~mage10d=>dw#remg3 mage12b=>dw#remg4~ edits=patch_data END
   LAF clone_creature STR_VAR creature=~dw#refg3=>dw#refg2~ editstring=~name1_string=>4419 name2_string=>4419~ END

   //	Light relief

   MAKE_PATCH
      insert_script_high=>dw#ranlr
      class=>FIGHTER
      delete_opcodes=>20
      strip_scs_scripts=>null
   END
   OUTER_FOR (i=1;i<=5;i+=1) BEGIN
      LAF clone_creature STR_VAR creature=~reband0%i%=>dw#rnlr%i%~ edits=patch_data END
   END
   LAF clone_creature STR_VAR creature=~icgob01=>dw#rnlg1~ edits=patch_data END

   LAF edit_creature STR_VAR creature=dw#rnlr1 editstring=~dv=>dw#rnlr1~ END
   LAF edit_creature STR_VAR creature=dw#rnlr2 editstring=~dv=>dw#rnlr2~ END

   LAF clone_creature STR_VAR creature=~dw#rnlg1=>dw#rnlg2~ editstring=~dv=>dw#rnlg2~ END
   LAF clone_creature STR_VAR creature=~dw#rnlg1=>dw#rnlg3~ editstring=~dv=>dw#rnlg3~ END
   
   LAF install STR_VAR file=dw#ranlr.baf location=resource END
END


