DEFINE_ACTION_FUNCTION spawn
BEGIN

    LAF define_difficulty STR_VAR type=spawn RET difficulty_variable END
    LAF process_table STR_VAR function=spawn_core table=spawn_list.2da inline=yes END
    LAF process_table STR_VAR function=c6_spawn_core table=c6_spawn_list.2da inline=yes END
    LAF spawn_misc END

END

<<<<<<<< .../stratagems-inline/spawn_list.2da
spawn_script    group1                           group2                                                   group3                                                       group4
spwnbeh         behgau01                         behgau01|behgau01|behgau01                               behold01|behgau01|behgau01                                   behold01|behold01|behold01|behgau01|behgau01
spwndead        skelwa01|shadow01|shadow01       skelwa01|skelwa01|shadfi01|shadfi01                      mumgre01|icbone01|skelwa01|skelwa03                          mumgre01|mumgre01|icbone01|icbone01|skelwa01|skelwa03|skelwa03
spwndrow        drow01|drow03|drow02|drow04      drow02|drow02|drow01|drow05                              drow01|drow04|drow06|drow04|drow05|drow07                    drow04|drow05|drow06|drow05|drow04|drow06|drow07
spwngol         golsto01                         golsto01|golsto01                                        golsto01|golsto01|golcla01|goliro01                          golada01|golsto01|golsto01
spwnmind        mindfl01                         mindfl01|mindfl01                                        mindfl01|mindfl01|mindul01|umbhul01                          mindfl01|mindfl01|mindfl01|mindfl01|mindul01|umbhul01|umbhul01
spwnmon         icsalfir|icsalcol                icsalfir|icsalcol|icyuan03                               icmin01|umbhul01|umbhul01|umbhul01|umbhul01|umbhul01         icyuan02|icyuan01|icyuan01|icyuan03|dw#icyu1|dw#icyu2
spwnorc         orc01|orc01|orc02|orc05|orc03    orc05|orc06|orc04|orc03|orc01|orc01                      orc03|orc05|orc06|rakruh01|raksha01|orc01                    orc05|orc06|orc05|rakraj01|rakmah01|orc06
spwnrak         raksha01                         raksha01|raksha01|ogre01|ogre01|ogre01                   rakraj01|rakruh01|rakruh01|orc05|orc05                       rakmah01|rakraj01|rakraj01|rakmah01
spwntoa         kuotoa01|kuotoa01|kuotoa01       kuotoa01|kuowar01|kuowar01|kuowar01|kuowhi01|kuomon01    kuowar01|kuolie01|kuopri01|kuomag01|kuomon01|kuowhi01        kuowar01|kuopri01|kuomag01|kuomon01|kuomon01|kuomon01|kuowhi01
spwntrol        troll01|trolic01|trolic01        troll01|troll01|troll01|troll01                          troll01|troll01|trolgi01|trolsi01|trolsp01                   trolsi01|trolsi01|trolsi01|troll01|troll01
spwnvamp        vampim01|vampim01                vampim01|vampim01|vampim01|vamold01|vamver01             vampim01|vampim01|vamold01|vamold01|vamver01|vamanc01        vamver01|vamver01|vamver01|vamanc01|vamanc01|vamemi01
spwnwolf        wolfwe01|wolfwe01                wolfwe01|wolfwe01|wolfwe01|wolfwe01                      wolfgr01|wolfwe01|wolfwe01|wolfdr01|wolfdr01                 wolfgr01|wolfgr01|wolfgr01|wolfdr01|wolfdr01
dw#spng         golsto01                         golsto01|golsto01                                        golsto01|golsto01|golcla01|golcla01                          goljug01|golsto01|golsto01
>>>>>>>>

<<<<<<<< .../stratagems-inline/c6_spawn_list.2da
spawn_script    group
c6behold        behold01|behgau01|behgau01
c6goljug        goljug01
c6golsto        trolsp01|trolsi01|trolsi01|trolsi01
c6mindul        mindul01|mindfl01|mindfl01
c6rakraj        goljug01
c6vamp          vamver01|lich01
c6weregr        wolfgr01|wolfgr01
>>>>>>>>

DEFINE_ACTION_FUNCTION spawn_core
  STR_VAR spawn_script=""
          group1=""
          group2=""
          group3=""
          group4=""
BEGIN
   LAF return_spawn_string STR_VAR list="%group1%" RET spawn_group_1=string END
   LAF return_spawn_string STR_VAR list="%group2%" RET spawn_group_2=string END
   LAF return_spawn_string STR_VAR list="%group3%" RET spawn_group_3=string END
   LAF return_spawn_string STR_VAR list="%group4%" RET spawn_group_4=string END
   LAF ssl_to_bcs STR_VAR script=spawn_prototype location=ssl rename_to="%spawn_script%" END
END

DEFINE_ACTION_FUNCTION c6_spawn_core
  STR_VAR spawn_script=""
          group=""
BEGIN
   LAF return_spawn_string INT_VAR door=1 STR_VAR list="%group%" RET spawn_group=string END
   LAF ssl_to_bcs STR_VAR script=c6_spawn_prototype location=ssl rename_to="%spawn_script%" END
END

DEFINE_ACTION_FUNCTION return_spawn_string
   INT_VAR door=0
   STR_VAR list=""
   RET string
BEGIN
   ACTION_IF door BEGIN 
        OUTER_SPRINT create_string CreateCreatureDoor
   END ELSE BEGIN
        OUTER_SPRINT create_string CreateCreature
   END
   OUTER_SPRINT string ""
   OUTER_WHILE "%list%" STRING_COMPARE "" BEGIN
      LAF return_first_entry STR_VAR list separator="|" RET entry list END
      OUTER_SPRINT string ~%string%%create_string%("%entry%",[-1.-1],8)~
   END
END

DEFINE_ACTION_FUNCTION spawn_misc BEGIN

     LAF swap_text STR_VAR files=tanhigh.bcs swaps="XPGT(Player1,399999)=>True() XPGT(Player1,999999)=>True()" END
     LAF swap_text STR_VAR files=ar3001.bcs swaps=~Global("SetDifficulty","AR3001",0)=>False()~ END
     LAF swap_text STR_VAR files=ar3007.bcs swaps=~LevelPartyGT(16)=>True()~ END
     LAF swap_text STR_VAR files=ar3012.bcs swaps=~Global("ReduceDemons","AR3012",0)=>False()~ END
     LAF swap_text STR_VAR files=ar3013.bcs swaps=~Global("ReduceDemons","AR3013",0)=>False()~ END
     LAF swap_text STR_VAR files=ar3016.bcs swaps=~Global("SetDifficulty","AR3016",0)=>False()~ END
     LAF swap_text STR_VAR files=ar4000.bcs swaps=~cut204f2=>cut204f cut204f3=>cut204f~ END
     LAF swap_text STR_VAR files=ar4500.bcs swaps=~SetGlobal("Chall1_Diff","AR4500",1)=>SetGlobal("Chall1_Diff","AR4500",4)
                                                   SetGlobal("Chall1_Diff","AR4500",2)=>SetGlobal("Chall1_Diff","AR4500",4)
                                                   SetGlobal("Chall1_Diff","AR4500",3)=>SetGlobal("Chall1_Diff","AR4500",4)~ END


     // Yuan-ti, in case "smarter mages" isn't installed
     
     ACTION_IF !FILE_EXISTS_IN_GAME "dw#icyu1.cre" BEGIN
        COPY_EXISTING "icyuan03.cre" "override/dw#icyu1.cre"
        COPY_EXISTING "icyuan03.cre" "override/dw#icyu2.cre"
     END


END


