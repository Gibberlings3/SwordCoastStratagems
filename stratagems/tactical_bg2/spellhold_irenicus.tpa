INCLUDE ~%MOD_FOLDER%/lib/ai_wrap.tph~
DEFINE_ACTION_FUNCTION spellhold_irenicus BEGIN

  // "mirrored clones" are actual copies
  
      LAF player_clones END
      LAF DELETE_SCRIPT_BLOCK STR_VAR script=ppjon match=~DisplayString(Myself,48812)~ END
      LAF extend INT_VAR ssl=1 STR_VAR file=ppjon location=ssl bottom=ppjon_add END



  // mildly upgrade the Murderers

<<<<<<<< .../stratagems-inline/ppmur.baf
IF
	!Detect([PC])
	!Range(Player1,20)
	!Range(Player2,20)
	!Range(Player3,20)
	!Range(Player4,20)
	!Range(Player5,20)
	!Range(Player6,20)
THEN
	RESPONSE #50
		MoveToObject(Player1)
	RESPONSE #20
		MoveToObject(Player2)
	RESPONSE #20
		MoveToObject(Player3)
	RESPONSE #20
		MoveToObject(Player4)
	RESPONSE #20
		MoveToObject(Player5)
	RESPONSE #20
		MoveToObject(Player6)
END
>>>>>>>>

LAF install INT_VAR inline=1 STR_VAR files=ppmur.baf END

MAKE_PATCH
    level=>7
    enforce_charclass=>null
END
LAF edit_creature STR_VAR creature=ppmur edits=patch_data END

// cosmetic fix to the pre-battle cutscene - mages teleport as well as party

       COPY_EXISTING "cut41l.bcs" override
         DECOMPILE_AND_PATCH BEGIN
            PATCH_FOR_EACH lunatic IN dili wanev nalj aph tiax dra BEGIN
               REPLACE_TEXTUALLY ~CreateVisualEffectObject("SPDIMNDR",Player6)~ ~CreateVisualEffectObject("SPDIMNDR",Player6)CreateVisualEffectObject("SPDIMNDR","pp%lunatic%2")~
               REPLACE_TEXTUALLY ~ActionOverride(Player6,CreateVisualEffectObject("SPDIMNDR",Myself))~ ~ActionOverride(Player6,CreateVisualEffectObject("SPDIMNDR",Myself))ActionOverride("pp%lunatic%2",CreateVisualEffectObject("SPDIMNDR",Myself))~
            END
         END
       BUT_ONLY


END

