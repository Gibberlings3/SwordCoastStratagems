INCLUDE ~%scsroot%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION darnise
STR_VAR version=""
BEGIN

     // define difficulty variables
     
     LAF define_difficulty STR_VAR type=troll RET difficulty_variable END

     // create the spy that spawns all the trolls, and its script

      LAF install STR_VAR files=~dw#spntr.baf dw#kpspy.cre~ location=resource END
      LAF edit_creature STR_VAR creature=dw#kpspy editstring=~script_override=>dw#spntr~ END

     // patch it in in place of extant spawns

     MAKE_PATCH
        wipe_spawns=>null
        spawn_creature1=>dw#kpspy
        spawn_count=>1
     END
     LAF edit_area STR_VAR area=~ar1300 ar1301 ar1302 ar1303~ editstring=~patch_spawn=>patch_data~ END

     // make go-hostile script
     
     LAF install STR_VAR file=dw#torg.baf location=resource END

     // make yuan-ti mages
     
     LAF clone_creature STR_VAR creature="icyuan03=>dw#kpyu1 icyuan03=>dw#kpyu2 icyuan03=>dw#kpyi1 icyuan03=>dw#kpyi2" editstring="allegiance=>neutral insert_script_high=>dw#torg" END
     LAF check_label STR_VAR label="dw#mage" RET value END
     ACTION_IF value BEGIN
        LAF edit_creature STR_VAR creature="dw#kpyu1 dw#kpyu2" editstring="level=>12 enforce_mage=>null" END
        LAF edit_creature STR_VAR creature="dw#kpyi1 dw#kpyi2" editstring="level=>14 enforce_mage=>null" END
     END 

     // make umber hulks
     
     LAF clone_creature STR_VAR creature="umbhul01=>dw#kpum1 umbhul02=>dw#kpum2" editstring="allegiance=>neutral insert_script_high=>dw#torg" END

     // make non-initially-hostile giant trolls
     
     MAKE_PATCH
        match=>"actor_resource=trolgi01"
        script_override=>dw#torg
     END
     LAF edit_area STR_VAR area=ar1301 editstring="patch_actor=>patch_data" END

     // Torgal becomes a 15th level Barbarian with some DR and a cloak-of-fear effect

     MAKE_PATCH
        class=>FIGHTER
        kit=>BARBARIAN
        level=>15
        enforce_charclass=>null
        resist_slashing=>30
        resist_piercing=>30
        resist_crushing=>30
        resist_missile=>30
     END
     LAF edit_creature STR_VAR creature=~torgal torgal3~ allow_missing=1 edits=patch_data END // allow for FP being missing
     LAF extend STR_VAR file=torgal2 bottom=torgal ssl=yes location=ssl END

     ACTION_IF !FILE_EXISTS_IN_GAME ~dw#icyua.cre~ THEN BEGIN
         LAF clone_creature STR_VAR creature=~icyuan03=>dw#icyua~ END
     END

     LAF check_ini STR_VAR ini=spirit_troll_magic RET value END
     ACTION_IF value BEGIN
     
          LAF extend STR_VAR file=~trolsi01 pptroll2~ bottom=spirit_troll location=ssl ssl=yes END
          MAKE_PATCH
             resist_slashing=>50
             resist_piercing=>50
             resist_crushing=>50
             resist_missile=>50
             level=>9
          END
          LAF edit_creature STR_VAR creature=~trolsi01 trolsp01 pptroll1~ edits=patch_data END

     END
END
