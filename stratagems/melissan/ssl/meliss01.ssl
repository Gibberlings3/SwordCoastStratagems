// This is the script Mel uses in the first and second fights
//
// Powers (other than various summonings that we take over verbatim:
//
// Heal 1/5 rds
// Vocalise at will
// Free action at will
// Stoneskin 1/5 rds
// Timestop once
// Truesight 1/10 rds
// Haste 1/3 rds
// Death spell 1/2 rds
// 
// and a Big Spell (these block one another for a bit):
//
// Earthquake 
// Unholy Word
// Sunray 
// Pierce Shield [Pierce Magic in SR]
// Dispel Magic
// Greater Malison
// Feeblemind
// Symbol: Fear
// Symbol: Death


//////////////////////////////////////////////////////////////////////////////
//////	Definitions
//////////////////////////////////////////////////////////////////////////////

BEGIN_ACTION_DEFINITION
	Name(MoveIfEmpty)
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		MoveToObject(scstarget)
END

/////   Melissan casts in one of two ways:
/////	(1) force-casting
/////	(2) a hard-to-interrupt spellcast which is sometimes force-cast, sometimes spell-no-dec.
/////	In vanilla Ascension, the ratio is 70/30; we relax it to 50/50.
/////

/////	(1) Three arguments: the spell, the timer name, the timer length

BEGIN_ACTION_DEFINITION
	Name(ForceSpellTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		ForceSpell(scstarget,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(ForceSpellMyselfTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		ForceSpell(Myself,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(ForceSpellRESTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		ForceSpellRES(scsargument1,scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(ForceSpellRESMyselfTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		ForceSpellRES(scsargument1,Myself)
END

/////	(2) Three arguments: the spell, the timer name, the timer length
/////	NOTE: because there are two spellcasting blocks, the number put into the Continue section of the SSL call must be doubled

BEGIN_ACTION_DEFINITION
	Name(SpellTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
//		ForceSpell(scstarget,scsargument1)
		SpellNoDec(scstarget,scsargument1)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		SpellNoDec(scstarget,scsargument1)

END

BEGIN_ACTION_DEFINITION
	Name(SpellMyselfTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
//		ForceSpell(Myself,scsargument1)
		SpellNoDec(Myself,scsargument1)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		SpellNoDec(Myself,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(SpellRESTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
//		ForceSpellRES(scsargument1,scstarget)
		SpellNoDecRES(scsargument1,scstarget)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		SpellNoDecRES(scsargument1,scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(SpellRESMyselfTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
//		ForceSpellRES(scsargument1,Myself)
		SpellNoDecRES(scsargument1,Myself)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		SpellNoDecRES(scsargument1,Myself)
END

BEGIN_ACTION_DEFINITION
	Name(SpellNoDec)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
//		ForceSpell(scstarget,scsargument1)
		SpellNoDec(scstarget,scsargument1)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(SpellNoDecMyself)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
//		ForceSpell(Myself,scsargument1)
		SpellNoDec(Myself,scsargument1)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(Myself,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(SpellNoDecRESMyself)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
//		ForceSpellRES(scsargument1,Myself)
		SpellNoDecRES(scsargument1,Myself)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDecRES(scsargument1,Myself)
END

/////	Melee
/////

BEGIN_ACTION_DEFINITION
	Name(Attack)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
	ACTION
		RESPONSE #scsprob1
		SetGlobal("validtarget","LOCALS",1)
		EquipMostDamagingMelee()
		AttackOneRound(scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(vsPMscroll)
	TRIGGER
		!GlobalTimerNotExpired("spellstrike","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("spellstrike","LOCALS",30)
		SpellNoDec(scstarget,WIZARD_SPELL_STRIKE)
END

BEGIN_ACTION_DEFINITION
	Name(DoNothing)
	TRIGGER
	ACTION
		RESPONSE #scsprob1
		NoAction()
END

////////////////////////////////////////////////////////////////////
////	Initialise
////////////////////////////////////////////////////////////////////

IF
  See([GOODCUTOFF])
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Enemy()
END

IF
  !Global("FinalFight","AR6200",3)
  Global("FightOne","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("FightOne","LOCALS",1)
    SetGlobalTimer("MelStoneSkin","LOCALS",15)
    ReallyForceSpellRES("melstone",Myself)  // ~Stoneskin~
END

IF
  Global("FinalFight","AR6200",3)
  Global("FightTwo","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("FightTwo","LOCALS",1)
    SetGlobalTimer("MelStoneSkin","LOCALS",30)
    ReallyForceSpellRES("melstone",Myself)  // ~Stoneskin~
    SetSequence(SEQ_ATTACK_JAB)
    ReallyForceSpellRES("dgwhirl",[PC])  // <Invalid Strref -1>
END

////////////////////////////////////////////////////////////////////
////	Summons; turning
////////////////////////////////////////////////////////////////////

IF
!GlobalTimerNotExpired("castspell","LOCALS")
  See(NearestEnemyOfType([GOODCUTOFF.UNDEAD]))
  !HPLT(Myself,20)
  !GlobalTimerNotExpired("UndeadTurn","LOCALS")
  Range(LastSeenBy(Myself),20)
  !HPGT(LastSeenBy(Myself),75)
THEN
  RESPONSE #100
    SetGlobalTimer("UndeadTurn","LOCALS",6)
    DisplayStringHead(Myself,73243)  // ~Undead thing begone!~
    CreateVisualEffectObject("SPDISPMA",NearestEnemyOfType([GOODCUTOFF.UNDEAD]))
    ActionOverride(NearestEnemyOfType([GOODCUTOFF.UNDEAD]),Kill(Myself))
END

IF
!GlobalTimerNotExpired("castspell","LOCALS")
  !HPLT(Myself,20)
  See([GOODCUTOFF.DEAD])
  !Allegiance(LastSeenBy(Myself),CHARMED)
  !Gender(LastSeenBy(Myself),ILLUSIONARY)
  !GlobalTimerNotExpired("MakeShadow","LOCALS")
  !InPartySlot(LastSeenBy(Myself),0)
THEN
  RESPONSE #100
    SetGlobalTimer("MakeShadow","LOCALS",6)
    SetInterrupt(FALSE)
    ReallyForceSpellRES("melis03",LastSeenBy(Myself))  // ~Taint of the Slayer~
    IncrementGlobal("EXTRACOUNT4","MYAREA",1)
    SetInterrupt(TRUE)
END

IF
!GlobalTimerNotExpired("castspell","LOCALS")
  Died([GOODCUTOFF])
  !Allegiance(LastTrigger,CHARMED)
  !Gender(LastTrigger,ILLUSIONARY)
  Range(LastTrigger,20)
  !InPartySlot(LastTrigger,0)
  !GlobalTimerNotExpired("MakeShadow","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("MakeShadow","LOCALS",6)
    SetInterrupt(FALSE)
    ReallyForceSpellRES("melis03",LastTrigger)  // ~Taint of the Slayer~
    IncrementGlobal("EXTRACOUNT4","MYAREA",1)
    SetInterrupt(TRUE)
END

IF
!GlobalTimerNotExpired("castspell","LOCALS")
  Allegiance(Myself,ENEMY)
  !HPLT(Myself,20)
  GlobalLT("EXTRACOUNT4","MYAREA",3)
  !GlobalTimerNotExpired("MelissanSummonUndead","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("MelissanSummonUndead","LOCALS",10)
    IncrementGlobal("EXTRACOUNT4","MYAREA",2)
    DisplayStringHead(Myself,73244)  // ~Come to your mistress, minions of the Abyss!~
    CreateVisualEffectObject("SPFLESHS",Myself)
    CreateCreatureObjectEffect("MELSUM01","SPGFLSH1",Myself)
    CreateCreatureObjectEffect("MELSUM01","SPGFLSH1",Myself)
  RESPONSE #100
    SetGlobalTimer("MelissanSummonUndead","LOCALS",10)
    IncrementGlobal("EXTRACOUNT4","MYAREA",2)
    DisplayStringHead(Myself,73244)  // ~Come to your mistress, minions of the Abyss!~
    CreateVisualEffectObject("SPFLESHS",Myself)
    CreateCreatureObjectEffect("MELSUM02","SPGFLSH1",Myself)
    CreateCreatureObjectEffect("MELSUM02","SPGFLSH1",Myself)
  RESPONSE #100
    SetGlobalTimer("MelissanSummonUndead","LOCALS",10)
    IncrementGlobal("EXTRACOUNT4","MYAREA",1)
    DisplayStringHead(Myself,73244)  // ~Come to your mistress, minions of the Abyss!~
    CreateVisualEffectObject("SPFLESHS",Myself)
    CreateCreatureObjectEffect("MELSUM03","SPGFLSH1",Myself)
  RESPONSE #100
    SetGlobalTimer("MelissanSummonUndead","LOCALS",8)
    IncrementGlobal("EXTRACOUNT4","MYAREA",1)
    DisplayStringHead(Myself,73244)  // ~Come to your mistress, minions of the Abyss!~
    CreateVisualEffectObject("SPFLESHS",Myself)
    CreateCreatureObjectEffect("SLAYSH01","SPGFLSH1",Myself)
END


////////////////////////////////////////////////////////////////////
////	Melee
////////////////////////////////////////////////////////////////////


DEFAULT TRIGGER(GlobalTimerNotExpired("castspell","LOCALS"))
INCLUDE FILE(%MOD_FOLDER%/melissan/ssl/melee.ssl)

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/chase.ssl)

////////////////////////////////////////////////////////////////////
////	Heal & recover
////////////////////////////////////////////////////////////////////

IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	HPPercentLT(Myself,40)
	!HPLT(Myself,20)
	See(NearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),10)
	!GlobalTimerNotExpired("MelissanHeal","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("castspell","LOCALS",6)
		SetGlobalTimer("MelissanHeal","LOCALS",30)
		SetInterrupt(FALSE)
		RunAwayFromNoLeaveArea(LastSeenBy(Myself),30)
		DisplayStringHead(Myself,73242) // My power shall restore me!
		SetGlobal("castspell","LOCALS",6)
		SpellNoDec(Myself,CLERIC_HEAL)
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	HPPercentLT(Myself,40)
	!HPLT(Myself,20)
	See(NearestEnemyOf(Myself))
	!Range(LastSeenBy(Myself),10)
	!GlobalTimerNotExpired("MelissanHeal","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("MelissanHeal","LOCALS",30)
		DisplayStringHead(Myself,73242) // My power shall restore me!
		SetGlobal("castspell","LOCALS",6)
		SpellNoDec(Myself,CLERIC_HEAL)
END

IF TRIGGER
  StateCheck(Myself,STATE_SILENCED)
  !HPLT(Myself,20)
THEN DO
	Action(SpellNoDecMyself,WIZARD_VOCALIZE)
END

IF TRIGGER
  OR(2)
    CheckStatGT(Myself,0,WEB)
    CheckStatGT(Myself,0,ENTANGLE)
THEN DO
	Action(SpellNoDecMyself,CLERIC_FREE_ACTION)
END


IF TRIGGER
        TriggerBlock(StoneskinFailing)
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("MelStoneSkin","LOCALS")
THEN DO
	Action(SpellNoDecRESMyself,"melstone")
	SetGlobalTimer("MelStoneSkin","LOCALS",12)
END

////////////////////////////////////////////////////////////////////
////	Truesight
////////////////////////////////////////////////////////////////////


IF TRIGGER
        TargetBlock(EnemiesInOrder)
	TriggerBlock(TruesightAlreadyCast|IllusionTarget|Enemy|SIDivination)
THEN DO
	Action(SpellMyselfTimer,WIZARD_TRUE_SIGHT,truesight,60|100|100)
END

////////////////////////////////////////////////////////////////////
////	Timestop - longish casting time, don't take risks
////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	OR(3)
		!Range(scstarget,12)
		StateCheck(scstarget,STATE_HELPLESS)
		CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	Action(SpellMyselfTimer,WIZARD_TIME_STOP,timestoprecast,1000|100|100)
	SetGlobalTimer("timestop","LOCALS",7)
END

////////////////////////////////////////////////////////////////////
////	Haste
////////////////////////////////////////////////////////////////////

IF TRIGGER
	!StateCheck(Myself,STATE_HASTED)
THEN DO
	Action(SpellMyselfTimer,WIZARD_HASTE,haste,18|100|100)
END

////////////////////////////////////////////////////////////////////
////	Dispel Magic
////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/dispel.ssl)

////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(Demivrgvs)
	TargetBlock(PCsInOrder)
	TriggerBlock(PriorityBreach|Enemy|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS") // the timestop timer is doing double duty here: it's also ensuring that we don't use "minor" antimagic more often than every four rounds
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(SpellTimer,WIZARD_PIERCE_SHIELD,pierceshield,18|100|100)
END



IF TRIGGER
        IgnoreBlock(Demivrgvs)
	TargetBlock(PCsInOrder)
	TargetBlock(Celestials)
	!CheckStatLT(scstarget,50,RESISTMAGIC)
	TriggerBlock(Enemy|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",24)
	Action(SpellTimer,WIZARD_PIERCE_SHIELD,pierceshield,18|100|100)
END

IF TRIGGER
        RequireBlock(Demivrgvs)
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellProtectionTarget|Enemy|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS") // the timestop timer is doing double duty here: it's also ensuring that we don't use "minor" antimagic more often than every four rounds
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(SpellTimer,WIZARD_PIERCE_MAGIC,pierceshield,18|100|100)
END



IF TRIGGER
        RequireBlock(Demivrgvs)
	TargetBlock(PCsInOrder)
	TargetBlock(Celestials)
	!CheckStatLT(scstarget,50,RESISTMAGIC)
	TriggerBlock(Enemy|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",24)
	Action(SpellTimer,WIZARD_PIERCE_MAGIC,pierceshield,18|100|100)
END


///////////////////////////////////////////////////////////////////////////
////	KILL SUMMONS
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(Summons)
	OR(2)
		Gender(scstarget,SUMMONED)
		Gender(scstarget,20)
	TriggerBlock(Enemy|Helpless)
	CheckStatGT(scstarget,10,Level)
THEN DO
	Combine()
	Action(SpellNoDec,WIZARD_DEATH_SPELL|100|50)
END

//////////////////////////////////////////////////////////////////////////
////	Malison
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	Range(ThirdNearestEnemyOf(Myself),30)
	Target(NearestEnemyOf(Myself))
THEN DO
	Action(SpellTimer,WIZARD_GREATER_MALISON,malison,120|100|50)
END

/////////////////////////////////////////////////////////////////////////////
/////  Earthquake (may hurt demons too, but c'est la vie)
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See([PC])
THEN DO
	Action(SpellMyselfTimer,CLERIC_EARTHQUAKE,earthquake,36|100|100)
END


//////////////////////////////
///   Symbol: Stun
//////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Panic|MR100|SIConjuration|Enemy|Helpless)
THEN DO
	Combine()
	Action(SpellTimer,WIZARD_NPC_SYMBOL_FEAR,symbol,18|100|100)
END

///////////////////////////////
////	Symbol: Death
///////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Slay|MR|SIConjuration)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Combine()
	Action(SpellTimer,WIZARD_NPC_SYMBOL_DEATH,symbol,18|100|100)
END

////////////////////////////////////////////////////////////////////////////
////	Unholy Word
///////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	Range(scstarget,10)
	TriggerBlock(MR|Enemy|Helpless|SIConjuration)
	Alignment(scstarget,MASK_GOOD)
	OR(5)
		Class(scstarget,BARD)
		Class(scstarget,MAGE_ALL)
		Class(scstarget,CLERIC_ALL)
		Class(scstarget,DRUID_ALL)
		!CheckStatGT(scstarget,11,Level)
THEN DO
	Combine()
	Action(SpellMyselfTimer,CLERIC_UNHOLY_WORD,unholyword,18|200|100)
END

////////////////////////////////////////////////////////////////////////////
////	Sunray
///////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	Range(scstarget,10)
	!Range(NearestMyGroupOfType,15)
THEN DO
	Action(SpellMyselfTimer,CLERIC_SUNRAY,sunray,18|200|100)
END


