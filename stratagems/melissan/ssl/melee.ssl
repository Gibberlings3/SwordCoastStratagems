////////////////////////////////////////////////////////////////////////
////
//// Melissan melee script
////
//// Melissan's ground circle is large and she tends to get stuck behind things; this
//// means she does need to prioritise nearby opponents more than many creatures would.
////
////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////
////	Don't melee in timestop if that option's disabled
////////////////////////////////////////////////////////////////////////////


IF TRIGGER
  Global("DMWWTimestopMeleeNever","GLOBAL",1)
  Global("fin_time_stop_running","GLOBAL",1)
THEN DO
  	   Action(DoNothing)
END

IF TRIGGER
  Global("DMWWTimestopMeleeAlways","GLOBAL",0)
  Global("fin_time_stop_running","GLOBAL",1)
   TriggerBlock(EasyMinus)
THEN DO
  	   Action(DoNothing)
END

IF TRIGGER
	Target(LastTargetedBy(Myself))
	Global("validtarget","LOCALS",1)
	CheckStat(scstarget,4,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

////////////////////////////////////////////////////////////////////////
///	Take a second to kill any illusions or summons etc (the non-Ascension
///	version insta-kills summons and undead, the Ascension version insta-kills
///	illusions and undead)
////////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsAscension)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Plus5Safe)
	Range(scstarget,5)
	CheckStatGT(scstarget,10,Level)
	OR(2)
		Gender(scstarget,ILLUSIONARY)
		General(scstarget,UNDEAD)
THEN DO
	Action(Attack)
END

IF TRIGGER
	IgnoreBlock(IsAscension)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Plus5Safe)
	Range(scstarget,5)
	CheckStatGT(scstarget,10,Level)
	OR(3)
		Gender(scstarget,SUMMONED)
		Gender(scstarget,20)
		General(scstarget,UNDEAD)
THEN DO
	Action(Attack)
END

///////////////////////////////////////////////////////////////////////
////	We start by looking at the very near opponents. Go for the 
////	most vulnerable of them. In the first instance, try for PCs with no stoneskins
////	and no mirror images.
////
////	Melissan breaks from the normal tradition of only attacking non-disabled
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|MirrorImage|Plus5Safe|SlashingSafe)
	!CheckStatGT(scstarget,0,STONESKINS)
	!CheckStatGT(scstarget,60,RESISTSLASHING)
	Range(scstarget,5)
THEN DO
	Action(Attack)
END



///////////////////////////////////////////////////////////////////////
////	If there's anyone else in a close attack position, get them
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Plus5Safe|SlashingSafe)
	Range(scstarget,5)
THEN DO
	Action(Attack)
END

///////////////////////////////////////////////////////////////////////
////	Look for another target
///////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TargetBlock(PlayersInRandomOrder)
	TriggerBlock(Plus5Safe|SlashingSafe)
THEN DO
	Action(MoveIfEmpty)
END


/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	If there's a combat going on and somehow you get here, it probably means you can't find
///	anything remotely worth attacking. Attack something anyway, just in case. (The variable "setmeleetarget" is for 
/// 	scriptsets, like the Demon ones, which use AttackOneRound) // June 07 - not sure what I'm talking about here...
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	!GlobalTimerNotExpired("setmeleetarget","LOCALS")
	Allegiance(Myself,ENEMY)
	See(NearestEnemyOf(Myself))
	ActionListEmpty()
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END
