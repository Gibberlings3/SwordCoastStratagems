
IF TRIGGER
  !HPLT(Myself,20)
  TriggerBlock(CorePlus)
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("castspell","LOCALS")
  !GlobalTimerNotExpired("Cleansing","LOCALS")
  Global("Draw","LOCALS",0)
  OR(9)
    CheckStatGT(Myself,-10,ARMORCLASS)
    CheckStatGT(Myself,-7,THAC0)
    CheckStatGT(Myself,0,SAVEVSSPELL)
    CheckStatLT(Myself,70,RESISTMAGIC)
    CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
    CheckSpellState(Myself,MALISON)
    StateCheck(Myself,STATE_SILENCED)
    StateCheck(Myself,STATE_SLOWED)
    StateCheck(Myself,STATE_BLIND)
THEN DO
     Action(Literal|100|50)
    SetGlobalTimer("castspell","LOCALS",6)
    SetGlobalTimer("Cleansing","LOCALS",30)
    ReallyForceSpellRES("melspl12",Myself)  // ~Divine Cleansing~
END

IF TRIGGER
	!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
THEN DO
	Action(ForceSpellRESMyselfTimer,"melspl01",DivineMantle,30|100|50)
END

IF TRIGGER
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
	OR(2)
		CheckStatLT(Myself,70,RESISTMAGIC)
		!CheckStatGT(Myself,0,WIZARD_SPELL_TRAP)
  	OR(4)
  	  See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
  	  See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
  	  See(NearestEnemyOfType([0.0.0.BARD_ALL]))
  	  See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
	!Range(SecondNearestEnemyOf(Myself),12) // not in melee
THEN DO
	Action(ForceSpellRESMyselfTimer,"melspl03",DivineShield,30|150|150)
END	


IF TRIGGER
	Range(NearestEnemyOf(Myself),8)
	BEGIN LOOP(scsnear||Nearest;SecondNearest;ThirdNearest;FourthNearest)
		!HasItem("sw2h10",scsnearEnemyOf(Myself))
		!HasItem("sw2h19",scsnearEnemyOf(Myself))
	END LOOP
THEN DO
	Action(ForceSpellRESMyselfTimer,"melis02",bladebarrier,30|100|100)
END


