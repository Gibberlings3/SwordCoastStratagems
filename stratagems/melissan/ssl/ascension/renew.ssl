
IF
  !HPLT(Myself,20)
  !DifficultyLT(NORMAL)
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("castspell","LOCALS")
  !GlobalTimerNotExpired("Cleansing","LOCALS")
  Global("Draw","LOCALS",0)
  OR(9)
    CheckStatGT(Myself,-10,ARMORCLASS)
    CheckStatGT(Myself,-7,THAC0)
    CheckStatGT(Myself,0,SAVEVSSPELL)
    CheckStatLT(Myself,70,RESISTMAGIC)
    CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
    CheckStatGT(Myself,0,WIZARD_GREATER_MALISON)
    StateCheck(Myself,STATE_SILENCED)
    StateCheck(Myself,STATE_SLOWED)
    StateCheck(Myself,STATE_BLIND)
THEN
  RESPONSE #100
    SetGlobalTimer("castspell","LOCALS",6)
    SetGlobalTimer("Cleansing","LOCALS",30)
    ReallyForceSpellRES("melspl12",Myself)  // ~Divine Cleansing~
  RESPONSE #50
    Continue()
END

IF TRIGGER
	!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
	BEGIN LOOP(scsnear||Nearest;SecondNearest;ThirdNearest;FourthNearest)
		OR(2)
			StateCheck(scsnearEnemyOf(Myself),STATE_DISABLED)
			!CheckStatGT(scsnearEnemyOf(Myself),5,WEAPON_ENCHANTMENT)	
	END LOOP
THEN DO
	Action(ForceSpellRESMyselfTimer,"melspl01",DivineMantle,30|100|50)
END

IF TRIGGER
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
	OR(2)
		CheckStatLT(Myself,70,RESISTMAGIC)
		!CheckStatGT(Myself,0,WIZARD_SPELL_TRAP)	
  	OR(4)
  	  See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
  	  See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
  	  See(NearestEnemyOfType([0.0.0.BARD_ALL]))
  	  See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
	!Range(SecondNearestEnemyOf(Myself),12) // not in melee
THEN DO
	Action(ForceSpellRESMyselfTimer,"melspl03",DivineShield,30|150|150)
END	


IF TRIGGER
	Range(NearestEnemyOf(Myself),8)
	BEGIN LOOP(scsnear||Nearest;SecondNearest;ThirdNearest;FourthNearest)
		!HasItem("sw2h10",scsnearEnemyOf(Myself))
		!HasItem("sw2h19",scsnearEnemyOf(Myself))
	END LOOP
THEN DO
	Action(ForceSpellRESMyselfTimer,"melis02",bladebarrier,30|100|100)
END


