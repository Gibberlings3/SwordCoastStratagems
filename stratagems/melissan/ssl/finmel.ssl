//////////////////////////////////////////////////////////////////////////////
//////	Definitions
//////////////////////////////////////////////////////////////////////////////

VARIABLE(IsAscension=True)

BEGIN_ACTION_DEFINITION
	Name(MoveIfEmpty)
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		MoveToObject(scstarget)
END

/////   Melissan casts in one of two ways:
/////	(1) force-casting
/////	(2) a hard-to-interrupt spellcast which is sometimes force-cast, sometimes spell-no-dec.
/////	In vanilla Ascension, the ratio is 70/30; we relax it to 50/50.
/////

/////	(1) Three arguments: the spell, the timer name, the timer length

BEGIN_ACTION_DEFINITION
	Name(ForceSpellTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		ForceSpell(scstarget,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(ForceSpellMyselfTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		ForceSpell(Myself,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(ForceSpellRESTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		ForceSpellRES(scsargument1,scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(ForceSpellRESMyselfTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		ForceSpellRES(scsargument1,Myself)
END

/////	(2) Three arguments: the spell, the timer name, the timer length
/////	NOTE: because there are two spellcasting blocks, the number put into the Continue section of the SSL call must be doubled

BEGIN_ACTION_DEFINITION
	Name(SpellTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
//		ForceSpell(scstarget,scsargument1)
		SpellNoDec(scstarget,scsargument1)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		SpellNoDec(scstarget,scsargument1)

END

BEGIN_ACTION_DEFINITION
	Name(SpellMyselfTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
//		ForceSpell(Myself,scsargument1)
		SpellNoDec(Myself,scsargument1)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		SpellNoDec(Myself,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(SpellRESTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
//		ForceSpellRES(scsargument1,scstarget)
		SpellNoDecRES(scsargument1,scstarget)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		SpellNoDecRES(scsargument1,scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(SpellRESMyselfTimer)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
//		ForceSpellRES(scsargument1,Myself)
		SpellNoDecRES(scsargument1,Myself)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		SpellNoDecRES(scsargument1,Myself)
END

BEGIN_ACTION_DEFINITION
	Name(SpellNoDec)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
//		ForceSpell(scstarget,scsargument1)
		SpellNoDec(scstarget,scsargument1)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(SpellNoDecMyself)
	TRIGGER
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
//		ForceSpell(Myself,scsargument1)
		SpellNoDec(Myself,scsargument1)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(Myself,scsargument1)
END

/////	Melee
/////

BEGIN_ACTION_DEFINITION
	Name(Attack)
	TRIGGER
		ActionListEmpty()
		!HPLT(Myself,20)
		Allegiance(Myself,ENEMY)
		Global("draw","LOCALS",0)
	ACTION
		RESPONSE #scsprob1
		SetGlobal("validtarget","LOCALS",1)
		EquipMostDamagingMelee()
		Attack(scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(vsPMscroll)
	TRIGGER
		!GlobalTimerNotExpired("spellstrike","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("spellstrike","LOCALS",30)
		SpellNoDec(scstarget,WIZARD_SPELL_STRIKE)
END

INCLUDE FILE(%scsroot%/melissan/ssl/ascension/initial.ssl)

IF
  HPLT(Myself,20)
  Global("allfive","GLOBAL",0)
  Global("Draw","LOCALS",0)
  !GlobalTimerNotExpired("Retreat","LOCALS")
THEN
  RESPONSE #100
    SetGlobal("Draw","LOCALS",1)
    SetInterrupt(FALSE)
    SetGlobalTimer("Retreat","LOCALS",15)
    IncrementGlobal("MelStillImmortal","AR6200",1)
    DisplayStringHead(Myself,67805)  // ~No!  Too strong!  Damn you!  I must take more essence...I must have more power!!~ [FINMEL27]
    ForceSpellPointRES("dimdoor",[1286.1122])
    Face(8)
    ForceSpellRES("melspl02",Myself)  // ~Draw Power~
    SetInterrupt(TRUE)
END


////////////////////////////////////////////////////////////////////
////	Circle kick - get foes away
////////////////////////////////////////////////////////////////////


IF
  !HPLT(Myself,20)
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("CircleKick","LOCALS")
  Global("Draw","LOCALS",0)
  Range(ThirdNearestEnemyOf(Myself),10)
  Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
  !GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks

THEN
  RESPONSE #100
    SetGlobalTimer("CircleKick","LOCALS",15)
    ReallyForceSpellRES("dgwhirl",[PC])  // <Invalid Strref -1>
    SetSequence(SEQ_ATTACK_JAB)
  RESPONSE #50
    Continue()
END

////////////////////////////////////////////////////////////////////
////	Timestop - longish casting time, don't take risks
////////////////////////////////////////////////////////////////////

IF TRIGGER
	BEGIN LOOP(scstype||Nearest;SecondNearest;ThirdNearest)
	OR(2)
		!Range(scstypeEnemyOf(Myself),12)
		StateCheck(scstypeEnemyOf(Myself),STATE_HELPLESS)
	END LOOP
THEN DO
	Action(SpellMyselfTimer,WIZARD_TIME_STOP,timestoprecast,120|100|50)
	SetGlobalTimer("timestop","LOCALS",7)
END

/// Try it straight after a teleport

IF TRIGGER
	GlobalTimerNotExpired("justjumped","LOCALS")
THEN DO
	Action(SpellMyselfTimer,WIZARD_TIME_STOP,timestoprecast,120|100|50)
	SetGlobalTimer("timestop","LOCALS",7)
END

////////////////////////////////////////////////////////////////////
////	Get some distance by teleporting
////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/melissan/ssl/ascension/teleport.ssl)


DEFAULT TRIGGER(GlobalTimerNotExpired("castspell","LOCALS"))
INCLUDE FILE(%scsroot%/melissan/ssl/melee.ssl)

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

INCLUDE FILE(%scsroot%/melissan/ssl/ascension/freedom.ssl)
INCLUDE FILE(%scsroot%/melissan/ssl/ascension/renew.ssl)

IF
	!Detect(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		ReallyForceSpellRES("dw#fitel",Player1)
END

IF
	Global("DMWWMelTest","GLOBAL",1)
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Enemy()
END

////////////////////////////////////////////////////////////////////
////	Truesight
////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(TruesightAlreadyCast|IllusionTarget|Enemy|SIDivination)
THEN DO
	Action(SpellNoDecMyself,WIZARD_TRUE_SIGHT)
END


////////////////////////////////////////////////////////////////////////////
////	Boneblades
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	Target("finbalth")
	Target("finbodh")
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	TriggerBlock(SIEvocation|SlashingSafe|Stoneskin|DirectDamageSafe|Enemy)
THEN DO
	Action(ForceSpellRESTimer,"melis01",boneblades,12|100|150)
END

////////////////////////////////////////////////////////////////////////////
////	Power Word: Kill
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	Target("finbalth")
	Target("finbodh")
	TriggerBlock(Enemy)
	OR(2)
		HPPercentLT(scstarget,75)
		!CheckStatGT(scstarget,12,Level)
	OR(3)
		HPPercentLT(scstarget,50)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	TriggerBlock(MR100|SpellDeflect|Slay|SIConjuration|Enemy|SRPowerWord|DirectDamageSafe)
THEN DO
	Combine()
	Action(SpellTimer,WIZARD_POWER_WORD_KILL,powerword,120|150|150)
END


////////////////////////////////////////////////////////////////////
////	Whirlwind 
////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	CheckStatGT(scstarget,15,Level)
	TriggerBlock(Enemy|Helpless|Plus5Safe|SlashingSafe)
	Range(scstarget,12)
	!GlobalTimerNotExpired("justjumped","LOCALS")
THEN DO
	Action(ForceSpellMyselfTimer,WARRIOR_GREATER_WHIRLWIND,whirlwind,30|200|100)
END


////////////////////////////////////////////////////////////////////
////	Control demons
////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/melissan/ssl/ascension/controldemons.ssl)

////////////////////////////////////////////////////////////////////
////	Restore level-drained allies
////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/melissan/ssl/ascension/restoration.ssl)

////////////////////////////////////////////////////////////////////
////	Summon more demons
////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/melissan/ssl/ascension/moredemons.ssl)


////////////////////////////////////////////////////////////////////
////	Attack magical defences
////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|SIAbjuration|Enemy|Helpless|PriorityBreach)
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
THEN DO
	SetGlobalTimer("DMWW_antimagic","LOCALS",12)
	Action(SpellTimer,WIZARD_BREACH,breach,18|100|100)
END

////////////////////////////////////////////////////////////////////////////
////	Attack PM scrolls
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(ProtectionFromMagicScroll|Enemy|Helpless)
THEN DO
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	Action(vsPMscroll|200|50)
END





///////////////////////////////////////////////////////////////////////////
////	KILL SUMMONS
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(Summons)
	OR(2)
		Gender(scstarget,SUMMONED)
		Gender(scstarget,20)
	TriggerBlock(Enemy|Helpless)
	CheckStatGT(scstarget,10,Level)
THEN DO
	Combine()
	Action(SpellNoDec,WIZARD_DEATH_SPELL|100|50)
END


////////////////////////////////////////////////////////////////////////////
////	Unleash, area-effect
///////////////////////////////////////////////////////////////////////////

IF
  !HPLT(Myself,20)
  !GlobalTimerNotExpired("castspell","LOCALS")
  Global("Draw","LOCALS",0)
  !GlobalTimerNotExpired("Unleash","LOCALS")
  Range(SecondNearestEnemyOf(Myself),15)
THEN
  RESPONSE #150
    SetGlobalTimer("Unleash","LOCALS",18)
    SetGlobalTimer("castspell","LOCALS",6)
    SetSequence(SEQ_ATTACK_JAB)
    ForceSpellRES("melspl08",LastSeenBy(Myself))  // ~Unleash~
  RESPONSE #100
    Continue()
END

////////////////////////////////////////////////////////////////////////////
////	Malison-style effect
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	Range(ThirdNearestEnemyOf(Myself),30)
THEN DO
	Action(SpellRESMyselfTimer,"melspl07",malison,120|100|50)
END


////////////////////////////////////////////////////////////////////////////
////	Uberattack spells - we'll save these till the second-wind phase
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	Target([PC])
	Target("finbalth")
	Target("finbodh")
	TriggerBlock(DirectDamageSafe)
	GlobalGT("FiveDead","AR6200",3)
	!GlobalTimerNotExpired("bigspell","LOCALS")
THEN DO
	Combine()
	SetGlobalTimer("bigspell","LOCALS",12)
	Action(SpellTimer,CLERIC_STORM_OF_VENGEANCE,vengeance,1000|100|100)
END


IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	!GlobalTimerNotExpired("bigspell","LOCALS")
	TargetBlock(PCsInOrderShort)
	Target("finbalth")
	Target("finbodh")
	TriggerBlock(ResistFire|Enemy)
	GlobalGT("FiveDead","AR6200",3)
THEN DO
	Combine()
	SetGlobalTimer("bigspell","LOCALS",12)
	Action(SpellTimer,WIZARD_COMET,comet,1000|100|100)
	Action(SpellTimer,WIZARD_DRAGONS_BREATH,dragonbreath,1000|100|100)
END





////////////////////////////////////////////////////////////////////////////
////	Symbol of Slow
///////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR|Slow|SIAlteration|Enemy|Helpless)
	!CheckStatLT(scstarget,0,SAVEVSSPELL)
	!StateCheck(scstarget,STATE_SLOWED)
THEN DO
	Combine()
	Action(SpellRESTimer,"spwi545",slowsymbol,30|150|50)
END

////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellProtectionTarget|Enemy|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS") // the timestop timer is doing double duty here: it's also ensuring that we don't use "minor" antimagic more often than every four rounds
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",18)
	Action(SpellTimer,WIZARD_SPELL_STRIKE,spellstrike,30|100|100)
	Action(SpellTimer,WIZARD_PIERCE_SHIELD,pierceshield,18|100|100)
END



IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(Celestials)
	!CheckStatLT(scstarget,50,RESISTMAGIC)
	TriggerBlock(Enemy|Helpless)
	!GlobalTimerNotExpired("DMWW_antimagic","LOCALS")
	!GlobalTimerNotExpired("timestop_antimagic","LOCALS")
	Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
	!GlobalTimerNotExpired("timestop","LOCALS") // embarrassing to dispel your own attacks
THEN DO
	Combine()
	SetGlobalTimer("DMWW_antimagic","LOCALS",18)
	SetGlobalTimer("timestop_antimagic","LOCALS",24)
	Action(SpellTimer,WIZARD_PIERCE_SHIELD,pierceshield,18|100|200)
END


////////////////////////////////////////////////////////////////////////////
////	Unholy Word
///////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	Range(scstarget,10)
	TriggerBlock(MR|Enemy|Helpless|SIConjuration)
	Alignment(scstarget,MASK_GOOD)
	OR(5)
		Class(scstarget,BARD)
		Class(scstarget,MAGE_ALL)
		Class(scstarget,CLERIC_ALL)
		Class(scstarget,DRUID_ALL)
		!CheckStatGT(scstarget,11,Level)
THEN DO
	Combine()
	Action(SpellMyselfTimer,CLERIC_UNHOLY_WORD,unholyword,30|200|100)
END


////////////////////////////////////////////////////////////////////////////
////	Finger of Death
///////////////////////////////////////////////////////////////////////////


IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|Slay|SpellTurn|SINecromancy|Enemy)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(SpellTimer,WIZARD_FINGER_OF_DEATH,finger,18|150|100)
END

