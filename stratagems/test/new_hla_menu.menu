menu
{
	name 'CHARGEN_HIGH_LEVEL_ABILITIES'
	align center center
	ignoreesc
	onOpen
	"
		currentHLASelection = nil
	"
	label
	{
		area 0 0 864 710
		mosaic GUICUSTB
	}
	label
	{
		area 82 12 700 40
		text "ABILITIES_TITLE"
		text style "title"
	}
	label
	{
		area 46 116 336 48
		text lua "chargen.numHLAs"
		text style "normal"
		text align center center
	}
	text
	{
		area 436 128 382 478
		text lua "Infinity_FetchString(chargenHLADescription())"
		text style "normal"
		scrollbar 'GUISCRC'
	}

	list
	{
		column
		{
			width 0
			label
			{
				bam GUIBTBUT
				area 0 0 52 52
				align center center
				icon lua "chargen.HLAs[rowNumber].icon"
				count lua "chargen.HLAs[rowNumber].count"
			}
		}
		column
		{
			width 72
			label
			{
				area 4 0 -1 -1
				text lua "Infinity_FetchString(chargen.HLAs[rowNumber].name)"
				text style "normal"
				align left center
			}
		}
		column
		{
			width 14
			label
			{
				area 0 4 45 42
				bam GUIOSW
				frame lua "chargenHLAPlusMinusFrame(3, rowNumber)"
				sequence 0
			}
		}
		column
		{
			width 14
			label
			{
				area 0 4 45 42
				bam GUIOSW
				frame lua "chargenHLAPlusMinusFrame(4, rowNumber)"
				sequence 1
			}
		}
		scrollbar 'GUISCRC'
		rowheight 52
		hidehighlight
		area 42 192 340 414
		table "chargen.HLAs"
		var currentHLASelection
		action
		"
			if cellNumber == 3 and chargenHLAPlusMinusFrame(3, currentHLASelection) ~= 3 then
				Infinity_PlaySound('GAM_09')
				createCharScreen:OnHLAButtonClick(currentHLASelection, 1)
			elseif cellNumber == 4 and chargenHLAPlusMinusFrame(4, currentHLASelection) ~= 3 then
				Infinity_PlaySound('GAM_09')
				createCharScreen:OnHLAButtonClick(currentHLASelection, -1)
			end
			cellNumber = nil
		"
	}

	button
	{
		on enter
		bam GUIBUTMT
		area 210 653 234 44
		text style "button"
		text "DONE_BUTTON"
		clickable lua "createCharScreen:IsDoneButtonClickable()"
		action
		"
			createCharScreen:OnDoneButtonClick()
			createCharScreen:OnDoneButtonClick()
		"
	}
	button
	{
		on escape
		bam GUIBUTMT
		area 464 653 234 44
		text style "button"
		text "CANCEL_BUTTON"
		action
		"
			Infinity_PopMenu()
			createCharScreen:OnCancelButtonClick()
		"
	}
}