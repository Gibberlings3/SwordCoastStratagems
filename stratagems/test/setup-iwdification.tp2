BACKUP ~iwdification/backup~ // location to store files for uninstall purposes
AUTHOR ~pcamagna@yahoo.com~ // email address displayed if install fails

VERSION ~Beta 4~

README ~iwdification/readme-iwdification.html~

ALWAYS

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN FAIL @1 END // Modmerge check

  ACTION_IF !VARIABLE_IS_SET ee_game THEN BEGIN
  
    INCLUDE ~iwdification/lib/functions.tpa~

    OUTER_SET tobex_game = 0
    OUTER_SET ee_game = 0
    OUTER_SET override_arcane = 1
    OUTER_SET override_divine = 1
  
    ACTION_IF GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ THEN BEGIN
  
      ACTION_IF FILE_EXISTS ~tobex_ini/tobexcore.ini~ THEN BEGIN
        OUTER_SET tobex_game = 1
      END

    END ELSE BEGIN

      OUTER_SET ee_game = 1

      // convert strings to UTF-8 for BGEE/BG2EE
      ACTION_DEFINE_ARRAY cdreload BEGIN game_strings END
      ACTION_DEFINE_ARRAY cdnoconvert BEGIN game_strings_ee setup END
      LAF HANDLE_CHARSETS INT_VAR infer_charset = 1 STR_VAR tra_path = ~iwdification/languages~ reload_array = cdreload noconvert_array = cdnoconvert END
      LOAD_TRA ~iwdification/languages/english/game_strings_ee.tra~
    
    END

  END

END

ASK_EVERY_COMPONENT

LANGUAGE ~English~ ~english~ ~iwdification/languages/english/setup.tra~        // installer strings
                             ~iwdification/languages/english/game_strings.tra~ // strings for the tlk

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Icewind Dale Casting Graphics                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @1000 DESIGNATED 10
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~70~ @0

ACTION_FOR_EACH file IN cgabjura cgaltera cgconjur cgdivina cgenchan cgillusi cginvoca cgnecrom BEGIN

  COPY ~iwdification/bam/%file%.bam~ ~override~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Commoners use drab colors                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @2000 DESIGNATED 20
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~100~ @0

COPY_EXISTING ~randcolr.2da~ ~override~
  REPLACE_TEXTUALLY ~HairSet1~ ~CD_DELETE_ME HairSet1~
  SET_2DA_ENTRY  2  9 15 114 // AthPeasantMajor (208)
  SET_2DA_ENTRY  3  9 15 107
  SET_2DA_ENTRY  4  9 15  36
  SET_2DA_ENTRY  5  9 15  37
  SET_2DA_ENTRY  6  9 15  38
  SET_2DA_ENTRY  7  9 15  39
  SET_2DA_ENTRY  8  9 15  40
  SET_2DA_ENTRY  9  9 15  41
  SET_2DA_ENTRY 10  9 15  42
  SET_2DA_ENTRY 11  9 15  43
  SET_2DA_ENTRY  2 10 15  63 // AthPeasantMinor (209)
  SET_2DA_ENTRY  3 10 15  64
  SET_2DA_ENTRY  4 10 15  65
  SET_2DA_ENTRY  5 10 15  66
  SET_2DA_ENTRY  6 10 15  87
  SET_2DA_ENTRY  7 10 15  91
  SET_2DA_ENTRY  8 10 15  92
  SET_2DA_ENTRY  9 10 15  93
  SET_2DA_ENTRY 10 10 15  94
  SET_2DA_ENTRY 11 10 15  95
  SET_2DA_ENTRY  2 13 15 114 // TradePeasantMajor (212)
  SET_2DA_ENTRY  3 13 15 107
  SET_2DA_ENTRY  4 13 15  36
  SET_2DA_ENTRY  5 13 15  37
  SET_2DA_ENTRY  6 13 15  38
  SET_2DA_ENTRY  7 13 15  39
  SET_2DA_ENTRY  8 13 15  40
  SET_2DA_ENTRY  9 13 15  41
  SET_2DA_ENTRY 10 13 15  42
  SET_2DA_ENTRY 11 13 15  43
  SET_2DA_ENTRY  2 14 15  63 // TradePeasantMinor (213)
  SET_2DA_ENTRY  3 14 15  64
  SET_2DA_ENTRY  4 14 15  65
  SET_2DA_ENTRY  5 14 15  66
  SET_2DA_ENTRY  6 14 15  87
  SET_2DA_ENTRY  7 14 15  91
  SET_2DA_ENTRY  8 14 15  92
  SET_2DA_ENTRY  9 14 15  93
  SET_2DA_ENTRY 10 14 15  94
  SET_2DA_ENTRY 11 14 15  95
  REPLACE_TEXTUALLY ~CD_DELETE_ME~ ~~
  PRETTY_PRINT_2DA
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD arcane spell pack                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @3000 DESIGNATED 30

INCLUDE ~iwdification/lib/spell_prep.tpa~

/////                                                  \\\\\
///// WIZARD_EMOTION_HOPELESSNESS                      \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_EMOTION_COURAGE~) OR override_arcane) BEGIN // checking courage to see if emotion series is already present, since hopelessness is a vanilla bg2 spell

  ADD_PROJECTILE ~iwdification/pro/cdi255.pro~

  COPY ~iwdification/spl/cdia411.spl~ ~override/spwi411.spl~ // hopelessness
    SAY 0x08 @3001
    SAY 0x50 @3002
    SAY 0x18e @3003
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi255 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 1280 parameter1 = string_stunned END
    PATCH_IF !ee_game BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
    END

  COPY_EXISTING ~scrl5h.itm~ ~override~
    SAY 0x0c @3001
    SAY 0x54 @3002
    WRITE_BYTE 0x2d BIT3 // invoker flag
    LPF ALTER_ITEM_HEADER INT_VAR range = 50 target = 4 END
  
  COPY ~iwdification/bam/cdia411a.bam~ ~override~
       ~iwdification/bam/cdia411b.bam~ ~override~
       ~iwdification/bam/cdia411c.bam~ ~override~
       ~iwdification/bam/cdienchx.bam~ ~override~
       ~iwdification/bam/cdiparal.bam~ ~override~
       ~iwdification/vvc/cdigench.vvc~ ~override~
       ~iwdification/vvc/cdiparal.vvc~ ~override~
       ~iwdification/wav/cdiarm21.wav~ ~override~
       ~iwdification/wav/cdiee05.wav~  ~override~
       ~iwdification/wav/cdiem05.wav~  ~override~

END

/////                                                  \\\\\
///// WIZARD_POLYMORPH_SELF                            \\\\\
/////                                                  \\\\\

// these generally 'clean out' the poly self subspells
COPY_EXISTING ~bhaal4a.spl~  ~override~ // resurrection
              ~ohbraise.spl~ ~override~ // nsi
              ~slayerw3.itm~ ~override~ // slater weapon
              ~spja01.spl~   ~override~ // harper's call
              ~sppr504.spl~  ~override~ // raise dead
              ~sppr550.spl~  ~override~ // recall spirit
              ~sppr712.spl~  ~override~ // resurrection
              ~sppr729.spl~  ~override~ // mass raise dead
              ~spwish10.spl~ ~override~ // mass raise dead
  PATCH_IF anim_beetle BEGIN
    LPF CLONE_EFFECT STR_VAR match_resource = spwi499 resource = cdia480 END // boring beetle
  END
  LPF CLONE_EFFECT STR_VAR match_resource = spwi499 resource = cdia481 END // polar bear
  LPF CLONE_EFFECT STR_VAR match_resource = spwi499 resource = cdia482 END // winter wolf
  BUT_ONLY IF_EXISTS

// add new forms to poly self
COPY_EXISTING ~spwi416.spl~  ~override~ // poly self
  PATCH_IF anim_beetle BEGIN
    LPF CLONE_EFFECT STR_VAR match_resource = spwi499 resource = cdia480 END // boring beetle
    SAY 0x50 @3004
  END ELSE BEGIN
    SAY 0x50 @3014
  END
  LPF CLONE_EFFECT STR_VAR match_resource = spwi499 resource = cdia481 END // polar bear
  LPF CLONE_EFFECT STR_VAR match_resource = spwi499 resource = cdia482 END // winter wolf

COPY ~iwdification/spl/cdia481.spl~ ~override~ // polar bear
  SAY 0x08 @3007
  SAY 0x50 @3008

COPY ~iwdification/spl/cdia482.spl~ ~override~ // winter wolf
  SAY 0x08 @3009
  SAY 0x50 @3012

COPY_EXISTING ~cdia481.spl~ ~override~
              ~cdia482.spl~ ~override~
  PATCH_FOR_EACH res IN spin122 spin123 spin124 spin150 spin151 spin160 spin160 spin160 spin529 spin667 spin718 spinhum spmdslay spwi489 spwi490 spwi491 BEGIN
    LPF CLONE_EFFECT INT_VAR match_opcode = 172 STR_VAR match_resource = spin974 resource = EVAL ~%res%~ END
  END
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 opcode = 171 timing = 1 STR_VAR resource = spwi490 END // move polymorph ops to the claws
  LPF DELETE_EFFECT INT_VAR match_opcode = 144 END // move delete button to claws, too

//LAF cd_animation STR_VAR code = 7203 END // 29187 MBER_PO bear_polar
COPY ~iwdification/cre/cdidrupb.cre~ ~override~
  SAY 0x08 @3015
  SAY 0x0c @3015

//LAF cd_animation STR_VAR code = 7b03 END // 31491 MWLF_WI wolf_winter
COPY ~iwdification/cre/cdidruww.cre~ ~override~
  WRITE_LONG 0x08 string_wwolf
  WRITE_LONG 0x0c string_wwolf

COPY ~iwdification/itm/cdiplypb.itm~ ~override~
     ~iwdification/itm/cdiplyww.itm~ ~override~
  WRITE_LONG 0x08 string_attack
  WRITE_LONG 0x0c string_attack

COPY_EXISTING ~cdiplypb.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 144 target = 1 parameter2 = 7 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 135 target = 1 timing = 2 STR_VAR resource = cdidrupb END

COPY_EXISTING ~cdiplyww.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 144 target = 1 parameter2 = 7 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 135 target = 1 timing = 2 STR_VAR resource = cdidruww END

COPY ~iwdification/bam/cdia481b.bam~ ~override~
     ~iwdification/bam/cdia482b.bam~ ~override~
     ~iwdification/bam/cdiplypb.bam~ ~override~
     ~iwdification/bam/cdiplyww.bam~ ~override~

ACTION_IF anim_beetle BEGIN

  COPY ~iwdification/spl/cdia480.spl~ ~override~ // boring beetle
    SAY 0x08 @3005
    SAY 0x50 @3006
    PATCH_FOR_EACH res IN spin122 spin123 spin124 spin150 spin151 spin160 spin160 spin160 spin529 spin667 spin718 spinhum spmdslay spwi489 spwi490 spwi491 BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 172 STR_VAR match_resource = spin974 resource = EVAL ~%res%~ END
    END
    LPF ALTER_EFFECT INT_VAR match_opcode = 135 opcode = 171 timing = 1 STR_VAR resource = spwi490 END // move polymorph ops to the claws
    LPF DELETE_EFFECT INT_VAR match_opcode = 144 END // move delete button to claws, too

  LAF cd_animation STR_VAR code = e220 END // 57888 MBBM beetle_black
  COPY ~iwdification/cre/cdidrubb.cre~ ~override~
    SAY 0x08 @3013
    SAY 0x0c @3013
    WRITE_LONG 0x28 anim_beetle

  COPY ~iwdification/itm/cdiplybb.itm~ ~override~
       ~iwdification/itm/cdis5-20.itm~ ~override~
    WRITE_LONG 0x08 string_attack
    WRITE_LONG 0x0c string_attack
  
  COPY_EXISTING ~cdiplybb.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 144 target = 1 parameter2 = 7 timing = 2 END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 135 target = 1 timing = 2 STR_VAR resource = cdidrubb END
  
  COPY ~iwdification/bam/cdia480b.bam~ ~override~
       ~iwdification/bam/cdiplybb.bam~ ~override~

END

/////                                                  \\\\\
///// WIZARD_EXPEDITIOUS_RETREAT                       \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_EXPEDITIOUS_RETREAT~) OR override_arcane) BEGIN

  ADD_SPELL ~iwdification/spl/cdia126.spl~ 2 1 WIZARD_EXPEDITIOUS_RETREAT
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3010 // name
    SAY 0x50 @3011 // descript
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37787 parameter1 = string_slowed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37638 parameter1 = string_slow END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 36035 parameter1 = string_hasted END
    PATCH_IF !ee_game BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia126.itm~
    SAY 0x0c @3010
    SAY 0x54 @3011
    LPF cd_scroll INT_VAR unusable2 = BIT6 target_hdr = 5 range = 1 price = 100 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia126a.bam~ ~override~
       ~iwdification/bam/cdia126b.bam~ ~override~
       ~iwdification/bam/cdia126c.bam~ ~override~

END

/////                                                  \\\\\
///// WIZARD_SNILLOCS_SNOWBALL_SWARM                   \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SNILLOCS_SNOWBALL_SWARM~) OR override_arcane) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi217.pro~

  ADD_SPELL ~iwdification/spl/cdia204.spl~ 2 2 WIZARD_SNILLOCS_SNOWBALL_SWARM
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3020 // name
    SAY 0x50 @3021 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi217 END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdia204 resource = EVAL ~%DEST_RES%~ END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 318 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 12 multi_match = 1 END // delete one of these
      LPF CD_SPLIT_SAVE_DAMAGE END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia204.itm~
    SAY 0x0c @3020
    SAY 0x54 @3021
    LPF cd_scroll INT_VAR unusable1 = BIT1 target_hdr = 4 range = 40 opcode = 148 target_eff = 1 price = 900 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia204a.bam~ ~override~
       ~iwdification/bam/cdia204b.bam~ ~override~
       ~iwdification/bam/cdia204c.bam~ ~override~
       ~iwdification/vvc/cdissswa.vvc~ ~override~ // pro resources
       ~iwdification/bam/cdiswarr.bam~ ~override~
       ~iwdification/bam/cdiswart.bam~ ~override~
       ~iwdification/bam/cdissswx.bam~ ~override~
       ~iwdification/wav/cdirngm2.wav~ ~override~
       ~iwdification/wav/cditra18.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY_EXISTING ~cdi217.pro~ ~override~
      WRITE_BYTE 0x217 14
  
  END

END

/////                                                  \\\\\
///// WIZARD_DECASTAVE                                 \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_DECASTAVE~) OR override_arcane) BEGIN

  ADD_SPELL ~iwdification/spl/cdia216.spl~ 2 2 WIZARD_DECASTAVE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3030 // name
    SAY 0x50 @3031 // descript

  COPY ~iwdification/itm/blank.itm~ ~override/cdia216.itm~
    SAY 0x0c @3030
    SAY 0x54 @3031
    LPF cd_scroll INT_VAR unusable1 = BIT1 target_hdr = 5 range = 1 target_eff = 1 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/itm/cdideca.itm~ ~override~
    SAY 0x08 @3030
    SAY 0x0c @3030
    PATCH_IF !ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 12 opcode = 146 parameter2 = 1 special = 0 dicenumber = 0 dicesize = 0 STR_VAR resource = cdideca END
    END

  COPY ~iwdification/bam/cdia216a.bam~ ~override~
       ~iwdification/bam/cdia216b.bam~ ~override~
       ~iwdification/bam/cdia216c.bam~ ~override~
       ~iwdification/bam/cdideca.bam~  ~override~
       ~iwdification/bam/cdiinvoc.bam~ ~override~
       ~iwdification/vvc/cdiinvoc.vvc~ ~override~
       ~iwdification/wav/cdiee02.wav~  ~override~
       ~iwdification/wav/cdiem06.wav~  ~override~

  ACTION_IF !ee_game BEGIN
    
    COPY ~iwdification/spl/cdideca.spl~  ~override~

  END

END

/////                                                  \\\\\
///// WIZARD_CATS_GRACE                                \\\\\
/////                                                  \\\\\

ACTION_IF ((ee_game OR tobex_game) AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_CATS_GRACE~) OR override_arcane)) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@3040) STR_VAR bam_file = cdia225d RET icon END

  ADD_SPELL ~iwdification/spl/cdia225.spl~ 2 2 WIZARD_CATS_GRACE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3040 // name
    SAY 0x50 @3041 // descript
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14024 parameter1 = string_dexmod END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdia225 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 185 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 328 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 15 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = EVAL ~%DEST_RES%~ END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia225.itm~
    SAY 0x0c @3040
    SAY 0x54 @3041
    LPF cd_scroll INT_VAR unusable2 = BIT6 range = 1 STR_VAR price = 200 spell = EVAL ~%current_spell_res%~ END

  ACTION_IF ee_game BEGIN
    APPEND ~splstate.ids~ ~45 CATS_GRACE~ UNLESS ~^45[ %TAB%]+CATS_GRACE[ %TAB%%LNL%%MNL%%WNL%]+~
  END

  COPY ~iwdification/bam/cdia225a.bam~ ~override~
       ~iwdification/bam/cdia225b.bam~ ~override~
       ~iwdification/bam/cdia225c.bam~ ~override~
       ~iwdification/bam/cdia225d.bam~ ~override~
       ~iwdification/wav/cdiem08.wav~  ~override~

END

/////                                                  \\\\\
///// WIZARD_ICELANCE                                  \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_ICELANCE~) OR override_arcane) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi251.pro~

  ADD_SPELL ~iwdification/spl/cdia327.spl~ 2 3 WIZARD_ICELANCE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3050 // name
    SAY 0x50 @3051 // descript
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 35568 parameter1 = string_stunned END
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi251 END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia327.itm~
    SAY 0x0c @3050
    SAY 0x54 @3051
    LPF cd_scroll INT_VAR unusable1 = BIT1 range = 100 price = 900 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia327a.bam~ ~override~
       ~iwdification/bam/cdia327b.bam~ ~override~
       ~iwdification/bam/cdia327c.bam~ ~override~
       ~iwdification/bam/cdiiceln.bam~ ~override~
       ~iwdification/wav/cditra19.wav~ ~override~

END

/////                                                  \\\\\
///// WIZARD_LANCE_OF_DISRUPTION                       \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_LANCE_OF_DISRUPTION~) OR override_arcane) BEGIN

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi313.pro~
  
  END ELSE BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi313a.pro~

  END

  ADD_SPELL ~iwdification/spl/cdia328.spl~ 2 3 WIZARD_LANCE_OF_DISRUPTION
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3060 // name
    SAY 0x50 @3061 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdia328 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi313 END
    END ELSE BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi313a END
      LPF ALTER_EFFECT INT_VAR match_opcode = 318 opcode = 206 target = 1 parameter2 = 0 STR_VAR match_resource = cdia328 resource = EVAL ~%DEST_RES%~ END
      LPF CD_SPLIT_SAVE_DAMAGE END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia328.itm~
    SAY 0x0c @3060
    SAY 0x54 @3061
    LPF cd_scroll INT_VAR unusable1 = BIT1 target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 600 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia328a.bam~ ~override~
       ~iwdification/bam/cdia328b.bam~ ~override~
       ~iwdification/bam/cdia328c.bam~ ~override~
       ~iwdification/bam/cdiinvoc.bam~ ~override~
       ~iwdification/bam/cdilodis.bam~ ~override~
       ~iwdification/vvc/cdiinvoc.vvc~ ~override~
       ~iwdification/wav/cdiem06.wav~  ~override~
       ~iwdification/wav/cditra59.wav~ ~override~

END

/////                                                  \\\\\
///// WIZARD_BELTYNS_BURNING_BLOOD                     \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_BELTYNS_BURNING_BLOOD~) OR override_arcane) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@3070) STR_VAR bam_file = cdia422d RET icon END

  ADD_SPELL ~iwdification/spl/cdia422.spl~ 2 4 WIZARD_BELTYNS_BURNING_BLOOD
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3070 // name
    SAY 0x50 @3071 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdia422 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 66 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 66 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  9 opcode = 177 parameter1 = 145 parameter2 = 4 STR_VAR resource = cdixa422 END // race = elemental
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 55 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixa422 END // race = golem
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 55 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixa422 END // general = undead
      LPF CD_CONVERT_333 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia422.itm~
    SAY 0x0c @3070
    SAY 0x54 @3071
    LPF cd_scroll INT_VAR unusable1 = BIT2 range = 200 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/spl/cdia422a.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37605 parameter1 = string_berserk END

  COPY ~iwdification/bam/cdia422a.bam~ ~override~
       ~iwdification/bam/cdia422b.bam~ ~override~
       ~iwdification/bam/cdia422c.bam~ ~override~
       ~iwdification/bam/cdia422d.bam~ ~override~
       ~iwdification/bam/cdinecro.bam~ ~override~
       ~iwdification/vvc/cdinecro.vvc~ ~override~
       ~iwdification/wav/cdiem07.wav~  ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixa422.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// WIZARD_SHADOW_MONSTERS                           \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SHADOW_MONSTERS~) OR override_arcane) BEGIN

  LAF cd_new_summon_table STR_VAR descript = "SHADOW_MONSTERS" 2da_file = cdismons RET table END

  ADD_SPELL ~iwdification/spl/cdia426.spl~ 2 4 WIZARD_SHADOW_MONSTERS
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3080 // name
    SAY 0x50 @3081 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      READ_SHORT 0x68 abil_num
      LPF ALTER_EFFECT INT_VAR header = 0 match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdismons END
      FOR (index = 1 ; index < abil_num ; ++index) BEGIN
        LPF ALTER_EFFECT INT_VAR header = index match_opcode = 331 opcode = 127 parameter1 = (index + 7) parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdismons END
      END
      LPF CLONE_EFFECT INT_VAR match_opcode = 127 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia426.itm~
    SAY 0x0c @3080
    SAY 0x54 @3081
    LPF cd_scroll INT_VAR unusable1 = BIT4 target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 1200 STR_VAR spell = EVAL ~%current_spell_res%~ END

  LAF cd_animation STR_VAR code = e400 END // 58368 MGO1 goblin_axe    
  COPY ~iwdification/cre/cdis1gb1.cre~ ~override~
       ~iwdification/cre/cdis1gb2.cre~ ~override~
       ~iwdification/cre/cdis1gb3.cre~ ~override~
    WRITE_LONG  0x08 string_goblin
    WRITE_LONG  0x0c string_goblin

  LAF cd_animation STR_VAR code = e510 END // 58640 MLI2 lizard_man
  COPY ~iwdification/cre/cdis1lz3.cre~ ~override~
       ~iwdification/cre/cdis1lz4.cre~ ~override~
    SAY 0x08 @3082
    SAY 0x0c @3082
    WRITE_LONG 0x28 anim_lizman

  LAF cd_animation STR_VAR code = e0b0 END // 57520 MTRO troll_blue - MTRO doubles with bg2 troll
  COPY ~iwdification/cre/cdis1tr6.cre~ ~override~
       ~iwdification/cre/cdis1tr7.cre~ ~override~
       ~iwdification/cre/cdis1tr8.cre~ ~override~
    WRITE_LONG  0x08 string_troll
    WRITE_LONG  0x0c string_troll
    WRITE_LONG 0x28 anim_btroll

  COPY ~iwdification/itm/cdim1d7s.itm~ ~override~
    WRITE_LONG  0x08 string_attack
    WRITE_LONG  0x0c string_attack

  COPY ~iwdification/2da/cdismons.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

  COPY ~iwdification/bam/cdia426a.bam~ ~override~
       ~iwdification/bam/cdia426b.bam~ ~override~
       ~iwdification/bam/cdia426c.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/bam/cdimsm1x.bam~ ~override~
       ~iwdification/itm/cdisumrn.itm~ ~override~
       ~iwdification/itm/cditrn20.itm~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~
       ~iwdification/vvc/cdimsm1x.vvc~ ~override~

END

/////                                                  \\\\\
///// WIZARD_EMOTION_COURAGE                           \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_EMOTION_COURAGE~) OR override_arcane) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@3092) STR_VAR bam_file = cdia427d RET icon END

  ADD_PROJECTILE ~iwdification/pro/cdi407.pro~

  ADD_SPELL ~iwdification/spl/cdia427.spl~ 2 4 WIZARD_EMOTION_COURAGE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3090 // name
    SAY 0x50 @3091 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi407 END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdia427 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 187 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 187 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 54 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = EVAL ~%DEST_RES%~ END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia427.itm~
    SAY 0x0c @3090
    SAY 0x54 @3091
    LPF cd_scroll INT_VAR unusable1 = BIT3 target_hdr = 4 range = 50 opcode = 148 target_eff = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia427a.bam~ ~override~
       ~iwdification/bam/cdia427b.bam~ ~override~
       ~iwdification/bam/cdia427c.bam~ ~override~
       ~iwdification/bam/cdia427d.bam~ ~override~
       ~iwdification/bam/cdiencha.bam~ ~override~
       ~iwdification/vvc/cdiencha.vvc~ ~override~
       ~iwdification/wav/cdiee03.wav~  ~override~
       ~iwdification/wav/cdiem05.wav~  ~override~

END

/////                                                  \\\\\
///// WIZARD_EMOTION_FEAR                              \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_EMOTION_FEAR~) OR override_arcane) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi255.pro~

  ADD_SPELL ~iwdification/spl/cdia428.spl~ 2 4 WIZARD_EMOTION_FEAR
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3100 // name
    SAY 0x50 @3101 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi255 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 35484 parameter1 = string_panic END
    PATCH_IF !ee_game BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia428.itm~
    SAY 0x0c @3100
    SAY 0x54 @3101
    LPF cd_scroll INT_VAR unusable1 = BIT3 target_hdr = 4 range = 50 opcode = 148 target_eff = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia428a.bam~ ~override~
       ~iwdification/bam/cdia428b.bam~ ~override~
       ~iwdification/bam/cdia428c.bam~ ~override~
       ~iwdification/bam/cdiencha.bam~ ~override~
       ~iwdification/bam/cdienchx.bam~ ~override~
       ~iwdification/vvc/cdiencha.vvc~ ~override~
       ~iwdification/vvc/cdigench.vvc~ ~override~
       ~iwdification/wav/cdiarm21.wav~ ~override~
       ~iwdification/wav/cdiee05.wav~  ~override~
       ~iwdification/wav/cdiem05.wav~  ~override~

END

/////                                                  \\\\\
///// WIZARD_EMOTION_HOPE                              \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_EMOTION_HOPE~) OR override_arcane) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@3112) STR_VAR bam_file = cdia429d RET icon END

  ADD_PROJECTILE ~iwdification/pro/cdi407.pro~

  ADD_SPELL ~iwdification/spl/cdia429.spl~ 2 4 WIZARD_EMOTION_HOPE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3110 // name
    SAY 0x50 @3111 // descript
    LPF DELETE_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource = spin115 END
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi407 END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 186 parameter2 = icon END
      LPF ALTER_EFFECT STR_VAR match_resource = cdia429 resource = EVAL ~%DEST_RES%~ END
    END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 186 parameter2 = 22 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 54 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = EVAL ~%DEST_RES%~ END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia429.itm~
    SAY 0x0c @3110
    SAY 0x54 @3111
    LPF cd_scroll INT_VAR unusable1 = BIT3 target_hdr = 4 range = 50 opcode = 148 target_eff = 1 price = 600 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia429a.bam~ ~override~
       ~iwdification/bam/cdia429b.bam~ ~override~
       ~iwdification/bam/cdia429c.bam~ ~override~
       ~iwdification/bam/cdia429d.bam~ ~override~
       ~iwdification/bam/cdiencha.bam~ ~override~
       ~iwdification/vvc/cdiencha.vvc~ ~override~
       ~iwdification/wav/cdiee03.wav~  ~override~
       ~iwdification/wav/cdiem05.wav~  ~override~

END

/////                                                  \\\\\
///// WIZARD_MORDENKAINENS_FORCE_MISSILES              \\\\\
/////                                                  \\\\\

ACTION_IF (ee_game AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_MORDENKAINENS_FORCE_MISSILES~) OR override_arcane)) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdimfm.pro~
  ADD_PROJECTILE ~iwdification/pro/cdimfm2.pro~

  // ADD_PROJECTILE won't add multiple versions of the same projectile, but it's needed for MFM's special projectile chain - do it manually
  OUTER_FOR (index = (cdimfm2 + 1) ; index < (cdimfm2 + 10) ; ++index) BEGIN
  
    APPEND ~projectl.ids~ ~%index% cdimfm2~
  
  END

  ADD_SPELL ~iwdification/spl/cdia430.spl~ 2 4 WIZARD_MORDENKAINENS_FORCE_MISSILES
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3120 // name
    SAY 0x50 @3121 // descript
    LPF ALTER_SPELL_HEADER INT_VAR header =  1 projectile = (cdimfm + 0) END
    LPF ALTER_SPELL_HEADER INT_VAR header =  2 projectile = (cdimfm + 0) END
    LPF ALTER_SPELL_HEADER INT_VAR header =  3 projectile = (cdimfm + 0) END
    LPF ALTER_SPELL_HEADER INT_VAR header =  4 projectile = (cdimfm + 1) END
    LPF ALTER_SPELL_HEADER INT_VAR header =  5 projectile = (cdimfm + 1) END
    LPF ALTER_SPELL_HEADER INT_VAR header =  6 projectile = (cdimfm + 1) END
    LPF ALTER_SPELL_HEADER INT_VAR header =  7 projectile = (cdimfm + 2) END
    LPF ALTER_SPELL_HEADER INT_VAR header =  8 projectile = (cdimfm + 2) END
    LPF ALTER_SPELL_HEADER INT_VAR header =  9 projectile = (cdimfm + 2) END
    LPF ALTER_SPELL_HEADER INT_VAR header = 10 projectile = (cdimfm + 3) END
    LPF ALTER_SPELL_HEADER INT_VAR header = 11 projectile = (cdimfm + 3) END
    LPF ALTER_SPELL_HEADER INT_VAR header = 12 projectile = (cdimfm + 3) END
    LPF ALTER_SPELL_HEADER INT_VAR header = 13 projectile = (cdimfm + 4) END
    LPF ALTER_SPELL_HEADER INT_VAR header = 14 projectile = (cdimfm + 4) END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia430.itm~
    SAY 0x0c @3120
    SAY 0x54 @3121
    LPF cd_scroll INT_VAR unusable1 = BIT1 range = 100 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia430a.bam~ ~override~
       ~iwdification/bam/cdia430b.bam~ ~override~
       ~iwdification/bam/cdia430c.bam~ ~override~
       ~iwdification/bam/cdimfmsx.bam~ ~override~
       ~iwdification/bam/cdimfmt.bam~ ~override~
       ~iwdification/spl/cdia430b.spl~ ~override~
       ~iwdification/vvc/cdimfmsx.vvc~ ~override~
       ~iwdification/wav/cdiarm02.wav~ ~override~
       ~iwdification/wav/cdiem103.wav~ ~override~
       ~iwdification/wav/cditra55.wav~ ~override~

END

/////                                                  \\\\\
///// WIZARD_SHOUT                                     \\\\\
/////                                                  \\\\\

ACTION_IF (ee_game AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SHOUT~) OR override_arcane)) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi315.pro~

  ADD_SPELL ~iwdification/spl/cdia431.spl~ 2 4 WIZARD_SHOUT
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3130 // name
    SAY 0x50 @3131 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi315 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14073 parameter1 = string_deafness END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia431.itm~
    SAY 0x0c @3130
    SAY 0x54 @3131
    LPF cd_scroll INT_VAR unusable1 = BIT1 target_hdr = 4 range = 22 opcode = 148 target_eff = 1 price = 1200 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia431a.bam~ ~override~
       ~iwdification/bam/cdia431b.bam~ ~override~
       ~iwdification/bam/cdia431c.bam~ ~override~
       ~iwdification/bam/cdishout.bam~ ~override~
       ~iwdification/wav/cdiem100.wav~ ~override~
       ~iwdification/wav/cditra08.wav~ ~override~

END

/////                                                  \\\\\
///// WIZARD_VITRIOLIC_SPHERE                          \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_VITRIOLIC_SPHERE~) OR override_arcane) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi316.pro~

  ADD_SPELL ~iwdification/spl/cdia432.spl~ 2 4 WIZARD_VITRIOLIC_SPHERE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3140 // name
    SAY 0x50 @3141 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi316 END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia432.itm~
    SAY 0x0c @3140
    SAY 0x54 @3141
    LPF cd_scroll INT_VAR unusable1 = (BIT0 + BIT3) range = 100 price = 1600 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia432a.bam~ ~override~
       ~iwdification/bam/cdia432b.bam~ ~override~
       ~iwdification/bam/cdia432c.bam~ ~override~
       ~iwdification/bam/cdiacid.bam~  ~override~
       ~iwdification/bam/cdivitst.bam~ ~override~
       ~iwdification/bam/cdivitsx.bam~ ~override~
       ~iwdification/spl/cdia432y.spl~ ~override~
       ~iwdification/spl/cdia432z.spl~ ~override~
       ~iwdification/vvc/cdiacid.vvc~  ~override~
       ~iwdification/vvc/cdivitsx.vvc~ ~override~
       ~iwdification/wav/cdiem34.wav~  ~override~
       ~iwdification/wav/cdiem47.wav~  ~override~
       ~iwdification/wav/cditra60.wav~ ~override~

END

/////                                                  \\\\\
///// WIZARD_SHROUD_OF_FLAME                           \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SHROUD_OF_FLAME~) OR override_arcane) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@3150) STR_VAR bam_file = cdia524d RET icon END

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdishrod.pro~
  
  END ELSE BEGIN
  
    OUTER_SET cdishrod = 216
  
  END

  ADD_SPELL ~iwdification/spl/cdia524.spl~ 2 5 WIZARD_SHROUD_OF_FLAME
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3150 // name
    SAY 0x50 @3151 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdia524 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 333 STR_VAR resource = cdia524b END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 94 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 94 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 328 END
      LPF CD_CONVERT_333 STR_VAR 333spell = cdia524b END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia524.itm~
    SAY 0x0c @3150
    SAY 0x54 @3151
    LPF cd_scroll INT_VAR unusable1 = BIT1 range = 100 price = 1500 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/spl/cdia524b.spl~ ~override~
    SAY 0x08 @3150 // name
    SAY 0x50 @3151 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdishrod END
    PATCH_IF !ee_game BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 328 END
      LPF CD_CONVERT_9_255 END
    END

  COPY ~iwdification/bam/cdia524a.bam~ ~override~
       ~iwdification/bam/cdia524b.bam~ ~override~
       ~iwdification/bam/cdia524c.bam~ ~override~
       ~iwdification/bam/cdia524d.bam~ ~override~
       ~iwdification/bam/cdisofl.bam~  ~override~
       ~iwdification/vvc/cdisofl.vvc~  ~override~

END

/////                                                  \\\\\
///// WIZARD_DEMI_SHADOW_MONSTERS                      \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_DEMI_SHADOW_MONSTERS~) OR override_arcane) BEGIN

  LAF cd_new_summon_table STR_VAR descript = "DEMI_SHADOW_MONSTERS" 2da_file = cdidsmon RET table END

  ADD_SPELL ~iwdification/spl/cdia525.spl~ 2 5 WIZARD_DEMI_SHADOW_MONSTERS
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3160 // name
    SAY 0x50 @3161 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      READ_SHORT 0x68 abil_num
      LPF ALTER_EFFECT INT_VAR header = 0 match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdidsmon END
      FOR (index = 1 ; index < abil_num ; ++index) BEGIN
        LPF ALTER_EFFECT INT_VAR header = index match_opcode = 331 opcode = 127 parameter1 = (index + 9) parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdidsmon END
      END
      LPF CLONE_EFFECT INT_VAR match_opcode = 127 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia525.itm~
    SAY 0x0c @3160
    SAY 0x54 @3161
    LPF cd_scroll INT_VAR unusable1 = BIT4 target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 1500 STR_VAR spell = EVAL ~%current_spell_res%~ END

  LAF cd_animation STR_VAR code = e420 END // 58400 MGO3 goblin_elite_axe
  COPY ~iwdification/cre/cdis2gb1.cre~ ~override~
       ~iwdification/cre/cdis2gb2.cre~ ~override~
       ~iwdification/cre/cdis2gb3.cre~ ~override~
    SAY 0x08 @3162
    SAY 0x0c @3162

  LAF cd_animation STR_VAR code = e500 END // 58624 MLIZ lizard_man_elite
  COPY ~iwdification/cre/cdis2lz5.cre~ ~override~
       ~iwdification/cre/cdis2lz6.cre~ ~override~
       ~iwdification/cre/cdis2lz7.cre~ ~override~
    SAY 0x08 @3163
    SAY 0x0c @3163

  COPY ~iwdification/bam/cdia525a.bam~ ~override~
       ~iwdification/bam/cdia525b.bam~ ~override~
       ~iwdification/bam/cdia525c.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/bam/cdimsm1x.bam~ ~override~
       ~iwdification/itm/cdisumrn.itm~ ~override~
       ~iwdification/itm/cditrn40.itm~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~
       ~iwdification/vvc/cdimsm1x.vvc~ ~override~

  COPY ~iwdification/2da/cdidsmon.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

END

/////                                                  \\\\\
///// WIZARD_SUMMON_SHADOW                             \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SUMMON_SHADOW~) OR override_arcane) BEGIN

  LAF cd_new_summon_table STR_VAR descript = "SUMMON_SHADOW" 2da_file = cdishadw RET table END

  ADD_SPELL ~iwdification/spl/cdia526.spl~ 2 5 WIZARD_SUMMON_SHADOW
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3170 // name
    SAY 0x50 @3171 // descript    
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 12 parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdishadw END // shadows are level 4, so p1=12 should mean 3 shadows
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia526.itm~
    SAY 0x0c @3170
    SAY 0x54 @3171
    LPF cd_scroll INT_VAR target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 1500 STR_VAR spell = EVAL ~%current_spell_res%~ END

  LAF cd_animation STR_VAR code = ea20 END // 59936 MSH2 shadow_large
  COPY ~iwdification/cre/cdisshad.cre~ ~override~
    WRITE_LONG 0x08 string_shadow
    WRITE_LONG 0x0c string_shadow
    WRITE_LONG 0x28 anim_shadow_lg

  COPY ~iwdification/itm/cdishdw1.itm~ ~override~
    WRITE_LONG  0x08 string_attack
    WRITE_LONG  0x0c string_attack

  COPY ~iwdification/spl/cdishdw1.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 35593 parameter1 = string_diseased END

  COPY ~iwdification/bam/cdia526a.bam~ ~override~
       ~iwdification/bam/cdia526b.bam~ ~override~
       ~iwdification/bam/cdia526c.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/bam/cdimsm1x.bam~ ~override~
       ~iwdification/bam/cdinecro.bam~ ~override~
       ~iwdification/itm/cditrn60.itm~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~
       ~iwdification/vvc/cdimsm1x.vvc~ ~override~
       ~iwdification/vvc/cdinecro.vvc~ ~override~

  COPY ~iwdification/2da/cdishadw.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

END

/////                                                  \\\\\
///// WIZARD_CONTACT_OTHER_PLANE                       \\\\\
/////                                                  \\\\\

/* awaiting dialogue for cdia528.d
ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_CONTACT_OTHER_PLANE~) OR override_arcane) BEGIN

  ADD_SPELL ~iwdification/spl/cdia528.spl~ 2 5 WIZARD_CONTACT_OTHER_PLANE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3180 // name
    SAY 0x50 @3181 // descript

  COPY ~iwdification/itm/blank.itm~ ~override/cdia528.itm~
    SAY 0x0c @3180
    SAY 0x54 @3181
    LPF cd_scroll INT_VAR unusable2 = BIT7 target_hdr = 5 range = 1 target_eff = 1 price = 500 STR_VAR spell = EVAL ~%current_spell_res%~ END

  LAF cd_animation STR_VAR code = e300 END // 58112 MGHO ghost
  COPY ~iwdification/cre/cdia528.cre~ ~override~
    SAY 0x08 @3182
    SAY 0x0c @3182
    WRITE_LONG 0x28 anim_ghost
    WRITE_ASCII 0x2cc ~cdia528~ #8 // dlg

  APPEND ~state.ids~ ~0x00000FC0 STATE_REALLY_DEAD~ UNLESS ~^0x00000FC0[ %TAB]+STATE_REALLY_DEAD[ %TAB%%LNL%%MNL%%WNL%]~

  COMPILE ~iwdification/baf/cdia528.baf~
          ~iwdification/dlg/cdia528.d~

  COPY ~iwdification/bam/cdia528a.bam~ ~override~
       ~iwdification/bam/cdia528b.bam~ ~override~
       ~iwdification/bam/cdia528c.bam~ ~override~
       ~iwdification/bam/cdidivin.bam~ ~override~
       ~iwdification/eff/cdia528.eff~  ~override~
       ~iwdification/vvc/cdidivin.vvc~ ~override~
       ~iwdification/wav/cdiem01.wav~  ~override~

END
*/

/////                                                  \\\\\
///// WIZARD_CONJURE_FIRE_ELEMENTAL                    \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_CONJURE_FIRE_ELEMENTAL~) OR override_arcane) BEGIN

  APPEND ~spell.ids~ ~2516 WIZARD_CONJURE_FIRE_ELEMENTAL~ // alternate identifier

  LAF cd_new_summon_table STR_VAR descript = "FIRE_ELEMENTAL_WIZ" 2da_file = cdifelmw RET table END

  COPY ~iwdification/spl/cdia531.spl~ ~override/spwi516.spl~
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3190 // name
    SAY 0x50 @3191 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR index match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdifelmw END
      LPF CLONE_EFFECT INT_VAR match_opcode = 127 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/scrl6x.itm~
    SAY 0x0c @3190
    SAY 0x54 @3191
    LPF cd_scroll INT_VAR unusable1 = (BIT0 + BIT3) target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 1500 STR_VAR spell = EVAL ~%current_spell_res%~ END

  LAF cd_animation STR_VAR code = e24c END // 57932 MELF elemental_fire_iwd
  COPY ~iwdification/cre/cdi8fire.cre~ ~override~
    SAY 0x08 @3192
    SAY 0x0c @3192
    WRITE_LONG 0x28 anim_felem

  COPY ~iwdification/itm/cdifel18.itm~ ~override~
    WRITE_LONG 0x08 string_attack
    WRITE_LONG 0x0c string_attack

  COPY ~iwdification/bam/cdia531a.bam~ ~override~
       ~iwdification/bam/cdia531b.bam~ ~override~
       ~iwdification/bam/cdia531c.bam~ ~override~
       ~iwdification/bam/cdicfelx.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/vvc/cdicfelx.vvc~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~

  COPY ~iwdification/2da/cdifelmw.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

END

/////                                                  \\\\\
///// WIZARD_CONJURE_EARTH_ELEMENTAL                    \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_CONJURE_EARTH_ELEMENTAL~) OR override_arcane) BEGIN

  APPEND ~spell.ids~ ~2521 WIZARD_CONJURE_EARTH_ELEMENTAL~ // alternate identifier

  LAF cd_new_summon_table STR_VAR descript = "EARTH_ELEMENTAL_WIZ" 2da_file = cdieelmw RET table END

  COPY ~iwdification/spl/cdia532.spl~ ~override/spwi521.spl~
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3200 // name
    SAY 0x50 @3201 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR index match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdieelmw END
      LPF CLONE_EFFECT INT_VAR match_opcode = 127 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/scrl7c.itm~
    SAY 0x0c @3200
    SAY 0x54 @3201
    LPF cd_scroll INT_VAR unusable1 = (BIT0 + BIT3) target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 1500 STR_VAR spell = EVAL ~%current_spell_res%~ END

  LAF cd_animation STR_VAR code = e24b END // 57931 MELE elemental_earth_iwd
  COPY ~iwdification/cre/cdi8erth.cre~ ~override~
    SAY 0x08 @3202
    SAY 0x0c @3202
    WRITE_LONG 0x28 anim_eelem

  COPY ~iwdification/itm/cdieelem.itm~ ~override~
    WRITE_LONG 0x08 string_attack
    WRITE_LONG 0x0c string_attack

  COPY ~iwdification/bam/cdia532a.bam~ ~override~
       ~iwdification/bam/cdia532b.bam~ ~override~
       ~iwdification/bam/cdia532c.bam~ ~override~
       ~iwdification/bam/cdiceelx.bam~ ~override~
       ~iwdification/bam/cdieelem.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/vvc/cdiceelx.vvc~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~

  COPY ~iwdification/2da/cdieelmw.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

END

/////                                                  \\\\\
///// WIZARD_CONJURE_WATER_ELEMENTAL                   \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_CONJURE_WATER_ELEMENTAL~) OR override_arcane) BEGIN

  LAF cd_new_summon_table STR_VAR descript = "WATER_ELEMENTAL_WIZ" 2da_file = cdiwelmw RET table END

  ADD_SPELL ~iwdification/spl/cdia533.spl~ 2 5 WIZARD_CONJURE_WATER_ELEMENTAL
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3220 // name
    SAY 0x50 @3221 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR index match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdiwelmw END
      LPF CLONE_EFFECT INT_VAR match_opcode = 127 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia533.itm~
    SAY 0x0c @3220
    SAY 0x54 @3221
    LPF cd_scroll INT_VAR unusable1 = BIT0 + BIT3 target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 1500 STR_VAR spell = EVAL ~%current_spell_res%~ END

  LAF cd_animation STR_VAR code = e24d END // 57933 MELW elemental_water
  COPY ~iwdification/cre/cdi8watr.cre~ ~override~
    SAY 0x08 @3222
    SAY 0x0c @3222
    WRITE_LONG 0x28 anim_welem
    WRITE_ASCII 0x250 ~~ #8 // blank script that makes elementals ignore targets with SHLDBCH, as not relevant outside of IWD

  COPY ~iwdification/itm/cdiwelem.itm~ ~override~
    WRITE_LONG 0x08 string_attack
    WRITE_LONG 0x0c string_attack

  COPY ~iwdification/bam/cdia533a.bam~ ~override~
       ~iwdification/bam/cdia533b.bam~ ~override~
       ~iwdification/bam/cdia533c.bam~ ~override~
       ~iwdification/bam/cdicwelx.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/bam/cdiwelem.bam~ ~override~
       ~iwdification/itm/cditrn4.itm~  ~override~
       ~iwdification/vvc/cdicwelx.vvc~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~

  COPY ~iwdification/2da/cdiwelmw.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

END

/////                                                  \\\\\
///// WIZARD_CONJURE_AIR_ELEMENTAL                    \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_CONJURE_AIR_ELEMENTAL~) OR override_arcane) BEGIN

  APPEND ~spell.ids~ ~2520 WIZARD_CONJURE_AIR_ELEMENTAL~ // alternate identifier

  LAF cd_new_summon_table STR_VAR descript = "AIR_ELEMENTAL_WIZ" 2da_file = cdiaelmw RET table END

  COPY ~iwdification/spl/cdia534.spl~ ~override/spwi520.spl~
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3210 // name
    SAY 0x50 @3211 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR index match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdiaelmw END
      LPF CLONE_EFFECT INT_VAR match_opcode = 127 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/scrl7b.itm~
    SAY 0x0c @3210
    SAY 0x54 @3211
    LPF cd_scroll INT_VAR unusable1 = (BIT0 + BIT3) target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 1500 STR_VAR spell = EVAL ~%current_spell_res%~ END

//  LAF cd_animation STR_VAR code = 7320 END // 29472 MAIR elemental_air
  COPY ~iwdification/cre/cdi8air.cre~ ~override~
    SAY 0x08 @3212
    SAY 0x0c @3212

  COPY ~iwdification/itm/cdiaelem.itm~ ~override~
    WRITE_LONG 0x08 string_attack
    WRITE_LONG 0x0c string_attack

  COPY ~iwdification/bam/cdia534a.bam~ ~override~
       ~iwdification/bam/cdia534b.bam~ ~override~
       ~iwdification/bam/cdia534c.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/itm/cditrn4.itm~  ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~

  COPY ~iwdification/2da/cdiaelmw.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

END

/////                                                  \\\\\
///// WIZARD_ANTIMAGIC_SHELL                           \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_ANTIMAGIC_SHELL~) OR override_arcane) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@3230) STR_VAR bam_file = cdia610d RET icon END

  ADD_SPELL ~iwdification/spl/cdia610.spl~ 2 6 WIZARD_ANTIMAGIC_SHELL
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3230 // name
    SAY 0x50 @3231 // descript
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 35499 parameter1 = string_dispel END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 93 parameter2 = icon END
    END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 93 parameter2 = 83 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia610.itm~
    SAY 0x0c @3230
    SAY 0x54 @3231
    LPF cd_scroll INT_VAR unusable1 = BIT5 target_hdr = 5 range = 1 target_eff = 1 price = 600 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia610a.bam~ ~override~
       ~iwdification/bam/cdia610b.bam~ ~override~
       ~iwdification/bam/cdia610c.bam~ ~override~
       ~iwdification/bam/cdia610d.bam~ ~override~
       ~iwdification/bam/cdishelc.bam~ ~override~
       ~iwdification/vvc/cdiamags.vvc~ ~override~
       ~iwdification/wav/cdiafm04.wav~ ~override~
       ~iwdification/wav/cdiee01.wav~  ~override~

END

/////                                                  \\\\\
///// WIZARD_LICH_TOUCH                                \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_LICH_TOUCH~) OR override_arcane) BEGIN

  ADD_SPELL ~iwdification/spl/cdia626.spl~ 2 6 WIZARD_LICH_TOUCH
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3240 // name
    SAY 0x50 @3241 // descript

  COPY ~iwdification/itm/blank.itm~ ~override/cdia626.itm~
    SAY 0x0c @3240
    SAY 0x54 @3241
    LPF cd_scroll INT_VAR unusable1 = BIT2 target_hdr = 5 range = 1 target_eff = 1 price= 1800 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/itm/cdiltou.itm~ ~override~
    SAY 0x08 @3240
    SAY 0x0c @3240
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35606 parameter1 = string_held END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35542 parameter1 = string_paralyzed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35484 parameter1 = string_panic END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 20568 parameter1 = string_mf_panic END
    PATCH_IF !ee_game BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 55 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixa626 END // race = golem
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 55 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixa626 END // general = undead
    END

  COPY ~iwdification/spl/cdia626a.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 109 target = 2 parameter2 = 0 duration = 42 savingthrow = BIT2 STR_VAR                     END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 2 parameter2 = 0 timing = 1    savingthrow = BIT2 STR_VAR resource = cdinecro END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 174 target = 2 parameter2 = 0 timing = 1    savingthrow = BIT2 STR_VAR resource = cdiem07  END

  COPY ~iwdification/bam/cdia626a.bam~ ~override~
       ~iwdification/bam/cdia626b.bam~ ~override~
       ~iwdification/bam/cdia626c.bam~ ~override~
       ~iwdification/bam/cdiltou.bam~  ~override~
       ~iwdification/bam/cdinecro.bam~ ~override~
       ~iwdification/vvc/cdinecro.vvc~ ~override~
       ~iwdification/wav/cdiee04.wav~  ~override~
       ~iwdification/wav/cdiem07.wav~  ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixa626.eff~
      WRITE_ASCII 0x30 ~cdia626a~ #8

  END

END

/////                                                  \\\\\
///// WIZARD_MONSTER_SUMMONING_4                       \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_MONSTER_SUMMONING_4~) OR override_arcane) BEGIN

  LAF cd_new_summon_table STR_VAR descript = "Monster_summoning_IV" 2da_file = cdimsum4 RET table END

  ADD_SPELL ~iwdification/spl/cdia627.spl~ 2 6 WIZARD_MONSTER_SUMMONING_4
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3250 // name
    SAY 0x50 @3251 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 probability1 = 33 dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum4 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 probability1 = 67 dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum4 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0                   dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum4 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia627.itm~
    SAY 0x0c @3250
    SAY 0x54 @3251
    LPF cd_scroll INT_VAR unusable1 = (BIT0 + BIT3) target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 1800 STR_VAR spell = EVAL ~%current_spell_res%~ END

  LAF cd_animation STR_VAR code = e320 END // 58144 MGH3 ghast_greater
  COPY ~iwdification/cre/cdi4ghst.cre~ ~override~
    WRITE_LONG 0x08 string_ghast
    WRITE_LONG 0x0c string_ghast
    WRITE_LONG 0x28 anim_ghast

//  LAF cd_animation STR_VAR code = 9000 END // 36864 MOGR ogre
  COPY ~iwdification/cre/cdi4ogre.cre~ ~override~
    WRITE_LONG 0x08 string_ogre
    WRITE_LONG 0x0c string_ogre

  COPY ~iwdification/2da/cdimsum4.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
      PATCH_IF !anim_yeti BEGIN
        REPLACE_TEXTUALLY ~^3[ %TAB%]+cdi4yeti[ %TAB%%LNL%%MNL%%WNL%]+~ ~~ // delete yeti line from msum table
      END
    END

  ACTION_IF anim_yeti BEGIN // yeti animation (e.g. ee or vanilla bg2 w/ infinity animations)

    LAF cd_animation STR_VAR code = e25d END // 57949 MYET tundra_yeti
    COPY ~iwdification/cre/cdi4yeti.cre~ ~override~
      SAY 0x08 @3252
      SAY 0x0c @3252

  END

  COPY ~iwdification/bam/cdia627a.bam~ ~override~
       ~iwdification/bam/cdia627b.bam~ ~override~
       ~iwdification/bam/cdia627c.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/bam/cdimsm1x.bam~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~
       ~iwdification/vvc/cdimsm1x.vvc~ ~override~

END

/////                                                  \\\\\
///// WIZARD_OTILUKES_FREEZING_SPHERE                  \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_OTILUKES_FREEZING_SPHERE~) OR override_arcane) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi269.pro~

  ADD_SPELL ~iwdification/spl/cdia628.spl~ 2 6 WIZARD_OTILUKES_FREEZING_SPHERE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3260 // name
    SAY 0x50 @3261 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi269 END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia628.itm~
    SAY 0x0c @3260
    SAY 0x54 @3261
    LPF cd_scroll INT_VAR range = 40 price = 1800 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia628a.bam~ ~override~
       ~iwdification/bam/cdia628b.bam~ ~override~
       ~iwdification/bam/cdia628c.bam~ ~override~
       ~iwdification/bam/cdiofspt.bam~ ~override~

END

/////                                                  \\\\\
///// WIZARD_SHADES                                    \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SHADES~) OR override_arcane) BEGIN

  LAF cd_new_summon_table STR_VAR descript = "Shades" 2da_file = cdishade RET table END

  ADD_SPELL ~iwdification/spl/cdia629.spl~ 2 6 WIZARD_SHADES
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3270 // name
    SAY 0x50 @3271 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      READ_SHORT 0x68 abil_num
      LPF ALTER_EFFECT INT_VAR header = 0 match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdishade END
      FOR (index = 1 ; index < abil_num ; ++index) BEGIN
        LPF ALTER_EFFECT INT_VAR header = index match_opcode = 331 opcode = 127 parameter1 = (index + 12) parameter2 = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = cdishade END
      END
      LPF CLONE_EFFECT INT_VAR match_opcode = 127 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia629.itm~
    SAY 0x0c @3270
    SAY 0x54 @3271
    LPF cd_scroll INT_VAR unusable1 = BIT4 target_hdr = 4 range = 50 opcode = 148 target_eff = 1 price = 1800 STR_VAR spell = EVAL ~%current_spell_res%~ END

  ACTION_IF ee_game BEGIN // use iwdee gaze for ee games

    ADD_PROJECTILE ~iwdification/pro/cdi281.pro~

    COMPILE ~iwdification/baf/cdiuhgaz.baf~

    COPY ~iwdification/spl/cdiumbr1.spl~ ~override~ // gaze staging
         ~iwdification/spl/cditrdie.spl~ ~override~ // ee troll death
    COPY ~iwdification/spl/cdiumbr2.spl~ ~override~ // actual gaze
      SAY 0xce @3273
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi281 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37604 parameter1 = string_confused END
  
  END ELSE BEGIN
  
    COPY_EXISTING ~umbhul01.bcs~ ~override/cdiuhgaz.bcs~ // use bg2 umber hulk scripting
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~AreaCheck("AR1106")~ ~False()~
      END

  END

  LAF cd_animation STR_VAR code = e420 END // 58400 MGO3 goblin_elite_axe
  COPY ~iwdification/cre/cdis2gb2.cre~ ~override~
       ~iwdification/cre/cdis2gb3.cre~ ~override~
    SAY 0x08 @3162
    SAY 0x0c @3162

  LAF cd_animation STR_VAR code = e500 END // 58624 MLIZ lizard_man_elite
  COPY ~iwdification/cre/cdis2lz5.cre~ ~override~
       ~iwdification/cre/cdis2lz6.cre~ ~override~
       ~iwdification/cre/cdis2lz7.cre~ ~override~
    SAY 0x08 @3163
    SAY 0x0c @3163  

  LAF cd_animation STR_VAR code = e0b0 END // 57520 MTRO troll_blue - MTRO doubles with bg2 troll
  COPY ~iwdification/cre/cdis3tr7.cre~ ~override~
       ~iwdification/cre/cdis3tr8.cre~ ~override~
    WRITE_LONG  0x08 string_troll
    WRITE_LONG  0x0c string_troll
    WRITE_LONG  0x28 anim_btroll
    WRITE_ASCII 0x258 ~~ #8 // blank old die-revive troll script

  LAF cd_animation STR_VAR code = e0d0 END // 57552 MUMB umber_hulk_elder - MUMB doubles with bg2 umber hulk
  COPY ~iwdification/cre/cdis3um8.cre~ ~override~
       ~iwdification/cre/cdis3um9.cre~ ~override~
    SAY 0x08 @3272
    SAY 0x0c @3272
    WRITE_LONG 0x28 anim_uhulk

 COPY ~iwdification/itm/cdiumbhk.itm~ ~override~
      ~iwdification/itm/cdim1d7s.itm~ ~override~
    WRITE_LONG 0x08 string_attack
    WRITE_LONG 0x0c string_attack
  
  COPY ~iwdification/itm/cdir1hp2.itm~ ~override~
    LPF DELETE_EFFECT INT_VAR match_opcode = 206 END // old iwd vorpal protections
    LPF DELETE_EFFECT INT_VAR match_opcode = 267 END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 232 STR_VAR resource = cditrdie END
    END ELSE BEGIN
      PATCH_FOR_EACH op IN 232 208 BEGIN // nuke everything but regen
        LPF DELETE_EFFECT INT_VAR match_opcode = op END
      END
    END

  COPY ~iwdification/bam/cdia629a.bam~ ~override~
       ~iwdification/bam/cdia629b.bam~ ~override~
       ~iwdification/bam/cdia629c.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/bam/cdimsm1x.bam~ ~override~
       ~iwdification/itm/cdisumam.itm~ ~override~
       ~iwdification/itm/cdisumrn.itm~ ~override~
       ~iwdification/itm/cditrn40.itm~ ~override~
       ~iwdification/itm/cditrn60.itm~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~
       ~iwdification/vvc/cdimsm1x.vvc~ ~override~

  COPY ~iwdification/2da/cdishade.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

END

/////                                                  \\\\\
///// WIZARD_DARTS_OF_BONE                             \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_DARTS_OF_BONE~) OR override_arcane) BEGIN

  ADD_SPELL ~iwdification/spl/cdia630.spl~ 2 6 WIZARD_DARTS_OF_BONE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3280 // name
    SAY 0x50 @3281 // descript

  COPY ~iwdification/itm/blank.itm~ ~override/cdia630.itm~
    SAY 0x0c @3280
    SAY 0x54 @3281
    LPF cd_scroll INT_VAR unusable1 = BIT2 target_hdr = 5 range = 1 target_eff = 1 price = 600 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/itm/cdidbone.itm~ ~override~
    SAY 0x08 @3280
    SAY 0x0c @3282

  COPY ~iwdification/bam/cdia630a.bam~ ~override~
       ~iwdification/bam/cdia630b.bam~ ~override~
       ~iwdification/bam/cdia630c.bam~ ~override~
       ~iwdification/bam/cdidbone.bam~ ~override~
       ~iwdification/vvc/cdinecro.vvc~ ~override~
       ~iwdification/bam/cdinecro.bam~ ~override~
       ~iwdification/wav/cdiem07.wav~  ~override~

END

/////                                                  \\\\\
///// WIZARD_SOUL_EATER                                \\\\\
/////                                                  \\\\\

ACTION_IF (ee_game AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SOUL_EATER~) OR override_arcane)) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi299.pro~

  ADD_SPELL ~iwdification/spl/cdia631.spl~ 2 6 WIZARD_SOUL_EATER
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3290 // name
    SAY 0x50 @3291 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi299 END
    LPF ALTER_EFFECT STR_VAR match_resource = cdia631 resource = EVAL ~%DEST_RES%~ END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia631.itm~
    SAY 0x0c @3290
    SAY 0x54 @3291
    LPF cd_scroll INT_VAR unusable1 = BIT2 target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 1800 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COMPILE ~iwdification/baf/cdi3sklm.baf~

  LAF cd_animation STR_VAR code = 6403 END // 25603 MSKL skeleton
  COPY ~iwdification/cre/cdi3sklm.cre~ ~override~
    WRITE_LONG  0x08 string_skelly
    WRITE_LONG  0x0c string_skelly
    WRITE_ASCII 0x248 ~cdi3sklm~ #8

  COPY ~iwdification/bam/cdia631a.bam~ ~override~
       ~iwdification/bam/cdia631b.bam~ ~override~
       ~iwdification/bam/cdia631c.bam~ ~override~
       ~iwdification/bam/cdiseata.bam~ ~override~
       ~iwdification/bam/cdiseath.bam~ ~override~
       ~iwdification/spl/cdi3sklm.spl~ ~override~
       ~iwdification/spl/cdia631b.spl~ ~override~
       ~iwdification/vvc/cdiseata.vvc~ ~override~
       ~iwdification/vvc/cdiseath.vvc~ ~override~
       ~iwdification/wav/cdiarm18.wav~ ~override~
       ~iwdification/wav/cdiem104.wav~ ~override~

END

/////                                                  \\\\\
///// WIZARD_TROLLISH_FORTITUDE                        \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_TROLLISH_FORTITUDE~) OR override_arcane) BEGIN

  ADD_SPELL ~iwdification/spl/cdia632.spl~ 2 6 WIZARD_TROLLISH_FORTITUDE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3300 // name
    SAY 0x50 @3301 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdia632 resource = EVAL ~%DEST_RES%~ END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 142 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = EVAL ~%DEST_RES%~ END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia632.itm~
    SAY 0x0c @3300
    SAY 0x54 @3301
    LPF cd_scroll INT_VAR unusable1 = BIT2 target_hdr = 5 range = 1 target_eff = 1 price = 600 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia632a.bam~ ~override~
       ~iwdification/bam/cdia632b.bam~ ~override~
       ~iwdification/bam/cdia632c.bam~ ~override~
       ~iwdification/eff/cditroll.eff~ ~override~

END

/////                                                  \\\\\
///// WIZARD_MONSTER_SUMMONING_5                       \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_MONSTER_SUMMONING_5~) OR override_arcane) BEGIN

  LAF cd_new_summon_table STR_VAR descript = "Monster_summoning_V" 2da_file = cdimsum5 RET table END

  ADD_SPELL ~iwdification/spl/cdia706.spl~ 2 7 WIZARD_MONSTER_SUMMONING_5
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3310 // name
    SAY 0x50 @3311 // descript    
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 probability1 = 33 dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum5 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 probability1 = 67 dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum5 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0                   dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum5 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia706.itm~
    SAY 0x0c @3310
    SAY 0x54 @3311
    LPF cd_scroll INT_VAR unusable1 = (BIT0 + BIT3) target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 2100 STR_VAR spell = EVAL ~%current_spell_res%~ END

//  LAF cd_animation STR_VAR code = 7a00 END // 31232 MSPI_GI spider_giant
  COPY ~iwdification/cre/cdi5gspi.cre~ ~override~
    WRITE_LONG  0x08 string_gspider
    WRITE_LONG  0x0c string_gspider

  LAF cd_animation STR_VAR code = ee00 END // 60928 MZO2 zombie_yellow
  COPY ~iwdification/cre/cdi5jzom.cre~ ~override~
    SAY  0x08 @3312
    SAY  0x0c @3312
    WRITE_LONG 0x28 anim_jzombie

  LAF cd_animation STR_VAR code = e070 END // 57456 MMIN minotaur - MMIN doubles with bg2 mindflayer
  COPY ~iwdification/cre/cdi5mino.cre~ ~override~
    SAY  0x08 @3313
    SAY  0x0c @3313

  COPY ~iwdification/itm/cdis18ml.itm~ ~override~
    WRITE_LONG  0x08 string_attack
    WRITE_LONG  0x0c string_attack

  COPY ~iwdification/bam/cdia706a.bam~ ~override~
       ~iwdification/bam/cdia706b.bam~ ~override~
       ~iwdification/bam/cdia706c.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/bam/cdimsm1x.bam~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~
       ~iwdification/vvc/cdimsm1x.vvc~ ~override~

  COPY ~iwdification/2da/cdimsum5.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

END

/////                                                  \\\\\
///// WIZARD_MALAVONS_RAGE                             \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_MALAVONS_RAGE~) OR override_arcane) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi209.pro~

  ADD_SPELL ~iwdification/spl/cdia709.spl~ 2 7 WIZARD_MALAVONS_RAGE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3320 // name
    SAY 0x50 @3321 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi209 END
    PATCH_IF !ee_game BEGIN
      LPF CD_SPLIT_SAVE_DAMAGE END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia709.itm~
    SAY 0x0c @3320
    SAY 0x54 @3321
    LPF cd_scroll INT_VAR unusable1 = BIT1 target_hdr = 5 range = 50 price = 2100 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia709a.bam~ ~override~
       ~iwdification/bam/cdia709b.bam~ ~override~
       ~iwdification/bam/cdia709c.bam~ ~override~
       ~iwdification/bam/cdimagrx.bam~ ~override~
       ~iwdification/bam/cdimrage.bam~ ~override~
       ~iwdification/vvc/cdimalrg.vvc~ ~override~
       ~iwdification/vvc/cdimrage.vvc~ ~override~

END

/////                                                  \\\\\
///// WIZARD_ACID_STORM                                \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_ACID_STORM~) OR override_arcane) BEGIN

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi211.pro~
    
  END ELSE BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi211a.pro~

  END

  ADD_SPELL ~iwdification/spl/cdia724.spl~ 2 7 WIZARD_ACID_STORM
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3330 // name
    SAY 0x50 @3331 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi211 END
    END ELSE BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi211a END
      LPF ALTER_EFFECT INT_VAR match_opcode = 12 match_dicesize = 4 dicesize = 2 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 12 match_dicesize = 6 dicesize = 3 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 12 match_dicesize = 8 dicesize = 4 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 12 savingthrow = BIT24 END
      PATCH_FOR_EACH res IN 0 1 2 3 4 5 6 7 8 9 x y z BEGIN // simulating clouds in bg2 sucks
        LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 1 parameter2 = 2 resist_dispel = 3 STR_VAR resource = EVAL ~cdia724%res%~ END
      END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia724.itm~
    SAY 0x0c @3330
    SAY 0x54 @3331
    LPF cd_scroll INT_VAR unusable1 = BIT1 target_hdr = 4 range = 40 opcode = 148 target_eff = 1 price = 2100 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia724a.bam~ ~override~
       ~iwdification/bam/cdia724b.bam~ ~override~
       ~iwdification/bam/cdia724c.bam~ ~override~
       ~iwdification/bam/cdiastra.bam~ ~override~
       ~iwdification/bam/cdiastrx.bam~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/vvc/cdia7240.vvc~ ~override~
         ~iwdification/vvc/cdia7241.vvc~ ~override~
         ~iwdification/vvc/cdia7242.vvc~ ~override~
         ~iwdification/vvc/cdia7243.vvc~ ~override~
         ~iwdification/vvc/cdia7244.vvc~ ~override~
         ~iwdification/vvc/cdia7245.vvc~ ~override~
         ~iwdification/vvc/cdia7246.vvc~ ~override~
         ~iwdification/vvc/cdia7247.vvc~ ~override~
         ~iwdification/vvc/cdia7248.vvc~ ~override~
         ~iwdification/vvc/cdia7249.vvc~ ~override~
         ~iwdification/vvc/cdia724x.vvc~ ~override~
         ~iwdification/vvc/cdia724y.vvc~ ~override~
         ~iwdification/vvc/cdia724z.vvc~ ~override~

  END

END

/////                                                  \\\\\
///// WIZARD_SEVEN_EYES                                \\\\\
/////                                                  \\\\\

ACTION_IF (ee_game AND (FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SHOUT~)) AND // eye of fortitude requires shout, no vanilla
          (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SEVEN_EYES~) OR override_arcane)) BEGIN

  ADD_SPELL ~iwdification/spl/cdia725.spl~ 2 7 WIZARD_SEVEN_EYES
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3340 // name
    SAY 0x50 @3341 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdia725 resource = EVAL ~%DEST_RES%~ END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia725.itm~
    SAY 0x0c @3340
    SAY 0x54 @3341
    LPF cd_scroll INT_VAR unusable1 = BIT5 target_hdr = 5 range = 1 target_eff = 1 price = 700 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/spl/cdia725t.spl~ ~override~
    SAY 0x08 @3342 // name
    SAY 0x50 @3343 // descript

  COPY ~iwdification/spl/cdia725u.spl~ ~override~
    SAY 0x08 @3344 // name
    SAY 0x50 @3345 // descript

  COPY ~iwdification/spl/cdia725v.spl~ ~override~
    SAY 0x08 @3346 // name
    SAY 0x50 @3347 // descript

  COPY ~iwdification/spl/cdia725w.spl~ ~override~
    SAY 0x08 @3348 // name
    SAY 0x50 @3349 // descript

  COPY ~iwdification/spl/cdia725x.spl~ ~override~
    SAY 0x08 @3350 // name
    SAY 0x50 @3351 // descript

  COPY ~iwdification/spl/cdia725y.spl~ ~override~
    SAY 0x08 @3352 // name
    SAY 0x50 @3353 // descript

  COPY ~iwdification/spl/cdia725z.spl~ ~override~
    SAY 0x08 @3354 // name
    SAY 0x50 @3355 // descript

  COPY ~iwdification/2da/7eyes.2da~    ~override~
    REPLACE ~21648~ @3356
    REPLACE ~21649~ @3357
    REPLACE ~21650~ @3358
    REPLACE ~21651~ @3359
    REPLACE ~21652~ @3360
    REPLACE ~21653~ @3361
    REPLACE ~21654~ @3362

  COPY ~iwdification/bam/cdia725a.bam~ ~override~
       ~iwdification/bam/cdia725b.bam~ ~override~
       ~iwdification/bam/cdia725c.bam~ ~override~
       ~iwdification/bam/cdi7eyc1.bam~ ~override~
       ~iwdification/bam/cdi7eyc2.bam~ ~override~
       ~iwdification/bam/cdia725t.bam~ ~override~
       ~iwdification/bam/cdia725u.bam~ ~override~
       ~iwdification/bam/cdia725v.bam~ ~override~
       ~iwdification/bam/cdia725w.bam~ ~override~
       ~iwdification/bam/cdia725x.bam~ ~override~
       ~iwdification/bam/cdia725y.bam~ ~override~
       ~iwdification/bam/cdia725z.bam~ ~override~
       ~iwdification/vvc/cdi7ey.vvc~   ~override~
       ~iwdification/vvc/cdi7ey1a.vvc~ ~override~
       ~iwdification/vvc/cdi7ey1b.vvc~ ~override~
       ~iwdification/vvc/cdi7ey1c.vvc~ ~override~
       ~iwdification/vvc/cdi7ey1d.vvc~ ~override~
       ~iwdification/vvc/cdi7ey1e.vvc~ ~override~
       ~iwdification/vvc/cdi7ey1f.vvc~ ~override~
       ~iwdification/vvc/cdi7ey1g.vvc~ ~override~
       ~iwdification/vvc/cdi7ey2a.vvc~ ~override~
       ~iwdification/vvc/cdi7ey2b.vvc~ ~override~
       ~iwdification/vvc/cdi7ey2c.vvc~ ~override~
       ~iwdification/vvc/cdi7ey2d.vvc~ ~override~
       ~iwdification/vvc/cdi7ey2e.vvc~ ~override~
       ~iwdification/vvc/cdi7ey2f.vvc~ ~override~
       ~iwdification/vvc/cdi7ey2g.vvc~ ~override~
       ~iwdification/wav/cdiafm13.wav~ ~override~
       ~iwdification/wav/cdiafm15.wav~ ~override~
       ~iwdification/wav/cdiafm16.wav~ ~override~
       ~iwdification/wav/cdiafm17.wav~ ~override~

END

/////                                                  \\\\\
///// WIZARD_SUFFOCATE                                 \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SUFFOCATE~) OR override_arcane) BEGIN

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi317.pro~
  
  END ELSE BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi317a.pro~

  END

  ADD_SPELL ~iwdification/spl/cdia726.spl~ 2 7 WIZARD_SUFFOCATE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3370 // name
    SAY 0x50 @3371 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi317 END
      LPF ALTER_EFFECT STR_VAR match_resource = cdia726 resource = EVAL ~%DEST_RES%~ END
    END ELSE BEGIN
      LPF DELETE_EFFECT END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 timing = 1 parameter2 = 2 STR_VAR resource = cdia726 END
      LPF ALTER_SPELL_HEADER INT_VAR projectile = 1 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia726.itm~
    SAY 0x0c @3370
    SAY 0x54 @3371
    LPF cd_scroll INT_VAR unusable2 = BIT6 target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 2100 STR_VAR spell = EVAL ~%current_spell_res%~ END


  COPY ~iwdification/bam/cdia726a.bam~ ~override~
       ~iwdification/bam/cdia726b.bam~ ~override~
       ~iwdification/bam/cdia726c.bam~ ~override~
       ~iwdification/bam/cdisuffo.bam~ ~override~

  ACTION_IF ee_game BEGIN
  
    APPEND ~clearair.2da~ ~Suffocate %cdi317%~

    COPY ~iwdification/vvc/cdisuffo.vvc~ ~override~
         ~iwdification/wav/cdiafm18.wav~ ~override~

  END ELSE BEGIN
  
    APPEND ~clearair.2da~ ~Suffocate %cdi317a%~

    LAUNCH_ACTION_FUNCTION cd_create_cloud INT_VAR visloop = 5 cloud_dur = 60 zosa = 1 STR_VAR code = CDIA726 anim = cdisuffo END

    COPY ~iwdification/spl/cdia726.spl~ ~override~
      WRITE_LONG 0x08 "-1" // name
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi317a END
      LPF CLONE_EFFECT INT_VAR match_opcode = 321 opcode = 206 duration = 6 STR_VAR insert = last END
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  9 opcode = 177 parameter1 = 145 parameter2 = 4 STR_VAR resource = cdixa726 END // race = elemental
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 27 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixa726 END // race = golem
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 31 opcode = 177 parameter1 = 121 parameter2 = 4 STR_VAR resource = cdixa726 END // race = demonic
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 31 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixa726 END // general = undead
      LPF CD_SPLIT_SAVE_DAMAGE END

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixa726.eff~
      WRITE_ASCII 0x30 ~cdia726~ #8

  END

END

/////                                                  \\\\\
///// WIZARD_MONSTER_SUMMONING_6                       \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_MONSTER_SUMMONING_6~) OR override_arcane) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdisalau.pro~

  LAF cd_new_summon_table STR_VAR descript = "Monster_summoning_VI" 2da_file = cdimsum6 RET table END

  ADD_SPELL ~iwdification/spl/cdia801.spl~ 2 8 WIZARD_MONSTER_SUMMONING_6
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3380 // name
    SAY 0x50 @3381 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 probability1 = 33 dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum6 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 probability1 = 67 dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum6 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0                   dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum6 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia801.itm~
    SAY 0x0c @3380
    SAY 0x54 @3381
    LPF cd_scroll INT_VAR unusable1 = (BIT0 + BIT3) target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 2400 STR_VAR spell = EVAL ~%current_spell_res%~ END
    
  COMPILE ~iwdification/baf/cdisalfi.baf~
          ~iwdification/baf/cdisalfr.baf~
          ~iwdification/baf/cdipspid.baf~

//  LAF cd_animation STR_VAR code = a100 END // 41216 MCAR carrion_crawler
  COPY ~iwdification/cre/cdi6crwl.cre~ ~override~
    WRITE_LONG  0x08 string_crawler
    WRITE_LONG  0x0c string_crawler

//  LAF cd_animation STR_VAR code = 7a02 END // 31234 MSPI_PH spider_phase
  COPY ~iwdification/cre/cdi6pspi.cre~ ~override~
    WRITE_LONG  0x08 string_pspider
    WRITE_LONG  0x0c string_pspider

  LAF cd_animation STR_VAR code = e910 END // 59664 MSA2 salamander_frost
  COPY ~iwdification/cre/cdi6salc.cre~ ~override~
    SAY  0x08 @3383
    SAY  0x0c @3383

  LAF cd_animation STR_VAR code = e900 END // 59648 MSAL salamander_fire
  COPY ~iwdification/cre/cdi6salf.cre~ ~override~
    SAY  0x08 @3382
    SAY  0x0c @3382

  COPY ~iwdification/itm/cdisalfi.itm~ ~override~
       ~iwdification/itm/cdisalfr.itm~ ~override~
    WRITE_LONG  0x08 string_attack
    WRITE_LONG  0x0c string_attack
    WRITE_ASCII 0x3a ~isper01~ #8
    WRITE_ASCII 0x76 ~isper01~ #8
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 215 target = 1 parameter2 = 1 timing = 2 duration = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ END

  COPY ~iwdification/spl/cdifire6.spl~ ~override~
       ~iwdification/spl/cdifros6.spl~ ~override~
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdisalau END
    END ELSE BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = 1 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 318 END
    END

  COPY ~iwdification/spl/cdisalfi.spl~ ~override~
       ~iwdification/spl/cdisalfr.spl~ ~override~
    PATCH_IF !ee_game BEGIN // change from EE's 232 distance check to straight once-per-round spam; needs effs copied below
      LPF ALTER_EFFECT INT_VAR match_opcode = 232 opcode = 272 target = 1 parameter1 = 6 parameter2 = 3 END
    END

  COPY ~iwdification/bam/cdia801a.bam~ ~override~
       ~iwdification/bam/cdia801b.bam~ ~override~
       ~iwdification/bam/cdia801c.bam~ ~override~
       ~iwdification/bam/cdidoorh.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/bam/cdimsm1x.bam~ ~override~
       ~iwdification/bam/cdisalfi.bam~ ~override~
       ~iwdification/bam/cdisalfr.bam~ ~override~
       ~iwdification/spl/cdiphase.spl~ ~override~
       ~iwdification/vvc/cdidoorh.vvc~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~
       ~iwdification/vvc/cdimsm1x.vvc~ ~override~
       ~iwdification/vvc/cdisalfi.vvc~ ~override~
       ~iwdification/vvc/cdisalfr.vvc~ ~override~

  COPY ~iwdification/2da/cdimsum6.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/spl/cdifire6.spl~ ~override/cdifire0.spl~
         ~iwdification/spl/cdifros6.spl~ ~override/cdifros0.spl~
      WRITE_BYTE 0x7e 1
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdisalau END
      LPF DELETE_EFFECT END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = EVAL ~%SOURCE_RES%~ END

    COPY ~iwdification/eff/cdifire6.eff~ ~override/cdifire6.eff~
         ~iwdification/eff/cdifire6.eff~ ~override/cdifros6.eff~
      WRITE_ASCIIE 0x30 ~%DEST_RES%~ #8
      WRITE_ASCII  0x37 ~0~ #1

    COPY_EXISTING ~cdisalfi.itm~ ~override~
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 timing = 2 STR_VAR resource = cdifire6 END

    COPY_EXISTING ~cdisalfr.itm~ ~override~
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 timing = 2 STR_VAR resource = cdifros6 END

  END

END

/////                                                  \\\\\
///// WIZARD_MIND_BLANK                                \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_MIND_BLANK~) OR override_arcane) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@3390) STR_VAR bam_file = cdia802d RET icon END

  ADD_SPELL ~iwdification/spl/cdia802.spl~ 2 8 WIZARD_MIND_BLANK
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3390 // name
    SAY 0x50 @3391 // descript
//    LPF ALTER_EFFECT STR_VAR match_resource = confush resource = cdiconfh END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14102 parameter1 = string_held END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 17392 parameter1 = string_dcharmed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 20568 parameter1 = string_mf_panic END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35484 parameter1 = string_panic END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35542 parameter1 = string_paralyzed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35544 parameter1 = string_dominated END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35606 parameter1 = string_held END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37603 parameter1 = string_rthinking END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37604 parameter1 = string_confused END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37605 parameter1 = string_berserk END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37606 parameter1 = string_intoxicated END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37801 parameter1 = string_charmed END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 166 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 166 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia802.itm~
    SAY 0x0c @3390
    SAY 0x54 @3391
    LPF cd_scroll INT_VAR unusable1 = BIT5 target_hdr = 5 range = 1 target_eff = 1 price = 800 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia802a.bam~ ~override~
       ~iwdification/bam/cdia802b.bam~ ~override~
       ~iwdification/bam/cdia802c.bam~ ~override~
       ~iwdification/bam/cdia802d.bam~ ~override~
       ~iwdification/bam/cdiabjur.bam~ ~override~
       ~iwdification/vvc/cdiabjur.vvc~ ~override~
       ~iwdification/wav/cdiem02.wav~  ~override~

END

/////                                                  \\\\\
///// WIZARD_GREAT_SHOUT                               \\\\\
/////                                                  \\\\\

ACTION_IF (ee_game AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_GREAT_SHOUT~) OR override_arcane)) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi319.pro~

  ADD_SPELL ~iwdification/spl/cdia806.spl~ 2 8 WIZARD_GREAT_SHOUT
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3400 // name
    SAY 0x50 @3401 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi319 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37340 parameter1 = string_uncon END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 35568 parameter1 = string_stunned END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14073 parameter1 = string_deafness END
    PATCH_IF !ee_game BEGIN
      LPF CD_SPLIT_SAVE_DAMAGE END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia806.itm~
    SAY 0x0c @3400
    SAY 0x54 @3401
    LPF cd_scroll INT_VAR unusable1 = BIT1 target_hdr = 4 range = 22 opcode = 148 target_eff = 1 price = 2400 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdia806a.bam~ ~override~
       ~iwdification/bam/cdia806b.bam~ ~override~
       ~iwdification/bam/cdia806c.bam~ ~override~
       ~iwdification/bam/cdigshou.bam~ ~override~
       ~iwdification/wav/cdiem101.wav~ ~override~
       ~iwdification/wav/cditra08.wav~ ~override~

END

/////                                                  \\\\\
///// WIZARD_IRON_BODY                                 \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_IRON_BODY~) OR override_arcane) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@3410) STR_VAR bam_file = cdia814d RET icon END

  ADD_SPELL ~iwdification/spl/cdia814.spl~ 2 8 WIZARD_IRON_BODY
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3410 // name
    SAY 0x50 @3411 // descript
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37607 parameter1 = string_poisoned END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 36317 parameter1 = string_poison END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35593 parameter1 = string_diseased END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 39752 parameter1 = string_stricken END
    LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi502 resource = spin673 END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdia814 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 172 parameter2 = icon END
      LPF CLONE_EFFECT INT_VAR match_opcode = 206 opcode = 318 parameter2 = 0 STR_VAR match_resource = spwi502 resource = wand13 END
    END ELSE BEGIN // move spell effects to 'paw'
      READ_LONG 0x6a fx_off
      SET fx_num = ((SOURCE_SIZE - fx_off) / 0x30)
      READ_ASCII fx_off fx (fx_num * 0x30)
      LPF DELETE_EFFECT END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 111 target = 1 power = 8 duration = 120 STR_VAR resource = cdiibody END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 174 target = 1 power = 8 timing = 1     STR_VAR resource = cdiem102 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia814.itm~
    SAY 0x0c @3410
    SAY 0x54 @3411
    LPF cd_scroll INT_VAR unusable2 = BIT6 target_hdr = 5 range = 1 target_eff = 1 price = 800 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/itm/cdiibody.itm~ ~override~
    SAY 0x08 @3412
    SAY 0x0c @3412
    PATCH_IF !ee_game BEGIN // move spell effects to 'paw'
      READ_LONG   0x64 abil_off ELSE 0
      READ_SHORT  0x68 abil_num ELSE 0
      READ_LONG   0x6a fx_off
      WRITE_SHORT 0x70 (THIS + fx_num)
      FOR (index = 0 ; index < abil_num ; ++index) BEGIN // start iterating through abilities
        WRITE_SHORT  (abil_off + 0x20 + (0x28 * index)) (THIS + fx_num)
      END
      INSERT_BYTES fx_off (fx_num * 0x30)
      WRITE_ASCIIE fx_off ~%fx%~
      PATCH_FOR_EACH op IN 321 111 174 BEGIN
        LPF DELETE_EFFECT INT_VAR match_opcode = op END
      END
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 172 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 1 multi_match = 1 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 318 opcode = 206 parameter1 = 0 END
      LPF ALTER_EFFECT INT_VAR timing = 2 END
    END

  COPY ~iwdification/bam/cdia814a.bam~ ~override~
       ~iwdification/bam/cdia814b.bam~ ~override~
       ~iwdification/bam/cdia814c.bam~ ~override~
       ~iwdification/bam/cdia814d.bam~ ~override~
       ~iwdification/bam/cdiibody.bam~ ~override~
       ~iwdification/wav/cdiem102.wav~ ~override~

END

/////                                                  \\\\\
///// WIZARD_MONSTER_SUMMONING_7                       \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_MONSTER_SUMMONING_7~) OR override_arcane) BEGIN

  LAF cd_new_summon_table STR_VAR descript = "Monster_summoning_VII" 2da_file = cdimsum7 RET table END

  ADD_SPELL ~iwdification/spl/cdia901.spl~ 2 9 WIZARD_MONSTER_SUMMONING_7
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @3420 // name
    SAY 0x50 @3421 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 probability1 = 49 dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum7 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0                   dicesize = 0 dicenumber = 0 STR_VAR resource = cdimsum7 END
    END

  COPY ~iwdification/itm/blank.itm~ ~override/cdia901.itm~
    SAY 0x0c @3420
    SAY 0x54 @3421
    LPF cd_scroll INT_VAR unusable1 = (BIT0 + BIT3) target_hdr = 4 range = 20 opcode = 148 target_eff = 1 price = 2700 STR_VAR spell = EVAL ~%current_spell_res%~ END

  ACTION_IF ee_game BEGIN // use iwdee gaze for ee games

    ADD_PROJECTILE ~iwdification/pro/cdi281.pro~

    COMPILE ~iwdification/baf/cdiuhgaz.baf~

    COPY ~iwdification/spl/cdiumbr1.spl~ ~override~ // gaze staging
    COPY ~iwdification/spl/cdiumbr2.spl~ ~override~ // actual gaze
      SAY 0xce @3273
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi281 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37604 parameter1 = string_confused END
  
  END ELSE BEGIN
  
    COPY_EXISTING ~umbhul01.bcs~ ~override/cdiuhgaz.bcs~ // use bg2 umber hulk scripting
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~AreaCheck("AR1106")~ ~False()~
      END

  END

  LAF cd_animation STR_VAR code = eb20 END // 60192 MSKB skeleton_fiend
  COPY ~iwdification/cre/cdi7bgrd.cre~ ~override~
    SAY  0x08 @3422
    SAY  0x0c @3422

  LAF cd_animation STR_VAR code = e0d0 END // 57552 MUMB umber_hulk_elder - MUMB doubles with bg2 umber hulk
  COPY ~iwdification/cre/cdi7umbh.cre~ ~override~
    SAY  0x08 @3272
    SAY  0x0c @3272
    WRITE_LONG 0x28 anim_uhulk

 COPY ~iwdification/itm/cdiumbhk.itm~ ~override~
    WRITE_LONG 0x08 string_attack
    WRITE_LONG 0x0c string_attack

  COPY ~iwdification/bam/cdia901a.bam~ ~override~
       ~iwdification/bam/cdia901b.bam~ ~override~
       ~iwdification/bam/cdia901c.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/bam/cdimsm1x.bam~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~
       ~iwdification/vvc/cdimsm1x.vvc~ ~override~

  COPY ~iwdification/2da/cdimsum7.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

END

/////                                                  \\\\\
///// arcane spell crosspatching                       \\\\\
/////                                                  \\\\\

OUTER_SET NUM_a411 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_EMOTION_HOPELESSNESS~)) - 2000)
OUTER_SET NUM_a422 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_BELTYNS_BURNING_BLOOD~)) - 2000)
OUTER_SET NUM_a427 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_EMOTION_COURAGE~)) - 2000)
OUTER_SET NUM_a428 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_EMOTION_FEAR~)) - 2000)
OUTER_SET NUM_a429 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_EMOTION_HOPE~)) - 2000)
OUTER_SET NUM_a431 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_SHOUT~)) - 2000)
OUTER_SET NUM_a432 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_VITRIOLIC_SPHERE~)) - 2000)
OUTER_SET NUM_a524 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_SHROUD_OF_FLAME~)) - 2000)
OUTER_SET NUM_a610 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_ANTIMAGIC_SHELL~)) - 2000)
OUTER_SET NUM_a631 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_SOUL_EATER~)) - 2000)
OUTER_SET NUM_a724 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_ACID_STORM~)) - 2000)
OUTER_SET NUM_a726 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_SUFFOCATE~)) - 2000)
OUTER_SET NUM_a814 = ((IDS_OF_SYMBOL (~spell~ ~WIZARD_IRON_BODY~)) - 2000)

ACTION_IF ((NUM_a411 > 0) AND (NUM_a429 > 0)) BEGIN

  COPY_EXISTING ~spwi%NUM_a411%.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia429 resource = EVAL ~spwi%NUM_a429%~ END
    BUT_ONLY

  COPY_EXISTING ~spwi%NUM_a429%.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia411 resource = EVAL ~spwi%NUM_a411%~ END
    BUT_ONLY

END

ACTION_IF ((NUM_a427 > 0) AND (NUM_a428 > 0)) BEGIN

  COPY_EXISTING ~spwi%NUM_a427%.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia428 resource = EVAL ~spwi%NUM_a428%~ END
    BUT_ONLY

  COPY_EXISTING ~spwi%NUM_a428%.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia427 resource = EVAL ~spwi%NUM_a427%~ END
    BUT_ONLY

END

ACTION_IF NUM_a610 > 0 BEGIN

  COPY_EXISTING ~spwi%NUM_a610%.spl~ ~override~
    PATCH_IF NUM_a726 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia726 resource = EVAL ~spwi%NUM_a726%~ END
    END
    PATCH_IF NUM_a724 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia724 resource = EVAL ~spwi%NUM_a724%~ END
    END
    PATCH_IF NUM_a631 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia631 resource = EVAL ~spwi%NUM_a631%~ END
    END
    PATCH_IF NUM_a524 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia524 resource = EVAL ~spwi%NUM_a524%~ END
    END
    PATCH_IF NUM_a432 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia432 resource = EVAL ~spwi%NUM_a432%~ END
    END
    BUT_ONLY

END

ACTION_IF NUM_a431 > 0 BEGIN

  COPY_EXISTING ~cdia725y.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia431 resource = EVAL ~spwi%NUM_a431%~ END
    BUT_ONLY IF_EXISTS

END

ACTION_IF NUM_a814 > 0 BEGIN

  COPY_EXISTING ~spwi%NUM_a814%.spl~ ~override~
    PATCH_IF NUM_a726 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia726 resource = EVAL ~spwi%NUM_a726%~ END
    END
    PATCH_IF NUM_a422 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdia724 resource = EVAL ~spwi%NUM_a422%~ END
    END
    BUT_ONLY

END

ACTION_IF !ee_game BEGIN

  COPY_EXISTING ~cdidbone.bam~ ~override~
                ~cdideca.bam~  ~override~
                ~cdiibody.bam~ ~override~
                ~cdiltou.bam~  ~override~
                ~cdiplybb.bam~ ~override~
                ~cdiplypb.bam~ ~override~
                ~cdiplyww.bam~ ~override~
                ~cdismcud.bam~ ~override~
    READ_LONG 0x0c frame_off
    WRITE_LONG frame_off + 0x04 0
    IF_EXISTS

END

/////                                                  \\\\\
///// arcane-divine cross-patching                     \\\\\
/////                                                  \\\\\

ACTION_IF MOD_IS_INSTALLED ~IWDIFICATION/SETUP-IWDIFICATION.TP2~ ~40~ THEN BEGIN

  INCLUDE ~iwdification/lib/cross_patch_both.tpa~

END

/////                                                  \\\\\
///// add scrolls to stores                            \\\\\
/////                                                  \\\\\

COPY_EXISTING_REGEXP GLOB ~^[_h]ighhedg\.sto$~ ~override~  // adds lev 1, 2 scrolls to High Hedge
                          ~^_?sto0703\.sto$~   ~override~  // adds lev 1, 2 scrolls to Sorcerous Sundries
                          ~^bdsorcsc\.sto$~    ~override~  // adds lev 1, 2 scrolls to SoD Sorcerous Sundries
                          ~^bdbeleg3\.sto$~    ~override~  // adds lev 1, 2 scrolls to SoD Quartermaster
                          ~^bpding01\.sto$~    ~override~  // adds lev 1, 2 scrolls to Dinguer the Mad (Black Pits)
  PATCH_IF FILE_EXISTS_IN_GAME cdia126.itm BEGIN ADD_STORE_ITEM ~cdia126~ AFTER ~scrl84 _scrl84~ #1 #0 #0 ~IDENTIFIED~ #2 END // expeditious retreat
  PATCH_IF FILE_EXISTS_IN_GAME cdia204.itm BEGIN ADD_STORE_ITEM ~cdia204~ AFTER ~scrl89 _scrl89~ #1 #0 #0 ~IDENTIFIED~ #2 END // Snilloc's Snowball Swarm
  PATCH_IF FILE_EXISTS_IN_GAME cdia216.itm BEGIN ADD_STORE_ITEM ~cdia216~ AFTER ~scrl89 _scrl89~ #1 #0 #0 ~IDENTIFIED~ #2 END // decastave
  PATCH_IF FILE_EXISTS_IN_GAME cdia225.itm BEGIN ADD_STORE_ITEM ~cdia225~ AFTER ~scrl89 _scrl89~ #1 #0 #0 ~IDENTIFIED~ #2 END // cats grace
  BUT_ONLY IF_EXISTS

COPY_EXISTING_REGEXP GLOB ~^_?sto0703\.sto$~ ~override~  // adds lev 3, 4, 5 scrolls to Sorcerous Sundries
                          ~^bdsorcsc\.sto$~  ~override~  // adds lev 3, 4, 5 scrolls to SoD Sorcerous Sundries
                          ~^bdbeleg3\.sto$~  ~override~  // adds lev 3, 4, 5 scrolls to SoD Quartermaster
                          ~^bpding01\.sto$~  ~override~  // adds lev 3, 4, 5 scrolls to Dinguer the Mad (Black Pits)
  PATCH_IF FILE_EXISTS_IN_GAME cdia327.itm BEGIN ADD_STORE_ITEM ~cdia327~ AFTER ~scrl1k _scrl1k~ #1 #0 #0 ~IDENTIFIED~ #2 END // Icelance
  PATCH_IF FILE_EXISTS_IN_GAME cdia328.itm BEGIN ADD_STORE_ITEM ~cdia328~ AFTER ~scrl1k _scrl1k~ #1 #0 #0 ~IDENTIFIED~ #2 END // Lance of Disruption
  PATCH_IF FILE_EXISTS_IN_GAME cdia422.itm BEGIN ADD_STORE_ITEM ~cdia422~ AFTER ~scrl5g _scrl5g~ #1 #0 #0 ~IDENTIFIED~ #2 END // Beltyn's Burning Blood
  PATCH_IF FILE_EXISTS_IN_GAME cdia426.itm BEGIN ADD_STORE_ITEM ~cdia426~ AFTER ~scrl5g _scrl5g~ #1 #0 #0 ~IDENTIFIED~ #2 END // Shadow Monsters
  PATCH_IF FILE_EXISTS_IN_GAME cdia427.itm BEGIN ADD_STORE_ITEM ~cdia427~ AFTER ~scrl5g _scrl5g~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Courage
  PATCH_IF FILE_EXISTS_IN_GAME cdia428.itm BEGIN ADD_STORE_ITEM ~cdia428~ AFTER ~scrl5g _scrl5g~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Fear
  PATCH_IF FILE_EXISTS_IN_GAME cdia429.itm BEGIN ADD_STORE_ITEM ~cdia429~ AFTER ~scrl5g _scrl5g~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Hope
  PATCH_IF FILE_EXISTS_IN_GAME cdia430.itm BEGIN ADD_STORE_ITEM ~cdia430~ AFTER ~scrl5g _scrl5g~ #1 #0 #0 ~IDENTIFIED~ #2 END // mordenkainen's force missiles
  PATCH_IF FILE_EXISTS_IN_GAME cdia432.itm BEGIN ADD_STORE_ITEM ~cdia432~ AFTER ~scrl5g _scrl5g~ #1 #0 #0 ~IDENTIFIED~ #2 END // Vitriolic Sphere
  PATCH_IF FILE_EXISTS_IN_GAME cdia524.itm BEGIN ADD_STORE_ITEM ~cdia524~ AFTER ~scrl5k _scrl5k~ #1 #0 #0 ~IDENTIFIED~ #1 END // shroud of flame
  PATCH_IF FILE_EXISTS_IN_GAME cdia525.itm BEGIN ADD_STORE_ITEM ~cdia525~ AFTER ~scrl5k _scrl5k~ #1 #0 #0 ~IDENTIFIED~ #1 END // Demi-Shadow Monsters
  PATCH_IF FILE_EXISTS_IN_GAME cdia526.itm BEGIN ADD_STORE_ITEM ~cdia526~ AFTER ~scrl5k _scrl5k~ #1 #0 #0 ~IDENTIFIED~ #1 END // Summon Shadow
  PATCH_IF FILE_EXISTS_IN_GAME cdia528.itm BEGIN ADD_STORE_ITEM ~cdia528~ AFTER ~scrl5k _scrl5k~ #1 #0 #0 ~IDENTIFIED~ #1 END // contact other plane
  PATCH_IF FILE_EXISTS_IN_GAME cdia533.itm BEGIN ADD_STORE_ITEM ~cdia533~ AFTER ~scrl5k _scrl5k~ #1 #0 #0 ~IDENTIFIED~ #1 END // Conjure water elemental
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~25spell.sto~  ~override~ // arcana archives
              ~25spell2.sto~ ~override~ // arcana archives
              ~ohbmhsm.sto~  ~override~ // Book Merchant (BG2EE Black Pits 2)
              ~ohnmhsm.sto~  ~override~ // Book Merchant (BG2EE Neera's Quest)
  PATCH_IF FILE_EXISTS_IN_GAME cdia126.itm BEGIN ADD_STORE_ITEM ~cdia126~ AFTER ~scrl84~ #1 #0 #0 ~IDENTIFIED~ #2 END // expeditious retreat
  PATCH_IF FILE_EXISTS_IN_GAME cdia204.itm BEGIN ADD_STORE_ITEM ~cdia204~ AFTER ~scrl93~ #1 #0 #0 ~IDENTIFIED~ #2 END // Snilloc's Snowball Swarm
  PATCH_IF FILE_EXISTS_IN_GAME cdia216.itm BEGIN ADD_STORE_ITEM ~cdia216~ AFTER ~scrl93~ #1 #0 #0 ~IDENTIFIED~ #2 END // decastave
  PATCH_IF FILE_EXISTS_IN_GAME cdia225.itm BEGIN ADD_STORE_ITEM ~cdia225~ AFTER ~scrl93~ #1 #0 #0 ~IDENTIFIED~ #2 END // cats grace
  PATCH_IF FILE_EXISTS_IN_GAME cdia327.itm BEGIN ADD_STORE_ITEM ~cdia327~ AFTER ~scrl1e~ #1 #0 #0 ~IDENTIFIED~ #2 END // Icelance
  PATCH_IF FILE_EXISTS_IN_GAME cdia328.itm BEGIN ADD_STORE_ITEM ~cdia328~ AFTER ~scrl1e~ #1 #0 #0 ~IDENTIFIED~ #2 END // Lance of Disruption
  PATCH_IF FILE_EXISTS_IN_GAME cdia422.itm BEGIN ADD_STORE_ITEM ~cdia422~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Beltyn's Burning Blood
  PATCH_IF FILE_EXISTS_IN_GAME cdia426.itm BEGIN ADD_STORE_ITEM ~cdia426~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Shadow Monsters
  PATCH_IF FILE_EXISTS_IN_GAME cdia427.itm BEGIN ADD_STORE_ITEM ~cdia427~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Courage
  PATCH_IF FILE_EXISTS_IN_GAME cdia428.itm BEGIN ADD_STORE_ITEM ~cdia428~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Fear
  PATCH_IF FILE_EXISTS_IN_GAME cdia429.itm BEGIN ADD_STORE_ITEM ~cdia429~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Hope
  PATCH_IF FILE_EXISTS_IN_GAME cdia430.itm BEGIN ADD_STORE_ITEM ~cdia430~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // mordenkainen's force missiles
  PATCH_IF FILE_EXISTS_IN_GAME cdia432.itm BEGIN ADD_STORE_ITEM ~cdia432~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Vitriolic Sphere
  PATCH_IF FILE_EXISTS_IN_GAME cdia524.itm BEGIN ADD_STORE_ITEM ~cdia524~ AFTER ~scrl6v~ #1 #0 #0 ~IDENTIFIED~ #2 END // Shroud of Flame
  PATCH_IF FILE_EXISTS_IN_GAME cdia525.itm BEGIN ADD_STORE_ITEM ~cdia525~ AFTER ~scrl6v~ #1 #0 #0 ~IDENTIFIED~ #2 END // Demi-Shadow Monsters
  PATCH_IF FILE_EXISTS_IN_GAME cdia526.itm BEGIN ADD_STORE_ITEM ~cdia526~ AFTER ~scrl6v~ #1 #0 #0 ~IDENTIFIED~ #2 END // Summon Shadow
  PATCH_IF FILE_EXISTS_IN_GAME cdia528.itm BEGIN ADD_STORE_ITEM ~cdia528~ AFTER ~scrl6v~ #1 #0 #0 ~IDENTIFIED~ #2 END // Contact Other Plane
  PATCH_IF FILE_EXISTS_IN_GAME cdia533.itm BEGIN ADD_STORE_ITEM ~cdia533~ AFTER ~scrl6v~ #1 #0 #0 ~IDENTIFIED~ #2 END // Conjure water elemental
  PATCH_IF FILE_EXISTS_IN_GAME cdia610.itm BEGIN ADD_STORE_ITEM ~cdia610~ AFTER ~scrl7p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Antimagic Shell
  PATCH_IF FILE_EXISTS_IN_GAME cdia626.itm BEGIN ADD_STORE_ITEM ~cdia626~ AFTER ~scrl7p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Lich Touch
  PATCH_IF FILE_EXISTS_IN_GAME cdia627.itm BEGIN ADD_STORE_ITEM ~cdia627~ AFTER ~scrl7p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Monster Summoning IV
  PATCH_IF FILE_EXISTS_IN_GAME cdia628.itm BEGIN ADD_STORE_ITEM ~cdia628~ AFTER ~scrl7p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Otiluke's Freezing Sphere
  PATCH_IF FILE_EXISTS_IN_GAME cdia629.itm BEGIN ADD_STORE_ITEM ~cdia629~ AFTER ~scrl7p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Shades
  PATCH_IF FILE_EXISTS_IN_GAME cdia630.itm BEGIN ADD_STORE_ITEM ~cdia630~ AFTER ~scrl7p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Darts of Bone
  PATCH_IF FILE_EXISTS_IN_GAME cdia631.itm BEGIN ADD_STORE_ITEM ~cdia631~ AFTER ~scrl7p~ #1 #0 #0 ~IDENTIFIED~ #2 END // soul eater
  PATCH_IF FILE_EXISTS_IN_GAME cdia632.itm BEGIN ADD_STORE_ITEM ~cdia632~ AFTER ~scrl7p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Trollish Fortitude
  PATCH_IF FILE_EXISTS_IN_GAME cdia706.itm BEGIN ADD_STORE_ITEM ~cdia706~ AFTER ~scrl8p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Monster Summoning V
  PATCH_IF FILE_EXISTS_IN_GAME cdia709.itm BEGIN ADD_STORE_ITEM ~cdia709~ AFTER ~scrl8p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Malavon's Rage
  PATCH_IF FILE_EXISTS_IN_GAME cdia724.itm BEGIN ADD_STORE_ITEM ~cdia724~ AFTER ~scrl8p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Acid Storm
  PATCH_IF FILE_EXISTS_IN_GAME cdia725.itm BEGIN ADD_STORE_ITEM ~cdia725~ AFTER ~scrl8p~ #1 #0 #0 ~IDENTIFIED~ #2 END // seven eyes
  PATCH_IF FILE_EXISTS_IN_GAME cdia726.itm BEGIN ADD_STORE_ITEM ~cdia726~ AFTER ~scrl8p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Suffocate
  PATCH_IF FILE_EXISTS_IN_GAME cdia801.itm BEGIN ADD_STORE_ITEM ~cdia801~ AFTER ~scrl8p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Monster Summoning VI
  PATCH_IF FILE_EXISTS_IN_GAME cdia802.itm BEGIN ADD_STORE_ITEM ~cdia802~ AFTER ~scrl8p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Mind Blank
  PATCH_IF FILE_EXISTS_IN_GAME cdia806.itm BEGIN ADD_STORE_ITEM ~cdia806~ AFTER ~scrl8p~ #1 #0 #0 ~IDENTIFIED~ #2 END // great shout
  PATCH_IF FILE_EXISTS_IN_GAME cdia814.itm BEGIN ADD_STORE_ITEM ~cdia814~ AFTER ~scrl8p~ #1 #0 #0 ~IDENTIFIED~ #2 END // Iron Body
  PATCH_IF FILE_EXISTS_IN_GAME cdia901.itm BEGIN ADD_STORE_ITEM ~cdia901~ AFTER ~scrlb4~ #1 #0 #0 ~IDENTIFIED~ #2 END // Monster Summoning VII
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~scrolls.sto~  ~override~ // yuth
  PATCH_IF FILE_EXISTS_IN_GAME cdia126.itm BEGIN ADD_STORE_ITEM ~cdia126~ AFTER ~scrl84~ #1 #0 #0 ~IDENTIFIED~ #2 END // expeditious retreat
  PATCH_IF FILE_EXISTS_IN_GAME cdia204.itm BEGIN ADD_STORE_ITEM ~cdia204~ AFTER ~scrl93~ #1 #0 #0 ~IDENTIFIED~ #2 END // Snilloc's Snowball Swarm
  PATCH_IF FILE_EXISTS_IN_GAME cdia216.itm BEGIN ADD_STORE_ITEM ~cdia216~ AFTER ~scrl93~ #1 #0 #0 ~IDENTIFIED~ #2 END // decastave
  PATCH_IF FILE_EXISTS_IN_GAME cdia225.itm BEGIN ADD_STORE_ITEM ~cdia225~ AFTER ~scrl93~ #1 #0 #0 ~IDENTIFIED~ #2 END // cats grace
  PATCH_IF FILE_EXISTS_IN_GAME cdia327.itm BEGIN ADD_STORE_ITEM ~cdia327~ AFTER ~scrl1e~ #1 #0 #0 ~IDENTIFIED~ #2 END // Icelance
  PATCH_IF FILE_EXISTS_IN_GAME cdia328.itm BEGIN ADD_STORE_ITEM ~cdia328~ AFTER ~scrl1e~ #1 #0 #0 ~IDENTIFIED~ #2 END // Lance of Disruption
  PATCH_IF FILE_EXISTS_IN_GAME cdia422.itm BEGIN ADD_STORE_ITEM ~cdia422~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #3 END // Beltyn's Burning Blood
  PATCH_IF FILE_EXISTS_IN_GAME cdia426.itm BEGIN ADD_STORE_ITEM ~cdia426~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Shadow Monsters
  PATCH_IF FILE_EXISTS_IN_GAME cdia427.itm BEGIN ADD_STORE_ITEM ~cdia427~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Courage
  PATCH_IF FILE_EXISTS_IN_GAME cdia428.itm BEGIN ADD_STORE_ITEM ~cdia428~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Fear
  PATCH_IF FILE_EXISTS_IN_GAME cdia429.itm BEGIN ADD_STORE_ITEM ~cdia429~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Hope
  PATCH_IF FILE_EXISTS_IN_GAME cdia430.itm BEGIN ADD_STORE_ITEM ~cdia430~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // mordenkainen's force missiles
  PATCH_IF FILE_EXISTS_IN_GAME cdia432.itm BEGIN ADD_STORE_ITEM ~cdia432~ AFTER ~scrl1y~ #1 #0 #0 ~IDENTIFIED~ #2 END // Vitriolic Sphere
  PATCH_IF FILE_EXISTS_IN_GAME cdia524.itm BEGIN ADD_STORE_ITEM ~cdia524~ AFTER ~scrl6v~ #1 #0 #0 ~IDENTIFIED~ #4 END // Shroud of Flame
  PATCH_IF FILE_EXISTS_IN_GAME cdia525.itm BEGIN ADD_STORE_ITEM ~cdia525~ AFTER ~scrl6v~ #1 #0 #0 ~IDENTIFIED~ #3 END // Demi-Shadow Monsters
  PATCH_IF FILE_EXISTS_IN_GAME cdia526.itm BEGIN ADD_STORE_ITEM ~cdia526~ AFTER ~scrl6v~ #1 #0 #0 ~IDENTIFIED~ #2 END // Summon Shadow
  PATCH_IF FILE_EXISTS_IN_GAME cdia528.itm BEGIN ADD_STORE_ITEM ~cdia528~ AFTER ~scrl6v~ #1 #0 #0 ~IDENTIFIED~ #2 END // Contact Other Plane
  PATCH_IF FILE_EXISTS_IN_GAME cdia533.itm BEGIN ADD_STORE_ITEM ~cdia533~ AFTER ~scrl6v~ #1 #0 #0 ~IDENTIFIED~ #2 END // Conjure water elemental
  PATCH_IF FILE_EXISTS_IN_GAME cdia610.itm BEGIN ADD_STORE_ITEM ~cdia610~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #2 END // Antimagic Shell
  PATCH_IF FILE_EXISTS_IN_GAME cdia626.itm BEGIN ADD_STORE_ITEM ~cdia626~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #2 END // Lich Touch
  PATCH_IF FILE_EXISTS_IN_GAME cdia627.itm BEGIN ADD_STORE_ITEM ~cdia627~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #2 END // Monster Summoning IV
  PATCH_IF FILE_EXISTS_IN_GAME cdia628.itm BEGIN ADD_STORE_ITEM ~cdia628~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #3 END // Otiluke's Freezing Sphere
  PATCH_IF FILE_EXISTS_IN_GAME cdia629.itm BEGIN ADD_STORE_ITEM ~cdia629~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #2 END // Shades
  PATCH_IF FILE_EXISTS_IN_GAME cdia630.itm BEGIN ADD_STORE_ITEM ~cdia630~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #3 END // Darts of Bone
  PATCH_IF FILE_EXISTS_IN_GAME cdia631.itm BEGIN ADD_STORE_ITEM ~cdia631~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #2 END // soul eater
  PATCH_IF FILE_EXISTS_IN_GAME cdia632.itm BEGIN ADD_STORE_ITEM ~cdia632~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #2 END // Trollish Fortitude
  PATCH_IF FILE_EXISTS_IN_GAME cdia706.itm BEGIN ADD_STORE_ITEM ~cdia706~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #2 END // Monster Summoning V
  PATCH_IF FILE_EXISTS_IN_GAME cdia709.itm BEGIN ADD_STORE_ITEM ~cdia709~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #2 END // Malavon's Rage
  PATCH_IF FILE_EXISTS_IN_GAME cdia724.itm BEGIN ADD_STORE_ITEM ~cdia724~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #3 END // Acid Storm
  PATCH_IF FILE_EXISTS_IN_GAME cdia725.itm BEGIN ADD_STORE_ITEM ~cdia725~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #2 END // seven eyes
  PATCH_IF FILE_EXISTS_IN_GAME cdia726.itm BEGIN ADD_STORE_ITEM ~cdia726~ AFTER ~scrlal~ #1 #0 #0 ~IDENTIFIED~ #2 END // Suffocate
  PATCH_IF FILE_EXISTS_IN_GAME cdia901.itm BEGIN ADD_STORE_ITEM ~cdia901~ AFTER ~scrlan~ #1 #0 #0 ~IDENTIFIED~ #2 END // Monster Summoning VII
  PATCH_IF FILE_EXISTS_IN_GAME cdia801.itm BEGIN ADD_STORE_ITEM ~cdia801~ AFTER ~scrlan~ #1 #0 #0 ~IDENTIFIED~ #2 END // Monster Summoning VI
  PATCH_IF FILE_EXISTS_IN_GAME cdia802.itm BEGIN ADD_STORE_ITEM ~cdia802~ AFTER ~scrlan~ #1 #0 #0 ~IDENTIFIED~ #3 END // Mind Blank
  PATCH_IF FILE_EXISTS_IN_GAME cdia806.itm BEGIN ADD_STORE_ITEM ~cdia806~ AFTER ~scrlan~ #1 #0 #0 ~IDENTIFIED~ #2 END // great shout
  PATCH_IF FILE_EXISTS_IN_GAME cdia814.itm BEGIN ADD_STORE_ITEM ~cdia814~ AFTER ~scrlan~ #1 #0 #0 ~IDENTIFIED~ #2 END // Iron Body
  BUT_ONLY IF_EXISTS

COPY_EXISTING ~shop08.sto~  ~override~ // galoomp
  PATCH_IF FILE_EXISTS_IN_GAME cdia126.itm BEGIN ADD_STORE_ITEM ~cdia126~ AFTER ~scrl84~ #1 #0 #0 ~IDENTIFIED~ #2 END // expeditious retreat
  PATCH_IF FILE_EXISTS_IN_GAME cdia204.itm BEGIN ADD_STORE_ITEM ~cdia204~ AFTER ~scrl93~ #1 #0 #0 ~IDENTIFIED~ #4 END // Snilloc's Snowball Swarm
  PATCH_IF FILE_EXISTS_IN_GAME cdia216.itm BEGIN ADD_STORE_ITEM ~cdia216~ AFTER ~scrl93~ #1 #0 #0 ~IDENTIFIED~ #2 END // decastave
  PATCH_IF FILE_EXISTS_IN_GAME cdia225.itm BEGIN ADD_STORE_ITEM ~cdia225~ AFTER ~scrl93~ #1 #0 #0 ~IDENTIFIED~ #2 END // cats grace
  PATCH_IF FILE_EXISTS_IN_GAME cdia327.itm BEGIN ADD_STORE_ITEM ~cdia327~ AFTER ~scrl1e~ #1 #0 #0 ~IDENTIFIED~ #2 END // Icelance
  PATCH_IF FILE_EXISTS_IN_GAME cdia328.itm BEGIN ADD_STORE_ITEM ~cdia328~ AFTER ~scrl1e~ #1 #0 #0 ~IDENTIFIED~ #2 END // Lance of Disruption
  PATCH_IF FILE_EXISTS_IN_GAME cdia422.itm BEGIN ADD_STORE_ITEM ~cdia422~ AFTER ~scrl5m~ #1 #0 #0 ~IDENTIFIED~ #2 END // Beltyn's Burning Blood
  PATCH_IF FILE_EXISTS_IN_GAME cdia426.itm BEGIN ADD_STORE_ITEM ~cdia426~ AFTER ~scrl5m~ #1 #0 #0 ~IDENTIFIED~ #3 END // Shadow Monsters
  PATCH_IF FILE_EXISTS_IN_GAME cdia427.itm BEGIN ADD_STORE_ITEM ~cdia427~ AFTER ~scrl5m~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Courage
  PATCH_IF FILE_EXISTS_IN_GAME cdia428.itm BEGIN ADD_STORE_ITEM ~cdia428~ AFTER ~scrl5m~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Fear
  PATCH_IF FILE_EXISTS_IN_GAME cdia429.itm BEGIN ADD_STORE_ITEM ~cdia429~ AFTER ~scrl5m~ #1 #0 #0 ~IDENTIFIED~ #2 END // Emotion: Hope
  PATCH_IF FILE_EXISTS_IN_GAME cdia430.itm BEGIN ADD_STORE_ITEM ~cdia430~ AFTER ~scrl5m~ #1 #0 #0 ~IDENTIFIED~ #2 END // mordenkainen's force missiles
  PATCH_IF FILE_EXISTS_IN_GAME cdia432.itm BEGIN ADD_STORE_ITEM ~cdia432~ AFTER ~scrl5m~ #1 #0 #0 ~IDENTIFIED~ #3 END // Vitriolic Sphere
  PATCH_IF FILE_EXISTS_IN_GAME cdia524.itm BEGIN ADD_STORE_ITEM ~cdia524~ AFTER ~scrl2f~ #1 #0 #0 ~IDENTIFIED~ #3 END // Shroud of Flame
  PATCH_IF FILE_EXISTS_IN_GAME cdia525.itm BEGIN ADD_STORE_ITEM ~cdia525~ AFTER ~scrl2f~ #1 #0 #0 ~IDENTIFIED~ #2 END // Demi-Shadow Monsters
  PATCH_IF FILE_EXISTS_IN_GAME cdia526.itm BEGIN ADD_STORE_ITEM ~cdia526~ AFTER ~scrl2f~ #1 #0 #0 ~IDENTIFIED~ #2 END // Summon Shadow
  PATCH_IF FILE_EXISTS_IN_GAME cdia528.itm BEGIN ADD_STORE_ITEM ~cdia528~ AFTER ~scrl2f~ #1 #0 #0 ~IDENTIFIED~ #1 END // Contact Other Plane
  PATCH_IF FILE_EXISTS_IN_GAME cdia533.itm BEGIN ADD_STORE_ITEM ~cdia533~ AFTER ~scrl2f~ #1 #0 #0 ~IDENTIFIED~ #2 END // Conjure water elemental
  BUT_ONLY IF_EXISTS

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD divine spell pack                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @4000 DESIGNATED 40

INCLUDE ~iwdification/lib/spell_prep.tpa~

/////                                                  \\\\\
///// CLERIC_CURSE                                     \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CURSE~) OR override_divine) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi237.pro~

  ADD_SPELL ~iwdification/spl/cdid112.spl~ 1 1 CLERIC_CURSE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4010 // name
    SAY 0x50 @4011 // descript
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14406 parameter1 = string_cursed END
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi237 END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdid112 resource = EVAL ~%DEST_RES%~ END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 54 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = EVAL ~%DEST_RES%~ END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid112.itm~
    SAY 0x0c @4010
    SAY 0x54 @4011
    LPF cd_scroll INT_VAR target_hdr = 4 range = 40 opcode = 148 target_eff = 1 price = 100 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdid112a.bam~ ~override~
       ~iwdification/bam/cdid112b.bam~ ~override~
       ~iwdification/bam/cdid112c.bam~ ~override~
       ~iwdification/bam/cdiare1x.bam~ ~override~
       ~iwdification/bam/cdicurse.bam~ ~override~
       ~iwdification/vvc/cdibless.vvc~ ~override~
       ~iwdification/vvc/cdicurse.vvc~ ~override~
       ~iwdification/wav/cdiee04.wav~  ~override~
       ~iwdification/wav/cdiep32.wav~  ~override~
       ~iwdification/wav/cdiep99.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_CAUSE_LIGHT_WOUNDS                        \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CAUSE_LIGHT_WOUNDS~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid114.spl~ 1 1 CLERIC_CAUSE_LIGHT_WOUNDS
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4020 // name
    SAY 0x50 @4021 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdid114 resource = EVAL ~%DEST_RES%~ END
    PATCH_IF !ee_game BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixd114 END // race = golem
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixd114 END // general = undead
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 104 opcode = 177                  parameter2 = 4 STR_VAR resource = cdixd114 END // race = parameter1
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid114.itm~
    SAY 0x0c @4020
    SAY 0x54 @4021
    LPF cd_scroll INT_VAR unusable0 = (BIT2 + BIT12 + BIT21 + BIT30) range = 1 price = 100 STR_VAR spell = EVAL ~%current_spell_res%~ END // non-good, cleric

  COPY ~iwdification/bam/cdid114a.bam~ ~override~
       ~iwdification/bam/cdid114b.bam~ ~override~
       ~iwdification/bam/cdid114c.bam~ ~override~
       ~iwdification/bam/cdicldam.bam~ ~override~
       ~iwdification/vvc/cdicldam.vvc~ ~override~
       ~iwdification/wav/cdiem103.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd114.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_SUNSCORCH                                 \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_SUNSCORCH~) OR override_divine) BEGIN

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdisunsc.pro~
  
  END

  ADD_SPELL ~iwdification/spl/cdid115.spl~ 1 1 CLERIC_SUNSCORCH
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4030 // name
    SAY 0x50 @4031 // descript
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37800 parameter1 = string_blinded END
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdisunsc END
    END ELSE BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 326 opcode = 177 parameter1 =   4 parameter2 = 3 END // general = undead
      LPF ALTER_EFFECT INT_VAR match_opcode = 326 opcode = 177 parameter1 = 164 parameter2 = 4 END // race = myconid
      READ_SHORT 0x68 abil_num
      FOR (index = 0 ; index < abil_num ; ++index) BEGIN
        LPF ALTER_EFFECT INT_VAR header = index match_opcode = 12 parameter1 = ((index + 1) / 2) dicesize = 3 special = 0 END
        LPF CLONE_EFFECT INT_VAR header = index match_opcode = 12 parameter1 = ((index + 0) / 2) savingthrow = 0 END
      END
      PATCH_FOR_EACH res IN cdid1151 cdid1152 cdid1153 cdid1154 BEGIN
        LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 2 power = 1 timing = 1 resist_dispel = 3 STR_VAR resource = EVAL ~%res%~ END
      END  
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid115.itm~
    SAY 0x0c @4030
    SAY 0x54 @4031
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) range = 100 price = 100 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

    COPY ~iwdification/bam/cdid115a.bam~ ~override~
         ~iwdification/bam/cdid115b.bam~ ~override~
         ~iwdification/bam/cdid115c.bam~ ~override~
         ~iwdification/bam/cdisunsc.bam~ ~override~
         ~iwdification/spl/cdid115d.spl~ ~override~
         ~iwdification/wav/cdiep39.wav~  ~override~

  ACTION_IF ee_game BEGIN

    COPY ~iwdification/vvc/cdisunsc.vvc~ ~override~

  END ELSE BEGIN

    COPY ~iwdification/vvc/cdid1151.vvc~ ~override~
         ~iwdification/vvc/cdid1152.vvc~ ~override~
         ~iwdification/vvc/cdid1153.vvc~ ~override~
         ~iwdification/vvc/cdid1154.vvc~ ~override~
    
    COPY ~iwdification/eff/326.eff~ ~override/cdid115d.eff~
      WRITE_ASCII 0x30 ~cdid115d~

  END

END

/////                                                  \\\\\
///// CLERIC_CURE_MODERATE_WOUNDS                      \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CURE_MODERATE_WOUNDS~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid217.spl~ 1 2 CLERIC_CURE_MODERATE_WOUNDS
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4040 // name
    SAY 0x50 @4041 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdid217 resource = EVAL ~%DEST_RES%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14022 parameter1 = string_healed END
    PATCH_IF !ee_game BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 61 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixd114 END // race = golem
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixd114 END // general = undead
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 104 opcode = 177                  parameter2 = 4 STR_VAR resource = cdixd114 END // race = parameter1
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid217.itm~
    SAY 0x0c @4040
    SAY 0x54 @4041
    LPF cd_scroll INT_VAR range = 1 price = 200 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdid217a.bam~ ~override~
       ~iwdification/bam/cdid217b.bam~ ~override~
       ~iwdification/bam/cdid217c.bam~ ~override~
       ~iwdification/bam/cdicmwou.bam~ ~override~
       ~iwdification/vvc/cdicmwou.vvc~ ~override~
       ~iwdification/wav/cdiep26.wav~  ~override~
    
  // add as option to temples that offer cure light wounds
  OUTER_SET desc = RESOLVE_STR_REF(@4042)
  APPEND ~speldesc.2da~ ~sppr217 %desc%~
  COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
    READ_LONG 0x70 cure_off
    READ_LONG 0x74 cure_num
    SET insert_cost = 0
    SET insert_off = 0
    FOR (index = 0 ; index < cure_num ; ++index) BEGIN
      READ_ASCII (cure_off +        (index * 0x0c)) spell
      PATCH_IF ("%spell%" STRING_COMPARE_REGEXP "sppr1[0-9][0-9]" = 0) BEGIN
        SET insert_off =  (cure_off +        ((index + 1) * 0x0c))
        PATCH_IF ("%spell%" STRING_COMPARE_CASE "sppr103" = 0) BEGIN
          READ_LONG (cure_off + 0x08 + (index * 0x0c)) insert_cost
        END
      END
    END
    PATCH_IF insert_off AND insert_cost BEGIN
      INSERT_BYTES   (insert_off       ) 0x0c
        WRITE_ASCIIE (insert_off       ) ~%current_spell_res%~ #8
        WRITE_LONG   (insert_off + 0x08) (insert_cost * 2)
      WRITE_LONG 0x74 (THIS + 1)
      PATCH_FOR_EACH off IN 0x2c 0x34 0x4c BEGIN // item, sale, drink offsets
        READ_LONG off val                        // read offset
        PATCH_IF val > cure_off BEGIN            // if after cure list...
          WRITE_LONG off (THIS + 0x0c)           // push back for new cure
        END
      END
    END
    BUT_ONLY

END

/////                                                  \\\\\
///// CLERIC_ALICORN_LANCE                             \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_ALICORN_LANCE~) OR override_divine) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi298.pro~

  ADD_SPELL ~iwdification/spl/cdid218.spl~ 1 2 CLERIC_ALICORN_LANCE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4050 // name
    SAY 0x50 @4051 // descript
    SAY 0x2ae @4052
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi298 END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdid218 resource = EVAL ~%DEST_RES%~ END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF CD_SPLIT_SAVE_DAMAGE END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid218.itm~
    SAY 0x0c @4050
    SAY 0x54 @4051
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) range = 100 price = 200 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/bam/cdid218a.bam~ ~override~
       ~iwdification/bam/cdid218b.bam~ ~override~
       ~iwdification/bam/cdid218c.bam~ ~override~
       ~iwdification/bam/cdilanct.bam~ ~override~
       ~iwdification/wav/cdiee08.wav~  ~override~
       ~iwdification/wav/cditra57.wav~ ~override~

END

/////                                                  \\\\\
///// CLERIC_BEAST_CLAW                                \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_BEAST_CLAW~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid219.spl~ 1 2 CLERIC_BEAST_CLAW
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4060 // name
    SAY 0x50 @4061 // descript

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid219.itm~
    SAY 0x0c @4060
    SAY 0x54 @4061
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 5 range = 1 price = 200 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/itm/cdibclaw.itm~ ~override~
    SAY 0x08 @4060
    SAY 0x0c @4060

  COPY ~iwdification/bam/cdid219a.bam~ ~override~
       ~iwdification/bam/cdid219b.bam~ ~override~
       ~iwdification/bam/cdid219c.bam~ ~override~
       ~iwdification/bam/cdialter.bam~ ~override~
       ~iwdification/bam/cdibclaw.bam~ ~override~
       ~iwdification/vvc/cdialter.vvc~ ~override~
       ~iwdification/wav/cdiep07.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_CAUSE_MODERATE_WOUNDS                     \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CAUSE_MODERATE_WOUNDS~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid220.spl~ 1 2 CLERIC_CAUSE_MODERATE_WOUNDS
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4070 // name
    SAY 0x50 @4071 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdid220 resource = EVAL ~%DEST_RES%~ END
    PATCH_IF !ee_game BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixd220 END // race = golem
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixd220 END // general = undead
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 104 opcode = 177                  parameter2 = 4 STR_VAR resource = cdixd220 END // race = parameter1
      LPF CD_SPLIT_SAVE_DAMAGE END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid220.itm~
    SAY 0x0c @4070
    SAY 0x54 @4071
    LPF cd_scroll INT_VAR unusable0 = (BIT2 + BIT12 + BIT21 + BIT30) range = 1 price = 200 STR_VAR spell = EVAL ~%current_spell_res%~ END // non-good cleric

  COPY ~iwdification/bam/cdid220a.bam~ ~override~
       ~iwdification/bam/cdid220b.bam~ ~override~
       ~iwdification/bam/cdid220c.bam~ ~override~
       ~iwdification/bam/cdicmdam.bam~ ~override~
       ~iwdification/vvc/cdicmdam.vvc~ ~override~
       ~iwdification/wav/cdiem103.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd220.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_PRAYER                                    \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_PRAYER~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4080) STR_VAR bam_file = cdid316d RET icon END

  ADD_PROJECTILE ~iwdification/pro/cdivrnp.pro~
  ADD_PROJECTILE ~iwdification/pro/cdivrpo.pro~

  ADD_SPELL ~iwdification/spl/cdid316.spl~ 1 3 CLERIC_PRAYER
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4080 // name
    SAY 0x50 @4081 // descript

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid316.itm~
    SAY 0x0c @4080
    SAY 0x54 @4081
    LPF cd_scroll INT_VAR unusable0 = (BIT12 + BIT21 + BIT30) target_hdr = 5 range = 40 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END // cleric

  COPY ~iwdification/spl/cdid316b.spl~ ~override~
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdivrnp END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40257 parameter1 = RESOLVE_STR_REF(@4082) END
    PATCH_IF !ee_game BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 54 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = cdid316b END
      LPF CD_CONVERT_9_255 END
    END

  COPY ~iwdification/spl/cdid316g.spl~ ~override~
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdivrpo END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40256 parameter1 = RESOLVE_STR_REF(@4083) END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 183 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 183 parameter2 = 18 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 54 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = cdid316g END
      LPF CD_CONVERT_9_255 END
    END

  COPY ~iwdification/bam/cdid316a.bam~ ~override~
       ~iwdification/bam/cdid316b.bam~ ~override~
       ~iwdification/bam/cdid316c.bam~ ~override~
       ~iwdification/bam/cdid316d.bam~ ~override~
       ~iwdification/bam/cdiprayh.bam~ ~override~
       ~iwdification/vvc/cdiprayg.vvc~ ~override~
       ~iwdification/vvc/cdiprayh.vvc~ ~override~
       ~iwdification/wav/cdiep31.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_CAUSE_DISEASE                             \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CAUSE_DISEASE~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid320.spl~ 1 3 CLERIC_CAUSE_DISEASE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4090 // name
    SAY 0x50 @4091 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdid320 resource = EVAL ~%DEST_RES%~ END
    PATCH_IF !ee_game BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixd320 END // race = golem
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixd320 END // general = undead
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 104 opcode = 177                  parameter2 = 4 STR_VAR resource = cdixd320 END // race = parameter1
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid320.itm~
    SAY 0x0c @4090
    SAY 0x54 @4091
    LPF cd_scroll INT_VAR unusable0 = (BIT2 + BIT12 + BIT21 + BIT30) range = 1 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END // non-good cleric

  COPY ~iwdification/bam/cdid320a.bam~ ~override~
       ~iwdification/bam/cdid320b.bam~ ~override~
       ~iwdification/bam/cdid320c.bam~ ~override~
       ~iwdification/bam/cdidise.bam~  ~override~
       ~iwdification/vvc/cdidise.vvc~  ~override~
       ~iwdification/wav/cdiep108.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd320.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_EXALTATION                                \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_EXALTATION~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4100) STR_VAR bam_file = cdid321d RET icon END

  ADD_SPELL ~iwdification/spl/cdid321.spl~ 1 3 CLERIC_EXALTATION
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4100 // name
    SAY 0x50 @4101 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdid321 resource = EVAL ~%DEST_RES%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 20568 parameter1 = string_mf_panic END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35484 parameter1 = string_panic END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37340 parameter1 = string_uncon END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37603 parameter1 = string_rthinking END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37604 parameter1 = string_confused END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37605 parameter1 = string_berserk END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37606 parameter1 = string_intoxicated END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37613 parameter1 = string_sleep END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 174 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 174 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 opcode = 206 target = 1 parameter1 = 0 parameter2 = 0 END // prevent self-cast
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid321.itm~
    SAY 0x0c @4100
    SAY 0x54 @4101
    LPF cd_scroll INT_VAR unusable0 = (BIT12 + BIT21 + BIT30) range = 1 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END // cleric

  COPY ~iwdification/bam/cdid321a.bam~ ~override~
       ~iwdification/bam/cdid321b.bam~ ~override~
       ~iwdification/bam/cdid321c.bam~ ~override~
       ~iwdification/bam/cdid321d.bam~ ~override~
       ~iwdification/bam/cdiexalt.bam~ ~override~
       ~iwdification/vvc/cdiexalt.vvc~ ~override~
       ~iwdification/wav/cdiep106.wav~ ~override~

END

/////                                                  \\\\\
///// CLERIC_MOONBLADE                                 \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_MOONBLADE~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid322.spl~ 1 3 CLERIC_MOONBLADE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4110 // name
    SAY 0x50 @4111 // descript

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid322.itm~
    SAY 0x0c @4110
    SAY 0x54 @4111
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 5 range = 1 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/itm/cdimoonb.itm~ ~override~
    SAY 0x08 @4110
    SAY 0x0c @4110
    LPF ALTER_EFFECT STR_VAR match_resource = moonbla resource = cdimoonb END
    PATCH_IF !ee_game BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 12 match_parameter1 = 0 END // delete undead damage, since it's going through an EFF
      LPF ALTER_EFFECT INT_VAR match_opcode = 318 opcode = 177 parameter1 = 4 parameter2 = 3 timing = 1 duration = 0 END
    END

  COPY ~iwdification/bam/cdid322a.bam~ ~override~
       ~iwdification/bam/cdid322b.bam~ ~override~
       ~iwdification/bam/cdid322c.bam~ ~override~
       ~iwdification/bam/cdiinvoc.bam~ ~override~
       ~iwdification/bam/cdimoonb.bam~ ~override~
       ~iwdification/vvc/cdiinvoc.vvc~ ~override~
       ~iwdification/wav/cdiem06.wav~  ~override~  
  
  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/cdimoonb.eff~ ~override~

  END

END

/////                                                  \\\\\
///// CLERIC_CIRCLE_OF_BONES                           \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CIRCLE_OF_BONES~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid323.spl~ 1 3 CLERIC_CIRCLE_OF_BONES
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4120 // name
    SAY 0x50 @4121 // descript
    LPF ALTER_EFFECT INT_VAR match_opcode = 126 opcode = 176 END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid323.itm~
    SAY 0x0c @4120
    SAY 0x54 @4121
    LPF cd_scroll INT_VAR unusable0 = (BIT2 + BIT12 + BIT21 + BIT30) target_hdr = 5 range = 1 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END // non-good cleric

  COPY ~iwdification/bam/cdid323a.bam~ ~override~
       ~iwdification/bam/cdid323b.bam~ ~override~
       ~iwdification/bam/cdid323c.bam~ ~override~
       ~iwdification/bam/cdibone1.bam~ ~override~
       ~iwdification/bam/cdibone2.bam~ ~override~
       ~iwdification/spl/cdid323d.spl~ ~override~
       ~iwdification/vvc/cdibone1.vvc~ ~override~
       ~iwdification/vvc/cdibone2.vvc~ ~override~
       ~iwdification/wav/cdiafp22.wav~ ~override~
       ~iwdification/wav/cdiarp21.wav~ ~override~
       ~iwdification/wav/cdiep101.wav~ ~override~

END

/////                                                  \\\\\
///// CLERIC_SPIKE_GROWTH                              \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_SPIKE_GROWTH~) OR override_divine) BEGIN

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi300.pro~
  
  END ELSE BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi300a.pro~

  END

  ADD_SPELL ~iwdification/spl/cdid324.spl~ 1 3 CLERIC_SPIKE_GROWTH
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4130 // name
    SAY 0x50 @4131 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi300 END
    END ELSE BEGIN
      LPF DELETE_EFFECT END
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi300a END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 parameter2 = 2 timing = 1 parameter2 = 2 STR_VAR resource = cdid324 END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 1 parameter2 = 2 timing = 1 STR_VAR resource = cdisgrwa END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid324.itm~
    SAY 0x0c @4130
    SAY 0x54 @4131
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/bam/cdid324a.bam~ ~override~
       ~iwdification/bam/cdid324b.bam~ ~override~
       ~iwdification/bam/cdid324c.bam~ ~override~
       ~iwdification/bam/cdisgrwa.bam~ ~override~
       ~iwdification/bam/cdisgrwx.bam~ ~override~
       ~iwdification/vvc/cdisgrwa.vvc~ ~override~
       ~iwdification/vvc/cdisgrwx.vvc~ ~override~
       ~iwdification/wav/cdiafp24.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    LAUNCH_ACTION_FUNCTION cd_create_cloud INT_VAR visloop = 5 cloud_dur = 60 STR_VAR code = cdid324 anim = cdisgrwa END

    COPY ~iwdification/spl/cdid324.spl~ ~override~
      WRITE_LONG 0x08 "-1" // name
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi300a END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 2 duration = 3 STR_VAR resource = cdisgrwx END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 duration = 6 STR_VAR resource = cdid324  END

  END

END

/////                                                  \\\\\
///// CLERIC_CLOUDBURST                                \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CLOUDBURST~) OR override_divine) BEGIN

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi301.pro~
  
  END ELSE BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi301a.pro~

  END

  ADD_SPELL ~iwdification/spl/cdid325.spl~ 1 3 CLERIC_CLOUDBURST
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4140 // name
    SAY 0x50 @4141 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi301 END
      PATCH_FOR_EACH res IN firau1d2 firau3d6 frostd10 spin187 spin128 BEGIN // leftover iwdee references
        LPF DELETE_EFFECT STR_VAR match_resource = EVAL ~%res%~ END
      END
      LPF ALTER_EFFECT STR_VAR match_resource = cdid325 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT STR_VAR match_resource = fsalring resource = cdisalfi END // iwdifcation moves aura visuals to weapons
      LPF ALTER_EFFECT STR_VAR match_resource = csalring resource = cdisalfr END // iwdifcation moves aura visuals to weapons
    END ELSE BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi301a END
      LPF DELETE_EFFECT END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 timing = 1 parameter2 = 2 STR_VAR resource = cdid325 END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid325.itm~
    SAY 0x0c @4140
    SAY 0x54 @4141
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/bam/cdid325a.bam~ ~override~
       ~iwdification/bam/cdid325b.bam~ ~override~
       ~iwdification/bam/cdid325c.bam~ ~override~
       ~iwdification/wav/cdiarp24.wav~ ~override~

  ACTION_IF ee_game BEGIN

    COPY ~iwdification/bam/cdibacld.bam~ ~override~
         ~iwdification/bam/cdibhcld.bam~ ~override~
         ~iwdification/vvc/cdibhcld.vvc~ ~override~

  END ELSE BEGIN

    LAUNCH_ACTION_FUNCTION cd_create_cloud INT_VAR visloop = 1 cloud_dur = 12 zosa = 1 STR_VAR code = cdid325 anim = cdibacld END

    COPY_EXISTING ~iwdification/spl/cdid325.spl~ ~override~
      WRITE_LONG 0x08 "-1" // name
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi301a END
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 318 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 206 END
      LPF CD_SPLIT_SAVE_DAMAGE END
      PATCH_FOR_EACH res IN cdifire6 cdifros6 cdia524 cdia524b BEGIN // leftover iwdee references
        LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 duration = 6 STR_VAR resource = EVAL ~%res%~ END
      END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 power = 3 parameter1 = 187 parameter2 = 5 timing = 1 resist_dispel = 1 STR_VAR resource = cdid325c END // extra dam for class: fire elemental
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 power = 3 parameter1 = 146 parameter2 = 5 timing = 1 resist_dispel = 1 STR_VAR resource = cdid325c END // extra dam for class: winter wolf
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 duration = 6 STR_VAR resource = cdid325 END

    COPY ~iwdification/bam/cdid325v.bam~ ~override/cdibacld.bam~
         ~iwdification/eff/cdid325c.eff~ ~override~

  END

END

/////                                                  \\\\\
///// CLERIC_MOLD_TOUCH                                \\\\\
/////                                                  \\\\\

ACTION_IF (ee_game AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_MOLD_TOUCH~) OR override_divine)) BEGIN

  ADD_SPELL ~iwdification/spl/cdid326.spl~ 1 3 CLERIC_MOLD_TOUCH
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4150 // name
    SAY 0x50 @4151 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdid326 resource = EVAL ~%DEST_RES%~ END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid326.itm~
    SAY 0x0c @4150
    SAY 0x54 @4151
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) range = 1 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/bam/cdid326a.bam~ ~override~
       ~iwdification/bam/cdid326b.bam~ ~override~
       ~iwdification/bam/cdid326c.bam~ ~override~
       ~iwdification/bam/cdimoldt.bam~ ~override~
       ~iwdification/vvc/cdimoldt.vvc~ ~override~
       ~iwdification/wav/cdiep107.wav~ ~override~

END

/////                                                  \\\\\
///// CLERIC_STORM_SHELL                               \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_STORM_SHELL~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4160) STR_VAR bam_file = cdid327d RET icon END

  ADD_SPELL ~iwdification/spl/cdid327.spl~ 1 3 CLERIC_STORM_SHELL
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4160 // name
    SAY 0x50 @4161 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 140 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 140 END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid327.itm~
    SAY 0x0c @4160
    SAY 0x54 @4161
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 5 range = 1 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/bam/cdid327a.bam~ ~override~
       ~iwdification/bam/cdid327b.bam~ ~override~
       ~iwdification/bam/cdid327c.bam~ ~override~
       ~iwdification/bam/cdid327d.bam~ ~override~
       ~iwdification/bam/cdisshel.bam~ ~override~
       ~iwdification/vvc/cdistorm.vvc~ ~override~
       ~iwdification/wav/cdiafp25.wav~ ~override~

END

/////                                                  \\\\\
///// CLERIC_CAUSE_MEDIUM_WOUNDS                       \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CAUSE_MEDIUM_WOUNDS~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid330.spl~ 1 3 CLERIC_CAUSE_MEDIUM_WOUNDS
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4170 // name
    SAY 0x50 @4171 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdid330 resource = EVAL ~%DEST_RES%~ END
    PATCH_IF !ee_game BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixd330 END // race = golem
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixd330 END // general = undead
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 104 opcode = 177                  parameter2 = 4 STR_VAR resource = cdixd330 END // race = parameter1
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid330.itm~
    SAY 0x0c @4170
    SAY 0x54 @4171
    LPF cd_scroll INT_VAR unusable0 = (BIT2 + BIT12 + BIT21 + BIT30) range = 1 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END // non-good cleric

  COPY ~iwdification/bam/cdid330a.bam~ ~override~
       ~iwdification/bam/cdid330b.bam~ ~override~
       ~iwdification/bam/cdid330c.bam~ ~override~
       ~iwdification/bam/cdicmdam.bam~ ~override~
       ~iwdification/vvc/cdicmdam.vvc~ ~override~
       ~iwdification/wav/cdiem103.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd330.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_FAVOR_OF_ILMATER                          \\\\\
/////                                                  \\\\\

ACTION_IF (ee_game AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_FAVOR_OF_ILMATER~) OR override_divine)) BEGIN

  ADD_SPELL ~iwdification/spl/cdid331.spl~ 1 3 CLERIC_FAVOR_OF_ILMATER
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4180 // name
    SAY 0x50 @4181 // descript
    LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = sppr315 resource = EVAL ~%DEST_RES%~ END // iwdee nightly has bug here
    LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdid331 resource = EVAL ~%DEST_RES%~ END // for when bug is fixed

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid331.itm~
    SAY 0x0c @4180
    SAY 0x54 @4181
    LPF cd_scroll INT_VAR unusable0 = (BIT1 + BIT12 + BIT21 + BIT30) range = 1 opcode = 146 target_eff = 2 price = 300 STR_VAR spell = EVAL ~%current_spell_res%~ END // non-evil cleric

  COPY ~iwdification/bam/cdid331a.bam~ ~override~
       ~iwdification/bam/cdid331b.bam~ ~override~
       ~iwdification/bam/cdid331c.bam~ ~override~
       ~iwdification/bam/cdicmdam.bam~ ~override~
       ~iwdification/bam/cdicmwou.bam~ ~override~
       ~iwdification/vvc/cdicmdam.vvc~ ~override~
       ~iwdification/vvc/cdicmwou.vvc~ ~override~
       ~iwdification/wav/cdiem103.wav~ ~override~
       ~iwdification/wav/cdiep26.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_GIANT_INSECT                              \\\\\
/////                                                  \\\\\

ACTION_IF (anim_beetle AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_GIANT_INSECT~) OR override_divine)) BEGIN // requires beetle animation, either EE or vanilla + infinity animations

  ADD_PROJECTILE ~iwdification/pro/cdi282.pro~

  LAF cd_new_summon_table STR_VAR descript = "Giant_insect" 2da_file = cdiinsct RET table END

  ADD_SPELL ~iwdification/spl/cdid418.spl~ 1 4 CLERIC_GIANT_INSECT
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4190 // name
    SAY 0x50 @4191 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END
    END ELSE BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 215 parameter2 = 2 STR_VAR resource = cdimsm1h END
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0 probability1 = 49 dicesize = 0 dicenumber = 0 STR_VAR resource = cdiinsct END
      LPF CLONE_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0                   dicesize = 0 dicenumber = 0 STR_VAR resource = cdiinsct END
      LPF ALTER_EFFECT INT_VAR match_opcode = 331 opcode = 127 parameter1 = 1 parameter2 = 0                   dicesize = 0 dicenumber = 0 STR_VAR resource = cdiinsct END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid418.itm~
    SAY 0x0c @4190
    SAY 0x54 @4191
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 opcode = 148 target_eff = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid
    
  COPY_EXISTING ~cdi282.pro~ ~override~
    SAY 0x30 @4193
    
  COMPILE ~iwdification/baf/cdibbcld.baf~

  COPY ~iwdification/spl/cdibombb.spl~ ~override~
    SAY 0xce @4193
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi282 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 35568 parameter1 = string_stunned END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14073 parameter1 = string_deafness END

  LAF cd_animation STR_VAR code = e220 END // 57888 MBBM beetle_black
  COPY ~iwdification/cre/cdiibomb.cre~ ~override~
    SAY 0x08 @4192
    SAY 0x0c @4192
    WRITE_LONG 0x28 anim_beetle
  
  COPY ~iwdification/cre/cdiiborb.cre~ ~override~
    SAY 0x08 @3013
    SAY 0x0c @3013
    WRITE_LONG 0x28 anim_beetle
  
  COPY ~iwdification/itm/cdis5-20.itm~ ~override~
    WRITE_LONG 0x08 string_attack
    WRITE_LONG 0x0c string_attack
  
  COPY ~iwdification/itm/cdifring.itm~ ~override~
    LPF ALTER_EFFECT STR_VAR match_resource = spin191 resource = cdibombb END

  COPY ~iwdification/bam/cdid418a.bam~ ~override~
       ~iwdification/bam/cdid418b.bam~ ~override~
       ~iwdification/bam/cdid418c.bam~ ~override~
       ~iwdification/bam/cdiasm1x.bam~ ~override~
       ~iwdification/bam/cdimsm1h.bam~ ~override~
       ~iwdification/bam/cdisclda.bam~ ~override~
       ~iwdification/bam/cdiscldr.bam~ ~override~
       ~iwdification/vvc/cdiasm1x.vvc~ ~override~
       ~iwdification/vvc/cdimsm1h.vvc~ ~override~
       ~iwdification/wav/cdiem47.wav~  ~override~

  COPY ~iwdification/2da/cdiinsct.2da~ ~override~
    PATCH_IF !ee_game BEGIN// delete 2nd & 3rd columns for vanilla summon tables
      REPLACE_TEXTUALLY ~[ %TAB%]+HitAnimation[ %TAB%]+AreaHitAnimation~ ~~
      REPLACE_TEXTUALLY ~[ %TAB%]+cdimsm1h[ %TAB%]+.+$~ ~~
    END

END

/////                                                  \\\\\
///// CLERIC_PRODUCE_FIRE                              \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_PRODUCE_FIRE~) OR override_divine) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi215.pro~

  ADD_SPELL ~iwdification/spl/cdid419.spl~ 1 4 CLERIC_PRODUCE_FIRE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4200 // name
    SAY 0x50 @4201 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi215 END
    LPF ALTER_EFFECT STR_VAR match_resource = cdid419 resource = EVAL ~%DEST_RES%~ END
    PATCH_IF !ee_game BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 1 parameter2 = 2 timing = 1 resist_dispel = 3 STR_VAR resource = cdiprfir END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid419.itm~
    SAY 0x0c @4200
    SAY 0x54 @4201
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 range = 40 opcode = 148 target_eff = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/bam/cdid419a.bam~ ~override~
       ~iwdification/bam/cdid419b.bam~ ~override~
       ~iwdification/bam/cdid419c.bam~ ~override~
       ~iwdification/bam/cdipfira.bam~ ~override~
       ~iwdification/bam/cdipfirx.bam~ ~override~
       ~iwdification/vvc/cdiprfir.vvc~ ~override~
       ~iwdification/wav/cdiarp03.wav~ ~override~
       ~iwdification/wav/cdiep45.wav~  ~override~

  ACTION_IF !ee_game BEGIN
    
    COPY_EXISTING ~cdi215.pro~ ~override~
      WRITE_BYTE 0x217 255
  
  END

END

/////                                                  \\\\\
///// CLERIC_STATIC_CHARGE                             \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_STATIC_CHARGE~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4210) STR_VAR bam_file = cdid420d RET icon END

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdistatc.pro~
  
  END

  ADD_SPELL ~iwdification/spl/cdid420.spl~ 1 4 CLERIC_STATIC_CHARGE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4210 // name
    SAY 0x50 @4211 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdid420 resource = EVAL ~%DEST_RES%~ END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 333 STR_VAR resource = cdid420b END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 42 parameter2 = icon END
    END ELSE BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR target = 1 projectile = 1 range = 30 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 42 END
      LPF ALTER_EFFECT INT_VAR target = 2 dicenumber = 0 dicesize = 0 END
      LPF CD_CONVERT_333 STR_VAR 333spell = cdid420b END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid420.itm~
    SAY 0x0c @4210
    SAY 0x54 @4211
    PATCH_IF ee_game BEGIN
      LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 5 range = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid
    END ELSE BEGIN
      LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 1 range = 30 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid
    END

  COPY ~iwdification/spl/cdid420b.spl~ ~override~
    SAY 0x9e @4212
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdistatc END
    END ELSE BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR target = 1 projectile = 1 range = 30 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 12 dicenumber = 4 dicesize = 8 special = 0 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 12 dicenumber = 5 savingthrow = 0 END
      LPF CD_EXTEND-O-MATIC INT_VAR base_dur = 0 step_dur = 0 level_cap = 20 min_lev_alt = 7 END
      READ_SHORT 0x68 abil_num
      FOR (index = 0 ; index < abil_num ; ++index) BEGIN
        LPF ALTER_EFFECT INT_VAR header = index match_opcode = 12 match_savingthrow = 0    dicenumber = ((index +  9) / 2) END
        LPF ALTER_EFFECT INT_VAR header = index match_opcode = 12 match_savingthrow = BIT0 dicenumber = ((index + 10) / 2) END
      END
    END

  COPY ~iwdification/bam/cdid420a.bam~ ~override~
       ~iwdification/bam/cdid420b.bam~ ~override~
       ~iwdification/bam/cdid420c.bam~ ~override~
       ~iwdification/bam/cdid420d.bam~ ~override~
       ~iwdification/bam/cdistath.bam~ ~override~
       ~iwdification/vvc/cdistath.vvc~ ~override~
       ~iwdification/wav/cdiep42.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_RECITATION                                \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_RECITATION~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4220) STR_VAR bam_file = cdid316d RET icon END

  ADD_PROJECTILE ~iwdification/pro/cdivrnp.pro~
  ADD_PROJECTILE ~iwdification/pro/cdivrpo.pro~

  ADD_SPELL ~iwdification/spl/cdid421.spl~ 1 4 CLERIC_RECITATION
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4220 // name
    SAY 0x50 @4221 // descript

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid421.itm~
    SAY 0x0c @4220
    SAY 0x54 @4221
    LPF cd_scroll INT_VAR unusable0 = (BIT12 + BIT21 + BIT30) target_hdr = 5 range = 40 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // cleric

  COPY ~iwdification/spl/cdid421b.spl~ ~override~
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdivrnp END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40257 parameter1 = RESOLVE_STR_REF(@4082) END
    PATCH_IF !ee_game BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 54 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = cdid421b END
      LPF CD_CONVERT_9_255 END
    END

  COPY ~iwdification/spl/cdid421g.spl~ ~override~
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdivrpo END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40256 parameter1 = RESOLVE_STR_REF(@4083) END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 175 parameter2 = icon END
    END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 175 parameter2 = 18 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 54 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = cdid421g END
      LPF CD_CONVERT_9_255 END
    END

  COPY ~iwdification/bam/cdid421a.bam~ ~override~
       ~iwdification/bam/cdid421b.bam~ ~override~
       ~iwdification/bam/cdid421c.bam~ ~override~
       ~iwdification/bam/cdid316d.bam~ ~override~
       ~iwdification/bam/cdirecih.bam~ ~override~
       ~iwdification/vvc/cdirecig.vvc~ ~override~
       ~iwdification/vvc/cdirecih.vvc~ ~override~
       ~iwdification/wav/cdiep44.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_BLOOD_RAGE                                \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_BLOOD_RAGE~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4230) STR_VAR bam_file = cdid422d RET icon END

  ADD_SPELL ~iwdification/spl/cdid422.spl~ 1 4 CLERIC_BLOOD_RAGE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4230 // name
    SAY 0x50 @4231 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdid422 resource = EVAL ~%DEST_RES%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 =  1280 parameter1 = string_stunned END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14022 parameter1 = string_healed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14043 parameter1 = string_stun END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14102 parameter1 = string_held END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 17392 parameter1 = string_dcharmed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 20568 parameter1 = string_mf_panic END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35473 parameter1 = string_healed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35484 parameter1 = string_panic END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35542 parameter1 = string_paralyzed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35544 parameter1 = string_dominated END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35606 parameter1 = string_held END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37340 parameter1 = string_uncon END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37603 parameter1 = string_rthinking END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37604 parameter1 = string_confused END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37613 parameter1 = string_sleep END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37801 parameter1 = string_charmed END
    PATCH_IF ee_game BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 318 STR_VAR match_resource = cdid422 END // should be last
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 176 parameter2 = icon END
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 opcode = 318 match_parameter2 = 50 parameter2 = 0 STR_VAR insert = last END // reinserting as last
    END ELSE BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 opcode = 206 match_parameter2 = 50 parameter2 = 0 STR_VAR insert = last END // reinserting as last
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 324 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 328 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 318 opcode = 206 parameter1 = 0 parameter2 = 0 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 176 parameter2 = 4 END
      PATCH_FOR_EACH ea IN 0 1 28 29 30 31 126 128 199 200 201 202 254 255 BEGIN // all ea values except charmed, controlled, ally, familiar, pc
        LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 power = 4 parameter1 = ea parameter2 = 2 resist_dispel = 3 duration = 120 insert_point = 0 STR_VAR resource = cdixd422 END
      END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 power = 4 parameter1 = 16 parameter2 = 8 resist_dispel = 3 duration = 120 insert_point = 0 STR_VAR resource = cdixd422 END // lawful
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid422.itm~
    SAY 0x0c @4230
    SAY 0x54 @4231
    LPF cd_scroll INT_VAR unusable0 = (BIT4 + BIT12 + BIT21 + BIT30) price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // non-lawful cleric

  COPY ~iwdification/bam/cdid422a.bam~ ~override~
       ~iwdification/bam/cdid422b.bam~ ~override~
       ~iwdification/bam/cdid422c.bam~ ~override~
       ~iwdification/bam/cdid422d.bam~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd422.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_CLOUD_OF_PESTILENCE                       \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CLOUD_OF_PESTILENCE~) OR override_divine) BEGIN

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi309.pro~

  END ELSE BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi309a.pro~

  END

  ADD_SPELL ~iwdification/spl/cdid423.spl~ 1 4 CLERIC_CLOUD_OF_PESTILENCE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4240 // name
    SAY 0x50 @4241 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi309 END
      LPF ALTER_EFFECT STR_VAR match_resource = cdid423 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37800 parameter1 = string_blinded END
    END ELSE BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi309a END
      LPF DELETE_EFFECT END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 timing = 1 parameter2 = 2 STR_VAR resource = cdid423 END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 duration = 24 STR_VAR resource = cdid423 END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid423.itm~
    SAY 0x0c @4240
    SAY 0x54 @4241
    LPF cd_scroll INT_VAR unusable0 = (BIT2 + BIT3 + BIT12 + BIT21 + BIT30) target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // evil cleric

  COPY ~iwdification/bam/cdid423a.bam~ ~override~
       ~iwdification/bam/cdid423b.bam~ ~override~
       ~iwdification/bam/cdid423c.bam~ ~override~
       ~iwdification/bam/cdicopes.bam~ ~override~
       ~iwdification/wav/cdiarp25.wav~ ~override~

  ACTION_IF ee_game BEGIN

    APPEND ~clearair.2da~ ~Cloud_of_Pest %cdi309%~

  END ELSE BEGIN

    APPEND ~clearair.2da~ ~Cloud_of_Pest %cdi309a%~
  
    LAUNCH_ACTION_FUNCTION cd_create_cloud INT_VAR visloop = 9 cloud_dur = 24 zosa = 1 STR_VAR code = cdid423 anim = cdicopes END

    COPY ~iwdification/spl/cdid423.spl~ ~override~
      WRITE_LONG 0x08 "-1" // name
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi309a END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 duration = 6 STR_VAR resource = cdid423 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 318 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 39 END // class = paladin, but bg2 paladins don't have disease immunity
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37800 parameter1 = string_blinded END
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  9 opcode = 177 parameter1 = 145 parameter2 = 4 STR_VAR resource = cdixd423 END // race = elemental
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 27 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixd423 END // race = golem
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  1 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixd423 END // general = undead

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd423.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_UNFAILING_ENDURANCE                       \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_UNFAILING_ENDURANCE~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid424.spl~ 1 4 CLERIC_UNFAILING_ENDURANCE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4250 // name
    SAY 0x50 @4251 // descript

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid424.itm~
    SAY 0x0c @4250
    SAY 0x54 @4251
    LPF cd_scroll INT_VAR unusable0 = (BIT12 + BIT21 + BIT30) range = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // cleric

  COPY ~iwdification/bam/cdid424a.bam~ ~override~
       ~iwdification/bam/cdid424b.bam~ ~override~
       ~iwdification/bam/cdid424c.bam~ ~override~
       ~iwdification/bam/cdinecro.bam~ ~override~
       ~iwdification/vvc/cdinecro.vvc~ ~override~
       ~iwdification/wav/cdiep112.wav~ ~override~

END

/////                                                  \\\\\
///// CLERIC_STAR_METAL_CUDGEL                         \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_STAR_METAL_CUDGEL~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid425.spl~ 1 4 CLERIC_STAR_METAL_CUDGEL
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4260 // name
    SAY 0x50 @4261 // descript

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid425.itm~
    SAY 0x0c @4260
    SAY 0x54 @4261
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 5 range = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/itm/cdismcud.itm~ ~override~
    SAY 0x08 @4260
    SAY 0x0c @4260

  COPY ~iwdification/bam/cdid425a.bam~ ~override~
       ~iwdification/bam/cdid425b.bam~ ~override~
       ~iwdification/bam/cdid425c.bam~ ~override~
       ~iwdification/bam/cdiconju.bam~ ~override~
       ~iwdification/bam/cdismcud.bam~ ~override~
       ~iwdification/eff/cdismcud.eff~ ~override~
       ~iwdification/vvc/cdiconju.vvc~ ~override~
       ~iwdification/wav/cdiep02.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_SMASHING_WAVE                             \\\\\
/////                                                  \\\\\

ACTION_IF (ee_game AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_SMASHING_WAVE~) OR override_divine)) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdiswave.pro~

  ADD_SPELL ~iwdification/spl/cdid426.spl~ 1 4 CLERIC_SMASHING_WAVE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4270 // name
    SAY 0x50 @4271 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdiswave END
    LPF ALTER_EFFECT STR_VAR match_resource = cdid426 resource = EVAL ~%DEST_RES%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 35568 parameter1 = string_stunned END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37340 parameter1 = string_uncon END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 20438 parameter1 = string_uncon END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid426.itm~
    SAY 0x0c @4270
    SAY 0x54 @4271
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/bam/cdid426a.bam~ ~override~
       ~iwdification/bam/cdid426b.bam~ ~override~
       ~iwdification/bam/cdid426c.bam~ ~override~
       ~iwdification/bam/cdiswavh.bam~ ~override~
       ~iwdification/bam/cdiswavx.bam~ ~override~
       ~iwdification/vvc/cdiswavh.vvc~ ~override~
       ~iwdification/wav/cdiep110.wav~ ~override~
       ~iwdification/wav/cditra56.wav~ ~override~

END

/////                                                  \\\\\
///// CLERIC_THORN_SPRAY                               \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_THORN_SPRAY~) OR override_divine) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi303.pro~

  ADD_SPELL ~iwdification/spl/cdid427.spl~ 1 4 CLERIC_THORN_SPRAY
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4280 // name
    SAY 0x50 @4281 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi303 END
    PATCH_IF !ee_game BEGIN
      LPF CD_SPLIT_SAVE_DAMAGE END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid427.itm~
    SAY 0x0c @4280
    SAY 0x54 @4281
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 range = 30 opcode = 148 target_eff = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/bam/cdid427a.bam~ ~override~
       ~iwdification/bam/cdid427b.bam~ ~override~
       ~iwdification/bam/cdid427c.bam~ ~override~
       ~iwdification/bam/cditspra.bam~ ~override~
       ~iwdification/wav/cditra61.wav~ ~override~

  ACTION_IF !ee_game BEGIN
    
    COPY_EXISTING ~cdi303.pro~ ~override~
      WRITE_BYTE 0x217 11 // re-use cone of cold
      WRITE_BYTE 0x218 0
  
  END

END

/////                                                  \\\\\
///// CLERIC_WALL_OF_MOONLIGHT                         \\\\\
/////                                                  \\\\\

ACTION_IF (ee_game AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_WALL_OF_MOONLIGHT~) OR override_divine)) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdiwallm.pro~

  ADD_SPELL ~iwdification/spl/cdid428.spl~ 1 4 CLERIC_WALL_OF_MOONLIGHT
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4290 // name
    SAY 0x50 @4291 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdiwallm END
    LPF ALTER_EFFECT STR_VAR match_resource = cdid428 resource = EVAL ~%DEST_RES%~ END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid428.itm~
    SAY 0x0c @4290
    SAY 0x54 @4291
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 400 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/bam/cdid428a.bam~ ~override~
       ~iwdification/bam/cdid428b.bam~ ~override~
       ~iwdification/bam/cdid428c.bam~ ~override~
       ~iwdification/bam/cdimoonx.bam~ ~override~
       ~iwdification/spl/cdid428a.spl~ ~override~
       ~iwdification/spl/cdid428b.spl~ ~override~
       ~iwdification/vvc/cdimoonx.vvc~ ~override~
       ~iwdification/wav/cdiafp21.wav~ ~override~
       ~iwdification/wav/cdiarp22.wav~ ~override~

END

/////                                                  \\\\\
///// CLERIC_RIGHTEOUS_WRATH_OF_THE_FAITHFUL           \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_RIGHTEOUS_WRATH_OF_THE_FAITHFUL~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4300) STR_VAR bam_file = cdid316d RET icon END

  ADD_PROJECTILE ~iwdification/pro/cdi266.pro~

  ADD_SPELL ~iwdification/spl/cdid518.spl~ 1 5 CLERIC_RIGHTEOUS_WRATH_OF_THE_FAITHFUL
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4300 // name
    SAY 0x50 @4301 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi266 END
    PATCH_IF !ee_game BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 318 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 146 STR_VAR match_resource = cdirwf00 END
      PATCH_FOR_EACH align IN 17 18 19 33 34 35 49 50 51 BEGIN
        LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 power = 5 parameter1 = align parameter2 = 8 timing = 1 resist_dispel = 3 STR_VAR resource = EVAL ~cdirwf%align%~ END
      END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid518.itm~
    SAY 0x0c @4300
    SAY 0x54 @4301
    LPF cd_scroll INT_VAR unusable0 = (BIT12 + BIT21 + BIT30) target_hdr = 5 range = 40 price = 500 STR_VAR spell = EVAL ~%current_spell_res%~ END // cleric

  COPY ~iwdification/spl/cdirwf00.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 17392 parameter1 = string_dcharmed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35542 parameter1 = string_paralyzed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35544 parameter1 = string_dominated END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35606 parameter1 = string_held END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 36035 parameter1 = string_hasted END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37801 parameter1 = string_charmed END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 184 parameter2 = icon END
    END ELSE BEGIN // in non-ee, characters will get 00 AND 99 if matching alignment, so need to strip out anything already covered in 99
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 184 END
      PATCH_FOR_EACH op IN 321 8 174 215 18 93 BEGIN // cosmetics, sound, fatigue, hp bonus
        LPF DELETE_EFFECT INT_VAR match_opcode = op END
      END
      PATCH_FOR_EACH op IN 54 33 34 35 36 37 BEGIN // already get +1 from 99, so only bump another +1 here
        LPF ALTER_EFFECT INT_VAR match_opcode = op parameter1 = 1 END
      END
      LPF CLONE_EFFECT INT_VAR match_opcode = 54 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = cdirwf00 END
    END

  COPY ~iwdification/spl/cdirwf99.spl~ ~override~
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 184 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 184 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 54 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = cdirwf99 END
      LPF CD_CONVERT_9_255 END
    END

  COPY ~iwdification/bam/cdid518a.bam~ ~override~
       ~iwdification/bam/cdid518b.bam~ ~override~
       ~iwdification/bam/cdid518c.bam~ ~override~
       ~iwdification/bam/cdid316d.bam~ ~override~
       ~iwdification/bam/cdiabjux.bam~ ~override~
       ~iwdification/bam/cdirwtfh.bam~ ~override~
       ~iwdification/vvc/cdigabju.vvc~ ~override~
       ~iwdification/vvc/cdirwtfg.vvc~ ~override~
       ~iwdification/vvc/cdirwtfh.vvc~ ~override~
       ~iwdification/wav/cdiarm20.wav~ ~override~
       ~iwdification/wav/cdiee01.wav~  ~override~
       ~iwdification/wav/cdiep36.wav~  ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/cdirwf17.eff~ ~override/cdirwf00.eff~
      WRITE_ASCIIE 0x30 ~cdirwf00~

    ACTION_FOR_EACH align IN 17 18 19 33 34 35 49 50 51 BEGIN

      COPY ~iwdification/eff/cdirwf17.eff~ ~override/cdirwf%align%.eff~
        WRITE_ASCIIE 0x30 ~cdirwf%align%~

      COPY ~iwdification/spl/cdirwf17.spl~ ~override/cdirwf%align%.spl~
        WRITE_LONG 0x9e align
        LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi266 END
    
    END
  
  END

END

/////                                                  \\\\\
///// CLERIC_SPIKE_STONES                              \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_SPIKE_STONES~) OR override_divine) BEGIN

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi213.pro~
  
  END ELSE BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi213a.pro~

  END

  ADD_SPELL ~iwdification/spl/cdid519.spl~ 1 5 CLERIC_SPIKE_STONES
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4310 // name
    SAY 0x50 @4311 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi213 END
      LPF DELETE_EFFECT STR_VAR match_resource = ~#sston~ END
    END ELSE BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi213a END
      LPF DELETE_EFFECT END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 timing = 1 parameter2 = 2 STR_VAR resource = cdid519 END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid519.itm~
    SAY 0x0c @4310
    SAY 0x54 @4311
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 range = 40 opcode = 148 target_eff = 1 price = 1500 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid only

  COPY ~iwdification/bam/cdid519a.bam~ ~override~
       ~iwdification/bam/cdid519b.bam~ ~override~
       ~iwdification/bam/cdid519c.bam~ ~override~
       ~iwdification/bam/cdistone.bam~ ~override~
       ~iwdification/vvc/cdistone.vvc~ ~override~
       ~iwdification/wav/cdiarp04.wav~ ~override~
       ~iwdification/wav/cdicp03.wav~  ~override~
       ~iwdification/wav/cdiep48.wav~  ~override~

  ACTION_IF ee_game BEGIN

    COPY ~iwdification/vvc/cdistone.vvc~ ~override~
    
  END ELSE BEGIN
  
    LAUNCH_ACTION_FUNCTION cd_create_cloud INT_VAR visloop = 5 cloud_dur = 72 STR_VAR code = cdid519 anim = cdistone END

    COPY ~iwdification/spl/cdid519.spl~ ~override~
      WRITE_LONG 0x08 "-1" // name
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi213a END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 duration = 6 STR_VAR resource = cdid519 END

  END

END

/////                                                  \\\\\
///// CLERIC_SHIELD_OF_LATHANDER                       \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_SHIELD_OF_LATHANDER~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4320) STR_VAR bam_file = cdid520d RET icon END

  ADD_SPELL ~iwdification/spl/cdid520.spl~ 1 5 CLERIC_SHIELD_OF_LATHANDER
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4320 // name
    SAY 0x50 @4321 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 STR_VAR resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 164 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 164 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 opcode = 177 parameter1 = 3 parameter2 = 8 STR_VAR resource = cdixd520 END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid520.itm~
    SAY 0x0c @4320
    SAY 0x54 @4321
    LPF cd_scroll INT_VAR unusable0 = (BIT1 + BIT12 + BIT21 + BIT30) range = 1 price = 500 STR_VAR spell = EVAL ~%current_spell_res%~ END // non-evil cleric

  COPY ~iwdification/bam/cdid520a.bam~ ~override~
       ~iwdification/bam/cdid520b.bam~ ~override~
       ~iwdification/bam/cdid520c.bam~ ~override~
       ~iwdification/bam/cdid520d.bam~ ~override~
       ~iwdification/bam/cdisol1.bam~  ~override~
       ~iwdification/bam/cdisol2.bam~  ~override~
       ~iwdification/vvc/cdilatl1.vvc~ ~override~
       ~iwdification/vvc/cdilatl2.vvc~ ~override~
       ~iwdification/wav/cdiafp20.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd520.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_UNDEAD_WARD                               \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_UNDEAD_WARD~) OR override_divine) BEGIN

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdiuward.pro~

  END ELSE BEGIN
    
    OUTER_SET cdiuward = 1

  END

  ADD_SPELL ~iwdification/spl/cdid521.spl~ 1 5 CLERIC_UNDEAD_WARD
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4330 // name
    SAY 0x50 @4331 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdiuward END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdid521 resource = EVAL ~%DEST_RES%~ END
    END ELSE BEGIN
      LPF DELETE_EFFECT END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 1 power = 5                resist_dispel = 2 duration = 60 STR_VAR resource = cdiuwrdx END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 power = 5 parameter2 = 2 resist_dispel = 2 duration = 60 END
      LPF CD_EXTEND-O-MATIC INT_VAR base_dur = 60 step_dur = 0 level_cap = 30 min_lev_alt = 9 END
      READ_SHORT 0x68 abil_num
      FOR (index = 9 ; index < (abil_num + 9) ; ++index) BEGIN
        LPF ALTER_EFFECT INT_VAR header = (index - 9) match_opcode = 177 STR_VAR resource = EVAL ~cdiuwr%index%~ END
      END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid521.itm~
    SAY 0x0c @4330
    SAY 0x54 @4331
    LPF cd_scroll INT_VAR unusable0 = (BIT12 + BIT21 + BIT30) target_hdr = 5 range = 1 price = 500 STR_VAR spell = EVAL ~%current_spell_res%~ END // cleric

  COPY ~iwdification/bam/cdid521a.bam~ ~override~
       ~iwdification/bam/cdid521b.bam~ ~override~
       ~iwdification/bam/cdid521c.bam~ ~override~
       ~iwdification/bam/cdiuwrdx.bam~ ~override~
       ~iwdification/vvc/cdiuwrdx.vvc~ ~override~
       ~iwdification/wav/cdiafp27.wav~ ~override~
       ~iwdification/wav/cdiarp28.wav~ ~override~
       
  ACTION_IF !ee_game BEGIN

    COMPILE ~iwdification/baf/cdid521.baf~

    OUTER_FOR (index = 9 ; index < 31 ; ++index) BEGIN

      COPY ~iwdification/eff/cdiuwr9.eff~ ~override/cdiuwr%index%.eff~
        WRITE_ASCIIE 0x30 "%DEST_RES%" #8

      COPY ~iwdification/cre/cdiuwr9.cre~ ~override/cdiuwr%index%.cre~
        WRITE_BYTE 0x234 index
        
    END

  END

END

/////                                                  \\\\\
///// CLERIC_ANIMAL_RAGE                               \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_ANIMAL_RAGE~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4340) STR_VAR bam_file = cdid522d RET icon END

  ADD_SPELL ~iwdification/spl/cdid522.spl~ 1 5 CLERIC_ANIMAL_RAGE
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4340 // name
    SAY 0x50 @4341 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdid522 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 173 parameter2 = icon END
      LPF ALTER_EFFECT INT_VAR match_opcode = 333 STR_VAR resource = cdid522b END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 173 parameter2 = 83 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 33 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = EVAL ~%DEST_RES%~ END
      LPF CD_CONVERT_333 STR_VAR 333spell = cdid522b END
      LPF CD_CONVERT_9_255 END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid522.itm~
    SAY 0x0c @4340
    SAY 0x54 @4341
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) range = 1 price = 500 STR_VAR spell = EVAL ~%current_spell_res%~ END // druid

  COPY ~iwdification/spl/cdid522b.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37605 parameter1 = string_berserk END

  COPY ~iwdification/bam/cdid522a.bam~ ~override~
       ~iwdification/bam/cdid522b.bam~ ~override~
       ~iwdification/bam/cdid522c.bam~ ~override~
       ~iwdification/bam/cdid522d.bam~ ~override~

END

/////                                                  \\\\\
///// CLERIC_MASS_CAUSE_LIGHT_WOUNDS                   \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_MASS_CAUSE_LIGHT_WOUNDS~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid523.spl~ 1 5 CLERIC_MASS_CAUSE_LIGHT_WOUNDS
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4350 // name
    SAY 0x50 @4351 // descript
    LPF ALTER_EFFECT STR_VAR match_resource = cdid523 resource = EVAL ~%DEST_RES%~ END
    PATCH_IF !ee_game BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixd523 END // race = golem
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixd523 END // general = undead
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 104 opcode = 177                  parameter2 = 4 STR_VAR resource = cdixd523 END // race = parameter1
      LPF CD_SPLIT_SAVE_DAMAGE END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid523.itm~
    SAY 0x0c @4350
    SAY 0x54 @4351
    LPF cd_scroll INT_VAR unusable0 = (BIT2 + BIT3) target_hdr = 5 range = 20 price = 500 STR_VAR spell = EVAL ~%current_spell_res%~ END // evil only

  COPY ~iwdification/bam/cdid523a.bam~ ~override~
       ~iwdification/bam/cdid523b.bam~ ~override~
       ~iwdification/bam/cdid523c.bam~ ~override~
       ~iwdification/bam/cdicldam.bam~ ~override~
       ~iwdification/vvc/cdicldam.vvc~ ~override~
       ~iwdification/wav/cdiem103.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd523.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_ENTROPY_SHIELD                            \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_ENTROPY_SHIELD~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4360) STR_VAR bam_file = cdid615d RET icon END

  ADD_SPELL ~iwdification/spl/cdid615.spl~ 1 6 CLERIC_ENTROPY_SHIELD
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4360 // name
    SAY 0x50 @4361 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdid615 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 56 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 56 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 318 opcode = 206 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 30 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = EVAL ~%DEST_RES%~ END
      // iwdee/bgee/bg2ee/vanilla all match in projectiles until the 1pp slots for EE, so remove 1pp refs for vanilla (EEs all match, fortunately)
      FOR (index = 282 ; index < 319 ; ++index) BEGIN
        LPF DELETE_EFFECT INT_VAR match_opcode = 83 match_parameter2 = index END
      END
      PATCH_IF FILE_EXISTS_IN_GAME ~1arow01.pro~ BEGIN // if 1pp is installed, re-add the references manually since they still may not sync with EE
        PATCH_FOR_EACH proj IN // 1pp stuff
          1stafm0c 1arow01 1arow02 1arow03 1arow04 1arow05 1arow07 1arow10 1arow11 1arow15 1axe05 1axe08 1axe09 1axe10 1axe16 1bolt01 1bolt02 1bolt03 1bolt04
          1bolt05 1bolt06 1bolt09 1bull02 1bull03 1bull04 1bull05 1bull06 1dagg05 1dagg11 1dagg12 1dagg16 1dart01 1dart02 1dart03 1dart04 1dart05 1dart08
        BEGIN
          SET projids = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
          PATCH_IF projids > 0 BEGIN
            LPF CLONE_EFFECT INT_VAR match_opcode = 83 match_parameter2 = 1 parameter2 = projids END
          END
        END
      END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid615.itm~
    SAY 0x0c @4360
    SAY 0x54 @4361
    LPF cd_scroll INT_VAR target_hdr = 5 range = 1 price = 600 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdid615a.bam~ ~override~
       ~iwdification/bam/cdid615b.bam~ ~override~
       ~iwdification/bam/cdid615c.bam~ ~override~
       ~iwdification/bam/cdid615d.bam~ ~override~
       ~iwdification/bam/cdiabjur.bam~ ~override~
       ~iwdification/bam/cdieshld.bam~ ~override~
       ~iwdification/vvc/cdiabjur.vvc~ ~override~
       ~iwdification/vvc/cdientro.vvc~ ~override~
       ~iwdification/wav/cdiafp03.wav~ ~override~

END

/////                                                  \\\\\
///// CLERIC_WHIRLWIND                                 \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_WHIRLWIND~) OR override_divine) BEGIN

  ACTION_IF ee_game BEGIN
  
    ADD_PROJECTILE ~iwdification/pro/cdiwhirl.pro~
    
  END ELSE BEGIN
  
    ADD_PROJECTILE ~iwdification/pro/cdiwhira.pro~

  END

  ADD_SPELL ~iwdification/spl/cdid617.spl~ 1 6 CLERIC_WHIRLWIND
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4370 // name
    SAY 0x50 @4371 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdiwhirl END
      LPF ALTER_EFFECT STR_VAR match_resource = cdid617 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 35568 parameter1 = string_stunned END
    END ELSE BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = 1 END
      LPF DELETE_EFFECT END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 parameter2 = 2 resist_dispel = 2 duration = 120 STR_VAR resource = cdid617 END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid617.itm~
    SAY 0x0c @4370
    SAY 0x54 @4371
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 600 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdid617a.bam~ ~override~
       ~iwdification/bam/cdid617b.bam~ ~override~
       ~iwdification/bam/cdid617c.bam~ ~override~
       ~iwdification/bam/cdiwhirx.bam~ ~override~
       ~iwdification/wav/cdiep105.wav~ ~override~

  ACTION_IF !ee_game BEGIN  
  
    COPY ~iwdification/spl/cdid617.spl~ ~override/cdid617b.spl~
      WRITE_LONG 0x08 "-1"
      LPF DELETE_EFFECT INT_VAR match_opcode = 318 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 324 END
      LPF ALTER_EFFECT STR_VAR match_resource = cdid617 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 35568 parameter1 = string_stunned END
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdiwhira END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 265 target = 1 parameter1 = 1 timing = 1 insert_point = 0 STR_VAR resource = cdizosa END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 273 target = 1 timing = 1 resist_dispel = 3 insert_point = 0 STR_VAR resource = cdizosa END
      PATCH_FOR_EACH race IN 146 118 119 142 101 102 145 144 127 199 BEGIN // dragon, syverns, slimes, giants, ankheg, basilisk, elemental, golem, otyugh, ettin
        LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = race parameter2 = 4 duration = 1 insert_point = 0 STR_VAR resource = cdixd617 END
      END

    COMPILE ~iwdification/baf/cdid617.baf~

    COPY ~iwdification/cre/cdid617.cre~  ~override~
         ~iwdification/eff/cdid617.eff~  ~override~
         ~iwdification/eff/cdid617b.eff~ ~override~
         ~iwdification/vvc/cdiwhirx.vvc~ ~override~

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd617.eff~
      WRITE_ASCII 0x30 ~cdid617b~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_SPIRITUAL_WRATH                           \\\\\
/////                                                  \\\\\

ACTION_IF (ee_game AND (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_SPIRITUAL_WRATH~) OR override_divine)) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi207.pro~
  ADD_PROJECTILE ~iwdification/pro/cdi312.pro~

  ADD_SPELL ~iwdification/spl/cdid618.spl~ 1 6 CLERIC_SPIRITUAL_WRATH
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4380 // name
    SAY 0x50 @4381 // descript
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi312 END
    LPF ALTER_EFFECT STR_VAR match_resource = cdid618 resource = EVAL ~%DEST_RES%~ END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid618.itm~
    SAY 0x0c @4380
    SAY 0x54 @4381
    LPF cd_scroll INT_VAR unusable0 = (BIT12 + BIT21 + BIT30) target_hdr = 5 range = 100 price = 600 STR_VAR spell = EVAL ~%current_spell_res%~ END
    
  COPY_EXISTING ~cdi312.pro~ ~override~
    WRITE_SHORT 0x21a cdi207

  COPY ~iwdification/bam/cdid618a.bam~ ~override~
       ~iwdification/bam/cdid618b.bam~ ~override~
       ~iwdification/bam/cdid618c.bam~ ~override~
       ~iwdification/bam/cdiliten.bam~ ~override~
       ~iwdification/vvc/cdiinvoc.vvc~ ~override~
       ~iwdification/wav/cdiep05.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_SYMBOL_OF_PAIN                            \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_SYMBOL_OF_PAIN~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4393) STR_VAR bam_file = cdid714d RET icon END

  ADD_SPELL ~iwdification/spl/cdid714.spl~ 1 7 CLERIC_SYMBOL_OF_PAIN
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4390 // name
    SAY 0x50 @4391 // descript
    SAY 0x21e @4392
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdid714 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 169 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 328 END
      LPF CLONE_EFFECT INT_VAR match_opcode = 54 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR insert = last resource = EVAL ~%DEST_RES%~ END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid714.itm~
    SAY 0x0c @4390
    SAY 0x54 @4391
    LPF cd_scroll INT_VAR unusable0 = (BIT12 + BIT21 + BIT30) target_hdr = 4 range = 35 opcode = 148 target_eff = 1 price = 700 STR_VAR spell = EVAL ~%current_spell_res%~ END // unusable by druid

  COPY ~iwdification/bam/cdid714a.bam~ ~override~
       ~iwdification/bam/cdid714b.bam~ ~override~
       ~iwdification/bam/cdid714c.bam~ ~override~
       ~iwdification/bam/cdid714d.bam~ ~override~
       ~iwdification/wav/cdiee04.wav~  ~override~
       ~iwdification/wav/cdiep02.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_SYMBOL_OF_HOPELESSNESS                    \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_SYMBOL_OF_HOPELESSNESS~) OR override_divine) BEGIN

  ADD_PROJECTILE ~iwdification/pro/cdi277.pro~

  ADD_SPELL ~iwdification/spl/cdid716.spl~ 1 7 CLERIC_SYMBOL_OF_HOPELESSNESS
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4400 // name
    SAY 0x50 @4401 // descript
    SAY 0x1be @3003
    LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi277 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 1280 parameter1 = string_stunned END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid716.itm~
    SAY 0x0c @4400
    SAY 0x54 @4401
    LPF cd_scroll INT_VAR unusable0 = (BIT12 + BIT21 + BIT30) target_hdr = 4 opcode = 148 target_eff = 1 price = 700 STR_VAR spell = EVAL ~%current_spell_res%~ END // cleric

  COPY ~iwdification/bam/cdid716a.bam~ ~override~
       ~iwdification/bam/cdid716b.bam~ ~override~
       ~iwdification/bam/cdid716c.bam~ ~override~
       ~iwdification/bam/cdiparal.bam~ ~override~
       ~iwdification/bam/cdishglp.bam~ ~override~
       ~iwdification/bam/cdisohx.bam~  ~override~
       ~iwdification/vvc/cdiparal.vvc~ ~override~
       ~iwdification/vvc/cdishglp.vvc~ ~override~
       ~iwdification/vvc/cdisohx.vvc~  ~override~
       ~iwdification/wav/cdiee04.wav~  ~override~
       ~iwdification/wav/cdiep02.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_IMPERVIOUS_SANCTITY_OF_MIND               \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_IMPERVIOUS_SANCTITY_OF_MIND~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4410) STR_VAR bam_file = cdid733d RET icon END

  ADD_SPELL ~iwdification/spl/cdid733.spl~ 1 7 CLERIC_IMPERVIOUS_SANCTITY_OF_MIND
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4410 // name
    SAY 0x50 @4411 // descript
    LPF ALTER_EFFECT INT_VAR match_opcode = 206 match_parameter1 = 4742 parameter1 = 0 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37613 parameter1 = string_sleep END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37340 parameter1 = string_uncon END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37605 parameter1 = string_berserk END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35544 parameter1 = string_dominated END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 17392 parameter1 = string_dcharmed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37801 parameter1 = string_charmed END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 20568 parameter1 = string_mf_panic END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35484 parameter1 = string_panic END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37606 parameter1 = string_intoxicated END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37604 parameter1 = string_confused END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 37603 parameter1 = string_rthinking END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14102 parameter1 = string_held END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35606 parameter1 = string_held END
    LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 35542 parameter1 = string_paralyzed END
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 170 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 170 END
      LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 318 opcode = 206 parameter1 = 0 parameter2 = 0 END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid733.itm~
    SAY 0x0c @4410
    SAY 0x54 @4411
    LPF cd_scroll INT_VAR target_hdr = 5 price = 700 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdid733a.bam~ ~override~
       ~iwdification/bam/cdid733b.bam~ ~override~
       ~iwdification/bam/cdid733c.bam~ ~override~
       ~iwdification/bam/cdid733d.bam~ ~override~
       ~iwdification/bam/cdiabjur.bam~ ~override~
       ~iwdification/vvc/cdiabjur.vvc~ ~override~
       ~iwdification/wav/cdiee03.wav~  ~override~
       ~iwdification/wav/cdiep01.wav~  ~override~

END

/////                                                  \\\\\
///// CLERIC_DESTRUCTION                               \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_DESTRUCTION~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid734.spl~ 1 7 CLERIC_DESTRUCTION
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4420 // name
    SAY 0x50 @4421 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdid734 resource = EVAL ~%DEST_RES%~ END
    END ELSE BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 opcode = 177 parameter1 = 4 parameter2 = 3 STR_VAR resource = cdixd734 END // undead are immune
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid734.itm~
    SAY 0x0c @4420
    SAY 0x54 @4421
    LPF cd_scroll INT_VAR unusable0 = (BIT2 + BIT3) range = 1 price = 700 STR_VAR spell = EVAL ~%current_spell_res%~ END // evil

  COPY ~iwdification/bam/cdid734a.bam~ ~override~
       ~iwdification/bam/cdid734b.bam~ ~override~
       ~iwdification/bam/cdid734c.bam~ ~override~
       ~iwdification/bam/cdidestr.bam~ ~override~
       ~iwdification/vvc/cdidestr.vvc~ ~override~
       ~iwdification/wav/cdiep113.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd734.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_GREATER_SHIELD_OF_LATHANDER               \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_GREATER_SHIELD_OF_LATHANDER~) OR override_divine) BEGIN

  LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@4430) STR_VAR bam_file = cdid735d RET icon END

  ADD_SPELL ~iwdification/spl/cdid735.spl~ 1 7 CLERIC_GREATER_SHIELD_OF_LATHANDER
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4430 // name
    SAY 0x50 @4431 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdid735 resource = EVAL ~%DEST_RES%~ END
      LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 165 parameter2 = icon END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 165 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 opcode = 177 parameter1 = 3 parameter2 = 8 STR_VAR resource = cdixd735 END // evil are immune
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid735.itm~
    SAY 0x0c @4430
    SAY 0x54 @4431
    LPF cd_scroll INT_VAR unusable0 = (BIT1 + BIT3 + BIT12 + BIT21 + BIT30) range = 1 price = 700 STR_VAR spell = EVAL ~%current_spell_res%~ END // good cleric

  COPY ~iwdification/bam/cdid735a.bam~ ~override~
       ~iwdification/bam/cdid735b.bam~ ~override~
       ~iwdification/bam/cdid735c.bam~ ~override~
       ~iwdification/bam/cdid735d.bam~ ~override~
       ~iwdification/bam/cdigsol1.bam~ ~override~
       ~iwdification/bam/cdigsol2.bam~ ~override~
       ~iwdification/vvc/cdilatg1.vvc~ ~override~
       ~iwdification/vvc/cdilatg2.vvc~ ~override~
       ~iwdification/wav/cdiafp26.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd735.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_MIST_OF_ELDATH                            \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_MIST_OF_ELDATH~) OR override_divine) BEGIN

  ACTION_IF ee_game BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi307.pro~
  
  END ELSE BEGIN

    ADD_PROJECTILE ~iwdification/pro/cdi307a.pro~

  END

  ADD_SPELL ~iwdification/spl/cdid736.spl~ 1 7 CLERIC_MIST_OF_ELDATH
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4440 // name
    SAY 0x50 @4441 // descript
    PATCH_IF ee_game BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi307 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14022 parameter1 = string_healed END
    END ELSE BEGIN
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi307a END
      LPF DELETE_EFFECT END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 timing = 1 parameter2 = 2 STR_VAR resource = cdid736 END
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid736.itm~
    SAY 0x0c @4440
    SAY 0x54 @4441
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 700 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdid736a.bam~ ~override~
       ~iwdification/bam/cdid736b.bam~ ~override~
       ~iwdification/bam/cdid736c.bam~ ~override~
       ~iwdification/bam/cdimoeld.bam~ ~override~
       ~iwdification/wav/cdiep104.wav~ ~override~

  ACTION_IF !ee_game BEGIN

    LAUNCH_ACTION_FUNCTION cd_create_cloud INT_VAR visloop = 7 cloud_dur = 6 zosa = 1 STR_VAR code = cdid736 anim = cdimoeld END

    COPY ~iwdification/spl/cdid736.spl~ ~override~
      WRITE_LONG 0x08 "-1" // name
      LPF ALTER_SPELL_HEADER INT_VAR projectile = cdi307a END
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14022 parameter1 = string_healed END
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 duration = 6 STR_VAR resource = cdid736 END

  END

END

/////                                                  \\\\\
///// CLERIC_STALKER                                   \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_STALKER~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid737.spl~ 1 7 CLERIC_STALKER
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4450 // name
    SAY 0x50 @4451 // descript

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid737.itm~
    SAY 0x0c @4450
    SAY 0x54 @4451
    LPF cd_scroll INT_VAR unusable0 = (BIT7 + BIT8 + BIT9 + BIT14 + BIT15 + BIT20 + BIT29) target_hdr = 4 range = 100 opcode = 148 target_eff = 1 price = 700 STR_VAR spell = EVAL ~%current_spell_res%~ END
    
  COMPILE ~iwdification/baf/cdishamb.baf~  

  LAF cd_animation STR_VAR code = 7302 END // 29442 MEAE_SH shambling_mound
  COPY ~iwdification/cre/cdishamb.cre~ ~override~
    SAY 0x08 @4452
    SAY 0x0c @4452

  COPY ~iwdification/itm/cdishmbl.itm~ ~override~
    WRITE_LONG 0x08 string_attack
    WRITE_LONG 0x0c string_attack
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = shmblr resource = cdishmbl END
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37709 parameter1 = string_entangled savingthrow = BIT1 END
      LPF ALTER_EFFECT INT_VAR match_opcode = 12 savingthrow = BIT1 END
    END ELSE BEGIN
      LPF DELETE_EFFECT INT_VAR check_globals = 0 END
      LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 177 target = 2 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixd737 END // general = undead
      LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 177 target = 2 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixd737 END // race = golem
      LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 177 target = 2 parameter1 = 145 parameter2 = 4 STR_VAR resource = cdixd737 END // race = elemental
      LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 177 target = 2 parameter1 = 164 parameter2 = 4 STR_VAR resource = cdixd737 END // race = myconid
      LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 146 target = 2 parameter2 = 1 STR_VAR resource = cdishmbl END
    END

  COPY ~iwdification/bam/cdid737a.bam~ ~override~
       ~iwdification/bam/cdid737b.bam~ ~override~
       ~iwdification/bam/cdid737c.bam~ ~override~
       ~iwdification/bam/cdishmbl.bam~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/spl/cdishmbl.spl~ ~override~
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 37709 parameter1 = string_entangled END

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd737.eff~
      WRITE_ASCII 0x30 ~cdishmbl~ #8

  END

END

/////                                                  \\\\\
///// CLERIC_ENERGY_DRAIN                              \\\\\
/////                                                  \\\\\

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_ENERGY_DRAIN~) OR override_divine) BEGIN

  ADD_SPELL ~iwdification/spl/cdid739.spl~ 1 7 CLERIC_ENERGY_DRAIN
    SPRINT current_spell_res "%DEST_RES%"
    SAY 0x08 @4460 // name
    SAY 0x50 @4461 // descript
    SAY 0x2ae @4462
    PATCH_IF ee_game BEGIN
      LPF ALTER_EFFECT STR_VAR match_resource = cdid739 resource = EVAL ~%DEST_RES%~ END
    END ELSE BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdixd739 END // race = golem
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdixd739 END // general = undead
      LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 = 104 opcode = 177                  parameter2 = 4 STR_VAR resource = cdixd739 END // race = parameter1
    END

  COPY ~iwdification/itm/blankpr.itm~ ~override/cdid739.itm~
    SAY 0x0c @4460
    SAY 0x54 @4461
    LPF cd_scroll INT_VAR unusable0 = (BIT2 + BIT3 + BIT12 + BIT21 + BIT30) range = 1 price = 700 STR_VAR spell = EVAL ~%current_spell_res%~ END

  COPY ~iwdification/bam/cdid739a.bam~ ~override~
       ~iwdification/bam/cdid739b.bam~ ~override~
       ~iwdification/bam/cdid739c.bam~ ~override~

  ACTION_IF !ee_game BEGIN

    COPY ~iwdification/eff/immunity.eff~ ~override/cdixd739.eff~
      WRITE_ASCIIE 0x30 ~%current_spell_res%~ #8

  END

END

/////                                                  \\\\\
///// joinable spellbook adjustments                   \\\\\
/////                                                  \\\\\

OUTER_SET NUM_d112 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CURSE~)) - 1000)
OUTER_SET NUM_d114 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CAUSE_LIGHT_WOUNDS~)) - 1000)
OUTER_SET NUM_d115 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SUNSCORCH~)) - 1000)
OUTER_SET NUM_d217 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CURE_MODERATE_WOUNDS~)) - 1000)
OUTER_SET NUM_d218 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_ALICORN_LANCE~)) - 1000)
OUTER_SET NUM_d219 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_BEAST_CLAW~)) - 1000)
OUTER_SET NUM_d220 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CAUSE_MODERATE_WOUNDS~)) - 1000)
OUTER_SET NUM_d316 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_PRAYER~)) - 1000)
OUTER_SET NUM_d320 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CAUSE_DISEASE~)) - 1000)
OUTER_SET NUM_d321 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_EXALTATION~)) - 1000)
OUTER_SET NUM_d322 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_MOONBLADE~)) - 1000)
OUTER_SET NUM_d323 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CIRCLE_OF_BONES~)) - 1000)
OUTER_SET NUM_d324 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SPIKE_GROWTH~)) - 1000)
OUTER_SET NUM_d325 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CLOUDBURST~)) - 1000)
OUTER_SET NUM_d326 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_MOLD_TOUCH~)) - 1000)
OUTER_SET NUM_d327 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_STORM_SHELL~)) - 1000)
OUTER_SET NUM_d330 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CAUSE_MEDIUM_WOUNDS~)) - 1000)
OUTER_SET NUM_d331 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_FAVOR_OF_ILMATER~)) - 1000)
OUTER_SET NUM_d418 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_GIANT_INSECT~)) - 1000)
OUTER_SET NUM_d419 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_PRODUCE_FIRE~)) - 1000)
OUTER_SET NUM_d420 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_STATIC_CHARGE~)) - 1000)
OUTER_SET NUM_d421 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_RECITATION~)) - 1000)
OUTER_SET NUM_d422 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_BLOOD_RAGE~)) - 1000)
OUTER_SET NUM_d423 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CLOUD_OF_PESTILENCE~)) - 1000)
OUTER_SET NUM_d424 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_UNFAILING_ENDURANCE~)) - 1000)
OUTER_SET NUM_d425 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_STAR_METAL_CUDGEL~)) - 1000)
OUTER_SET NUM_d426 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SMASHING_WAVE~)) - 1000)
OUTER_SET NUM_d427 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_THORN_SPRAY~)) - 1000)
OUTER_SET NUM_d428 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_WALL_OF_MOONLIGHT~)) - 1000)
OUTER_SET NUM_d518 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_RIGHTEOUS_WRATH_OF_THE_FAITHFUL~)) - 1000)
OUTER_SET NUM_d519 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SPIKE_STONES~)) - 1000)
OUTER_SET NUM_d520 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SHIELD_OF_LATHANDER~)) - 1000)
OUTER_SET NUM_d521 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_UNDEAD_WARD~)) - 1000)
OUTER_SET NUM_d522 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_ANIMAL_RAGE~)) - 1000)
OUTER_SET NUM_d523 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_MASS_CAUSE_LIGHT_WOUNDS~)) - 1000)
OUTER_SET NUM_d615 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_ENTROPY_SHIELD~)) - 1000)
OUTER_SET NUM_d617 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_WHIRLWIND~)) - 1000)
OUTER_SET NUM_d618 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SPIRITUAL_WRATH~)) - 1000)
OUTER_SET NUM_d714 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SYMBOL_OF_PAIN~)) - 1000)
OUTER_SET NUM_d716 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_SYMBOL_OF_HOPELESSNESS~)) - 1000)
OUTER_SET NUM_d733 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_IMPERVIOUS_SANCTITY_OF_MIND~)) - 1000)
OUTER_SET NUM_d734 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_DESTRUCTION~)) - 1000)
OUTER_SET NUM_d735 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_GREATER_SHIELD_OF_LATHANDER~)) - 1000)
OUTER_SET NUM_d736 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_MIST_OF_ELDATH~)) - 1000)
OUTER_SET NUM_d737 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_STALKER~)) - 1000)
OUTER_SET NUM_d739 = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_ENERGY_DRAIN~)) - 1000)

ACTION_FOR_EACH scroll IN 
  112 114 115 217 218 219 220 316 320 321 322 323 324 325 326 327 330 331 418 419 420 421 422 423 424 425 426 427 428 518 519 520 521 522 523 615 617 618 714 716 734 735 736 737 738 
BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~cdid%scroll%.itm~ BEGIN

    COPY ~iwdification/lib/divine_spellbooks.tpa~ ~iwdification/lib/divine_spellbooks.tpa~
      REPLACE_TEXTUALLY ~//NUM_d%scroll% ~ ~~ // could I be more lazy? maaaaaaybe.

  END

END

REINCLUDE ~iwdification/lib/divine_spellbooks.tpa~

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  READ_LONG 0x1cc bio
  PATCH_IF ((bio > 0) AND (bio < 999999)) BEGIN // joinable...ish
    LPF CD_IWDIFICATION_SPELLBOOKS END
  END
  BUT_ONLY

/////                                                  \\\\\
///// divine spell crosspatching                       \\\\\
/////                                                  \\\\\

ACTION_IF NUM_d422 > 0 BEGIN

  COPY_EXISTING ~sppr%NUM_d422%.spl~ ~override~
    PATCH_IF NUM_d714 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdid714 resource = EVAL ~sppr%NUM_d714%~ END
    END
    PATCH_IF NUM_d716 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdid716 resource = EVAL ~sppr%NUM_d716%~ END
    END
    BUT_ONLY

  ACTION_IF NUM_d321 > 0 BEGIN
  
    COPY_EXISTING ~sppr%NUM_d321%.spl~ ~override~
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdid422 resource = EVAL ~sppr%NUM_a422%~ END
      BUT_ONLY
  
  END

END

ACTION_IF NUM_d733 > 0 BEGIN

  COPY_EXISTING ~sppr%NUM_d733%.spl~ ~override~
    PATCH_IF NUM_d422 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdid422 resource = EVAL ~sppr%NUM_d422%~ END
    END
    PATCH_IF NUM_d716 > 0 BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 STR_VAR match_resource = cdid716 resource = EVAL ~sppr%NUM_d716%~ END
    END
    BUT_ONLY

END

ACTION_IF !ee_game BEGIN

  COPY_EXISTING ~cdibclaw.bam~ ~override~
                ~cdimoonb.bam~ ~override~
                ~cdismcud.bam~ ~override~
    READ_LONG 0x0c frame_off
    WRITE_LONG frame_off + 0x04 0
    IF_EXISTS
    
END

/////                                                  \\\\\
///// arcane-divine cross-patching                     \\\\\
/////                                                  \\\\\

ACTION_IF MOD_IS_INSTALLED ~IWDIFICATION/SETUP-IWDIFICATION.TP2~ ~30~ THEN BEGIN

  INCLUDE ~iwdification/lib/cross_patch_both.tpa~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// bard song                                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @5000 DESIGNATED 50

APPEND ~clabba01.2da~ ~ABILITYX AP_CDIBAR0 **** GA_CDIBAR1 **** **** **** **** **** **** **** **** CDREPLACE
ABILITYY **** **** GA_CDIBAR2 **** GA_CDIBAR3 **** GA_CDIBAR4 **** GA_CDIBAR5 **** GA_CDIBAR6 CDREPLACE~

COPY_EXISTING ~clabba01.2da~ ~override~
  COUNT_2DA_COLS cols
  FOR (index = 13 ; index < cols ; ++index) BEGIN
    REPLACE_TEXTUALLY ~CDREPLACE~ ~**** CDREPLACE~
  END
  REPLACE_TEXTUALLY ~CDREPLACE~ ~****~
  SET "ability" = 0
  REPLACE_EVALUATE ~^\(ABILITY[0-9A-Z]+\)[ %TAB%]~ BEGIN
    SET "ability" = ("%ability%" + 1)
  END
  ~ABILITY%ability% ~
  PRETTY_PRINT_2DA

ACTION_IF FILE_EXISTS_IN_GAME luba0.2da THEN BEGIN

  COPY_EXISTING ~luba0.2da~ ~override~ // remove enhanced bard song from HLAs
    REPLACE_TEXTUALLY ~^\([0-9]+[ %TAB%]+AP_SPCL920[ %TAB%]+\*[ %TAB%]+\*[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+\)\*\([ %TAB%]+\*[ %TAB%]+\*\)~
      ~\1SPCL920\2~ // create self-reference, never allowing it to be selected
    PRETTY_PRINT_2DA

END

ADD_PROJECTILE ~iwdification/pro/cdivrnp.pro~ // visual range, non-party for siren's yearning

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@5002) STR_VAR bam_file = cdibar0 RET cdibarap = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@5005) STR_VAR bam_file = cdibar0 RET cdibarbp = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@5008) STR_VAR bam_file = cdibar0 RET cdibarcp = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@5011) STR_VAR bam_file = cdibar0 RET cdibardp = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@5014) STR_VAR bam_file = cdibar0 RET cdibarep = icon END
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@5018) STR_VAR bam_file = cdibar0 RET cdibarfp = icon END

COPY ~iwdification/bam/cdibar0.bam~  ~override~
     ~iwdification/bam/cdibara.bam~  ~override~
     ~iwdification/bam/cdibarb.bam~  ~override~
     ~iwdification/bam/cdibarc.bam~  ~override~
     ~iwdification/bam/cdibard.bam~  ~override~
     ~iwdification/bam/cdibare.bam~  ~override~
     ~iwdification/bam/cdibarf.bam~  ~override~
     ~iwdification/spl/cdibar0.spl~  ~override~

ACTION_IF ee_game BEGIN

  STRING_SET 9562 @5020

  COPY ~iwdification/bam/cdibar0.bam~  ~override~
       ~iwdification/spl/cdibare0.spl~ ~override~

END ELSE BEGIN

  COPY ~iwdification/eff/immunity.eff~ ~override/cdibarex.eff~
    WRITE_ASCII 0x30 ~cdibare~ #8

END

COPY ~iwdification/spl/cdibar1.spl~ ~override~ // The Ballad of Three Heroes - Combat Bonuses
  SAY 0x08 @5001
  SAY 0x0c @5001
  SAY 0x50 @5003
  SAY 0x54 @5003

COPY ~iwdification/spl/cdibara.spl~ ~override~
  SAY 0x08 @5002
  SAY 0x0c @5002
  SAY 0x50 @5003
  SAY 0x54 @5003
  PATCH_IF ee_game BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 177 parameter2 = cdibarap END
  END ELSE BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
    LPF DELETE_EFFECT INT_VAR match_opcode = 324 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 177 parameter2 = 40 END
    LPF ALTER_EFFECT INT_VAR match_duration = 7 timing = 10 duration = 100 END
  END

COPY ~iwdification/spl/cdibar2.spl~ ~override~ // The Tale of Curran Strongheart - Immunity Fear
  SAY 0x08 @5004
  SAY 0x0c @5004
  SAY 0x50 @5006
  SAY 0x54 @5006

COPY ~iwdification/spl/cdibarb.spl~ ~override~
  SAY 0x08 @5005
  SAY 0x0c @5005
  SAY 0x50 @5006
  SAY 0x54 @5006
  LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 111111 parameter1 = string_mf_panic END
  LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 222222 parameter1 = string_panic END
  PATCH_IF ee_game BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 178 parameter2 = cdibarbp END
  END ELSE BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
    LPF DELETE_EFFECT INT_VAR match_opcode = 324 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 318 opcode = 206 parameter1 = 0 parameter2 = 0 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 178 parameter2 = 40 END
    LPF ALTER_EFFECT INT_VAR match_duration = 7 timing = 10 duration = 100 END
  END

COPY ~iwdification/spl/cdibar3.spl~ ~override~ // Tymora's Melody - Luck and Skill Bonuses
  SAY 0x08 @5007
  SAY 0x0c @5007
  SAY 0x50 @5009
  SAY 0x54 @5009

COPY ~iwdification/spl/cdibarc.spl~ ~override~
  SAY 0x08 @5008
  SAY 0x0c @5008
  SAY 0x50 @5009
  SAY 0x54 @5009
  PATCH_IF ee_game BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 179 parameter2 = cdibarcp END
  END ELSE BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
    LPF DELETE_EFFECT INT_VAR match_opcode = 324 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 179 parameter2 = 40 END
    LPF ALTER_EFFECT INT_VAR match_duration = 7 timing = 10 duration = 100 END
  END

COPY ~iwdification/spl/cdibar4.spl~ ~override~ // The Song of Kaudies - Resistant to Sound Attacks
  SAY 0x08 @5010
  SAY 0x0c @5010
  SAY 0x50 @5012
  SAY 0x54 @5012

COPY ~iwdification/spl/cdibard.spl~ ~override~
  SAY 0x08 @5011
  SAY 0x0c @5011
  SAY 0x50 @5012
  SAY 0x54 @5012
  LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14002 parameter1 = string_silence END
  LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 14676 parameter1 = string_silenced END
  LPF ALTER_EFFECT INT_VAR match_opcode = 206 match_parameter1 =  4742 parameter1 = string_ineffective END
  PATCH_FOR_EACH res IN 
    POWERWORD_KILL POWERWORD_STUN POWERWORD_BLIND PLAYER1_WORD_OF_POWER IRENICUS_WORD_OF_POWER DEMILICH_DEATH MOON_DOG_FEAR BANSHEE_WAIL ELVEN_GUARD_WAIL // bg2ee, bgee
    WIZARD_SHOUT WIZARD_GREAT_SHOUT INNATE_MOURNFUL_WAIL INNATE_DEATH_KNELL INNATE_WAR_CRY INNATE_UNDYING_LAMENT INNATE_GREAT_ROAR INNATE_HARPY_WAIL_INTERNAL // iwdee, iwdification
  BEGIN
    SET num = IDS_OF_SYMBOL (~spell~ ~%res%~)
    PATCH_IF num > 3999 BEGIN SET num -= 4000 SPRINT prefix spcl END
    PATCH_IF num > 2999 BEGIN SET num -= 3000 SPRINT prefix spin END
    PATCH_IF num > 1999 BEGIN SET num -= 2000 SPRINT prefix spwi END
    PATCH_IF num >  999 BEGIN SET num -= 1000 SPRINT prefix sppr END
    PATCH_IF num >  0 BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 3 parameter1 = string_ineffective duration = 7 probability1 = 50 STR_VAR resource = EVAL ~%prefix%%num%~  END
    END
  END
  PATCH_IF ee_game BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 180 parameter2 = cdibardp END
  END ELSE BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
    LPF DELETE_EFFECT INT_VAR match_opcode = 324 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 180 parameter2 = 40 END
    LPF ALTER_EFFECT INT_VAR match_duration = 7 timing = 10 duration = 100 END
  END

COPY ~iwdification/spl/cdibar5.spl~ ~override~ // The Siren's Yearning - Enthralls Creatures
  SAY 0x08 @5013
  SAY 0x0c @5013
  SAY 0x50 @5015
  SAY 0x54 @5015

COPY ~iwdification/spl/cdibare.spl~ ~override~
  SAY 0x08 @5014
  SAY 0x0c @5014
  SAY 0x50 @5015
  SAY 0x54 @5015
  SAY 0x2ae @5016
  LPF ALTER_SPELL_HEADER INT_VAR projectile = cdivrnp END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 267 match_parameter1 = 1280 parameter1 = string_stunned END
  PATCH_IF ee_game BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 181 parameter2 = cdibarep END
  END ELSE BEGIN
    LPF CLONE_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 = 144 parameter2 = 4 STR_VAR resource = cdibarex END // race = golem
    LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  55 opcode = 177 parameter1 =   4 parameter2 = 3 STR_VAR resource = cdibarex END // general = undead
    LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  15 opcode = 177 parameter1 =   2 parameter2 = 4 STR_VAR resource = cdibarex END // race = elf
    LPF ALTER_EFFECT INT_VAR match_opcode = 324 match_parameter2 =  19 opcode = 177 parameter1 =   3 parameter2 = 4 STR_VAR resource = cdibarex END // race = halfelf
    LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
    LPF DELETE_EFFECT INT_VAR match_opcode = 324 END
    LPF DELETE_EFFECT INT_VAR match_opcode = 232 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 181 parameter2 = 40 END
    LPF ALTER_EFFECT INT_VAR match_duration = 7 timing = 10 duration = 100 END
  END

COPY ~iwdification/spl/cdibar6.spl~ ~override~ // War Chant of Sith - Armor Bonuses and Regeneration
  SAY 0x08 @5017
  SAY 0x0c @5017
  SAY 0x50 @5019
  SAY 0x54 @5019

COPY ~iwdification/spl/cdibarf.spl~ ~override~
  SAY 0x08 @5018
  SAY 0x0c @5018
  SAY 0x50 @5019
  SAY 0x54 @5019
  PATCH_IF ee_game BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 182 parameter2 = cdibarfp END
  END ELSE BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
    LPF DELETE_EFFECT INT_VAR match_opcode = 324 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 182 parameter2 = 40 END
    LPF ALTER_EFFECT INT_VAR match_duration = 7 timing = 10 duration = 100 END
  END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// add two-handed axes                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @6000 DESIGNATED 60

COPY ~iwdification/bam/cdax2h1i.bam~ ~override~
     ~iwdification/bam/cdax2h2i.bam~ ~override~
     ~iwdification/bam/cdax2h3i.bam~ ~override~
     ~iwdification/bam/cdax2h4i.bam~ ~override~
     ~iwdification/bam/cdax2h5i.bam~ ~override~
     ~iwdification/bam/cdax2h5i.bam~ ~override~
     ~iwdification/bam/cdax2h5i.bam~ ~override~
     ~iwdification/bam/cdax2h5s.bam~ ~override~
     ~iwdification/bam/cdax2h6i.bam~ ~override~
     ~iwdification/spl/cdax2h5b.spl~ ~override~

COPY ~iwdification/itm/cdax2h1.itm~ ~override~ // generic 2h axe
  SAY 0x08 @6001
  SAY 0x0c @6001
  SAY 0x50 @6002
  SAY 0x54 @6002

COPY ~iwdification/itm/cdax2h2.itm~ ~override~ // +1 axe
  SAY 0x08 @6001
  SAY 0x0c @6003
  SAY 0x50 @6002
  SAY 0x54 @6004

COPY ~iwdification/itm/cdax2h3.itm~ ~override~ // +2 axe
  SAY 0x08 @6001
  SAY 0x0c @6005
  SAY 0x50 @6002
  SAY 0x54 @6006

COPY ~iwdification/itm/cdax2h4.itm~ ~override~ // +3 axe
  SAY 0x08 @6001
  SAY 0x0c @6007
  SAY 0x50 @6002
  SAY 0x54 @6008

COPY ~iwdification/itm/cdax2h5.itm~ ~override~ // +4 axe
  SAY 0x08 @6001
  SAY 0x0c @6009
  SAY 0x50 @6002
  SAY 0x54 @6010

COPY ~iwdification/itm/cdax2h6.itm~ ~override~ // +5 axe
  SAY 0x08 @6001
  SAY 0x0c @6011
  SAY 0x50 @6002
  SAY 0x54 @6012

COPY ~iwdification/spl/cdax2h5g.spl~ ~override~
  SAY 0x08 @6013
  SAY 0x0c @6013
  SAY 0x50 @6013
  SAY 0x54 @6013
  READ_LONG 0x08 string1

COPY ~iwdification/spl/cdax2h6g.spl~ ~override~
  SAY 0x08 @6014
  SAY 0x0c @6014
  SAY 0x50 @6014
  SAY 0x54 @6014
  READ_LONG 0x08 string2

APPEND ~tooltip.2da~ ~cdax2h5 15529 %string1% -1
cdax2h6 15529 %string2% -1~

COPY_EXISTING ~tooltip.2da~ ~override~
  PRETTY_PRINT_2DA

/////                                                  \\\\\
///// places axes in game                              \\\\\
/////                                                  \\\\\

// add generic 2h axes to any store that sells generic 1h axes; same with +1 models
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x34 sale_off ELSE 0
  READ_LONG 0x38 sale_num ELSE 0
  SET ax_idx = 0
  SET ax1_idx = 0
  SET delta = 0
  FOR (index = 0 ; index < sale_num ; ++index) BEGIN
    READ_ASCII (sale_off +        (index * 0x1c)) item
    PATCH_IF (("%item%" STRING_COMPARE_CASE "ax1h01" = 0) OR ("%item%" STRING_COMPARE_CASE "_ax1h01" = 0)) BEGIN // 1h axe
      READ_ASCII (sale_off +        (index * 0x1c)) clone (28) // read entry
      SET ax_idx = (index + 1) // sets right after as insert point
    END ELSE
    PATCH_IF (("%item%" STRING_COMPARE_CASE "ax1h02" = 0) OR ("%item%" STRING_COMPARE_CASE "_ax1h02" = 0)) BEGIN // 1h axe +1
      READ_ASCII (sale_off +        (index * 0x1c)) clone1 (28) // read entry
      SET ax1_idx = (index + 1) // sets right after as insert point
    END
  END
  PATCH_IF (ax1_idx > 0) BEGIN // if 1h axes present, add in 2h axes
    INSERT_BYTES   (sale_off + (ax1_idx * 0x1c)) 28
      WRITE_ASCIIE (sale_off + (ax1_idx * 0x1c)) ~%clone1%~    // clones 1h axe +1 entry
      WRITE_ASCII  (sale_off + (ax1_idx * 0x1c)) ~cdax2h2~ #8 // change to 2h axe +1
    SET delta += 1
  END
  PATCH_IF (ax_idx > 0) BEGIN // if 1h axes present, add in 2h axes
    INSERT_BYTES   (sale_off + (ax_idx * 0x1c)) 28
      WRITE_ASCIIE (sale_off + (ax_idx * 0x1c)) ~%clone%~    // clones 1h axe entry
      WRITE_ASCII  (sale_off + (ax_idx * 0x1c)) ~cdax2h1~ #8 // change to 2h axe
    SET delta += 1
  END
  PATCH_IF (delta > 0) BEGIN
    WRITE_LONG 0x38 (sale_num + delta)
    PATCH_FOR_EACH offset IN 0x2c 0x4c 0x70 BEGIN
      READ_LONG offset off
      PATCH_IF (off > sale_off) BEGIN
        WRITE_LONG offset (THIS + (delta * 0x1c))
      END
    END
  END
  BUT_ONLY

ACTION_IF ((FILE_EXISTS_IN_GAME ~garkla.cre~) OR (FILE_EXISTS_IN_GAME ~_garkla.cre~)) THEN BEGIN // bgt/bgee/tutu

  COPY_EXISTING_REGEXP GLOB ~_?garkla.cre~ ~override~ // garclax at the bandit camp
    REPLACE_CRE_ITEM ~cdax2h3~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP TWOHANDED // add +2 axe to garclax in bandit camp

END

ACTION_IF ((FILE_EXISTS_IN_GAME ~ar0602.bcs~) AND (FILE_EXISTS_IN_GAME ~hlolaf.cre~)) THEN BEGIN // soa

  EXTEND_BOTTOM ~ar0602.bcs~ ~iwdification/baf/ar0602.baf~ // add generic 2H axe to CI

  COPY_EXISTING ~hlolaf.cre~ ~override~
    REPLACE_CRE_ITEM ~cdax2h3~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP TWOHANDED // add +2 axe to olaf at guarded compound
    IF_EXISTS

END

ACTION_IF FILE_EXISTS_IN_GAME ~botsmith.bcs~ THEN BEGIN // tob

  EXTEND_BOTTOM ~ar3017.bcs~ ~iwdification/baf/ar3017.baf~ // add generic 2H axe to wk (pile of normal weapons for magic golems)

  EXTEND_BOTTOM ~botsmith.bcs~ ~iwdification/baf/botsmith.baf~ // cespy's upgrade of the +4 axe to +5
  COMPILE ~iwdification/dlg/botsmith.d~

  // add +3 2h axes to any store that sells generic +3 1h axes (opening stores in ToB)
  COPY_EXISTING ~amsmug01.sto~ ~override~
                ~amsmug02.sto~ ~override~
    ADD_STORE_ITEM ~cdax2h4~ AFTER ~ax1h17~ #0 #0 #0 ~IDENTIFIED~ #1 // +3 2h axe

  // add +3 2h axes to any store that sells generic +3 1h axes (opening stores in ToB)
  COPY_EXISTING ~sarbar01.sto~ ~override~
    ADD_STORE_ITEM ~cdax2h4~ AFTER ~ax1h17~ #0 #0 #0 ~IDENTIFIED~ #3 // +3 2h axe
  
  COPY_EXISTING ~gromg04.cre~ ~override~
    REPLACE_CRE_ITEM ~cdax2h5~ #2 #2 #2 ~NONE~ ~WEAPON1~ EQUIP TWOHANDED // add +4 axe to one of gromnir's bodyguards

END