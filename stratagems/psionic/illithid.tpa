LAF include STR_VAR file=psionic_shared.tph END

DEFINE_ACTION_FUNCTION illithid STR_VAR version="" BEGIN

      LAF define_difficulty STR_VAR type=illithid RET difficulty_variable END

      LAF shared_psionic_resources END
      LAF illithid_resources END
      LAF illithid_scripts END
      LAF illithid_cre END
      LAF illithid_compatibility END
      LAF illithid_rest END


      // alhoon

      LAF check_label STR_VAR label=dw#mage RET value END
      ACTION_IF value BEGIN
          LAF run STR_VAR file=alhoon END
      END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////     Install scripts
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION illithid_scripts BEGIN
 
       LAF ssl_to_bcs STR_VAR script=~flayer01 flayer02 flayun udmaster~ location=~ssl/illithid~ END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////     Modify cre files
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION illithid_cre BEGIN

       COPY_EXISTING_REGEXP GLOB ".*\.cre" override
		PATCH_TRY
            PATCH_IF INDEX_BUFFER ("flayer01")>=0 BEGIN
               PATCH_PRINT "applying illithid changes to %SOURCE_RES%"
               LPF make_illithid END
            END
            PATCH_IF INDEX_BUFFER ("flayer02")>=0 BEGIN
               PATCH_PRINT "applying ulitharid changes to %SOURCE_RES%"
               LPF make_ulitharid END
            END
            PATCH_IF INDEX_BUFFER ("flayun")>=0 BEGIN
               PATCH_PRINT "applying vampiric illithid changes to %SOURCE_RES%"
               LPF make_vampiric_illithid END
            END
		WITH DEFAULT
			LPF regexp_warning STR_VAR parent=illithid_cre END
		END
       BUT_ONLY

       // master brain
       
       MAKE_PATCH
          level=>20
          class=>mage
          illithid_general=>null
          remove_spells=>all
          add_spells=>"PSIONIC_DISPEL_MAGIC PSIONIC_MAZE PSIONIC_DETONATE PSIONIC_BALLISTIC MIND_FLAYER_DOMINATION"
       END
       LAF edit_creature STR_VAR creature=udmaster edits=patch_data END


END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////     Install illithid-specific resources
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION illithid_resources BEGIN

        // make longer-range Ulitharid attack
        
        MAKE_PATCH
           patch_ability_inline=>~ability_rangeGT=>3~
        END
        LAF clone_item STR_VAR item=~mindflay=>dw#ulith~ edits=patch_data END

        // vampiric illithid attacks still ought to drain intelligence 
        MAKE_PATCH
           add_effect_inline=>~match=>"ability_type=1" opcode=>19 target=>2 parameter1=>"-5" duration=>30~
           add_effect_inline'=>~match=>"ability_type=1" opcode=>142 target=>2 parameter2=>86 duration=>30~
           add_effect_inline''=>~match=>"ability_type=1" opcode=>139 timing=>1 target=>2  parameter1=>32089~
        END
        LAF edit_item STR_VAR item=mindfl02 edits=patch_data END

        // spells to boost illithid DR and let them see invisible
        
        LAF resistance_boost_effects INT_VAR bonus=50 STR_VAR spell=dw#illre END
        LAF innate_see_invisible_effects END


END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////     Compatibility fixes
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION illithid_compatibility BEGIN

        // WoP compatibility

	ACTION_IF FILE_EXISTS_IN_GAME ~dw#melis.cre~ THEN BEGIN
	        LAF edit_creature STR_VAR creature=dw#viill editstring=~make_illithid=>null~ END
	END




END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////     Resting in illithid lair
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION illithid_rest BEGIN

// add random illithid spawns

    APPEND_COL ~spawngrp.2da~ ~$ $ RDILLITH 60 mindfl01 mindfl01 mindfl01 umbhul01 umbhul01 mindul01 * *~

// modify spawns in area file

   COPY_EXISTING
          ~ar2400.are~ ~override~
      READ_LONG 0xc0 ~rest_off~
      WRITE_ASCII 0x48+~rest_off~ ~RDILLITH~
      WRITE_SHORT 0x98+~rest_off~ 1
      WRITE_SHORT 0x9a+~rest_off~ 2
      WRITE_SHORT 0xa4+~rest_off~ 6
      WRITE_SHORT 0xa8+~rest_off~ 0



   <<<<<<<< .../stratagems-inline/illithid_rest.ssl
IF TRIGGER
  !Global("DMWWRestSet","MYAREA",0)
  Global("DMWWOverrideIllithidRest","GLOBAL",0)
  TriggerBlock(EasyMinus)
THEN DO
    	     Action(Literal)
             SetGlobal("DMWWRestSet","MYAREA",0)
    	     SetRestEncounterProbabilityDay(0)
    	     SetRestEncounterProbabilityNight(0)
    	     Continue()
END

IF TRIGGER
  !Global("DMWWRestSet","MYAREA",1)
  Global("DMWWOverrideIllithidRest","GLOBAL",0)
  !Dead("udmaster")
  TriggerBlock(Core)
THEN DO
    	     Action(Literal)
             SetGlobal("DMWWRestSet","MYAREA",1)
    	     SetRestEncounterProbabilityDay(25)
    	     SetRestEncounterProbabilityNight(25)
    	     Continue()
END

IF TRIGGER
  !Global("DMWWRestSet","MYAREA",2)
  Global("DMWWOverrideIllithidRest","GLOBAL",0)
  !Dead("udmaster")
  TriggerBlock(Hard)
THEN DO
    	     Action(Literal)
             SetGlobal("DMWWRestSet","MYAREA",2)
    	     SetRestEncounterProbabilityDay(50)
    	     SetRestEncounterProbabilityNight(50)
    	     Continue()
END

IF TRIGGER
  !Global("DMWWRestSet","MYAREA",3)
  Global("DMWWOverrideIllithidRest","GLOBAL",0)
  !Dead("udmaster")
  TriggerBlock(Hard)
THEN DO
    	     Action(Literal)
             SetGlobal("DMWWRestSet","MYAREA",3)
    	     SetRestEncounterProbabilityDay(75)
    	     SetRestEncounterProbabilityNight(75)
    	     Continue()
END

IF
  !Global("DMWWRestSet","MYAREA",0)
  OR(2)
     Dead("udmaster")
     !Global("DMWWOverrideIllithidRest","GLOBAL",0)
THEN
    RESPONSE #100
    	     SetGlobal("DMWWRestSet","MYAREA",2)
    	     SetRestEncounterProbabilityDay(0)
    	     SetRestEncounterProbabilityNight(0)
    	     Continue()
END
>>>>>>>>

   LAF extend_area_script INT_VAR ssl=1 inline=1 STR_VAR area=ar2400 top=illithid_rest END




END

