///////////////////////////////////////////////////////////////////////////////////////
///	Definitions
///////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(!StateCheck\(scstarget,STATE_INVISIBLE\)||OR(2)%WNL%!StateCheck(scstarget,STATE_INVISIBLE)%WNL%Global("see_invisible","LOCALS",1))
BEGIN LOOP(!StateCheck\(scstarget,STATE_NOT_TARGETABLE\)||OR(2)%WNL%!StateCheck(scstarget,STATE_NOT_TARGETABLE)%WNL%Global("see_invisible","LOCALS",1))
BEGIN LOOP(CheckStatLT\(Myself,60,SPELLFAILUREMAGE\)|| )
INCLUDE FILE (%MOD_FOLDER%\lib\ssl\shared_definitions.ssl)
END LOOP
END LOOP
END LOOP

///////////////////////////////////////////////////////////////////////////////////////
///	Redefinitions
///////////////////////////////////////////////////////////////////////////////////////

BEGIN_ACTION_DEFINITION
	Name(Attack)
	TRIGGER
		OR(2)   
                      !StateCheck(scstarget,STATE_INVISIBLE)
                      Global("see_invisible","LOCALS",1)
		!Race(scstarget,SKELETON)
		!Race(scstarget,ELEMENTAL)
		!Race(scstarget,WRAITH)
		!Race(scstarget,SHADOW)
		!Race(scstarget,SLIME)
	ACTION
		RESPONSE #scsprob1
		SetGlobal("validtarget","LOCALS",1)
		SetGlobal("melee","LOCALS",1)
		EquipMostDamagingMelee()
		AttackOneRound(scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(AttackIfEmpty)
	TRIGGER
		OR(2)
                      !StateCheck(scstarget,STATE_INVISIBLE)
                      Global("see_invisible","LOCALS",1)
		!Race(scstarget,SKELETON)
		!Race(scstarget,ELEMENTAL)
		!Race(scstarget,WRAITH)
		!Race(scstarget,SHADOW)
		!Race(scstarget,SLIME)
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		EquipMostDamagingMelee()
		SetGlobal("melee","LOCALS",1)
		AttackOneRound(scstarget)
END


BEGIN_ACTION_DEFINITION
	Name(MoveIfEmpty)
	TRIGGER
		OR(2)
                      !StateCheck(scstarget,STATE_INVISIBLE)
                      Global("see_invisible","LOCALS",1)
		!Race(scstarget,SKELETON)
		!Race(scstarget,ELEMENTAL)
		!Race(scstarget,WRAITH)
		!Race(scstarget,SHADOW)
		!Race(scstarget,SLIME)
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		MoveToObject(scstarget)
END

///////////////////////////////////////////////////////////////////////////////////////
///	Non-interruptable spell use
///////////////////////////////////////////////////////////////////////////////////////

IF
  !Global("no_interrupts","LOCALS",1)
THEN
    RESPONSE #100
             ApplySpellRES("dw#innat",Myself)
             SetGlobal("no_interrupts","LOCALS",1)
             Continue()
END

///////////////////////////////////////////////////////////////////////////////////////
///	Difficulty adjustments
///////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
   TriggerBlock(CorePlus)
   Global("see_invisible","LOCALS",0)
THEN DO
     Action(Literal)
     ApplySpellRES("dw#sinat",Myself)
     SetGlobal("see_invisible","LOCALS",1)
     Continue()
END

IF TRIGGER
   TriggerBlock(EasyMinus)
   Global("see_invisible","LOCALS",1)
THEN DO
     Action(Literal)
     ApplySpellRES("dw#sirem",Myself)
     SetGlobal("see_invisible","LOCALS",0)
     Continue()
END

IF TRIGGER
   TriggerBlock(HardPlus)
   Global("staying_power","LOCALS",0)
THEN DO
     Action(Literal)
     ApplySpellRES("dw#illre",Myself)
     SetGlobal("staying_power","LOCALS",1)
     Continue()
END

IF TRIGGER
   TriggerBlock(CoreMinus)
   Global("staying_power","LOCALS",1)
THEN DO
     Action(Literal)
     ApplySpellRES("dw#rerem",Myself)
     SetGlobal("staying_power","LOCALS",0)
     Continue()
END



//////////////////////////////////////////////////////////
////	When to attack the bad guys (borrowed from Weimer Tactics)
////////////////////////////////////////////////////////////

IF
	!Global("inafight","LOCALS",1)
    OR(7)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
      SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
THEN
  RESPONSE #100
    Enemy()
    SetGlobal("inafight","LOCALS",1)
	SetGlobalTimer("stayontarget","LOCALS",12)
	SetGlobalTimer("seedefences","LOCALS",6)
    Continue()
END

IF	
	!Global("inafight","LOCALS",1)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("inafight","LOCALS",1)
		SetGlobalTimer("stayontarget","LOCALS",12)
		SetGlobalTimer("redefend","LOCALS",3)
		SetGlobalTimer("melee_delay","LOCALS",12)
		Continue()
END

///////////////////////////////////////////////////////////////
///	Start invisible
///////////////////////////////////////////////////////////////

IF TRIGGER
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
	TriggerBlock(HardPlus)
BEGIN LOOP(scsplayer||1;2;3;4;5;6)  // without this loop we ceaselessly re-invisibilise when we can only see charmed people
	!See(Playerscsplayer)
END LOOP
	!StateCheck(Myself,STATE_INVISIBLE)
THEN DO
	        Action(Literal)
		ApplySpellRES("dw#illin",Myself)
END

////////////////////////////////////////////////////////////
////	This is a rest-and-recover step for when
////	enemies leave you alone...
////////////////////////////////////////////////////////////

INCLUDE FILE (%MOD_FOLDER%\lib\ssl\common_rest.ssl)

///////////////////////////////////////////////////
//// seek out foes if taking damage (borrowed from Blucher)
///////////////////////////////////////////////////


IF
	!Detect([PC])
	OR(2)
		HitBy([GOODCUTOFF],CRUSHING)
		See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	HaveSpell(PSIONIC_ASTRAL_TRAVEL)
	BEGIN LOOP(scsplayer||1;2;3;4;5;6)
		InMyArea(Playerscsplayer)
	END LOOP
THEN
BEGIN LOOP(scsplayer||1;2;3;4;5;6)
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpell(Playerscsplayer,PSIONIC_ASTRAL_TRAVEL)
END LOOP
END

IF
	!Detect([PC])
	OR(2)
		HitBy([GOODCUTOFF],CRUSHING)
		See(NearestEnemyOf(Myself))
	HaveSpell(PSIONIC_ASTRAL_TRAVEL)
	Allegiance(Myself,ENEMY)
	BEGIN LOOP(scsplayer||1;2;3)
		InMyArea(Playerscsplayer)
	END LOOP
THEN
BEGIN LOOP(scsplayer||1;2;3)
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpell(Playerscsplayer,PSIONIC_ASTRAL_TRAVEL)
END LOOP
END

IF
	!Detect([PC])
	OR(2)
		HitBy([GOODCUTOFF],CRUSHING)
		See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
		InMyArea(Player1)
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpell(Player1,PSIONIC_ASTRAL_TRAVEL)
END


///////////////////////////////////////////////////////////////////////////////////////
///	Leave clouds
///////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scscloudmelee||!See([PC]))
BEGIN LOOP(scscloudresponse||RunAwayFromNoLeaveArea(LastHeardBy(Myself),30))
INCLUDE FILE(%MOD_FOLDER%\genai\ssl\fleecloud.ssl)
END LOOP
END LOOP

BEGIN LOOP(scscloudmelee||See([PC])!Range([PC],8))
BEGIN LOOP(scscloudresponse||MoveToObject([PC]))
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/fleecloud.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/cloud_stop.ssl)
END LOOP
END LOOP



INCLUDE FILE(%MOD_FOLDER%\genai\ssl\chase_top.ssl)

///////////////////////////////////////////////////////////
///	CPU saver
///////////////////////////////////////////////////////////

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END
