DEFINE_ACTION_FUNCTION shared_psionic_resources BEGIN
 WITH_TRA "%scs_tra_loc%/english/psionic.tra"  "%scs_tra_loc%/%LANGUAGE%/psionic.tra" BEGIN
   OUTER_SPRINT component_loc psionic
   LAF check_label STR_VAR label=dw#psionic_shared RET value=value END
   ACTION_IF value=0 BEGIN

        LAF make_label STR_VAR label=dw#psionic_shared END
        // Chaotic commands needs to give immunity to psionic version of Maze
        MAKE_PATCH
           match=>"resource=%WIZARD_MAZE%"
           resource=>~%PSIONIC_MAZE%~
        END
        LAF edit_spell STR_VAR spell= ~%CLERIC_CHAOTIC_COMMANDS%~ editstring=~clone_effect=>patch_data~ END

	// astral travel (clone of Dimension Door)

        LAF install_spell STR_VAR spell=dw#mftel idsname=PSIONIC_ASTRAL_TRAVEL location=resource editstring=~say_name=>301~ RET newname END
        OUTER_SPRINT PSIONIC_ASTRAL_TRAVEL "%newname%"

        LAF clone_spell STR_VAR spell="%DRYAD_TELEPORT%=>dw#mfesc" editstring=~say_name=>301~ END

	// make prep version of psionic invisibility

        LAF clone_spell STR_VAR spell=~%PSIONIC_SUPERIOR_INVISIBILITY%=>dw#illin~
                                editstring=~delete_effect=>"opcode is_in [139 141 174]"~
        END

	// increase Life Drain damage to 5d6+4

        MAKE_PATCH
           patch_effect_inline=>~match=>"opcode=12"
                               dicenum=>5
                               dicesize=>6~
        END
        LAF edit_spell STR_VAR spell=PSIONIC_LIFE_DRAIN edits=patch_data  END

	// increase Ballistic Attack damage to 5d12

        MAKE_PATCH
           patch_effect_inline=>~match=>"opcode=12"
                               dicenum=>5
                               dicesize=>12~
        END
        LAF edit_spell STR_VAR spell=PSIONIC_BALLISTIC edits=patch_data  END

	// increase Detonate damage to 5d6, and change projectile to an enemy-only one; 
        // remove the (de facto infinite range) TargetParty

        MAKE_PATCH
           patch_effect_inline=>~match=>"opcode=12"
                               dicenum=>5
                               dicesize=>6~
           patch_effect_inline'=>~match=>"target=3"
                                target=>2~
           patch_ability_inline''=>~projectile=>159~
        END
        LAF edit_spell STR_VAR spell=PSIONIC_DETONATE edits=patch_data END

        // make psionic versions of Remove Magic and blur

        LAF add_spell_to_ids STR_VAR typename=innate idsname=PSIONIC_DISPEL_MAGIC RET newname END
        OUTER_SPRINT PSIONIC_DISPEL_MAGIC "%newname%"
        LAF add_spell_to_ids STR_VAR typename=innate idsname=PSIONIC_BLUR RET newname END
        OUTER_SPRINT PSIONIC_BLUR "%newname%"

        LAF clone_spell STR_VAR spell= ~%WIZARD_DISPEL_MAGIC%=>%PSIONIC_DISPEL_MAGIC%~ editstring=~say_name=>300~ END
        LAF clone_spell STR_VAR spell= ~%WIZARD_BLUR%=>%PSIONIC_BLUR%~ editstring="" END

	// give the Mind Blast power a name

	LAF edit_spell STR_VAR spell= ~%PSIONIC_MIND_BLAST%~ editstring=~say_name=>303~ END

	// ensure all psionic powers have CT 0 and bypass MR / dispel

        MAKE_PATCH
           patch_effect_inline=>~resist_dispel=>0~
           patch_ability_inline'=>~casting_time=>0~
        END

        ACTION_FOR_EACH power IN DETONATE BALLISTIC ASTRAL_TRAVEL DOMINATION EGOWHIP EMOTION_KILL MIND_BLAST
                                 MAZE PROJECT_FORCE INERTIAL_BARRIER LIFE_DRAIN SUPERIOR_INVISIBILITY
                                 INFLICT_PAIN DISINTEGRATE DISPEL_MAGIC BLUR
        BEGIN
           OUTER_SPRINT spell EVALUATE_BUFFER ~%PSIONIC_%power%%~
           LAF edit_spell STR_VAR spell edits=patch_data END
        END
        LAF edit_spell STR_VAR spell= ~%MIND_FLAYER_DOMINATION% %MIND_FLAYER_PSIONIC_BLAST% %ULITHARID_PSIONIC_BLAST% 
                                                      %BRAIN_PSIONIC_BLAST% %PSIONIC_ASTRAL_TRAVEL%~  edits=patch_data
        END

        // innate-repeating bit
        
        LAF make_innate_repeating_spell
            INT_VAR overwrite=1 cooldown=12
            STR_VAR arguments="PSIONIC_DETONATE PSIONIC_BALLISTIC PSIONIC_SUPERIOR_INVISIBILITY PSIONIC_EGOWHIP
                               MIND_FLAYER_PSIONIC_BLAST MIND_FLAYER_DOMINATION ULITHARID_PSIONIC_BLAST ULITHARID_CURE_SERIOUS_WOUNDS"
        END

        LAF make_innate_repeating_spell
            INT_VAR overwrite=1 cooldown=18
            STR_VAR arguments="PSIONIC_MAZE PSIONIC_ASTRAL_TRAVEL PSIONIC_MIND_BLAST PSIONIC_BLUR"
        END

        LAF make_innate_repeating_spell
            INT_VAR overwrite=1 cooldown=120
            STR_VAR arguments="PSIONIC_DISPEL_MAGIC"
        END

        // make version of Detonation used for blowing up skeletons
	
	LAF install STR_VAR file=dw#detun.spl location=resource END

	// exempt psionic domination from the Fixpack protections for elves and half-elves

        LAF edit_spell STR_VAR spell= ~%PSIONIC_DOMINATION% %PSIONIC_MIND_BLAST%~ editstring=~delete_opcodes=>177~ END

        // exempt physical psionic powers (ballistic attack, detonate, project force, maze) from blanket protect-from-psionics


        LAF edit_item STR_VAR item=~amul17 misc9y~
                              editstring=~delete_effect=>"opcode=206 and resource is_in [%PSIONIC_BALLISTIC% %PSIONIC_DETONATE% %PSIONIC_PROJECT_FORCE% %PSIONIC_MAZE%]"~
        END
        LAF edit_item STR_VAR item=~sw2h21 c2helm02 c2shld01~ allow_missing=1
                              editstring=~delete_effect=>"opcode=206 and resource is_in [%PSIONIC_BALLISTIC% %PSIONIC_DETONATE% %PSIONIC_PROJECT_FORCE% %PSIONIC_MAZE%]"~
        END
   END // end of outer IF-THEN
  END // end of WITH_TRA
END

////////////////////////////////////////////////////////////////////////////////////////////
///// Used for all illithids
////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION illithid_general BEGIN

      PATCH_MAKE_PATCH
         resist_fire=>0
         resist_cold=>0
         resist_acid=>0
         resist_electricity=>0
         resist_magic_cold=>0
         resist_magic_fire=>0
         morale=>10
         morale_break=>0 // illithids who panic flee by script, astrally
      END
      LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END
END

////////////////////////////////////////////////////////////////////////////////////////////
///// Used for generic flayers
////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION make_illithid BEGIN

      LPF illithid_general END
      PATCH_MAKE_PATCH
           resist_magic=>90       // from mindfl01
           save_vs_deathLT=>5
           save_vs_wandLT=>6
           save_vs_polyLT=>6
           save_vs_breathLT=>5
           save_vs_spellLT=>5
           levelGT=>12
           remove_spells=>all
           add_spells=>"PSIONIC_MAZE PSIONIC_DETONATE PSIONIC_BALLISTIC MIND_FLAYER_DOMINATION MIND_FLAYER_PSIONIC_BLAST PSIONIC_SUPERIOR_INVISIBILITY PSIONIC_ASTRAL_TRAVEL"
           strip_scs_scripts=>null
      END
      LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END

////////////////////////////////////////////////////////////////////////////////////////////
///// Used for ulitharids
////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION make_ulitharid BEGIN

      LPF illithid_general END
      PATCH_MAKE_PATCH
           resist_magic=>95       // from mindul01
           save_vs_deathLT=>3
           save_vs_wandLT=>4
           save_vs_polyLT=>4
           save_vs_breathLT=>4
           save_vs_spellLT=>3
           levelGT=>14
           swap_items=>~mindflay=>dw#ulith~
           remove_spells=>all
           add_spells=>"PSIONIC_MAZE PSIONIC_DETONATE PSIONIC_BALLISTIC MIND_FLAYER_DOMINATION ULITHARID_PSIONIC_BLAST PSIONIC_SUPERIOR_INVISIBILITY PSIONIC_ASTRAL_TRAVEL ULITHARID_CURE_SERIOUS_WOUNDS"
           strip_scs_scripts=>null
      END
      LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END

////////////////////////////////////////////////////////////////////////////////////////////
///// Used for vampire illithids
////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION make_vampiric_illithid BEGIN

      LPF illithid_general END
      PATCH_MAKE_PATCH
           resist_magic=>90       // from mindfl01
           resist_coldGT=>50
           resist_electricityGT=>50
           save_vs_deathLT=>5
           save_vs_wandLT=>6
           save_vs_polyLT=>6
           save_vs_breathLT=>5
           save_vs_spellLT=>5
           levelGT=>12
           remove_spells=>all
           add_spells=>"MIND_FLAYER_PSIONIC_BLAST"
      END
      LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END
