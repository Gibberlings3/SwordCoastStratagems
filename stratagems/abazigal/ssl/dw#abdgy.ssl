//////////////////////////////////////////////////////////////////////////
////	Ana control variables:
////
////    DMWWYellowFight:
////    0 = initial
////    1 = fighting till subdual
////    2 = fighting to the death
////    3 = has yielded
//////////////////////////////////////////////////////////////////////////


BEGIN_ACTION_DEFINITION
	Name(vsPMscroll)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		HaveSpell(WIZARD_SPELL_STRIKE)
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		Spell(scstarget,WIZARD_SPELL_STRIKE)
END


//////////////////////////////////////////////////////////////////////////
///	Initial chat
//////////////////////////////////////////////////////////////////////////

IF
	NumTimesTalkedTo(0)
	Detect([PC])
THEN
	RESPONSE #100
		StartCutSceneMode()
		FaceObject([PC])
		Explore()
		MoveViewPoint([1630.958],2)
		Wait(3)
                StartDialogueNoSet(Player1)
END


//////////////////////////////////////////////////////////////////////////
////	Yielding block
//////////////////////////////////////////////////////////////////////////

IF
	Allegiance(Myself,ENEMY)
	!Global("DMWWYellowFight","GLOBAL",1)
	!Global("DMWWYellowFight","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("DMWWYellowFight","GLOBAL",2)
		Continue()
END


IF
	GlobalGT("DMWWYellowFight","GLOBAL",1)
	HasItem("minhp1",Myself)
THEN
	RESPONSE #100
		DestroyItem("minhp1")
END

IF
	HasItem("minhp1",Myself)
	HPPercentLT(Myself,25)
THEN
	RESPONSE #100
		ClearActions(Myself)
		ChangeEnemyAlly(Myself,NEUTRAL)
		StartDialogNoSet(Player1)
END

IF
	Dead("Abazigal")
	!Global("DMWWYellowGloat","GLOBAL",1)
	Detect(Player1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

IF
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		NoAction()
END

///////////////////////////////////////////////////////////////////////
///	Usual pre-battle stuff
///////////////////////////////////////////////////////////////////////


INCLUDE FILE(%MOD_FOLDER%/dragon/ssl/dragondef.ssl)
INCLUDE FILE(%MOD_FOLDER%/dragon/ssl/dragonsetup.ssl)


VARIABLE(/*slowspell*/=)

////////////////////////////////////////////////////////////////////////////
/// initial buffing
////////////////////////////////////////////////////////////////////////////

IF
	!GlobalTimerNotExpired("initialstoneskin","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("initialstoneskin","LOCALS",1000)
		ApplySpell(Myself,DRAGON_STONE_SKIN)
		ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		Continue()
END



////////////////////////////////////////////////////////////////////////////
////	Chain Contingency, plus dragonfear
////////////////////////////////////////////////////////////////////////////


IF
	!Global("ChainContingencyFired","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("ChainContingencyFired","LOCALS",1)
		DisplayStringHead(Myself,26328)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		ReallyForceSpell(Myself,WIZARD_IMPROVED_HASTE)
		ReallyForceSpell(Myself,SPELL_DEFLECTION_NO_VISUAL)
		ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
		SetGlobalTimer("dragonbreath","LOCALS",1)
		SetGlobalTimer("redefend","LOCALS",3)
		Continue()
END


//////////////////////////////////////////////////////////////////////////
///  Pursue!
///////////////////////////////////////////////////////////////////


INCLUDE FILE(%MOD_FOLDER%/genai/ssl/chase.ssl)

////////////////////////////////////////////////////////////////////////////
////	Contingency
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	GlobalTimerExpired("redefend","LOCALS")
	Global("contingency","LOCALS",0)
	Detect(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(ILackSpellTurning)
THEN DO
		Action(Literal)
                DisplayStringHead(Myself,25942)
		SetGlobal("contingency","LOCALS",1)
		ReallyForceSpellRES("spin710",Myself)
END

/////////////////////////////////////////////////////////////////////////////
////	Wish enemy defences down
/////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/mage/ssl/combatblocks/wish.ssl)

//////////////////////////////////////////////////////////////////////////
///	Use breath weapon as often as possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrderShort)
	CheckStat(scstarget,0,STONESKINS)
	TriggerBlock(AoE)
THEN DO
	Action(DragonBreath,YELLOW_DRAGON_BREATH,"dragonbreath",black05)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	CheckStat(scstarget,0,STONESKINS)
THEN DO
	Action(DragonBreath,YELLOW_DRAGON_BREATH,"dragonbreath",black05)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
THEN DO
	Action(DragonBreath,YELLOW_DRAGON_BREATH,"dragonbreath",black05)
END

//////////////////////////////////////////////////////////////////////////
///	Use wing buffet to drive away anything at all dangerous
//////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%MOD_FOLDER%/dragon/ssl/wingbuffet.ssl)


/////////////////////////////////////////////////////////////////////////////
///	Renew defences
/////////////////////////////////////////////////////////////////////////////


IF TRIGGER	
	!GlobalTimerNotExpired("castspell","LOCALS")
	TriggerBlock(ILackWeaponProtection)
	!GlobalTimerNotExpired("redefend","LOCALS")
	Detect(NearestEnemyOf(Myself))
THEN DO
	Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END

//////////////////////////////////////////////////////////////////////////
////	Fire off a defensive trigger if appropriate
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(Myself)
	GlobalTimerExpired("redefend","LOCALS")
	TriggerBlock(StoneskinFailing)
THEN DO
	Action(SequencerMyself,GREEN_DRAGON_DEFENSIVE_SEQUENCER)
END

IF TRIGGER
	Target(Myself)
	GlobalTimerExpired("redefend","LOCALS")
	HPPercentLT(Myself,50)
	TriggerBlock(SpellDeflect)
THEN DO
	Action(SequencerMyself,GREEN_DRAGON_DEFENSIVE_SEQUENCER)
END

IF TRIGGER
	Target(Myself)
	GlobalTimerExpired("redefend","LOCALS")
	HPPercentLT(Myself,50)
	TriggerBlock(StoneskinFailing)
	!CheckStatGT(Myself,1,STONESKINS)
THEN DO
	Action(SequencerMyself,GREEN_DRAGON_DEFENSIVE_SEQUENCER)
END

IF TRIGGER
	!GlobalTimerNotExpired("castspell","LOCALS")
	TriggerBlock(ILackStoneskin)
	GlobalTimerExpired("redefend","LOCALS")
	Target(Myself)
THEN DO
	Action(SequencerMyself,GREEN_DRAGON_DEFENSIVE_SEQUENCER)
END

IF TRIGGER
        TriggerBlock(ILackImprovedHaste)
THEN DO
	Action(SpellMyself,WIZARD_IMPROVED_HASTE|100|100)
END

////////////////////////////////////////////////////////////////////////
////  Renew anti-magic defences 
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(Myself)
THEN DO
	Action(SequencerMyself,ANADRAMATIS_TRIGGER)
END

IF TRIGGER
	GlobalTimerExpired("redefend","LOCALS")
	See(NearestEnemyOf(Myself))
	TriggerBlock(ILackSpellProtection)
THEN DO
	Action(SpellMyself,WIZARD_SPELL_TRAP)
END


//////////////////////////////////////////////////////////////////////////
////  Attack enemy defences
/////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////////
////	Hit people with Stoneskin, Chaotic Commands, Protection from Magic
////	Energy, Death Ward or Protection from Magic Weapons with a Breach
////
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
	TriggerBlock(PriorityBreach)
	!GlobalTimerNotExpired("DMWW_breach","LOCALS")
THEN DO
	Combine()
	SetGlobalTimer("DMWW_dispel","LOCALS",12)
	SetGlobalTimer("DMWW_breach","LOCALS",18)
	Action(Spell,WIZARD_BREACH|150|150)
END




IF TRIGGER
	!GlobalTimerNotExpired("DMWW_dispel","LOCALS")
	TargetBlock(PCsInOrder)
	TriggerBlock(DispelTarget|AvoidDispelIgnoreInsects|AvoidDispellingMalison)
	TriggerBlock(SIAbjuration|Enemy)
THEN DO
	Combine()
	SetGlobalTimer("DMWW_dispel","LOCALS",18)
	Action(Spell,WIZARD_DISPEL_MAGIC|100|50)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|NotMalisoned)
	!GlobalTimerNotExpired("malison","LOCALS")
THEN DO
	SetGlobalTimer("malison","LOCALS",24)
	Action(Spell,WIZARD_GREATER_MALISON)
END


IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(Celestials)
	TriggerBlock(Enemy|Helpless|LowerMRTarget)
THEN DO
	Combine()
	Action(SpellArea,WIZARD_LOWER_RESISTANCE|150|50)
	Action(SpellArea,WIZARD_PIERCE_SHIELD|150|50)
END

////////////////////////////////////////////////////////////////////////////
////	Attack PM scrolls
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless|ProtectionFromMagicScroll)
THEN DO
	Action(vsPMscroll|200|50)
END



////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless|SpellProtectionTarget)
THEN DO
	Combine()
	Action(SpellArea,WIZARD_SPELL_STRIKE|100|100)
	Action(SpellArea,WIZARD_PIERCE_SHIELD|100|100)
	Action(SpellArea,WIZARD_RUBY_RAY_OF_REVERSAL|100|100)
	Action(SpellArea,WIZARD_WARDING_WHIP|100|100)
END

///////////////////////////////////////////////////////////////////////////
////	Kill swords (don't worry about other summons)
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([GOODCUTOFF.0.SWORD])
	Gender(scstarget,SUMMONED)
	TriggerBlock(Enemy|Helpless)
	HasItem("morsword",scstarget)
THEN DO
	Combine()
	Action(Spell,WIZARD_DEATH_SPELL|150|50)
	Action(Sequencer,DRAGON_MAGIC_MISSILE_SEQUENCER)
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

/////////////////////////////////////////////////////////////////////////////
/////  Wilting
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	OR(2)
		!Gender(scstarget,SUMMONED)
		HasItem("morsword",scstarget)
	TriggerBlock(ResistMagicDamage|MR|SINecromancy|Enemy|MirrorImage)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Combine()
	Action(Spell,WIZARD_ABI_DALZIMS_HORRID_WILTING|200|100)
END

/////////////////////////////////////////////////////////////////////////////
///	Incendiary Cloud
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|ResistFire|SIEvocation|Enemy)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(SpellArea,WIZARD_INCENDIARY_CLOUD|200|50)
END



/////////////////////////////////////////////////////////////////////////////
/////  Power words
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	OR(2)
		HPPercentLT(scstarget,75)
		!CheckStatGT(scstarget,12,Level)
	OR(3)
		HPPercentLT(scstarget,50)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	TriggerBlock(MR100|SpellDeflect|Slay|SIConjuration|Enemy)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Combine()
	Action(Spell,WIZARD_POWER_WORD_KILL|150|50)
END

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	TriggerBlock(MR100|SpellTurn|Stun|Enemy|Helpless|SIConjuration)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	!CheckSpellState(Myself,TIME_STOP)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|150|50)
END


//////////////////////////////////////////////////////////////////////////
///// Oracle
//////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(TruesightAlreadyCast|IllusionTarget)
	!Allegiance(scstarget,ENEMY)
	TriggerBlock(SIDivination)
THEN DO
	Action(SpellMyself,WIZARD_ORACLE)
END

//////////////////////////////////////////////////////////////////////////
///	Use flame arrows
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;HPPercentLT(scstarget,40))
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR|ResistFire|Enemy|ArrowSafe|SpellTurn|MinorGlobe|MirrorImage|Helpless|SIConjuration)
THEN DO
	Action(Spell,WIZARD_FLAME_ARROW|150|50)
END


//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIEvocation|Enemy)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Combine()
	Action(SpellMarker,WIZARD_MAGIC_MISSILE,ITEM_MM_SEEN)
END



//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs 
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|Enemy)
THEN DO
	Combine()
	Action(SpellMarker,WIZARD_MAGIC_MISSILE,ITEM_MM_SEEN)
END

//////////////////////////////////////////////////////////////////////////////////
/////	Melee phase
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/dragon/ssl/melee.ssl)

////////////////////////////////////////////////////////
///	Closedown: avoid interaction with other scripts
////////////////////////////////////////////////////////////

IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END
