/////////////////////////////////////////////////////////////////////////////////
////	Initial kit setup
//////////////////////////////////////////////////////////////////////////////////

IF                                                                                
       RequireBlock(IsBlackguard)
       !Global("kit_setup","LOCALS",1)
THEN
    RESPONSE #100
             SetGlobal("kit_setup","LOCALS",1)
             ApplySpellRES("spcl104",Myself)
             ApplySpellRES("spcl105",Myself)
             Continue()
END

/////////////////////////////////////////////////////////////////////////////////
////	Let timers cope with Time Stop
//////////////////////////////////////////////////////////////////////////////////

IF
        IgnoreBlock(IsBG1)
        IgnoreBlock(ImmuneToTimestop)
        Global("FIN_TIME_STOP_RUNNING","GLOBAL",1)
        !GlobalTimerNotExpired("timestopreset","LOCALS")
THEN
        RESPONSE #100
                 SetGlobalTimer("castspell","LOCALS",22)
                 SetGlobalTimer("timestopreset","LOCALS",18)
END


//////////////////////////////////////////////////////////////////////////////////
////	Deal with rogue undead
//////////////////////////////////////////////////////////////////////////////////

IF
	AttackedBy([ENEMY.UNDEAD],DEFAULT)
	Allegiance(LastAttackerOf(Myself),ENEMY)
	General(LastAttackerOf(Myself),UNDEAD)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		ClearActions(LastAttackerOf(Myself))
		Continue()
END


//////////////////////////////////////////////////////////////////////////////////
////  Look for illusion-detecting rogues
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/lib/ssl/thief_detect_illusion.ssl)

//////////////////////////////////////////////////////////////////////
////	Thief types are invisible where possible
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsThief|IsAssassin|IsStalker)
	TriggerBlock(EasyPlus)
	OnCreation()
	!See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN DO
                 Action(Literal)
    	         ApplySpellRES("dw#silin",Myself)
END

IF TRIGGER
	RequireBlock(IsThief|IsAssassin|IsStalker)
	TriggerBlock(EasyPlus)
	OnCreation()
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN DO
                Action(Literal)
		ApplySpellRES("dw#silin",Myself)
		SetGlobal("backstabplan","LOCALS",1)
		SetGlobal("backstabtimer","LOCALS",6)
END

IF TRIGGER
	RequireBlock(IsThief|IsAssassin|IsStalker)
	TriggerBlock(EasyPlus)
	OnCreation()
	See(NearestEnemyOf(Myself))
	StateCheck(Myself,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN DO
                Action(Literal)
		SetGlobal("backstabplan","LOCALS",1)
                SetGlobalTimer("backstabtimer","LOCALS",6)
END

IF TRIGGER
	RequireBlock(IsThief|IsAssassin|IsStalker)
	TriggerBlock(EasyPlus)
	IgnoreBlock(Demivrgvs)
	!StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_BLIND)
	!StateCheck(Myself,STATE_DISABLED)
	Allegiance(Myself,ENEMY)
	!Detect(NearestEnemyOf(Myself))
	!Detect(Player1)
	!Detect(Player2)
	!Detect(Player3)
	!Detect(Player4)
	!Detect(Player5)
	!Detect(Player6)
THEN DO
                Action(Literal)
		ApplySpellRES("dw#silin",Myself)
END

IF TRIGGER
	RequireBlock(IsThief|IsAssassin|IsStalker)
	RequireBlock(Demivrgvs)
	TriggerBlock(EasyPlus)
	!Global("dvgldust","LOCALS",1)
	!StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_BLIND)
	!StateCheck(Myself,STATE_DISABLED)
	Allegiance(Myself,ENEMY)
	!Detect(NearestEnemyOf(Myself))
	!Detect(Player1)
	!Detect(Player2)
	!Detect(Player3)
	!Detect(Player4)
	!Detect(Player5)
	!Detect(Player6)

THEN DO
                Action(Literal)
		ApplySpellRES("dw#silin",Myself)
END

//////////////////////////////////////////////////////////
////	When to attack the bad guys
////////////////////////////////////////////////////////////

IF TRIGGER 
    OR(2)
         !Global("inafight","LOCALS",1)
         !Allegiance(Myself,ENEMY)
    !Gender(Myself,SUMMONED)
    !Gender(Myself,20)
    TriggerBlock(AttackedByPlayer)
THEN DO
    Action(Literal)
    Enemy()
    SetGlobal("inafight","LOCALS",1)
    SetGlobalTimer("stayontarget","LOCALS",12)
    SetGlobalTimer("seedefences","LOCALS",6)
    Continue()
END

IF
	!Global("inafight","LOCALS",1)
	OR(9)
		See(NearestEnemyOf(Myself))
		Heard([ANYONE],404) // cloudkill
		Heard([ANYONE],405) 
		Heard([ANYONE],406) 
		Heard([ANYONE],407) 
		Heard([ANYONE],408)
		Heard([ANYONE],409)
		Heard([ANYONE],410)
		Heard([ANYONE],411)
THEN
	RESPONSE #100
		SetGlobal("inafight","LOCALS",1)
		SetGlobalTimer("stayontarget","LOCALS",12)
		SetGlobalTimer("seedefences","LOCALS",6)
		Continue()
END

IF
        RequireBlock(NeedsBattleCry)
	!Global("inafight","LOCALS",1)
	See(NearestEnemyOf(Myself))

THEN
	RESPONSE #100
		VerbalConstant(Myself,BATTLE_CRY)
		Continue()
	RESPONSE #100
		VerbalConstant(Myself,10)       // BATTLE_CRY2 - not in SOUNDOFF.ids
		Continue()
END

////////////////////////////////////////////////////////////
////	This is a rest-and-recover step for when
////	enemies leave you alone...
////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("rest","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobal("rest","LOCALS",1)
		Continue()
END

IF
	Global("rest","LOCALS",1)
	!GlobalTimerNotExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("rest","LOCALS",0)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		Rest()
END

///////////////////////////////////////////////////
////	If attacked by an unseen foe, mill around
////	in confusion. You never know, you might
////	bump into them!
//////////////////////////////////////////////////

IF TRIGGER
	Allegiance(Myself,ENEMY)   
	TriggerBlock(EasyPlus)
    !See(NearestEnemyOf(Myself))
    OR(2)
      !GlobalTimerNotExpired("DMWWCuringBlindness","GLOBAL")
      !StateCheck(Myself,STATE_BLIND)
    OR(13)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
      SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
      SpellCastOnMe([PC],0)
      SpellCastOnMe([FAMILIAR],0)
      SpellCastOnMe([ALLY],0)
      SpellCastOnMe([CONTROLLED],0)
      SpellCastOnMe([CHARMED],0)
      StateCheck([ENEMY],STATE_DISABLED)
!GlobalTimerNotExpired("whatthe","LOCALS")
THEN DO
    Action(Literal)
    SetGlobalTimer("whatthe","LOCALS",12)
    Continue()
END

IF
  !See(NearestEnemyOf(Myself))
  !GlobalTimerNotExpired("seencloud","LOCALS")
  GlobalTimerNotExpired("whatthe","LOCALS")
THEN
  RESPONSE #100
    RandomWalkContinuous()
END


BEGIN LOOP(scscloudresponse||RunAwayFromNoLeaveArea(LastHeardBy(Myself),30))
INCLUDE FILE(%scsroot%\genai\ssl\fleecloud.ssl)
END LOOP

INCLUDE FILE(%scsroot%\genai\ssl\chase_top.ssl)