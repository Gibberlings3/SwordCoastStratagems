//////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////
/////	Now the core combat block      ///////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
////	Am I a moron? If so, don't waste time on this "find a target" crap. 
////	Just kill the nearest opponent
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsMoron)
	Target(NearestEnemyOf(Myself))
	TriggerBlock(Enemy)
	Range(scstarget,10)
THEN DO
	Action(MoronAttack)
END



////////////////////////////////////////////////////////////////////////////
////	If I'm not a moron and I'm attacking something I can't hit, STOP!
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(EnhancedEdition)
	IgnoreBlock(IsMoron)
        Target(LastTargetedBy(Myself))
        Global("validtarget","LOCALS",1)
        OR(2)
            !WeaponEffectiveVs(scstarget,MAINHAND)
            !WeaponCanDamage(scstarget,MAINHAND)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END


/////////////////////////////////////////////////////////////////////////////
////	Backstab if you're an invisible thief; 
////	don't attack stoneskinned or mirror imaged targets.
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsThief|IsAssassin|IsStalker)
	StateCheck(Myself,STATE_INVISIBLE)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Helpless|BackstabSafe|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	OR(2)
		Range(scstarget,4)
		Global("backstabplan","LOCALS",1)
THEN DO
	Action(Attack2)
END

/////////////////////////////////////////////////////////////////////////////
////	Kill spellcasters if you're a Wizard Slayer
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsWizardSlayer)
	TargetBlock(PCSpellcasters)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Helpless|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,10)
THEN DO
	Action(Attack)
END


///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////
////    SAVAGE opponents actively begin by trying to kill the helpless
////
////    If not also Smart, they continue to attack the weaker, without
////    paying attention to MI and Stoneskin
///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////

IF TRIGGER
       RequireBlock(IsSavage)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	OR(2)
	      StateCheck(scstarget,STATE_HELPLESS)
	      StateCheck(scstarget,STATE_IMMOBILE)
	Range(scstarget,4)
THEN DO
     Action(Attack)
END

IF TRIGGER
        RequireBlock(IsSavage)
        IgnoreBlock(IsSmart)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Helpless|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	!HPPercentGT(scstarget,20)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
        RequireBlock(IsSavage)
        IgnoreBlock(IsSmart)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Helpless|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	!HPPercentGT(scstarget,50)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
        RequireBlock(IsSavage)
        IgnoreBlock(IsSmart)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Helpless|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END



///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////
////    SMART opponent targetting
////
////	We start by looking at the very near opponents. Go for the 
////	most vulnerable of them. In the first instance, try for PCs with no stoneskins
////	and no mirror images
////	Don't attack the helpless (that is: the stunned etc); don't attack
////	people protected from your weapon.
////
////    If an invisible (or Assassinate-using) thief, still don't attack mirrored/stoneskinned foes
///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(IsSmart)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Helpless|MirrorImage|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	!CheckStatGT(scstarget,0,STONESKINS)
	!HPPercentGT(scstarget,20)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
        RequireBlock(IsSmart)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Helpless|MirrorImage|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	!CheckStatGT(scstarget,0,STONESKINS)
	!HPPercentGT(scstarget,50)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
        RequireBlock(IsSmart)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Helpless|MirrorImage|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	!CheckStatGT(scstarget,0,STONESKINS)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
        RequireBlock(IsSmart)
	IgnoreBlock(IsThief)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Helpless|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
	RequireBlock(IsThief)
	!StateCheck(Myself,STATE_INVISIBLE)
	!CheckStat(Myself,4,SCRIPTINGSTATE4)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Helpless|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
	RequireBlock(IsThief)
	OR(2)
            StateCheck(Myself,STATE_INVISIBLE)
	    CheckStat(Myself,4,SCRIPTINGSTATE4)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Helpless|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe|MirrorImage)
	!CheckStatGT(scstarget,0,STONESKINS)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

///////////////////////////////////////////////////////////////////////
////	... but if there's a nearby target who *is* helpless (and no-one else), scac'em.
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(IsSmart)
        IgnoreBlock(IsHolder)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////
////    BRAVE opponent targetting
////
////	We start by looking at the very near opponents. Go for the 
////	strongest of them (but go for wounded first). In the first instance, try for PCs with no  mirror images
////	Don't attack the helpless (that is: the stunned etc); don't attack
////	people protected from your weapon.
////
///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(IsBrave)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Helpless|MirrorImage|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	!HPPercentGT(scstarget,20)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
        RequireBlock(IsBrave)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Helpless|MirrorImage|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	!HPPercentGT(scstarget,50)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
        RequireBlock(IsBrave)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Helpless|MirrorImage|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
        RequireBlock(IsBrave)
	IgnoreBlock(IsThief)
	TargetBlock(PCsPreferringStrong)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Helpless|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////
////    HOLDER opponent targetting for the non-smart
////
////	Just attack anyone not immobile or helpless
////
///////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsHolder)
	IgnoreBlock(IsSmart)
	Target(NearestEnemyOf(Myself))
	TriggerBlock(Enemy|Helpless)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END



///////////////////////////////////////////////////////////////////////
////	No targets? I'm probably being a bit fastidious about damage resistance
///////////////////////////////////////////////////////////////////////

IF
  Global("inafight","LOCALS",1)  
  ActionListEmpty()
   !GlobalTimerNotExpired("targetcompromise","LOCALS")
THEN 
   RESPONSE #100
             SetGlobalTimer("targetcompromise","LOCALS",24)
END
