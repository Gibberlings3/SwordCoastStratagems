////////////////////////////////////////////////////////////////////////
/////	This block to go in the Initial segment of any
/////	creature that dislikes cloud spells. scsmelee should be
///// 	set to Range([PC],5) if you want to fight in a cloud, to " " 
/////   if you don't (e.g., mages, archers)
/////
////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
///	Storm of Vengeance
////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsBG1)
	IgnoreBlock(IgnoreCloud)
	TriggerBlock(EasyPlus)
	Heard([ANYONE],407)
	Range(LastHeardBy(Myself),20)
	// can I be affected?
	OR(3)
		!CheckStatGT(Myself,99,RESISTACID)
		!CheckStatGT(Myself,99,RESISTFIRE)
		!CheckStatGT(Myself,99,RESISTELECTRICITY)
	// am I either not sufficiently magic-resistant, or in a "safe" situation?
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN DO
                Action(Literal)
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END

///////////////////////////////////////////////////////////////////////
///	Incendiary Cloud / Firestorm / Meteor Swarm
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsBG1)
	IgnoreBlock(IgnoreCloud)
	TriggerBlock(EasyPlus)
	OR(2)
		Heard([ANYONE],405)
		Heard([ANYONE],409)
	Range(LastHeardBy(Myself),20)
	// can I be affected?
	!CheckStatGT(Myself,99,RESISTFIRE)
	// am I either not sufficiently resistant, or in a "safe" situation?
	OR(2)
		!CheckStatGT(Myself,79,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	OR(2)
		!CheckStatGT(Myself,59,RESISTFIRE)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN DO
                Action(Literal)
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END

////////////////////////////////////////////////////////////////////////
///	Death Fog
////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsBG1)
	IgnoreBlock(IsRakshasa)
	IgnoreBlock(IgnoreCloud)
	TriggerBlock(EasyPlus)
	Heard([ANYONE],408)
	Range(LastHeardBy(Myself),20)
	// can I be affected?
	!CheckStatGT(Myself,99,RESISTACID)
	// am I either not sufficiently magic-resistant, or in a "safe" situation?
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	OR(2)
		!CheckStatGT(Myself,59,RESISTACID)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN DO
                Action(Literal)
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END



/////////////////////////////////////////////////////////////////
///     Cloud Kill
/////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IgnoreCloud)
	IgnoreBlock(IsLich|IsRakshasa|IsFiend)
	TriggerBlock(EasyPlus)
	Heard([ANYONE],404)
	Range(LastHeardBy(Myself),20)
	!General(Myself,UNDEAD)
	!Race(Myself,DEMONIC)
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN DO
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END

/////////////////////////////////////////////////////////////////
///    Stinking Cloud
/////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IgnoreCloud)
	IgnoreBlock(IsLich|IsRakshasa|IsFiend)
	TriggerBlock(EasyPlus)
	Heard([ANYONE],411)
	Range(LastHeardBy(Myself),15)
        !CheckStatGT(Myself,0,MINORGLOBE)
	!General(Myself,UNDEAD)
	!Race(Myself,DEMONIC)
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN DO
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END

/////////////////////////////////////////////////////////////////
///     Web
/////////////////////////////////////////////////////////////////

IF TRIGGER
        Target(Myself)
	IgnoreBlock(IsLich|IsRakshasa)
        TriggerBlock(Hold|Antiweb|EasyPlus)
        !CheckStatGT(Myself,0,MINORGLOBE)
        IgnoreBlock(IgnoreCloud)
        Heard([ANYONE],410)
	Range(LastHeardBy(Myself),15)
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN DO
		Action(Literal)
                SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END


/////////////////////////////////////////////////////////////////
///     Ice storm / Writhing Fog
/////////////////////////////////////////////////////////////////

IF TRIGGER
       Target(Myself)
	IgnoreBlock(IsLich|IsRakshasa)
        IgnoreBlock(IgnoreCloud)
        TriggerBlock(EasyPlus)
        Heard([ANYONE],412)
	Range(LastHeardBy(Myself),20)
	!CheckStatGT(Myself,99,RESISTCOLD)

	// am I either not sufficiently magic-resistant, or in a "safe" situation?
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	OR(2)
		!CheckStatGT(Myself,59,RESISTCOLD)
		!See(NearestEnemyOf(Myself))
        scscloudmelee
THEN DO
		Action(Literal)
                SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END

////////////////////////////////////////////////////////////////////////
///	Suffocate and Cloud of Pestilence
////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsBG1)
	IgnoreBlock(IgnoreCloud)
	IgnoreBlock(IsLich|IsVampire|IsRakshasa)
	RequireBlock(IsIWDArcane)
	TriggerBlock(EasyPlus)
	Heard([ANYONE],413)
	Range(LastHeardBy(Myself),10)
	// can I be affected?
	!General(Myself,UNDEAD)
	!Race(Myself,ELEMENTAL)
	!Race(Myself,GOLEM)
	!Race(Myself,MYCONID)
	// am I either not sufficiently magic-resistant, or in a "safe" situation?
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN DO
                Action(Literal)
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END
