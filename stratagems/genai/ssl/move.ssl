/////////////////////////////////////////////////////////////////////////////////
///	A few creatures carry a trigger item to tell them to move only when PCs are within 5
///
///		
///////////////////////////////////////////////////////////////////////////////////////

IF	
	HasItem("dw#move0",Myself)
THEN
	RESPONSE #100
		NoAction()
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////
////	Moronic creatures just look for any worthwhile targets.
///////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsMoron)
	Target(NearestEnemyOf(Myself))
	TriggerBlock(Enemy)
THEN DO
	Action(MoronAttack)
END

///////////////////////////////////////////////////////////////////////
////	If there's someone helpless nearby and you can't find a non-helpless
////    target in weapon range, by all means kill them
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsMoron)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	OR(2)
	      StateCheck(scstarget,STATE_HELPLESS)
	      StateCheck(scstarget,STATE_IMMOBILE)
	Range(scstarget,10)
THEN DO
	Action(MoveIfEmpty)
END

//////////////////////////////////////////////////////////////////////////
////	Intelligent creatures look for PCs - preferably easily killed ones
//// 	- and, failing that, settle for anything.
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(IsWizardSlayer)
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
THEN DO
	Action(Attack)
END

IF TRIGGER
	IgnoreBlock(IsMoron)
	IgnoreBlock(IsThief|IsAssassin|IsStalker)
	ConditionalTargetBlock(PCsPreferringWeak;!HPPercentGT(scstarget,40))
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
THEN DO
	Action(MoveIfEmpty)
END

IF TRIGGER
	IgnoreBlock(IsMoron)
	RequireBlock(IsThief|IsAssassin|IsStalker)
	!StateCheck(Myself,STATE_INVISIBLE)
	!CheckStat(Myself,4,SCRIPTINGSTATE4)
	ConditionalTargetBlock(PCsPreferringWeak;!HPPercentGT(scstarget,40))
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe|ILackPoison)
THEN DO
	Action(MoveIfEmpty)
END

IF TRIGGER
	IgnoreBlock(IsMoron)
	RequireBlock(IsThief|IsAssassin|IsStalker)
	ConditionalTargetBlock(PCsPreferringWeak;!HPPercentGT(scstarget,40))
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe|MirrorImage|PoisonOrInvisible)
	CheckStat(scstarget,0,STONESKINS)

THEN DO
	Action(MoveIfEmpty)
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////    If we've got this far, then there are no good targets in the vicinity. So just kill the nearest PC, 
////    even if they are confused/charmed/whatever. Failing that, kill anything that moves.
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Allegiance(Myself,ENEMY)
	TargetBlock(PlayersInRandomOrder)
	TriggerBlock(PlusMyWeaponStrengthSafe)
THEN DO
	Action(AttackIfEmpty)
END

IF TRIGGER
	Allegiance(Myself,ENEMY)
	Target(NearestEnemyOf(Myself))
	TriggerBlock(PlusMyWeaponStrengthSafe)
THEN DO
	Action(AttackIfEmpty)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	If there's a combat going on and somehow you get here, it probably means you can't find
///	anything remotely worth attacking. Attack something anyway, just in case.
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Allegiance(Myself,ENEMY)
	Target(NearestEnemyOf(Myself))
THEN DO
		Action(AttackIfEmpty)
END




