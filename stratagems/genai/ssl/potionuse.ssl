

/////////////////////////////////////////////////////////
////	First priority: if poisoned, take something
/////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsMoron)
        TriggerBlock(CorePlus)
	StateCheck(Myself,STATE_POISONED)
	!General(Myself,UNDEAD)
THEN DO
	Action(Potion,"dw#ptn17",@101017)
	Action(Potion,"%tutu_var%potn17",@101017)
	Action(Potion,"dw#ptn20",@101017)
	Action(Potion,"%tutu_var%potn20",@101020)
END

//////////////////////////////////////////////////
////	Next, use a superior or extra-healing potion.
////	(We used to use ordinary healing potions
////	here too, but that sometimes gets in the
////	way of battle potions)
////
////	In fact, only take an extra-healing potion
////	here until ToB
////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsMoron)
        TriggerBlock(CorePlus)
	HPPercentLT(Myself,50)
	RequireBlock(IsMonk)
	RequireBlock(IsBG1)
THEN DO
        Action(SpellMyself,MONK_LAY_ON_HANDS)
END

IF TRIGGER
        IgnoreBlock(IsMoron)
        TriggerBlock(CorePlus)
	HPPercentLT(Myself,50)
	IgnoreBlock(IsIWD|IsBG1)
THEN DO
        Action(SpellMyself,MONK_LAY_ON_HANDS)
	Action(Potion,"dw#ptn55",@101055)
	Action(Potion,"potn55",@101055)
	ActionCondition(Potion,"dw#ptn52",@101052;!GlobalGT("Chapter","GLOBAL",%bg2_chapter_7%))
	ActionCondition(Potion,"potn52",@101052;!GlobalGT("Chapter","GLOBAL",%bg2_chapter_7%))
END

IF TRIGGER
        IgnoreBlock(IsMoron)
        TriggerBlock(CorePlus)
	HPPercentLT(Myself,50)
	RequireBlock(IsIWD)
THEN DO
        Action(SpellMyself,MONK_LAY_ON_HANDS)
	Action(Potion,"extheal",@101052)
END

////////////////////////////////////////////////////
///	For the next few, we avoid doing it if invisible
///	since it gives away our position and existence
/////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////
/////	Now, if you've got a battle potion, take it!
/////	First priority is oil of speed, which is completely lethal in
////	the right circumstance. (Check to see if already hasted)
////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsMoron)
        TriggerBlock(CorePlus)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_HASTED)
THEN DO
	Action(Potion,"dw#ptn14",@101014)
	Action(Potion,"%tutu_var%potn14",@101014)
END

/////////////////////////////////////////////////
////	Now strength potions (check to see if already strong
////	enough not to bother)
/////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsMoron)
        TriggerBlock(CorePlus)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	Range(NearestEnemyOf(Myself),5)
	OR(3)
            Class(Myself,FIGHTER_ALL)
            Class(Myself,RANGER_ALL)
            Class(Myself,PALADIN_ALL)
THEN DO
	ActionCondition(Potion,"dw#ptn07",@101007;CheckStatLT(Myself,24,STR))
	ActionCondition(Potion,"%tutu_var%potn07",@101007;CheckStatLT(Myself,24,STR))
	ActionCondition(Potion,"dw#ptn06",@101006;CheckStatLT(Myself,23,STR))
	ActionCondition(Potion,"%tutu_var%potn06",@101006;CheckStatLT(Myself,23,STR))
	ActionCondition(Potion,"dw#ptn05",@101005;CheckStatLT(Myself,22,STR))
	ActionCondition(Potion,"%tutu_var%potn05",@101005;CheckStatLT(Myself,22,STR))
	ActionCondition(Potion,"dw#ptn04",@101004;CheckStatLT(Myself,21,STR))
	ActionCondition(Potion,"%tutu_var%potn04",@101004;CheckStatLT(Myself,21,STR))
	ActionCondition(Potion,"dw#ptn12",@101012;CheckStatLT(Myself,20,STR))
	ActionCondition(Potion,"%tutu_var%potn12",@101012;CheckStatLT(Myself,20,STR))
	ActionCondition(Potion,"dw#ptn03",@101003;CheckStatLT(Myself,19,STR))
	ActionCondition(Potion,"%tutu_var%potn03",@101003;CheckStatLT(Myself,19,STR))
END

/////////////////////////////////////////////////////
////	If you've got a defence potion, take it right
////	away. For crude convenience, we just use a 
////	global to record if we've done so.
////
////	This is for L14+characters.
/////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsMoron)
        TriggerBlock(CorePlus)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	!Global("takendefence","LOCALS",1)
	CheckStatGT(Myself,13,Level)
THEN DO
	Action(Potion,"dw#ptn34",@101034)
	Action(Potion,"dw#ptn35",@101035)
	Action(Potion,"dw#ptn21",@101021)
	Action(Potion,"dw#ptn45",@101045)
	Action(Potion,"%tutu_var%potn34",@101034)
	Action(Potion,"%tutu_var%potn35",@101035)
	Action(Potion,"%tutu_var%potn21",@101021)
	Action(Potion,"%tutu_var%potn45",@101045)
	SetGlobal("takendefence","LOCALS",1)
END

///////////////////////////////////////////////////////////////
///	Failing that, take potions of Power or Heroism. 
///	We use a counter to see if he's taken one already
/////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsMoron)
        TriggerBlock(CorePlus)
  See(NearestEnemyOf(Myself))
!StateCheck(Myself,STATE_INVISIBLE)
 Global("heroptn","LOCALS",0)
  Range(NearestEnemyOf(Myself),5)
THEN DO
    SetGlobal("heroptn","LOCALS",1)
	Action(Potion,"dw#ptn41",@101041)
	Action(Potion,"%tutu_var%potn41",@101041)
	Action(Potion,"dw#ptn09",@101009)
	Action(Potion,"%tutu_var%potn09",@101009)
END

////////////////////////////////////////////////////
////	Finally, a few misc. potions
/////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsMoron)
        TriggerBlock(CorePlus)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
THEN DO
	ActionCondition(Potion,"dw#ptn24",@101024;CheckStatGT(Myself,0,ARMORCLASS))
	ActionCondition(Potion,"%tutu_var%potn24",@101024;CheckStatGT(Myself,0,ARMORCLASS))		
	ActionCondition(Potion,"dw#ptn28",@101028;CheckStatLT(Myself,15,CON))
	ActionCondition(Potion,"%tutu_var%potn28",@101028;CheckStatLT(Myself,15,CON))
	Action(Potion,"dw#ptn42",@101042)
	Action(Potion,"%tutu_var%potn42",@101042)
END

//////////////////////////////////////////////////////////////
////	Now we take ordinary healing potions
//////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsMoron)
        TriggerBlock(CorePlus)
	!General(Myself,UNDEAD)
	HPPercentLT(Myself,75)
THEN DO
	Action(Potion,"dw#ptn08",@101008)
	Action(Potion,"%tutu_var%potn08",@101008)
END


