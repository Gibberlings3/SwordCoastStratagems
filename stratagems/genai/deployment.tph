DEFINE_ACTION_FUNCTION deployment BEGIN

     LAF ssl_to_bcs STR_VAR script=dw#shdlg location=~genai/ssl~ END

     LAF edit_creature INT_VAR tv=1 STR_VAR creature=~amaran islsir izefia kahrk neira nexlit nimbul ragefa
                                        takiyah tarnes telano teven benjy coksmth droth
                                        ogrmba osmadi slythe zordra~
                               editstring=~swap_script=>"%tutu_var%initdlg=>dw#shdlg"~
     END

     LAF edit_creature STR_VAR creature=~%tutu_scripto%grema02 %tutu_scripto%grema03 %tutu_scripto%grema_d~
                               editstring=~swap_script=>"%tutu_var%initdlg=>dw#shdlg"~
     END

    MAKE_PATCH
       tamoko=>dw#tamok
       marek=>dw#marek
       silke=>dw#silke
       tranzi=>dw#tranz
       mulahe=>dw#mudep
    END
    ACTION_PHP_EACH patch_data AS creature=>script BEGIN
       LAF ssl_to_bcs STR_VAR inline=yes script END
       LAF edit_creature INT_VAR tv=1 STR_VAR creature editstring= ~strip_script=>%script% insert_script_high=>%script%~ END
    END


COPY_EXISTING ~%tutu_var%nevill.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
	REPLACE_TEXTUALLY ~CreateCreature("%tutu_var%HOBGOBA",\[3621.706\],0)~ ~CreateCreatureOffScreen("%tutu_var%HOBGOBA",0)~
	REPLACE_TEXTUALLY ~CreateCreature("%tutu_var%HOBGOBA",\[3631.716\],0)~ ~CreateCreatureOffScreen("%tutu_var%HOBGOBA",0)~
	REPLACE_TEXTUALLY ~CreateCreature("%tutu_var%HOBGOBA",\[3611.696\],0)~ ~CreateCreatureOffScreen("%tutu_var%HOBGOBA",0)~
	REPLACE_TEXTUALLY ~CreateCreature("%tutu_var%HOBGOBA",\[3641.706\],0)~ ~CreateCreatureOffScreen("%tutu_var%HOBGOBA",0)~
	REPLACE_TEXTUALLY ~CreateCreature("%tutu_var%HOBGOBA",\[3651.706\],0)~ ~CreateCreatureOffScreen("%tutu_var%HOBGOBA",0)~
	END

COPY_EXISTING ~%tutu_var%baruk.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
	REPLACE_TEXTUALLY ~CreateCreature("%tutu_var%KOBCOMM",\[766.988\],0)~ ~CreateCreatureOffScreen("%tutu_var%KOBCOMM",0)~
	REPLACE_TEXTUALLY ~CreateCreature("%tutu_var%KOBCOMM",\[776.998\],0)~ ~CreateCreatureOffScreen("%tutu_var%KOBCOMM",0)~
	REPLACE_TEXTUALLY ~CreateCreature("%tutu_var%KOBCOMM",\[756.978\],0)~ ~CreateCreatureOffScreen("%tutu_var%KOBCOMM",0)~
	REPLACE_TEXTUALLY ~CreateCreature("%tutu_var%KOBCOMM",\[746.988\],0)~ ~CreateCreatureOffScreen("%tutu_var%KOBCOMM",0)~
	REPLACE_TEXTUALLY ~CreateCreature("%tutu_var%KOBCOMM",\[736.988\],0)~ ~CreateCreatureOffScreen("%tutu_var%KOBCOMM",0)~
	END
	
	
END

/// inline-defined files

<<<<<<<< .../stratagems-inline/dw#silke.ssl
IF TRIGGER
      TriggerBlock(CorePlus)
      See([PC])
      See("Garrick")
      Global("talked","LOCALS",0)
THEN DO
    Action(Literal)
    SetGlobal("talked","LOCALS",1)
    StartDialog("%tutu_var%silke",LastSeenBy(Myself))
END
>>>>>>>>
<<<<<<<< .../stratagems-inline/dw#tranz.ssl
IF TRIGGER
  TriggerBlock(CorePlus)
  Detect([PC])
  NumberOfTimesTalkedTo(0)
THEN DO
    Action(Literal)
    StartDialog("%tutu_var%tranzi",LastSeenBy(Myself))
END
>>>>>>>>
<<<<<<<< .../stratagems-inline/dw#mudep.ssl
IF TRIGGER
  TriggerBlock(CorePlus)
  See([PC])
  NumberOfTimesTalkedTo(0)
THEN DO
    Action(Literal)
    SetGlobal("talked","LOCALS",0)
    StartDialog("%tutu_var%mulahe",LastSeenBy(Myself))
END
>>>>>>>>


<<<<<<<< .../stratagems-inline/dw#marek.ssl
IF TRIGGER
  TriggerBlock(CorePlus)
  See([PC])
  Global("DMWWmarek","GLOBAL",0)
  Global("MarekMove","GLOBAL",1)
THEN DO
    Action(Literal)
    SetGlobal("DMWWmarek","GLOBAL",1)
    StartDialog("%tutu_var%marek",LastSeenBy(Myself))
END
>>>>>>>>
<<<<<<<< .../stratagems-inline/dw#tamok.ssl
IF TRIGGER
  TriggerBlock(CorePlus)
  See([PC])
  Global("DMWWtamoko","GLOBAL",0)
  Global("TamokoMove","GLOBAL",2)
THEN DO
    Action(Literal)
    SetGlobal("DMWWtamoko","GLOBAL",1)
    StartDialog("%tutu_var%tamoko",LastSeenBy(Myself))
END
>>>>>>>>
