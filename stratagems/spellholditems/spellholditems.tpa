DEFINE_ACTION_FUNCTION ~spellholditems~ BEGIN  // mostly legacy code

        //// define the difficulty

        LAF define_difficulty STR_VAR type=spellholditem RET difficulty_variable END

        //// read in familiars (and shapeshift tokens)

        ACTION_IF FILE_EXISTS_IN_GAME "familiar.2da" BEGIN
              COPY_EXISTING "familiar.2da" "%workspace%"
        END ELSE BEGIN
              COPY ~%MOD_FOLDER%/spellholditems/familiar.2da~ ~%workspace%~
        END
	COPY ~%workspace%/familiar.2da~ ~%workspace%~
		COUNT_2DA_ROWS 2 ~rowcount~
		FOR (i=1;i<~%rowcount%~;i=i+1) BEGIN
			READ_2DA_ENTRY i 1 2 entry
			TO_LOWER entry
			SPRINT $item_name_familiars(~%entry%~) ~~
		END
	BUT_ONLY
	ACTION_FOR_EACH token IN "dw#shwe1" "dw#shwe2" "dw#shwe3" "dw#shwol" "dw#shspi" "dw#shwyv" "dw#shsal" "dw#shfir" "dw#shear" BEGIN
			ACTION_TO_LOWER token // guard against own future incompetence
			OUTER_SPRINT $item_name_familiars(~%token%~) ~~	
	END
	
	
	
	// go through all items in game and categorise them
	
	COPY_EXISTING_REGEXP GLOB ".*\.itm" "%workspace%"
		PATCH_TRY
	      SPRINT entry "%SOURCE_RES%"
	      PATCH_IF !VARIABLE_IS_SET $item_name_familiars("%entry%") BEGIN
	         LPF ITM_read_droppable RET value END
	         PATCH_IF value BEGIN
                     LPF ITM_read_category RET cat=value END
                     PATCH_IF cat=36 BEGIN //a bag
                          SPRINT $container_list("%entry%") ""
                     END ELSE BEGIN
                          LPF ITM_is_stackable RET stackable=value END
                          PATCH_IF stackable BEGIN
                             SPRINT $stackable_list("%entry%") ""
                          END ELSE BEGIN
                             SPRINT $item_list("%entry%") ""
                          END
                     END
                 END
	      END
		WITH DEFAULT
			LPF regexp_warning STR_VAR parent=spellholditems END
		END
	BUT_ONLY
	
	// make a stackable-item stealing script

	OUTER_SPRINT stackable_string ""
	ACTION_PHP_EACH stackable_list AS entry=>discard BEGIN
	   OUTER_SPRINT stackable_string ~%stackable_string%TakePartyItemAll("%entry%")~
	END

	// make the bag-stealing file
	
	COPY ~.../stratagems-inline/blank~ ~override/dw#bags.2da~
		INSERT_2DA_ROW 0 0 ~2DA V1.0~
		INSERT_2DA_ROW 1 0 ~XXX~
		INSERT_2DA_ROW 2 0 ~     ITEMS~
		INSERT_2DA_ROW 3 0 ~1 CDPOTION~
		INSERT_2DA_ROW 4 0 ~2 CDPOTBAG~
		INSERT_2DA_ROW 5 0 ~3 CDPLQUIV~
		INSERT_2DA_ROW 6 0 ~4 CDPLCASE~
		INSERT_2DA_ROW 7 0 ~5 CDPLBAG~
		INSERT_2DA_ROW 8 0 ~6 CDAMMOBT~
		INSERT_2DA_ROW 9 0 ~7 CDAMMO~
		INSERT_2DA_ROW 10 0 ~8 CDAMMO2~
		INSERT_2DA_ROW 11 0 ~9 CDPOTIO2~
		SET row=12
		SET number=10
		PHP_EACH container_list AS entry=>discard BEGIN
		   INSERT_2DA_ROW row 0 "%number% %entry%"
		   row+=1
		   number+=1
		END
	BUT_ONLY
	
	// organise the items into list of 200 items

        OUTER_SET item_index=0
        OUTER_SET count=0
        OUTER_SPRINT item_string ""
        ACTION_PHP_EACH item_list AS item=>discard BEGIN
           OUTER_SPRINT $item_index_here("%item%") ""
           OUTER_SET count+=1
           ACTION_IF count=200 BEGIN
              OUTER_SET count=0
              OUTER_SET item_index+=1
              LAF spellhold_item_list_helper INT_VAR item_index STR_VAR item_string RET item_string END
              ACTION_CLEAR_ARRAY item_index_here
           END
        END
        // write the residue
        ACTION_IF count>0 BEGIN
            OUTER_SET item_index+=1
            LAF spellhold_item_list_helper INT_VAR item_index STR_VAR item_string RET item_string END
        END

        // copy over extra items from Tweak Pack
	COPY ~%MOD_FOLDER%/spellholditems/dw#tpitm.2da~ ~override~
	OUTER_SPRINT item_string ~%item_string%TakeItemListParty("dw#tpitm")~

        // make the desk script that actually does all the stealing
        
        <<<<<<<< .../stratagems-inline/dw#shdsk.baf
        IF
          Global("DW_desk_steals","GLOBAL",1)
        THEN
            RESPONSE #100
		     FadeToColor([0.0],0)
                     SetGlobal("DW_desk_steals","GLOBAL",11)
                     %stackable_string%
                     Continue()
        END

        IF
          Global("DW_desk_steals","GLOBAL",11)
        THEN
            RESPONSE #100
		     FadeToColor([0.0],0)
                   SetGlobal("DW_desk_steals","GLOBAL",12)
                   TakeItemListParty("dw#bags")
        END

        IF
          Global("DW_desk_steals","GLOBAL",12)
        THEN
            RESPONSE #100
		     FadeToColor([0.0],0)
                   SetGlobal("DW_desk_steals","GLOBAL",13)
                   %item_string%
        END

        IF
          Global("DW_desk_steals","GLOBAL",13)
        THEN
            RESPONSE #100
		     FadeToColor([0.0],0)
                   SetGlobal("DW_desk_steals","GLOBAL",14)
                   ApplySpell(Player1,NOHOLD_PARTY)
                   ActionOverride(Player1,MakeUnselectable(1))
                     StartCutSceneMode()
                     StartCutScene("Cut41i")
        END
        >>>>>>>>

        COMPILE ".../stratagems-inline/dw#shdsk.baf" EVALUATE_BUFFER

        // install it into area file
        
        MAKE_PATCH
           match=>"container_xloc=1689"
           container_script=>dw#shdsk
        END
        LAF edit_area STR_VAR area=ar1515 editstring="patch_container=>patch_data" END

        // cosmetic tweak to cut41i
        LAF swap_text STR_VAR file=cut41i.bcs swaps="\[20\.0\]=>[0.0]" END

        // install the cutscene that teleports party back to AR1515 so the desk can steal their stuff
        
        LAF install STR_VAR file=dw#shcut.baf  END

        // patch it into Imoen's dream dialog
        
        LAF swap_text STR_VAR file=ppdrimo.dlg swaps="cut41i=>dw#shcut" END

	// remove Clay Golem on Core or lower
	
	MAKE_PATCH
	   match=>"actor_resource=GOLCLA01"
	   script_override=>dw#dsclg
	END
	LAF edit_area STR_VAR area=ar1512 editstring="patch_actor=>patch_data" END
        LAF ssl_to_bcs STR_VAR script=dw#dsclg location=ssl END

        // orog and minotaur spawns with extra gear

        LAF clone_creature STR_VAR creature="icmin01=>dw#sphmi" editstring="add_items=>chan01" END
        LAF clone_creature STR_VAR creature="orc05=>dw#spho1" editstring="add_items=>helm01" END
        LAF clone_creature STR_VAR creature="orc06=>dw#spho2" editstring="remove_items=>leat01 add_items=>~chan04 xbow01 shld03~" END

        // wizard's corpse with a Staff +1

        MAKE_PATCH
           state_dead=>1
           permanent_corpse=>1
        END
        LAF clone_creature STR_VAR creature="mage20=>dw#sphco" edits=patch_data END
        MAKE_PATCH
           script_override=>dw#sphco
           remove_items=>all
           add_items=>"brac02 staf02 potn52(x3)"
        END
        LAF clone_creature STR_VAR creature="mage20=>dw#sphc2" edits=patch_data END


        MAKE_PATCH
           add_item_inline=>~match=>"container_xloc=2506" item_resource=>blun02~
           add_item_inline'0=>~match=>"container_xloc=2506" item_resource=>blun04~
           add_item_inline'1=>~match=>"container_xloc=2506" item_resource=>hamm01~
           add_item_inline'2=>~match=>"container_xloc=1233" item_resource=>staf01~
           add_item_inline'3=>~match=>"container_xloc=1233" item_resource=>sw1h07~
           add_item_inline'4=>~match=>"container_xloc=2656" item_resource=>bow03~
           add_item_inline'5=>~match=>"container_xloc=2656" item_resource=>bow05~
           add_item_inline'6=>~match=>"container_xloc=1483" item_resource=>slng01~
           clone_actor_inline=>"match=>~actor_resource=ppbodhi4~ actor_x_coord=>2026 actor_y_coord=>1782 actor_name=>20323 actor_resource=>dw#sphco"
           clone_actor_inline'=>"match=>~actor_resource=ppbodhi4~ actor_x_coord=>2026 actor_y_coord=>1782 actor_name=>20323 actor_resource=>dw#sphc2"
           patch_trigger_inline=>~match=>"trigger_script=spwnmon" trigger_script=>dw#sphmn~
        END
        LAF edit_area STR_VAR area=ar1512 edits=patch_data END

        LAF install STR_VAR file="dw#sphmn.baf dw#sphco.baf" END


END

DEFINE_ACTION_FUNCTION spellhold_item_list_helper // not fully encapsulated, assumes item_index_here array
      INT_VAR item_index=0
      STR_VAR item_string=""
      RET item_string
BEGIN
              COPY ".../stratagems-inline/blank" "override/dw#spl%item_index%.2da"
			INSERT_2DA_ROW 0 0 ~2DA V1.0~
			INSERT_2DA_ROW 1 0 ~XXX~
                   	INSERT_2DA_ROW 2 0 ~     ITEMS~
                   	SET row=3
                   	SET number=1
                   	PHP_EACH item_index_here AS item_here=>discard BEGIN
                   	   INSERT_2DA_ROW row 0 "%number% %item_here%"
                   	   row+=1
                   	   number+=1
                   	END
              BUT_ONLY
              OUTER_SPRINT item_string ~%item_string%TakeItemListParty("dw#spl%item_index%")~

END



DEFINE_ACTION_FUNCTION spellhold_legacy_bag BEGIN

               // not actually called, but I'll keep it here to avoid losing it
               LAF check_ini STR_VAR ini=spellhold_hardcore RET value END
               ACTION_IF !value BEGIN



		  COPY_EXISTING ~plot01n.itm~ ~override/dw#bdnot.itm~
			SAY 0x54 @2300

		  COPY_EXISTING ~bag04.sto~ ~override~
			ADD_STORE_ITEM ~dw#bdnot~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~shld01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~shld03~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~shld05~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~plat01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~chan01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~chan04~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~leat01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~leat04~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~helm01~ #0 #0 #0 ~NONE~ #3
			ADD_STORE_ITEM ~sw2h01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~sw1h01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~sw1h04~ #0 #0 #0 ~NONE~ #2
			ADD_STORE_ITEM ~sw1h07~ #0 #0 #0 ~NONE~ #2
			ADD_STORE_ITEM ~staf01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~sper01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~arow01~ #0 #0 #0 ~NONE~ #10
			ADD_STORE_ITEM ~bolt01~ #0 #0 #0 ~NONE~ #10
			ADD_STORE_ITEM ~bull01~ #0 #0 #0 ~NONE~ #10
			ADD_STORE_ITEM ~bow01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~bow03~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~bow05~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~xbow04~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~slng01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~blun02~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~blun04~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~hamm01~ #0 #0 #0 ~NONE~ #1	
                END // end of merciful bit


END
