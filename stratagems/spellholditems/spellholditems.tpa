DEFINE_ACTION_FUNCTION ~spellholditems~ BEGIN  // basically legacy code

        LAF define_difficulty STR_VAR type=spellholditem RET difficulty_variable END

//// read in familiars

        ACTION_IF FILE_EXISTS_IN_GAME "familiar.2da" BEGIN
              COPY_EXISTING "familiar.2da" "%workspace%"
        END ELSE BEGIN
              COPY ~%scsroot%/spellholditems/familiar.2da~ ~%workspace%~
        END
	COPY ~%workspace%/familiar.2da~ ~%workspace%~
		COUNT_2DA_ROWS 2 ~rowcount~
		FOR (i=1;i<~%rowcount%~;i=i+1) BEGIN
			READ_2DA_ENTRY ~%i%~ 1 2 ~entry~
			TO_LOWER ~entry~
			SPRINT $item_name_familiars( ~%entry%~) ~~
		END
	BUT_ONLY_IF_IT_CHANGES

	OUTER_SET ~i~=0	
	OUTER_SET ~j~=0
	COPY_EXISTING_REGEXP GLOB ~.+\.itm$~ ~override~
		PATCH_IF !VARIABLE_IS_SET $item_name_familiars( ~%magecheck%~) THEN BEGIN
			PATCH_IF (SOURCE_SIZE>0x1) THEN BEGIN 
				READ_BYTE 0x18 ~droppablebyte~
                                PATCH_IF ((~%droppablebyte%~ BAND ~0b00000100~)=~0b00000100~) THEN BEGIN
					READ_SHORT 0x1c ~containercheck~
					PATCH_IF ~%containercheck%~=36 THEN BEGIN
						SPRINT $containerlist(~%j%~) ~%SOURCE_RES%~
						SET ~j~=~%j%~+1	
					END ELSE BEGIN
						SPRINT $itemlist(~%i%~) ~%SOURCE_RES%~
						SET ~i~=~%i%~+1	
					END
				END   
			END
		END
	BUT_ONLY_IF_IT_CHANGES

	OUTER_SET ~containercount~=~%j%~
	OUTER_SET ~itemcount~=~%i%~

<<<<<<<< .../stratagems-inline/blank.2da

>>>>>>>>

	OUTER_SET ~bagcounter~=0
	COPY ~.../stratagems-inline/blank.2da~ ~override/dw#bags.2da~
		WHILE ~%bagcounter%~<~%containercount%~ BEGIN
			SPRINT ~string~ $containerlist(~%bagcounter%~)
			SET ~number~=~%bagcounter%~+10
			SPRINT ~string~ ~%number%~^~  ~^~%string%~
			INSERT_2DA_ROW ~%bagcounter%~ 0 ~%string%~
			SET ~bagcounter~=~%bagcounter%~+1
		END
		INSERT_2DA_ROW 0 0 ~9 CDPOTIO2~
		INSERT_2DA_ROW 0 0 ~8 CDAMMO2~
		INSERT_2DA_ROW 0 0 ~7 CDAMMO~
		INSERT_2DA_ROW 0 0 ~6 CDAMMOBT~
		INSERT_2DA_ROW 0 0 ~5 CDPLBAG~
		INSERT_2DA_ROW 0 0 ~4 CDPLCASE~
		INSERT_2DA_ROW 0 0 ~3 CDPLQUIV~
		INSERT_2DA_ROW 0 0 ~2 CDPOTBAG~
		INSERT_2DA_ROW 0 0 ~1 CDPOTION~
		INSERT_2DA_ROW 0 0 ~     ITEMS~
		INSERT_2DA_ROW 0 0 ~XXX~
		INSERT_2DA_ROW 0 0 ~2DA V1.0~








	OUTER_SET ~file_index~=0
	OUTER_SET ~globalcounter~=0

	OUTER_FOR (i=0;i<~%itemcount%~;i=i+200) BEGIN
		OUTER_SET ~counter~=0
		OUTER_SET ~file_index~=~%file_index%~+1
		OUTER_SPRINT ~filename~ ~dw#spl~^~%file_index%~
		COPY ~.../stratagems-inline/blank.2da~ ~override/%filename%.2da~
			WHILE ~%counter%~<200 AND ~%globalcounter%~<~%itemcount%~ BEGIN
				SPRINT ~string~ $itemlist(~%globalcounter%~)
				SET ~number~=~%counter%~+1
				SPRINT ~string~ ~%number%~^~  ~^~%string%~
				INSERT_2DA_ROW ~%counter%~ 0 ~%string%~
				SET ~counter~=~%counter%~+1
				SET ~globalcounter~=~%globalcounter%~+1
			END	
			INSERT_2DA_ROW 0 0 ~     ITEMS~
			INSERT_2DA_ROW 0 0 ~XXX~
			INSERT_2DA_ROW 0 0 ~2DA V1.0~
	END

	COPY ~%scsroot%/spellholditems/dw#tpitm.2da~ ~override~
	COPY ~%scsroot%/spellholditems/dw#dritm.2da~ ~override~


	ACTION_IF !(FILE_EXISTS_IN_GAME ~cut41gbp.bcs~) THEN BEGIN // BP Improved Spellhold not installed


		OUTER_SPRINT ~oldstring~ ~ActionOverride("ppireni2",DestroySelf())~
		OUTER_SPRINT ~newstring~ ~%oldstring%~
		OUTER_SPRINT ~newstring~ ~%newstring%~^~ActionOverride("Yoshimo",MakeUnselectable(1))ActionOverride("Yoshimo",LeaveParty())ActionOverride("Yoshimo",ChangeAIScript("",DEFAULT))ActionOverride("Yoshimo",SetDialog("yoshimox"))ActionOverride("Yoshimo",LeaveAreaLUA("AR1516","",[748.1064],6))~
		OUTER_SPRINT ~newstring~ ~%newstring%~^~SetInterrupt(FALSE) ActionOverride("desk1",TakeItemListParty("dw#bags")) Wait(2)ActionOverride("desk1",TakeItemListParty("dw#tpitm"))~
		OUTER_FOR (i=1;i<=~%file_index%~;i=i+1) BEGIN
			OUTER_SPRINT ~supplement~ ~ActionOverride("desk1",TakeItemListParty("dw#spl~ ^ ~%i%~^~"))~
			OUTER_SPRINT ~newstring~ ~%newstring%~^~%supplement%~
		END
		OUTER_SPRINT ~newstring~ ~%newstring%~^~Wait(4) SetInterrupt(TRUE)~
		COPY_EXISTING ~cut41g.bcs~ ~override~
			DECOMPILE_BCS_TO_BAF
			REPLACE_TEXTUALLY ~ActionOverride("Yoshimo",MakeUnselectable(1))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("Yoshimo",LeaveParty())~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("Yoshimo",ChangeAIScript("",DEFAULT))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("Yoshimo",SetDialog("yoshimox"))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("Yoshimo",LeaveAreaLUA("AR1516","",\[748.1064\],6))~ ~~
			REPLACE_TEXTUALLY ~%oldstring%~ ~%newstring%~
			COMPILE_BAF_TO_BCS
		ACTION_IF FILE_EXISTS_IN_GAME ~u!cut41g.bcs~ THEN BEGIN
			COPY_EXISTING ~u!cut41g.bcs~ ~override~
			DECOMPILE_BCS_TO_BAF
			REPLACE_TEXTUALLY ~ActionOverride("Yoshimo",MakeUnselectable(1))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("Yoshimo",LeaveParty())~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("Yoshimo",ChangeAIScript("",DEFAULT))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("Yoshimo",SetDialog("yoshimox"))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("Yoshimo",LeaveAreaLUA("AR1516","",\[748.1064\],6))~ ~~
			REPLACE_TEXTUALLY ~%oldstring%~ ~%newstring%~
			COMPILE_BAF_TO_BCS
		END



		EXTEND_BOTTOM ~ar0028.bcs~ ~%scsroot%/spellholditems/ar0028add.baf~

		EXTEND_TOP ~ar1512.bcs~ ~%scsroot%/spellholditems/ar1512add.baf~

                LAF edit_creature STR_VAR creature="imoen10 imoen15 imoen211 imoen213" editstring="remove_spells=>~MINOR_GLOBE_OF_INVULNERABILITY~ add_spells=>~WIZARD_ENCHANTED_WEAPON~ add_known_spells=>~WIZARD_ENCHANTED_WEAPON WIZARD_REMOVE_CURSE~" END

		LAF check_ini STR_VAR ini=spellhold_easycore RET value END
		ACTION_IF value BEGIN
                  COMPILE ~%scsroot%/spellholditems/cut41l.baf~
		  COMPILE ~%scsroot%/spellholditems/dw#splct.baf~
		  EXTEND_TOP ~ar1516.bcs~ ~%scsroot%/spellholditems/ar1516add.baf~

                  COPY_EXISTING ~ar1515.bcs~ ~override~
			DECOMPILE_AND_PATCH BEGIN
			    REPLACE_TEXTUALLY ~GlobalGT("AsylumPlot","GLOBAL",54)~ ~Global("AsylumPlot","GLOBAL",54)~
			END
                  BUT_ONLY
               END

	END ELSE BEGIN

		OUTER_SPRINT ~oldstring~ ~ActionOverride("desk1",TakeItemListParty("bpbags"))~
		OUTER_SPRINT ~newstring~ ~ActionOverride("desk1",TakeItemListParty("dw#bags"))Wait(2)~
		OUTER_FOR (i=1;i<=~%file_index%~;i=i+1) BEGIN
			OUTER_SPRINT ~supplement~ ~ActionOverride("desk1",TakeItemListParty("dw#spl~ ^ ~%i%~^~"))~
			OUTER_SPRINT ~newstring~ ~%newstring%~^~%supplement%~
		END
		COPY_EXISTING ~cut41gbp.bcs~ ~override~
			DECOMPILE_BCS_TO_BAF
			REPLACE_TEXTUALLY ~%oldstring%~ ~%newstring%~
			REPLACE_TEXTUALLY ~ActionOverride("desk1",TakeItemListParty("BPITEM01"))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("desk1",TakeItemListParty("BPITEM02"))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("desk1",TakeItemListParty("BPITEM03"))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("desk1",TakeItemListParty("BPITEM04"))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("desk1",TakeItemListParty("BPITEM05"))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("desk1",TakeItemListParty("BPITEM06"))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("desk1",TakeItemListParty("BPITEM07"))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("desk1",TakeItemListParty("BPITEM08"))~ ~~
			REPLACE_TEXTUALLY ~ActionOverride("desk1",TakeItemListParty("BPITEM09"))~ ~~
			COMPILE_BAF_TO_BCS


	END

	// remove Clay Golem on Core or lower
	
	MAKE_PATCH
	   match=>"actor_resource=GOLCLA01"
	   script_override=>dw#dsclg
	END
	LAF edit_area STR_VAR area=ar1512 editstring="patch_actor=>patch_data" END
        LAF ssl_to_bcs STR_VAR script=dw#dsclg location=ssl END

        // orog and minotaur spawns with extra gear, plus corpse with staff +1

        LAF clone_creature STR_VAR creature="icmin01=>dw#sphmi" editstring="add_items=>chan01" END
        LAF clone_creature STR_VAR creature="orc05=>dw#spho1" editstring="add_items=>helm01" END
        LAF clone_creature STR_VAR creature="orc06=>dw#spho2" editstring="remove_items=>leat01 add_items=>~chan04 xbow01 shld03~" END

        // wizard's corpse with a Staff +1

        MAKE_PATCH
           state_dead=>1
           permanent_corpse=>1
        END
        LAF clone_creature STR_VAR creature="mage20=>dw#sphco" edits=patch_data END
        MAKE_PATCH
           script_override=>dw#sphco
           remove_items=>all
           add_items=>"brac02 staf02 potn52(x3)"
        END
        LAF clone_creature STR_VAR creature="mage20=>dw#sphc2" edits=patch_data END


        MAKE_PATCH
           add_item_inline=>~match=>"container_xloc=2506" item_resource=>blun02~
           add_item_inline'0=>~match=>"container_xloc=2506" item_resource=>blun04~
           add_item_inline'1=>~match=>"container_xloc=2506" item_resource=>hamm01~
           add_item_inline'2=>~match=>"container_xloc=1233" item_resource=>staf01~
           add_item_inline'3=>~match=>"container_xloc=1233" item_resource=>sw1h07~
           add_item_inline'4=>~match=>"container_xloc=2656" item_resource=>bow03~
           add_item_inline'5=>~match=>"container_xloc=2656" item_resource=>bow05~
           add_item_inline'6=>~match=>"container_xloc=1483" item_resource=>slng01~
           clone_actor_inline=>"match=>~actor_resource=ppbodhi4~ actor_x_coord=>1585 actor_y_coord=>1416 actor_name=>20323 actor_resource=>dw#sphco"
           clone_actor_inline'=>"match=>~actor_resource=ppbodhi4~ actor_x_coord=>1585 actor_y_coord=>1416 actor_name=>20323 actor_resource=>dw#sphc2"
           patch_trigger_inline=>~match=>"trigger_script=spwnmon" trigger_script=>dw#sphmn~
        END
        LAF edit_area STR_VAR area=ar1512 edits=patch_data END

        LAF install STR_VAR file="dw#sphmn.baf dw#sphco.baf" END


END

DEFINE_ACTION_FUNCTION spellhold_legacy_bag BEGIN

               // not actually called, but I'll keep it here to avoid losing it
               LAF check_ini STR_VAR ini=spellhold_hardcore RET value END
               ACTION_IF !value BEGIN



		  COPY_EXISTING ~plot01n.itm~ ~override/dw#bdnot.itm~
			SAY 0x54 @2300

		  COPY_EXISTING ~bag04.sto~ ~override~
			ADD_STORE_ITEM ~dw#bdnot~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~shld01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~shld03~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~shld05~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~plat01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~chan01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~chan04~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~leat01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~leat04~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~helm01~ #0 #0 #0 ~NONE~ #3
			ADD_STORE_ITEM ~sw2h01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~sw1h01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~sw1h04~ #0 #0 #0 ~NONE~ #2
			ADD_STORE_ITEM ~sw1h07~ #0 #0 #0 ~NONE~ #2
			ADD_STORE_ITEM ~staf01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~sper01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~arow01~ #0 #0 #0 ~NONE~ #10
			ADD_STORE_ITEM ~bolt01~ #0 #0 #0 ~NONE~ #10
			ADD_STORE_ITEM ~bull01~ #0 #0 #0 ~NONE~ #10
			ADD_STORE_ITEM ~bow01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~bow03~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~bow05~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~xbow04~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~slng01~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~blun02~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~blun04~ #0 #0 #0 ~NONE~ #1
			ADD_STORE_ITEM ~hamm01~ #0 #0 #0 ~NONE~ #1	
                END // end of merciful bit


END
