INCLUDE ~%scsroot%/lib/ai_wrap.tph~
LAF include STR_VAR file=dragon_shared.tph locbase=dragon END
DEFINE_ACTION_FUNCTION firegiant BEGIN


    ACTION_FOR_EACH func IN giant skull imix burn dragon misc ssl BEGIN
       LAF ~firegiant_%func%~ END
    END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION firegiant_ssl BEGIN

   CLEAR_IDS_MAP
   LAF ssl_to_bcs STR_VAR script=~gorsku01 golbur01 elemimix~ location= ~%component_loc%/ssl~ END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION firegiant_giant BEGIN

       ACTION_FOR_EACH code IN 1 2 3 4 5 6 BEGIN
          LAF clone_creature STR_VAR creature= ~hgwar01=>dw#hgwa%code%~ editstring=~level=>21 enforce_charclass=>is_bg2~ END
       END

       LAF edit_creature STR_VAR creature=hgwar01 editstring=~add_effect_inline=>"opcode=>271" script_override=>dw#mkfgi~ END
       LAF install STR_VAR files=~dw#mkfgi.baf~ location=resource END

       MAKE_PATCH
          ysfire01=>22
          ysfire02=>22
          yscara03=>23
          giafir01=>21
          ysg2=>22
          ysguar01=>22
          yssold10=>21
          hgwar02=>23
       END
       ACTION_PHP_EACH patch_data AS creature=>level BEGIN
          LAF edit_creature STR_VAR creature editstring=~levelGT=>%level% enforce_charclass=>is_bg2~ END
       END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION firegiant_skull BEGIN

   LAF install STR_VAR files=~dw#skrng.itm~ location=resource END
   LAF install STR_VAR files=~balaura.spl~ locbase=~fiend/resource~ END

   MAKE_PATCH
      hitpoints=>133
      resist_slashing=>75
      resist_piercing=>75
      resist_missile=>75
      levelGT=>20
      add_items=>~dw#skrng(WEAPON)~
      add_effect_inline=>~opcode=>297~ // immunity to turning
      immunity_to_spell=>SKULL_EXPLODE
   END
   LAF edit_creature STR_VAR creature=~hgsku01 gorsku01~ edits=patch_data END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION firegiant_burn BEGIN

	COPY_EXISTING ~spbehbla.pro~ ~%workspace%/dw#bmdis.pro~
	ADD_PROJECTILE ~%workspace%/dw#bmdis.pro~

        MAKE_PATCH
           say_name=>2100
           say_description=>3001
           projectile=>"%dw#bmdis%"
        END
        LAF install_spell STR_VAR idsname=BURNING_MAN_BANEFIRE spell=dw#bmdis location=resource RET newname END
        OUTER_SPRINT BURNING_MAN_BANEFIRE "%newname%"
        
        LAF edit_spell STR_VAR spell=BURNING_MAN_CINDER_SHOWER editstring="say_description=>3000" END

        MAKE_PATCH
           BANEFIRE=>12
           CINDER_SHOWER=>36
        END
        ACTION_PHP_EACH patch_data AS spell=>cooldown BEGIN
           LAF make_innate_repeating_spell INT_VAR cooldown overwrite=1 STR_VAR arguments="BURNING_MAN_%spell%" END
        END

        MAKE_PATCH
           immunity_to_spell=>dw#imsta
           add_effect_inline=>~opcode=>193 parameter2=>1~
           add_spells=>"BURNING_MAN_BANEFIRE BURNING_MAN_CINDER_SHOWER"
        END
        LAF edit_creature STR_VAR creature=golbur01 edits=patch_data END

END


////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION firegiant_dragon BEGIN // basically a Firkraag clone

       MAKE_PATCH
          say_both_names=>2102
          insert_script_high=>grpsht01
          replace_items=>~dragring(rring)~
          specifics=>255 // grpsht01 code
       END
       LAF clone_creature STR_VAR creature=~dragred=>dw#ysdra~ edits=patch_data END


       MAKE_PATCH
           actor_baseline=>null
           actor_x_coord=>2068
           actor_y_coord=>1325
           actor_resource=>DW#YSDRA
           actor_name=>Brimstone
       END

       LAF edit_area STR_VAR area=ar5204 editstring="add_actor=>patch_data" END


END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION firegiant_imix BEGIN

       MAKE_PATCH
          str=>23
          hitpoints=>480
          strip_scs_scripts=>null
       END
       LAF edit_creature STR_VAR creature=~elemimix~ edits=patch_data END

       LAF clone_spell STR_VAR spell=~spin695=>dw#imsta~ editstring=~say_name=>2103~ END



      // Traps around Imix shouldn't affect him

      COPY_EXISTING
	~gdarts.bcs~ ~override\dw#gdart.bcs~
	~gspear.bcs~ ~override\dw#gspea.bcs~
      DECOMPILE_AND_PATCH BEGIN
	REPLACE_TEXTUALLY CASE_INSENSITIVE ~\[ANYONE\]~ ~[GOODCUTOFF]~
      END

      LAF swap_text STR_VAR files=~ar5204.are~ swaps=~gdarts=>dw#gdart gspear=>dw#gspea~ END

      // Make Imix's weapon +5
      
      LAF edit_item STR_VAR item=elemimix editstring=~enchantment=>5~ END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION firegiant_misc BEGIN

        // cats

	COPY ~%scsroot%/%component_loc%/resource/dw#fell.itm~ ~override~
		SAY 0x10e @2101

        LAF swap_text STR_VAR files=~gorcamb6.cre gorcamb7.cre hgfel01.cre~ swaps=~P3-12M4=>dw#fell~ END

        // salamanders
        
        LAF install STR_VAR files=~hgsalf.itm~ location=resource END
        
        // fire lich
        
        LAF extend STR_VAR file=itiwall1 top=wall1add location=resource END
        LAF extend STR_VAR file=itiwall2 top=wall2add location=resource END
        LAF extend_area_script STR_VAR area=ar5201 top=initialise location=resource END
        
        // Lava shouldn't affect bad guys
        
        LAF swap_text STR_VAR files=~trlava2.bcs~ swaps=~ANYONE=>GOODCUTOFF~ END

END
