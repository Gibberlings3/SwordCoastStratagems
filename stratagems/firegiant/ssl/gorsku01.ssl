BEGIN_ACTION_DEFINITION
	Name(TimedInnate)
	TRIGGER
		!GlobalTimerNotExpired("scsargument2","LOCALS")
		!CheckStatGT(scstarget,0,SANCTUARY)
		!StateCheck(Myself,STATE_DISABLED)
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		FaceObject(scstarget)
		ForceSpell(scstarget,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(TimedInnateRES)
	TRIGGER
		!GlobalTimerNotExpired("scsargument2","LOCALS")
		!CheckStatGT(scstarget,0,SANCTUARY)
		!StateCheck(Myself,STATE_DISABLED)
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		FaceObject(scstarget)
		ForceSpellRES(scsargument1,scstarget)
END

/////////////////////////////////////////////////////////////////////////////
/////		Explode on death
/////////////////////////////////////////////////////////////////////////////

IF
	Die()
THEN
	RESPONSE #50
		ReallyForceSpellDead(Myself,SKULL_EXPLODE)
		DestroySelf()
END

////////////////////////////////////////////////////////////
////	This is a rest-and-recover step for when
////	enemies leave you alone...
////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%\lib\ssl\common_rest.ssl)

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END

//////////////////////////////////////////////////////////////////////////////////////
///	Begin stoneskinned
//////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("stoneskin","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("stoneskin","LOCALS",1000)
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		Continue()
END

//////////////////////////////////////////////////////////////////////////////////////
///	Incendiary Cloud
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
THEN DO
	Action(TimedInnate,WIZARD_INCENDIARY_CLOUD,belch,36|100|100)
END

///////////////////////////////////////////////////////////////////////////////////////
////	Fireball
///////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR|ResistFire|Enemy|MinorGlobe|SIEvocation)
THEN DO
	Combine()
	Action(TimedInnate,DEATHKNIGHT_FIREBALL,fireball,18|200|100)
END

IF TRIGGER
	TargetBlock(EnemiesInOrder)
THEN DO
	Action(TimedInnate,DEATHKNIGHT_FIREBALL,fireball,18|100|100)
END

/////////////////////////////////////////////////////////////////////////
////   Flame Arrows
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTFIRE))
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(SpellDeflect|MR|Enemy|ArrowSafe|MinorGlobe|MirrorImage|Helpless|SIConjuration)
THEN DO
	Action(TimedInnate,WIZARD_FLAME_ARROW,flamearrow,18|150|50)
END

/////////////////////////////////////////////////////////////////////////////
//	Magma ball
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTFIRE))
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR|Enemy|MinorGlobe)
THEN DO
	Action(TimedInnateRES,"spin539",magma,6)
END

/////////////////////////////////////////////////////////////////////////
////   Flame Arrows  again, this time fine vs spell deflection/turn
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR|Enemy|ArrowSafe|MinorGlobe|MirrorImage|Helpless|SIConjuration)
THEN DO
	Action(TimedInnate,WIZARD_FLAME_ARROW,flamearrow,18|150|50)
END

///////////////////////////////////////////////////////////////////////////////
///	Run away!
///////////////////////////////////////////////////////////////////////////////


IF
	See(NearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),10)
THEN
	RESPONSE #100
		RunAwayFromNoLeaveArea(NearestEnemyOf(Myself),15)
		Continue()
END

////////////////////////////////////////////////////////////////////////////////
////	Seek the heat
////////////////////////////////////////////////////////////////////////////////

IF
	OR(2)
		Heard([ANYONE],405)
		Heard([ANYONE],409)
	!Range(LastHeardBy(Myself),15)
THEN
	RESPONSE #100
		MoveToObject(LastHeardBy(Myself))
END
