IF
   OR(3)
	Die()
	StateCheck(Myself,STATE_REALLY_DEAD)
	!HPPercentGT(Myself,0)
THEN
	RESPONSE #100
		Wait(3)
		ReallyForceSpellDead(Myself,CLERIC_FLAME_STRIKE)
		Wait(1)
		ReallyForceSpellDeadRES("dw#tazsk",Myself)
		Wait(1)
		DestroySelf()
END

IF
        See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("castspell","LOCALS")
	CheckStatLT(Myself,50,RESISTFIRE)
	HasItem("%tutu_var%potn02",Myself)
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		DisplayStringHead(Myself,@101002)
		UseItem("%tutu_var%potn02",Myself)
END		

IF
        See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("castspell","LOCALS")
	HasItem("%tutu_var%potn18",Myself)
	CheckStatLT(Myself,50,RESISTELECTRICITY)
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		DisplayStringHead(Myself,@101018)
		UseItem("%tutu_var%potn18",Myself)
END		

IF
	!Global("potionprep","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("potionprep","LOCALS",1)
		CreateItem("potn52",0,0,0)
		CreateItem("potn52",0,0,0)
		CreateItem("%tutu_var%potn45",0,0,0)
END

IF
	Global("DMWWSarevokFight","GLOBAL",0)
	See(NearestEnemyOf(Myself))
	!Global("DMWWTazokSeesEnemy","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("DMWWTazokSeesEnemy","GLOBAL",1)
END

IF
	Global("DMWWSarevokFight","GLOBAL",0)
THEN
	RESPONSE #100
		NoAction()
END

IF
         RequireBlock(SpellsAreBG1)
	OR(2)
		!Global("DMWWSemajCast","LOCALS",1)
		!GlobalTimerExpired("DMWWTazokTimer","GLOBAL")   // this means he doesn't take generic battle potions till
	!Dead("semaj")				// Semaj's dispel comes in
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
	!See(NearestEnemyOf(Myself))
	GlobalGT("DMWWSarevokFight","GLOBAL",0)
THEN
	RESPONSE #100
		MoveToObject("sarevok")
END


