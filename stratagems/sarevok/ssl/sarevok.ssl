IF
	Global("EndGame","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("EndGame","GLOBAL",-1)
		Continue()
END

IF
	Global("SarevokRun","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("SarevokRun","GLOBAL",-1)
		Continue()
END


IF
	OR(2)
		HPLT(Myself,1)
		Die()
THEN
	RESPONSE #100
		ClearAllActions()
		SetGlobalTimer("DeathOfSarevok","GLOBAL",3)
		MoveViewObject("sarevok",INSTANT)
END

IF
	GlobalTimerExpired("EndGame","GLOBAL")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("EndGame","GLOBAL",100)
		CreateCreature("%tutu_var%WINSKI",[693.578],0) // Winski Perorate
		ActionOverride("winski",ForceSpell(Player1,SAREVOK_STRIKE))
		Wait(2)
		Kill(Player1)
		SetInterrupt(TRUE)
END

IF
	AttackedBy([GOODCUTOFF],DEFAULT)
	Allegiance(Myself,NEUTRAL)
	!Global("SarevokBehavior","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("SarevokBehavior","GLOBAL",3)
		SetGlobalTimer("EndGame","GLOBAL",5)
		Enemy()
END

IF
	See([PC])
	Global("SarevokBehavior","GLOBAL",0)
	Dead("liia") // Liia Jannath
	Dead("belt") // Belt
THEN
	RESPONSE #100
		StartDialog("%tutu_var%SAREVO",[PC])
END

IF
	IgnoreBlock(IsTrilogy)
        See([PC])
	GlobalTimerExpired("SarevokRun","GLOBAL")
	Global("SarevokBehavior","GLOBAL",4)
	!Exists("winski")
THEN
	RESPONSE #100
		CreateCreature("%tutu_var%WINSKI",[693.578],0) // Winski Perorate
		TriggerActivation("door0200",TRUE)
		TriggerActivation("door0109a",TRUE)
		TriggerActivation("door0109b",TRUE)
		TriggerActivation("door0111",TRUE)
		StartCutSceneMode()
		StartCutScene("%tutu_scripte%NDCUT01")
END

IF
	RequireBlock(IsTrilogy)
        See([PC])
	GlobalTimerExpired("SarevokRun","GLOBAL")
	Global("SarevokBehavior","GLOBAL",4)
	!Exists("winski")
THEN
	RESPONSE #100
		CreateCreature("%tutu_var%WINSKI",[693.578],0) // Winski Perorate
		TriggerActivation("door7300",TRUE)
		TriggerActivation("door7209a",TRUE)
		TriggerActivation("door7209b",TRUE)
		TriggerActivation("door7211",TRUE)
		StartCutSceneMode()
		StartCutScene("%tutu_scripte%NDCUT01")
END

IF
	Detect([PC])
	Global("SarevokBehavior","GLOBAL",2)
	Global("DMWWSarevokFight","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("DMWWSarevokFight","GLOBAL",1)
		StartDialog("%tutu_var%SAREVO",LastSeenBy(Myself))
		SetGlobalTimer("SarevokBanter","LOCALS",9)
		SetGlobalTimer("SarevokBackup","LOCALS",3)
END

IF
	ActionListEmpty()
	!Detect([PC])
	Global("SarevokBehavior","GLOBAL",2)
	Global("DMWWTazokSeesEnemy","GLOBAL",1)
THEN
	RESPONSE #100
		MoveToObject(Player1)
END
		

IF
	!Global("backup","LOCALS",1)
	GlobalTimerExpired("SarevokBackup","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("backup","LOCALS",1)
		CreateCreatureDoor("%tutu_var%galdor",[965.482],0)
		CreateCreatureDoor("dw#diarm",[362.631],0)
	RESPONSE #100
		SetGlobal("backup","LOCALS",1)
		CreateCreatureDoor("%tutu_var%galdor",[362.631],0)
		CreateCreatureDoor("dw#diarm",[965.482],0)
END

IF
	Dead("galdor")
	HasItem("dw#sarr1",Myself)
	!Global("angelo","LOCALS",1)
THEN	
	RESPONSE #100
		DestroyItem("dw#sarr1")
END	

IF
	Dead("diarmi")
	HasItem("dw#sarr2",Myself)
	!Global("diarmid","LOCALS",1)
THEN	
	RESPONSE #100
		DestroyItem("dw#sarr2")
END	

IF
	Dead("semaj")
	HasItem("dw#sarr3",Myself)
	!Global("semaj","LOCALS",1)
THEN	
	RESPONSE #100
		DestroyItem("dw#sarr3")
END

IF
	Dead("tazok")
	HasItem("dw#sarr4",Myself)
	!Global("tazok","LOCALS",1)
THEN	
	RESPONSE #100
		DestroyItem("dw#sarr4")
END

IF
	!GlobalGT("DMWWSarevokFight","GLOBAL",2)
	Global("FirstDeath","LOCALS",0)
	OR(4)
		Dead("galdor")
		Dead("diarmi")
		Dead("semaj")
                Dead("tazok")
THEN
	RESPONSE #100
		SetGlobal("FirstDeath","LOCALS",1)
		StartDialog("%tutu_var%SAREVO",Player1)
END

IF
	Global("DMWWSarevokFight","GLOBAL",3)
	Global("FourthDeath","LOCALS",0)
	Dead("galdor")
	Dead("diarmi")
	Dead("semaj")
	Dead("tazok")
THEN
	RESPONSE #100
		SetGlobal("FourthDeath","LOCALS",1)
		StartDialog("%tutu_var%SAREVO",Player1)
END


IF
  RequireBlock(IsTrilogy)
  HP(Myself,1)
  Global("SarevokBoutToDie","GLOBAL",0)
  Global("SarevokBehavior","GLOBAL",3)
  Dead("galdor")
  Dead("diarmi")
  Dead("semaj")
  Dead("tazok")
THEN
  RESPONSE #100
    SetGlobal("SarevokBoutToDie","GLOBAL",1)
    ActionOverride(Myself,StartDialogue("SARDEAD",[PC]))  
END

IF
	GlobalTimerExpired("SarevokBanter","LOCALS")
	Global("BanteredYet","LOCALS",0)
	Global("DMWWSarevokFight","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("BanteredYet","LOCALS",1)
		StartDialog("%tutu_var%SAREVO",Player1)
END

IF
	!GlobalTimerExpired("DMWWTazokTimer","GLOBAL")
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
  GlobalGT("DMWWSarevokFight","GLOBAL",0)
  !GlobalTimerNotExpired("castspell","LOCALS") 
  HasItem("%tutu_var%potn14",Myself) 
THEN
  RESPONSE #100
    SetGlobalTimer("castspell","LOCALS",6)
    UseItem("%tutu_var%potn14",Myself) 
    DisplayStringHead(Myself,@101014)
END



IF
  Global("DMWWSarevokFight","GLOBAL",4)	
  !GlobalTimerNotExpired("castspell","LOCALS") 
  HasItem("%tutu_var%potn07",Myself)  
THEN
  RESPONSE #100
    SetGlobalTimer("castspell","LOCALS",6)
    UseItem("%tutu_var%potn07",Myself)
    DisplayStringHead(Myself,@101007)
END

IF
  !GlobalTimerNotExpired("castspell","LOCALS") 
  HPPercentLT(Myself,75)
  HasItem("potn52",Myself)
THEN
  RESPONSE #100
    SetGlobalTimer("castspell","LOCALS",6)
    UseItem("potn52",Myself)
    DisplayStringHead(Myself,@101052)
END

IF
	See(Player1)
	!Range(NearestEnemyOf(Myself),6)
THEN
	RESPONSE #100
		MoveToObject(Player1)
END

IF
	GlobalGT("DMWWSarevokFight","GLOBAL",0)
	!Global("sarevoktaunt","LOCALS",1)
	!See(NearestEnemyOf(Myself))
	!Range(Player1,10)
THEN
	RESPONSE #100
		SetGlobal("sarevoktaunt","LOCALS",1)
		DisplayString(Myself,@22001)
END

IF
	GlobalGT("DMWWSarevokFight","GLOBAL",0)
	!See(NearestEnemyOf(Myself))
	!Range(Player1,10)
THEN
	RESPONSE #100
		MoveToObject(Player1)
END

IF
	Global("DMWWSarevokReset","GLOBAL",1) // for testing
THEN
	RESPONSE #100
		SetGlobal("DMWWSarevokReset","GLOBAL",0)
		SetGlobal("DMWWSarevokBehavior","GLOBAL",1)
		DestroySelf()
END
