DEFINE_PATCH_FUNCTION kit_hardcode
    INT_VAR level=1
    STR_VAR kit=""
BEGIN
   PATCH_IF "%kit%" STRING_EQUAL "" BEGIN
      LPF CRE_read_kit RET kit=value END
   END
   LPF CRE_enforce_kit_spells_hardcoded INT_VAR level STR_VAR kit END
   PATCH_MATCH "%kit%" WITH
   ARCHER BEGIN
           PATCH_IF level<=18 BEGIN
              SET num_archer= level / 3
           END ELSE BEGIN
              SET num_archer= 6 + (level - 18) / 5
           END
           LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=167 END
           LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=286 END
           PATCH_IF level>=3 BEGIN
            LPF ADD_CRE_EFFECT
                INT_VAR opcode=167
                      target=2
                      timing=9
                      parameter1=num_archer
             END
             LPF ADD_CRE_EFFECT
                INT_VAR opcode=286
                      target=2
                      timing=9
                      parameter1=num_archer
             END
            END
   END
   ASSASIN BEGIN
           LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=73 END
           LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=278 END
           LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=263 END
           PATCH_IF level>=1 BEGIN
            LPF ADD_CRE_EFFECT
              INT_VAR opcode=167
                      target=1
                      timing=9
                      parameter1=1
            END
            LPF ADD_CRE_EFFECT
              INT_VAR opcode=286
                      target=1
                      timing=9
                      parameter1=1
            END
           END
           PATCH_IF level>=23 BEGIN
                SET backstab_inc=2
           END ELSE
           PATCH_IF level>=17 BEGIN
                SET backstab_inc=1
           END ELSE BEGIN
                SET backstab_inc=0
           END
           PATCH_IF backstab_inc>0 BEGIN
              LPF ADD_CRE_EFFECT
                 INT_VAR opcode=263
                      target=1
                      timing=9
                      parameter1=backstab_inc
              END
           END

   END
   BARBARIAN BEGIN
      LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=176 END
      PATCH_IF level>=1 BEGIN
        LPF ADD_CRE_EFFECT
              INT_VAR opcode=176
                      target=1
                      timing=9
                      parameter1=2
        END
      END
      PATCH_IF level>=19 BEGIN
            WRITE_BYTE 0x60 20
            WRITE_BYTE 0x61 20
            WRITE_BYTE 0x62 20
            WRITE_BYTE 0x63 20
      END ELSE
      PATCH_IF level>=15 BEGIN
            WRITE_BYTE 0x60 15
            WRITE_BYTE 0x61 15
            WRITE_BYTE 0x62 15
            WRITE_BYTE 0x63 15
      END ELSE
      PATCH_IF level>=11 BEGIN
            WRITE_BYTE 0x60 10
            WRITE_BYTE 0x61 10
            WRITE_BYTE 0x62 10
            WRITE_BYTE 0x63 10
      END



   END
   BERSERKER BEGIN

   END
   BLACKGUARD BEGIN
          // SPCL104,105 applied by script
   END
   KENSAI BEGIN
          LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=0 END
          PATCH_IF level>0 BEGIN
           LPF ADD_CRE_EFFECT
              STR_VAR opcode=0
                      target=2
                      parameter1=2
                      timing=9
           END
          END
          SET attack_boost=level / 3
          PATCH_IF attack_boost>0 BEGIN
             LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=73 END
             LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=278 END
             LPF ADD_CRE_EFFECT
                 INT_VAR opcode=73
                         target=1
                         timing=9
                         parameter1=attack_boost
             END
             LPF ADD_CRE_EFFECT
                 INT_VAR opcode=278
                         target=1
                         timing=9
                         parameter1=attack_boost
             END
          END
          SET speed_boost = level / 4
          PATCH_IF speed_boost>0 BEGIN
             LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=190 END
             LPF ADD_CRE_EFFECT
                 INT_VAR opcode=190
                         target=1
                         timing=9
                         parameter1=speed_boost
             END
          END
   END
   SWASHBUCKLER BEGIN
          SET attack_boost=level / 5
          PATCH_IF attack_boost>0 BEGIN
             LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=73 END
             LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=278 END
             LPF ADD_CRE_EFFECT
                 INT_VAR opcode=73
                         target=1
                         timing=9
                         parameter1=attack_boost
             END
             LPF ADD_CRE_EFFECT
                 INT_VAR opcode=278
                         target=1
                         timing=9
                         parameter1=attack_boost
             END
          END
          PATCH_IF level>0 BEGIN
           SET AC_boost = (level / 5) + 1
           LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=0 END
           LPF ADD_CRE_EFFECT
              INT_VAR opcode=0
                      target=2
                      parameter1=AC_boost
                      timing=9
           END
          END
   END
   WIZARDSLAYER BEGIN
          PATCH_IF level>20 BEGIN
             SET mr=20 + (level - 20) * 3
          END ELSE BEGIN
             SET mr=level
          END
          LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=166 END
          LPF CRE_resist_magic STR_VAR arguments="%mr%" END
          // spell failure
          PATCH_IF level>0 BEGIN
            PATCH_FOR_EACH opcode IN 248 249 BEGIN
              LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete=opcode END
              LPF ADD_CRE_EFFECT
                 INT_VAR opcode
                         target=1
                         timing=9
                 STR_VAR resource=spcl132
              END
              LPF ADD_CRE_EFFECT
                 INT_VAR opcode
                         target=1
                         timing=9
                 STR_VAR resource=spcl132a
              END
            END
          END
   END
   DEFAULT
   END
END