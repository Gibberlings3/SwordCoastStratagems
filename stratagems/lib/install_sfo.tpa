ACTION_IF !VARIABLE_IS_SET sfo_installed BEGIN

  OUTER_SET sfo_installed=1

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// in case Fixpack hasn't already done it, disable the 0-byte items & broken areas (we do this here for the benefit of the count section)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_FOR_EACH ~dodgyitem~ IN iplot01k iplot04g iplot04h iplot04i BEGIN
   ACTION_IF !FILE_EXISTS ~override/%dodgyitem%.itm~ THEN BEGIN
        DISABLE_FROM_KEY ~%dodgyitem%.itm~
   END
END

ACTION_FOR_EACH ~dodgyarea~ IN xr2400 xr2600 BEGIN
   ACTION_IF !FILE_EXISTS ~override/%dodgyarea%.are~ THEN BEGIN
        DISABLE_FROM_KEY ~%dodgyarea%.are~
   END
END


///////////////////////////////////////////////////
// read in ini
///////////////////////////////////////////////////

  ACTION_IF FILE_EXISTS ~%scsroot%/%scsroot%.ini~ BEGIN
    COPY ~%scsroot%/%scsroot%.ini~ ~%workspace%~
     READ_2DA_ENTRIES_NOW ~scs_ini_array~ 1
        FOR (i=0;i<~scs_ini_array~;i=i+1) BEGIN
          READ_2DA_ENTRY_FORMER ~scs_ini_array~ ~i~ 0 ~key~
          READ_2DA_ENTRY_FORMER ~scs_ini_array~ ~i~ 1 ~value~
          TO_LOWER key
          SPRINT $scs_ini_hash(EVALUATE_BUFFER ~%key%~) ~%value%~
        END
    BUT_ONLY
  END

  ACTION_IF !VARIABLE_IS_SET ~debug_variable~ BEGIN
     OUTER_SET debug_variable=0
  END
  OUTER_SPRINT ~component_loc~ ~~
  OUTER_SPRINT ~local_loc~ ~~
  ACTION_IF debug_variable=0 BEGIN SILENT END
  // note that macro must be installed first, then general, then the rest



  INCLUDE ~%scsroot%/lib/lib_macro.tpa~
  INCLUDE ~%scsroot%/lib/lib_general.tpa~

  ACTION_IF FILE_EXISTS_IN_GAME ~dorn.cre~ BEGIN
    ACTION_FOR_EACH postfix IN cre itm BEGIN
     ACTION_BASH_FOR ~%scsroot%/ee~ ~.*\.%postfix%~ BEGIN
        COPY ~%BASH_FOR_FILESPEC%~ ~override~
     END
    END

  END




  ACTION_BASH_FOR ~%scsroot%/lib~ ~lib_.*\.tpa~ BEGIN
     ACTION_IF (~%BASH_FOR_RES%~ STRING_COMPARE_CASE ~lib_general~ && ~%BASH_FOR_RES%~ STRING_COMPARE_CASE ~lib_macro~) BEGIN
         INCLUDE ~%scsroot%/lib/%BASH_FOR_RES%.tpa~
     END
  END
  LAF action_check_ini STR_VAR ini=integrated RET value=value END
  ACTION_IF value BEGIN
     ACTION_FOR_EACH file_prefix IN CRE BEGIN
          LAF count_files STR_VAR file_prefix RET value=value END
          OUTER_SET EVALUATE_BUFFER ~file_count_%file_prefix%~=value
          ACTION_IF ~%tutu_var%~ STRING_EQUAL_CASE ~_~ BEGIN
             LAF count_files STR_VAR file_prefix tv=yes RET value=value END
             OUTER_SET EVALUATE_BUFFER ~file_count_%file_prefix%_tv~=value
          END
     END
  END ELSE BEGIN
     ACTION_FOR_EACH file_prefix IN ARE CRE ITM SPL STO BEGIN
          LAF count_files STR_VAR file_prefix RET value=value END
          OUTER_SET EVALUATE_BUFFER ~file_count_%file_prefix%~=value
          ACTION_IF ~%tutu_var%~ STRING_EQUAL_CASE ~_~ BEGIN
             LAF count_files STR_VAR file_prefix tv=yes RET value=value END
             OUTER_SET EVALUATE_BUFFER ~file_count_%file_prefix%_tv~=value
          END
     END
  END

  ACTION_FOR_EACH macro IN autofunctions spellcodes NPC_dialogs saving_throws hit_point_levels spellcodes stats BEGIN
     PRINT ~Reading in data: %macro%~
     LAUNCH_ACTION_MACRO ~read_in_%macro%~
  END
  VERBOSE
END
