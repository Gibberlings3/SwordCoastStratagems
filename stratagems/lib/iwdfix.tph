DEFINE_ACTION_FUNCTION iwd_spell_fixes BEGIN

   // broken file
   
   COPY_EXISTING "#bonecir.spl" override WRITE_ASCII 0x0 "SPL "

   // lich touch (paralysis effect missing)

   MAKE_PATCH
      patch_effect_inline=>"save_vs_death=>0 parameter2b=>64"
      add_effect_inline=>"opcode=>175 resist_dispel=>1 target=>2 parameter2=>2 duration=>600 save_vs_death=>1"
      add_effect_inline'=>"opcode=>142 resist_dispel=>1 target=>2 parameter2=>13 duration=>600 save_vs_death=>1"
      add_effect_inline''=>"opcode=>174 resist_dispel=>1 timing=>1 target=>2 resource=>~#eff_m07~ save_vs_death=>1"
      add_effect_inline'''=>"opcode=>215 resist_dispel=>1 timing=>1 target=>2 resource=>~necroh~"
   END
   LAF edit_spell STR_VAR spell=ltouch edits=patch_data END
   
   // entropy shield (remove spurious protection from Breach/Spellstrike/Imprisonment; doesn't protect against Flame Strike)
   
  COPY_EXISTING "%CLERIC_ENTROPY_SHIELD%.spl" override
		LPF DELETE_EFFECT INT_VAR match_opcode=83 match_parameter2=54 END
		PATCH_FOR_EACH resource IN sppr503 spimix01 ohbeflam BEGIN
			LPF CLONE_EFFECT STR_VAR match_resource=sppr984 resource END			
		END
   BUT_ONLY

   // Vitriolic sphere 1/2 (splash shouldn't affect primary target; shouldn't be blocked by primary target's MR)
   COPY_EXISTING "%WIZARD_VITRIOLIC_SPHERE%.spl" override
       LPF ALTER_EFFECT INT_VAR match_opcode=146 resist_dispel=0 END
       LPF ALTER_EFFECT INT_VAR match_opcode=215 resist_dispel=0 END
       LPF CLONE_EFFECT INT_VAR match_opcode=146 opcode=318 parameter1=0 parameter2=0 duration=1 timing=0 STR_VAR resource=spwi431y END
       
   // Vitriolic sphere 2/2 (splash damage doesn't decrease with time)
   COPY_EXISTING "%WIZARD_VITRIOLIC_SPHERE%.spl" override
                 "%WIZARD_VITRIOLIC_SPHERE%z.spl" override
      LPF ALTER_EFFECT INT_VAR match_opcode=146 parameter2=2 END

   // 7 eyes (eye of the sword shouldn't protect against stunning damage, which is only used for the hp backlash of the Enrage ability)
   
   COPY_EXISTING "7eyes.2da" override
     REPLACE_TEXTUALLY "12*0x8000000" "*"
     PRETTY_PRINT_2DA

   // The projectile for Bombardier Beetle clouds has a duplicate string
   
   COPY_EXISTING "%INNATE_BOMBARDIER_BEETLE_CLOUD%.spl" override
        LPF DELETE_EFFECT INT_VAR match_opcode=139 match_parameter1=35568 multi_match=1 END
   BUT_ONLY
   
   // Cure Light Wounds has mismatched damage
   
   COPY_EXISTING "%CLERIC_CAUSE_LIGHT_WOUNDS%.spl" override
        LPF DELETE_EFFECT INT_VAR match_opcode=12 match_save_vs_spell=0 END
        LPF ALTER_EFFECT INT_VAR match_opcode=12 parameter1=8 save_for_half=1 END
   BUT_ONLY
   
   // Storm Shell shouldn't stack with itself
   
   COPY_EXISTING "%CLERIC_STORM_SHELL%.spl" override
       LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 timing=0 resist_dispel=3 STR_VAR resource="%CLERIC_STORM_SHELL%" END
	   LPF ALTER_EFFECT INT_VAR target=1 END // for some subtle reason, ability_target=5 and target=2 doesn't play nice with the 321 trick
	 BUT_ONLY
   
	// Mind Blank is supposed to protect against petrification
	
	COPY_EXISTING "%WIZARD_MIND_BLANK%.spl" override
		LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=175 parameter2=134 END // immune to petrification opcode
		LPF CLONE_EFFECT INT_VAR match_opcode=267 match_parameter1=14102 parameter2=14127 END // immune to petrification string
		LPF CLONE_EFFECT INT_VAR match_ocpode=169 match_parameter2=3 parameter2=171 END // immune to petrification icon
	BUT_ONLY


END

DEFINE_ACTION_FUNCTION iwd_misc_fixes BEGIN

   // Hjollder wind-walk cutscenes cosmetic fix (restore to more like IWDvanilla)

   LAF swap_text STR_VAR file="bctoil.bcs bctoku.bcs bitoku.bcs hjescape.bcs kutobc.bcs kutobi.bcs kutolw.bcs" swaps=~Wait(2)=>SmallWait(15)ApplySpellRES("dw#silin",Myself)Wait(2)~ END



END