   /// general section

      // file naming - general case (these are used enough that they tend to creep into even BG2 code, so it's worth defining them once and for all)

      ACTION_IF is_tutu BEGIN
         OUTER_SPRINT tutu_var "_"
      END ELSE BEGIN
         OUTER_SPRINT tutu_var ""
      END

      ACTION_FOR_EACH letter IN a b c d e f g h i j k l m n o p q r s t u v w x y z BEGIN
       ACTION_MATCH "%is_tutu%" WITH
          1 BEGIN
              OUTER_SPRINT ~tutu_script%letter%~ ~_~
          END
          DEFAULT
              OUTER_SPRINT ~tutu_script%letter%~ ~%letter%~
       END
      END
      



   /// BG1 section

   ACTION_IF is_bg1 BEGIN
   
   // setup
     
      ACTION_IF is_tutu BEGIN
            OUTER_SPRINT thegame tutu
      END ELSE BEGIN
         ACTION_IF GAME_IS bgt BEGIN
            OUTER_SPRINT thegame bgt
         END ELSE BEGIN
            ACTION_IF GAME_IS bgee BEGIN
               OUTER_SPRINT thegame bgee
            END ELSE BEGIN
               ACTION_IF GAME_IS eet BEGIN
                   OUTER_SPRINT thegame eet
               END ELSE BEGIN
                   FAIL ~Error in crossplatform code: unrecognised BG1 game~
               END
            END
         END
      END


   // area codes
   
      DEFINE_PATCH_FUNCTION bg1_area_swap
         STR_VAR arguments=""
         RET value
      BEGIN
           PATCH_MATCH ~%thegame%~ WITH
               eet BEGIN
                  INNER_PATCH_SAVE value "%arguments%" BEGIN
                      WRITE_ASCII 0x0 ~BG~ (2)
                  END
               END
               tutu BEGIN
                  INNER_PATCH_SAVE value ~%arguments%~ BEGIN
                      WRITE_ASCII 0x0 ~FW~ (2)
                  END
               END
               DEFAULT
                  SPRINT value "%arguments%"
           END
      END

      OUTER_SPRINT array_to_read ~bg1_area_list.2da~
      OUTER_SPRINT array_name ~bg1_areas~
      ACTION_IF GAME_IS bgt BEGIN
         OUTER_SPRINT read_array_column_name bgt
      END ELSE BEGIN
         OUTER_SPRINT read_array_column_name default
      END
      OUTER_SPRINT read_in_func bg1_area_swap
      LAM read_in_variables

      // file naming - special cases


      OUTER_SPRINT array_to_read "bg1_file_names.2da"
      OUTER_SPRINT array_name bg1_names
      ACTION_IF GAME_IS bgee BEGIN
           OUTER_SPRINT read_array_column_name bgee
      END ELSE
      ACTION_IF GAME_IS bgt BEGIN
           OUTER_SPRINT read_array_column_name bgt
      END ELSE
      ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
           OUTER_SPRINT read_array_column_name tutu
      END ELSE
      ACTION_IF GAME_IS eet BEGIN
           OUTER_SPRINT read_array_column_name eet
      END ELSE BEGIN
         FAIL "unidentified game at crossplatform"
      END

      LAM read_in_variables

      ACTION_PHP_EACH bg1_names AS name=>rename BEGIN
       ACTION_IF ~%rename%~ STRING_EQUAL_CASE null BEGIN
          OUTER_SPRINT rename ~~
       END
       OUTER_SPRINT ~%name%~ ~%rename%~
      END

      // chapters
      
      OUTER_SET chapter_delta= ( ~%thegame%~ STRING_EQUAL bgt ? 1 : 0)
      OUTER_FOR (num=1;num<=7;num +=1) BEGIN
         OUTER_SET ~bg1_chapter_%num%~ =num + chapter_delta
      END


      // testing
      /*
      PRINT ~tutu_var is "%tutu_var%"~
      PRINT ~tutu_scriptbg is %tutu_scriptbg%~
      PRINT ~tutu_scriptf is %tutu_scriptf%~
      PRINT ~bg1_skeleton_warrior is %bg1_skeleton_warrior%~
      PRINT ~bg1_chapter_4 is %bg1_chapter_4%~
      ACTION_READLN bloook
      */

  END // end of BG1 section

  ACTION_IF is_bg2 BEGIN    // BG2 section

      OUTER_SET chapter_delta= ( ~%thegame%~ STRING_EQUAL eet ? 12 : 0)
      OUTER_FOR (num=1;num<=10;num +=1) BEGIN
         OUTER_SET ~bg2_chapter_%num%~ =num + chapter_delta
      END


  END
