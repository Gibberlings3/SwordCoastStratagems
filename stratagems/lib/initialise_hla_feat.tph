DEFINE_ACTION_FUNCTION initialise_hla_feat
   STR_VAR extant_list=""
           extant_list_dir=lib
BEGIN



    // create initial blank file

       <<<<<<<< .../stratagems-inline/dw#hla_feat.2da
       2DA v1.0
           resref type       
           * >>>>>>>>

       COPY ".../stratagems-inline/dw#hla_feat.2da" "%data_loc_root%/dw_shared/dw#hla_feat.2da"


   // if we're bg2 then...
   
   ACTION_IF is_bg2 BEGIN

    // read extant HLAs onto the list
    
     MAKE_PATCH
        USE_ANY_ITEM=>spcl915
        EXTRA_LEVEL_6=>spcl928
        EXTRA_LEVEL_7=>spcl929
        EXTRA_LEVEL_8=>spcl930
     END
     ACTION_PHP_EACH patch_data AS ids=>resref BEGIN
        ACTION_TO_UPPER ids
        ACTION_TO_UPPER resref
        OUTER_SPRINT "%ids%" "%resref%"
    END
    
    // read in extras
    
    ACTION_IF "%extant_list%" STRING_EQUAL_CASE "" BEGIN
       ACTION_IF enhanced_edition BEGIN
          OUTER_SPRINT extant_list vanilla_hlas
       END ELSE BEGIN
          OUTER_SPRINT extant_list vanilla_hlas_no_ee
       END
    END

    COPY "%scsroot%/%extant_list_dir%/%extant_list%.2da" "%workspace%"
         READ_2DA_ENTRIES_NOW extant_table 2
         FOR (i=1;i<extant_table;i+=1) BEGIN
            READ_2DA_ENTRY_FORMER extant_table i 0 ids
            READ_2DA_ENTRY_FORMER extant_table i 1 type
            LPF get_spellcode STR_VAR input="%ids%" RET resref=value END
            PATCH_IF FILE_EXISTS_IN_GAME "%resref%.spl" BEGIN
               INNER_ACTION BEGIN
                  ACTION_IF !FILE_CONTAINS "%data_loc_root%/dw_shared/dw#hla_feat.2da" "%ids%" BEGIN
                     APPEND_OUTER "%data_loc_root%/dw_shared/dw#hla_feat.2da" "%ids% %resref% %type%"
                  END
               END
            END ELSE BEGIN
               LPF warning STR_VAR warning="apparently 'extant' HLA %ids% not found" END
            END
         END
    BUT_ONLY 
  END

END