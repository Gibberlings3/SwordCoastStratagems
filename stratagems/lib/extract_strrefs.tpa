DEFINE_PATCH_FUNCTION log_atsymbol_strrefs
INT_VAR max=0
STR_VAR log=""
BEGIN
   SET var=INDEX_BUFFER ("%atsymbol%[0-9]")
   WHILE var>=0 BEGIN
        SET number_starts = var + 1
        SET number_ends=INDEX_BUFFER ("[^0-9]" number_starts)
        PATCH_IF number_ends<0 BEGIN
           SET number_ends=BUFFER_LENGTH
        END
        SET number_length=number_ends - number_starts
        PATCH_IF number_length>=0 BEGIN
          READ_ASCII number_starts number (number_length)
          PATCH_IF number<= max BEGIN
            SET number += 100000 // to ensure no number is a substring of another
            LPF log_this STR_VAR file="%log%" input="%number%" repeat="no" END
          END
          SET var=INDEX_BUFFER ("%atsymbol%[0-9]" number_ends)
        END ELSE BEGIN
           SET var="-1"
        END

   END
END

DEFINE_PATCH_FUNCTION log_say_strrefs
INT_VAR max=0
STR_VAR log=""
BEGIN
   SET var=INDEX_BUFFER ("say_[a-z0-9_]+ *= *> *[0-9]")
   WHILE var>=0 BEGIN
        DELETE_BYTES 0x0  var
        SET chevron=INDEX_BUFFER (">")
        DELETE_BYTES 0x0 chevron + 1
        SET number_starts=INDEX_BUFFER ("[0-9]")
        DELETE_BYTES 0x0 number_starts
        SET number_ends=INDEX_BUFFER ("[^0-9]")
        READ_ASCII 0x0 number (number_ends)
        PATCH_IF number<=max BEGIN
            SET number += 100000 // to ensure no number is a substring of another
           LPF log_this STR_VAR file="%log%" input="%number%" repeat="no" END
        END
        SET var=INDEX_BUFFER ("say_[a-z0-9_]+ *= *>")
   END
END

DEFINE_PATCH_FUNCTION log_other_strrefs
INT_VAR max=0
STR_VAR log=""
BEGIN
   SET var=INDEX_BUFFER ("( *AT [0-9]")
   WHILE var>=0 BEGIN
        DELETE_BYTES 0x0  var 
        SET number_starts=INDEX_BUFFER ("[0-9]")
        DELETE_BYTES 0x0 number_starts
        SET number_ends=INDEX_BUFFER ("[^0-9]")
        READ_ASCII 0x0 number (number_ends)
        PATCH_IF number <= max BEGIN
            SET number += 100000 // to ensure no number is a substring of another
           LPF log_this STR_VAR file="%log%" input="%number%" repeat="no" END
        END
        SET var=INDEX_BUFFER ("( *AT [0-9]")
   END
END

DEFINE_ACTION_FUNCTION strref_top_level
    STR_VAR file_res=""
            file_ext=""
            file_dir=""
            arguments=""
BEGIN
     COPY "%file_dir%/%file_res%.%file_ext%" "%workspace%/discard"
         LPF log_atsymbol_strrefs INT_VAR max=100000 STR_VAR log="tra_records/%arguments%" END
     ACTION_MATCH "%file_ext%" WITH
     tph tpa tp2 BEGIN
        COPY "%file_dir%/%file_res%.%file_ext%" "%workspace%/discard"
            LPF log_say_strrefs INT_VAR max=100000 STR_VAR log="tra_records/%arguments%" END
        COPY "%file_dir%/%file_res%.%file_ext%" "%workspace%/discard"
            LPF log_other_strrefs INT_VAR max=100000 STR_VAR log="tra_records/%arguments%" END
     END
     DEFAULT
     END
END
