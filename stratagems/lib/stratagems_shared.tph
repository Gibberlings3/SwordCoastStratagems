DEFINE_PATCH_FUNCTION strip_scs_scripts
   STR_VAR arguments=""
BEGIN
  PATCH_IF "%arguments%" STRING_COMPARE_CASE no_change BEGIN
   FOR (i=0x248;i<0x270;i+=8) BEGIN
      READ_ASCII i check4 (4)
      TO_LOWER check4
      READ_ASCII i check5 (5)
      TO_LOWER check5
      PATCH_MATCH ~%check4%~ WITH  // caster
         dw#2 dw#1 dw1r dw2r dw3r dw1m dw2m dw3m BEGIN
            WRITE_ASCII i ~~ (8)
         END
         DEFAULT
            PATCH_MATCH "%check5%" WITH
               dw#mg dw#pr BEGIN
                   WRITE_ASCII i ~~ (8)
               END
               DEFAULT
               END
         END
   END
 END
END
DEFINE_PATCH_FUNCTION is_tob_char
      STR_VAR filename=""
      RET value
BEGIN
      TO_LOWER filename
      PATCH_MATCH ~%filename%~ WITH
            shugar01 shugfg01 shugho01 shugod01 shugpr01 shupol01 gorarc gorpri1 gorsta01
            gorsta02 gorsta03 gorsta04 gorsta05 gorsta06 gorsta06 gorsta07 gorsta08 gorsta09
            gorsta10 gorsta11 gorsta12 gorgrd01 gorgrd02 gorcamb gorcamb2 gorcam3 gorcamb4
            gorcamb5 alufie01 alufie02 cambion1 cambion2 giafir01 gorfir03 gorfir04 gorgitc1
            gorgitc2 gorminc1 gorminc2 gorminc3 gorminc4 kobswi02 shugod02 zilarc01 zilfgt01
            zilfgt02 zilmag02 fshorde1 fshorde2 fshorde3 fshorde4 fshorde5 fshorde6 temcam1
            telalu1 telsuc1 teltief1 teltief2 teltief3 amarch01 amarch02 ammerc01 ammerc02
            amcarras amsmug01 garock gromg01 gromg02 gromg03 gromg04 gromg05 gromnir
            gororc01 gororc02 gororc03 hgber01 hgmag01 hgwar01 yaga02 yaga03 yaga04 yaga05
            yarmy01 yarmy02 yarmy03 amcler01 amcler02 ammag01 ammerc03 ammerc04 ammonk01
            ammonk02 ammonk03 ammonk04 ammonk05 ammonk06 fsmage01 fsmage02 fsmage03 fsmage04
            sarbha01 sarbha02 sarbha03 sargrd01 sargrd02 sarmag01 sendai gorcamb6 gorwom01
            gorwom02 gorwom03 gorwom04 gorwom05 gorwom06 gorcamb7 bazliz01 bazliz02 bazliz03
            bazliz04 bazmonk abazigal gorgua02 gorgua03 gorgua04 sargrd03 sargrd07 sargrd08
            sargrd09 sendai2 sendai3 sendai4 sendai5 sendro01 sendro02 sendro03 sendro04 sendro05
            sengua01 sengua02 sengua03 sengua04 kuoarc20 kuocle20 kuowar20 ammonk07 ammonk08
            sarbul01 sarbul02 sarbul03 sarbul04 sarbul05 sardw01 sardw02 sardw03 sarelf01
            sarelf02 sarelf03 sarelf04 sarbul06 sardw04 sardw05 sarelf05 sarelf06 sendai6 
            sendai7 sendai8 sarbul07 ammajira ammgrd01 ammgrd02 amcapt01 amshep01 amconw01 amconw02
            amconw03 amduel03 gromg06 gromg07 sarorc01 saroro01 sarcuilt2 sengua05 amduel01 amduel02
            ammerc07 ammerc08 amsmug02 sarmil01 sarmil02 senken01 hgwar02 senorc01 senorc02
            sendark amelm01 ammerc09 ammgrd03 drofod01 drofod02 drofod03 drofod04 gorgua05 
            sargrd10 sargrd11 amtarc01 amtcap01 amtgen01 amtmag01 amtpik01 balelite ammgrd04
            icyuan04 icyuan05 sewdue01 sewdue02 seworo01 sewyag01 sewyag02 sewyag03 sewyag04 
            ysguar01 ysguar02 ysguar03 ysguar04 ysguar05 ysguar06 ysguar06 ysguar07 ysguar08
            yscara01 yscara02 yscara03 yscara04 senorc03 senorc04 yscara05 yssold12 yssold13 
            yssold14 yssold15 yssold16 tobban01 tobban02 tobban03 tobban04 tobpar01 tobpar02
            tobpar03 tobpar04 tobpar05 tobpar06 ysgrunt ysmage01 ysmage02 yssold01 yssold02
            yssold03 yssold04 sewarc01 sewarc02 ammgrd05 gorkuo01 gorkuo02 gorkuo03 gorkuo04
            gorkuo05 sarkis01 sarkis02 sarkis03 sarkis04 sargrd14 sargrd15 sargrd16  gromg08
            gromg09 gromg10 amtcler0 amtcle01 gromg11 gromg12 gromg13 gromg14 senfod01 senfod02
      BEGIN SET value=1 END
      DEFAULT SET value=0 END
END

DEFINE_PATCH_FUNCTION is_bg1_char 
      STR_VAR filename=""
      RET value
BEGIN
      PATCH_MATCH "%is_bg1%%is_bg2%" WITH
           01 BEGIN
               SET value=0
           END
           10 BEGIN
               SET value=1
           END
           11 BEGIN
               LPF is_bg1_char_core STR_VAR filename RET value END
           END
           DEFAULT
              PATCH_FAIL "Running is_bg1_char on %filename%, but game isn't bg1 *or* bg2(!?)"
      END

END


DEFINE_PATCH_FUNCTION is_bg1_char_core // used only on BGT and EET installs
      STR_VAR filename=""
      RET value
BEGIN
      TO_UPPER filename
      PATCH_MATCH ~%filename%~ WITH
AASIM    ACHEN    ADDY 	AGNASI        AJANTI	AJANTI4   AJANTI6  ALAI      ALANBL	ALATOS    ALBERT  ALDETH      ALEXAN  ALGERN    ALORA   ALORA6
ALVANH  ALYTH     AMARAN  AMBMAEVR    AMBSTHF0 AMBTHIF2 AMNIS    AMNIS3      AMNIS4   AMNISE   AMNISE2  AMSLEEP    ANDARS   ANDOUT   ANDRIS   ANGELO
AOLN     ARDROU   ARKANI   ARKANI3    ARKION   ARKUSH   ARLIN    ASHEN       ASHIRU   ATTSLIM  AVARICE  AVARICEM   BAERIN   BANDCAP  BANDCR   BANDIC
BANDIT   BANDIT2  BANDITA  BANDITC    BANDITCY BARDOL   BARESH   BARESH2     BARSLIM  BART10   BART11   BART12     BART13   BART14   BART15   BART16
BART2    BART3    BART4    BART5      BART6    BART7    BARUK    BARWLF      BASSIL   BAYARD   BEGBAX_A BEGBAX_B   BEGBAX_C BEGBAX_D BEGGBA   BEGGBAX
BEGGNA   BELAND   BELDIN   BELLAM     BELTBRD  BENDAL   BENJY    BENTAN      BENTHA   BENTLY   BERRUN   BEYN       BGBISHOP BGCALAHA BGCARBOS BGFLAM6
BGFLAM62 BGFTOWN2 BGFTOWN3 BGFTOWN4   BGIGIBB  BGKING   BGKNIGH1 BGKNIGHT    BGMTOWN2 BGMTOWN3 BGMTOWN4 BGMTOWN5   BGNEB    BGPAWN   BGQUAYLE BGQUEEN
BGROGER  BGROOK   BGSENDAI BGSHANK    BGSHOP01 BGSHOP03 BGSHOP04 BGSHOP06    BGSHOP07 BGSHOP08 BGSKEL02 BGSKEL03   BGSKELWA BGSTALKE BGTARNOR BGTOWNCR
BGWILLIA BGXAN    BGXZAR   BHEREN     BHOBGOB  BILL     BILLY    BINKOS      BINKY    BIP      BJORNI   BLACKL     BLANE    BOB      ~BOR~      BORDA
BORIN    BORK     BOUNCER  BOUNTY     BOUNTY2  BOYBA1   BOYBA1_A BOYBA1_C    BOYBA2   BOYBA3_A BOYBA3_B BOYBAX     BOYBAX_A BOYBAX_B BOYBAX_C BOYBE1
BOYBE2   BPDYNAHE BPIMOEN  BPJAHEIR   BPKHALID BPMINSC  BRAGE    BRAN        BRANDI   BRANWE   BRANWE5  BRATHL     BRENDA   BRENNA   BREVLI   BRIELB
BRILLA   BRUNOS   BUB      BULLRUSH   BYSTAND2 CADDER   CAEDMO   CAILAN      CALDO    CANDER   CANTIC   CANTO      CARSA    CARSTO   CASSON   CATURA
CAUTER   CENTEO   CHANTE   CHANTE2    CHANTH   CHARLE   CHASE    CHELAN      CHILDULG CHILNA   CHILNA_A CHILNA_B   CHILNA_C CHILNA_D CHIMP5   CHLOE
CLAIRD   COKSMTH  CONNOL   CORAN      CORAN5   CORDYR   CORPSE1  CORPSE2     CORSONE  CORY     CREDUS   CREW1      CREW2    CREW3    CREW4    CREW5
CREW6    CREW7    CUCHOL   CULT1      CULT2    CULT3    CULTD1   CULTD2      CULTD3   CULTD4   CULTD5   CULTD6     CULTT1   CULTT2   CULTT3   CULTT4
CYDERM   CYTHAN   DABRON   DAERRAGH   DAITEL   DANDAL   DAVAEO   DEAD        DEAD1    DEAD2    DEADFUCK DEATHK     DEATHK1  DEDER    DEDER3   DEGROD
DELGOD   DELORN   DELSVIR  DELTAN     DELTAN2  DELTHY   DENAK    DENKOD      DESRET   DESSLO   DESTUS   DETRAN     DEZKIE   DHANIA   DIANA    DIARMID
DIGGER   DILAK    DILLAR   DINK       DIRK     DIVINE   DIYAB    DOOMDUR     DRADEE   DRADEE2  DRAKAR   DRASUS     DRATAN   DRELIK   DREPPI   DREPPI2
DRIBBE   DRIENN   DRIZZT   DROTH      DRUID3   DRUNK    DUMFUE   DUMISL      DUMKIE   DUN      DUNKIN   DUPLICA1   DURLAGT  DURSWORD DUSHAI   DYNAHE2
DYNAHE4  DYNAHE6  EDWIN    EDWIN2     EDWIN4   EDWIN6   ELDOTH   ELDOTH5     ELKART   ELMIN1   ELMIN2   ELMIN3     ELMIN5   EMERSO   EMITAR   EMITAR2
ENDER    ENTILL   ERDANE   ERIK       EURIC    FABER    FAFIGHT  FAHRIN      FAIZAH   FALDOR   FALDOR5  FARLUC     FARM     FARM3    FARM4    FARMBR
FARMER   FATMAN   FATWOM   FCOOK2     FCOOK3   FEAR     FECOOK   FELONI      FENTEN   FERGUS   FFHUNT   FFSLEEP    FGOLEM   FIREB1   FIREBE   FLAM10
FLAM11   FLAM12   FLAM2    FLAM4      FLAM5    FLAM7    FLAM9    FLAMAL      FLAMANG  FLAMBRID FLAME    FLAMEN     FLAMEN2  FLAMGG   FLAMHUSB FLAMPUN
FLAMPUN2 FLAMSCO  FLAMVAI  FLAMWIZ    FOOTY    FORESH   FORTHE   FRIEND      FRIWEN   FTOBE2   FTOBE3   FTOBE4     FTOBE5   FTOBE6   FTOBE7   FTOBE8
FTOBE9   FTOWB5   FTOWB6   FTOWB7     FTOWBA   FTOWBASC FTOWBASN FTOWBAX     FTOWBA_A FTOWBA_B FTOWBA_C FTOWBA_D   FTOWBA_E FTOWBESC FTOWBEX  FTOWBE_A
FTOWBE_B FTOWBE_C FTOWBE_D FTOWBE_E   FTOWFR   FTOWFRSC FTOWFR_B FTOWFR_D    FTOWFR_E FTOWNA   FTOWNA_A FTOWNA_B   FTOWNA_E FTOWUB   FTOWUBX  FTOWUB_A
FTOWUB_B FTOWUB_E FTWBAX_A FTWBAX_B   FTWBAX_C FTWBAX_D FTWBAX_E FULLER      FULLER2  GALDOR   GALILE   GALKEN     GALLOR   GALTOK   GAMES    GAMES2
GAMES3   GAMES4   GAMES5   GANDOL     GANTOL   GANTOL1  GANTOL2  GARAN       GARDEN   GARDUS   GARRIC   GARRIC2    GARRIC4  GARRIC6  GATEWA   GATEWA2
GAXIR    GAZIB    GELLAN   GELTIK     GENTHO   GERDE    GERVIS   GHORAK      GIBBER   GIBBER2  GIRBA1   GIRBA2     GIRBA3   GIRBA3_A GIRBA3_C GIRBAX_A
GIRBAX_B GIRBAX_C GIRBAX_D GIRBAX_E   GLANMA   GLART    GNOLL02  GNOLL03     GNOLL3   GNOLL5   GNOLL5A  GOLINV     GOODDEAT GORD     GORION   GORPEL
GREGOR   GRETEK   GREYWO   GUARD      GURKE    GYLLIA   HAFIZ    HAKT        HALACA   HALBAZ   HALFFG   HALFG3     HALFG4   HALFG5   HALFG6   HALFGU
HALFMIRI HALFTR   HANNAH   HARBOR     HAREIS   HASEO    HECITO   HELSHA      HENTOL   HEPHIS   HERSCH   HICK       HOBELITE HOBGO5   HOBGOA_A HOBGOA_B
HOBGOA_C HOBGOA_D HOBGOA_E HOBGOBA    HOBGOBC  HOBGOB_A HOBGOB_B HOBGOB_C    HOBGOB_D HOBGOB_E HOBGZHUR HOBJOIA    HOUSEN   HOUSEN2  HOUSG1   HOUSG2
HOUSG3   HOUSG4   HOUSG5   HOUSG6     HULL     HULL2    HULL3    HULRIK      HUNTER   HURGAN   HUSAM    HUSAM2     IHOBGOB  IKE      IKE2     IMOEN1
IMOEN2   IMOEN4   IMOEN61  INNKE3     INNKN2   IRLENT   IRON10   IRON11      IRON12   IRON13   IRON14   IRON15     IRON2    IRON3    IRON4    IRON5
IRON6    IRON7    IRON8    IRON9      IRONELIT IRONGU   IRONGUAR IRONM2      IRONM3   IRONM4   IRONME   ISLA       ITHMEE   ITHTYL   IVANNE   IZEFIA
JACIL    JAHEIR2  JAHEIR4  JAHEIR6    JALANT   JAMIE    JARDAK   JARED       JASE     JEBADO   JEMBY    JENKAL     JESSUP   JHASSO   JONAVI   JONDAL
JOPI     JOULAR   KAELLA   KAGAIN     KAGAIN2  KAGAIN4  KAGAIN6  KALESS      KARAN    KARAN1   KARLAT   KARP       KELDDA   KENT     KERRAC   KESHEE
KESTOR   KHALID2  KHALID4  KHALID6    KIEL2    KIERES   KIRIAN   KISSIQ      KIVAN    KIVAN4   KIVAN6   KNIGHT2    KNIGHT3  KNIGHT4  KNIGHT5  KNIGHT6
KNOTT    KOLVAR   KORAX    KOVERA     KRUMM    KRYSTI   KYLEE    KYSUS       LACHLU   LAERTA   LAKADA   LAMALH     LANDRI   LANTAN   LAOLA    LARYSS
LASALA   LASKAL   LAUREL   LEAGGU1    LEAGGU2  LEAGGU3  LEAGGU4  LEAGGU5     LEAGGU6  LEAGGU7  LENA     LENDAR     LESLEY   LESTOR   LINDIN   LOBAR
LOTHAN   LOUISE   LOVE     LOVEM      LUCKY    LUSSEL   MADARC   MAKA        MALKAX   MALTZ    MANEIR   MAPLE      MARALE   MARCEL   MAREK    MARL
MARVIN   MCOOK    MCOOK2   MCOOK3     MCOOK4   MCOOK5   MEAKIN   MEILUM      MELICA   MERC     MERCH2   MERCH4     MERCH5   MERCH6   MERCHG   MERLEA
MERSIL   MERSIL2  MERSIL3  MICHAE     MICK     MINEC2   MINEC3   MINEC4      MINEC5   MINERC   MINERN   MINSC2     MINSC4   MINSC6   MIRIAN   MOLKAR
MONKTU10 MONKTU11 MONKTU12 MONKTU13   MONKTU14 MONKTU2  MONKTU3  MONKTU4     MONKTU5  MONKTU7  MONKTU8  MONKTU9    MONTAR   MONTAR2  MONTAR4  MONTAR6
MOORLOCK MORDAI   MORVIN   MRSHADE    MTBE10   MTBE11   MTBE2    MTBE3       MTBE4    MTBE5    MTBE6    MTBE7      MTBE8    MTBE9    MTOB2    MTOB3
MTOB6    MTOB7    MTOB8    MTOB9      MTOWBA   MTOWBASC MTOWBASN MTOWBAST    MTOWBAX  MTOWBA_A MTOWBA_B MTOWBA_C   MTOWBA_D MTOWBA_E MTOWBE   MTOWBESC
MTOWBESN MTOWBEST MTOWBEX  MTOWBE_A   MTOWBE_B MTOWBE_C MTOWBE_D MTOWBE_E    MTOWF2   MTOWFR   MTOWFRSC MTOWFRST   MTOWFR_A MTOWFR_B MTOWFR_C MTOWFR_D
MTOWFR_E MTOWNA   MTOWNASC MTOWNAST   MTOWNA_A MTOWNA_B MTOWNA_C MTOWNA_D    MTOWUB   MTOWUBSC MTOWUBX  MTOWUB_A   MTOWUB_B MTOWUB_C MTOWUB_D MTOWUB_E
MTWBAX_A MTWBAX_B MTWBAX_C MTWBAX_D   MTWBAX_E MTWSLIM  MTWSLIM2 MTWSLIM3    MULAHE   MUTAMI   MYSMER   NAAMAN     NADARI   NADER    ADINE   NANTRI
NARCIL   NARLEN   NATASH   NEIRA      NELIK    NEMPHR   NESSIE   NESTOR      NEVILL   NIEMAI   NIKLOS   NIKOLA     NILA     NIMBUL   NIVEK    NOBBAX_A
NOBBAX_B NOBBAX_C NOBBAX_D NOBBAX_E   NOBL10   NOBL11   NOBL12   NOBL14      NOBL2    NOBL3    NOBL4    NOBL5      NOBL6    NOBL7    NOBL8    NOBL9
NOBLBA_A NOBLBA_B NOBLBA_C NOBLBA_D   NOBLBA_E NOBLPA   NOBLPA1  NOBLPA2     NOBLPA3  NOBLPA4  NOBLPA5  NOBW2      NOBW3    NOBW5    NOBW7    NOBW8
NOBW9    NOBWBA   NOBWBA_A NOBWBA_D   NOBWBA_E NOOBER   NORALE   NORTUA      NOSFER   NOWBAX_A NOWBAX_B NOWBAX_C   NOWBAX_D NOWBAX_E OBERAN   OCELLI
OOGIEW   ORDULI   OSMADI   OSPREY     OUBLEK   OULAM    PALLON   PANGWA      PARDA    PARDA1   PARGUS   PERDUE     PERMID   PETER    PETRIN   PGOND
PHAERS   PHANDA   PHEIRK   PHEOARCH   PHEOGURD PHLYDI2  PHOENIX  PIATO       PNASHK   POE      POGHM10  POGHMA4    POGHMA5  POGHMA7  POISOD   PORTAL
PRAT     PRIDE    PRIDEM   PRIHEL     PRIILMU  PRISM    PRISON   PROSBA_B    PROSBA_C PROSLAY  PROST2   PROST3     PROST4   PROST5   PROST6   PROST7
PROST8   PRSBAX_A PRSBAX_B PRSBAX_C   PUMBER2  PUMBERL  QUAYLE4  QUAYLE6     QUENAS   QUINN    RAEMON   RAHVIN     RAIKEN   RALEO    RAMAZI   RANCE
READ2    READ3    REDEDG   REEVOR     REEVOR2  REGINA   RESAR    REZDAN      RIELTA   RIGGILO  RINNIE   RODNIM     RUFFIE   SAFANA   SAFANA4  SAFANA6
SAILAL   SAILDAN  SAILFIN  SAILJOS    SAILNED  SAILNEW  SAKUL    SAMANT      SAMUEL   SANADAL  SAREV1   SAREVO     SAREVO2  SARHED   SATH     SCAR
SEMAJ    SENIYA   SERVA2   SERVAN     SEWERC   SEWERF5  SHADOW   SHAELL      SHALDR   SHANDAL2 SHARTE   SHARTE4    SHARTE6  SHELLI   SHEP     SHISTA
SHOPKN   SHVERT   SILENC   SILKE      SIRLOT   SKIE     SKIE6    SLAVE       SLAVE4   SLAVE5   SLAVE6   SLAVFREE   SLAVLEAD SLAVTELL SLEEPEL  SLEEPFAT
SLEEPHAL SLYTHE   SMITH    SMUGGL     SONNER   SORCED   SORREL   SPENGIL     SPRAT    STEPHA   STONEC   STONED     STONEDL  STONEDW  STONEG   SUGAR
SUNIN    SURGEO   SURREY   TAEROM     TAKIYAH  TAMAH    TAMOKO   TANAR       TARNES   TARTUS   TAUGOS   TAXEK      TELANO   TELKA    TELLAN   TELMEN
TENYA    TENYA2   TERSUS   TETHTO     TEVEN    TEYNGA   THALAN   THALDO      THEREL   THIEF    THIEFF   THIEFG     THIEFM   TIAX     TIAX4    TIAX6
TIBER    TICK     TIPIAN   TORLO      TORLOB   TOURIST1 TOURIST2 TOURIST3    TRACEA   TRALIT   TRANZI   TRAVE2     TREMAI   TRIADO   TURPIN   TUTH
ULCAST   ULF      ULRAUN   UNSHEY     VAIL     VARCI    VAX      VAYYA       VENKT    VICONI   VICONI4  VICONI61   VITIARE  VIVIEN   VOIEAS   VOINOR
VOISOU   VOIWES   VOLETA   VOLO       VOLTIN   WAITER   WASLEEP  WATCH       WATCH2   WATCH3   WATCH6   WATCH9     WATCHM   WATCHN   WATCHN2  WENRIC
WHEBER   WHELP    WILCO    WILF       WILLIAM1 WINSKI   WINSKI2  WINTHR2     WIRTHI   WIVEN    WUDEI    XAN4       XAN6     XZAR2    XZAR4    XZAR6
YAGO     YESLIC   YESLIC5  ZAL        ZARGAL   ZEELA    ZEKAR    ZEKE        ZHALIM   ZHURLO   ZORDRA
         BEGIN
              SET value=1
         END
         DEFAULT
                SET value=0
      END
      PATCH_IF GAME_IS eet BEGIN
         PATCH_MATCH "%filename%" WITH 
BISHOP KING KNIGHT PAWN QUEEN ROOK FLAM6 CALAHA_    CARBOS_  FTOWN2_  FTOWN3_  FTOWN4_
IGIBBER_ IMOEN6_  KNIGHT1_ MTOWN2_    MTOWN3_  MTOWN4_  MTOWN5_  NEB_        QUAYLE_  ROGER_   SENDAI_  SHANK_     SHOP01_  SHOP02_  SHOP03_  SHOP04_
SHOP05_  SHOP06_  SHOP07_  SHOP08_    SKELWA_  STALKE_  TARNOR_  TOWNCR_     VICONI6_ WILLIA_  WILLIAM_ XAN_       XZAR_
              BEGIN
                   SET value=1
              END
              DEFAULT
          END
      END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////
///      code to make creatures tougher in a diff-slider-conditional way
/////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION diff_based_clone
   STR_VAR creature=""
           edits=""
           editstring=""
           hardname=""
BEGIN
    LAF check_ini STR_VAR ini=Enable_Easy_Option RET value END
    ACTION_IF value BEGIN

       LAF clone_creature STR_VAR creature=~%creature%=>%hardname%~ edits editstring="%editstring% strip_script=>dw#swpdn insert_script_high=>dw#swpdn" END
       LAF edit_creature STR_VAR creature edits=~~ editstring=~strip_script=>dw#swpup insert_script_high=>dw#swpup~ END

    END ELSE BEGIN

       LAF edit_creature STR_VAR creature edits editstring END

    END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////
///      code to evaluate an ability score that, if set to 9, should be reset to between 14 and 18
/////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION determine_stat
   INT_VAR lower=14
           higher=18
   STR_VAR stat=""
   RET value
BEGIN
   LPF "CRE_read_%stat%" RET value END
   PATCH_IF value=9 BEGIN
      SET value=RANDOM (lower higher)
   END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////
////   markers, to put a temporary Splstate on the target
//////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION build_marker_spell
    INT_VAR duration=0
    STR_VAR marker_name=""
            splstate=""
BEGIN
   ACTION_IF enhanced_edition BEGIN
     LAF ds_resolve_stat INT_VAR ids=1 STR_VAR id="%splstate%" RET code=stat_ind END
     ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
        parameter2=>"%code%"
        parameter1=>1
        opcode=>328
        target=>2
        timing=>0
        duration=>"%duration%"
        mode=>1
     END
   END ELSE BEGIN
     LAF ds_resolve_stat INT_VAR ids=2 STR_VAR id="%splstate%" RET code=stat_ind END
     ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
        parameter2a=>"%code%"
        parameter2b=>1
        parameter1=>1
        opcode=>318
        target=>2
        timing=>0
        duration=>"%duration%"
     END
   END
   LAF make_spell STR_VAR spell="%marker_name%" editstring="add_basic_ability=>null add_effect=>patch_data" END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION return_detectable_string STR_VAR type="" RET string BEGIN
   OUTER_SET ids=enhanced_edition? 1: 2
   LAF ds_resolve_stat INT_VAR ids STR_VAR id="%type%" RET stat_ind END
   ACTION_IF enhanced_edition BEGIN
      OUTER_SPRINT string "opcode=>328 parameter1=>1 parameter2=>%stat_ind% mode=>1 target=>2"
   END ELSE BEGIN
      OUTER_SPRINT string "opcode=>318 parameter1=>1 parameter2a=>%stat_ind% parameter2b=>1 target=>2"
   END
END

//////////////////////////////////////////////////////////////////////////////////////
// duplicate of version in ds.tph

DEFINE_ACTION_FUNCTION ds_resolve_stat INT_VAR ids=2 index=0 delete=0 STR_VAR id="" RET stat_ind BEGIN

OUTER_SPRINT ids_file stats OUTER_SET min_new=401

ACTION_IF ids=0 BEGIN OUTER_SPRINT ids_file extstate OUTER_SET min_new=0 END
ACTION_IF ids=1 BEGIN OUTER_SPRINT ids_file splstate OUTER_SET min_new=0 END

COPY_EXISTING ~%ids_file%.ids~ override
  stat_ind=0
  found=0

  READ_2DA_ENTRIES_NOW stats 2
  FOR (row=0;row<stats;row+=1) BEGIN
    READ_2DA_ENTRY_FORMER stats row 0 ind
    READ_2DA_ENTRY_FORMER stats row 1 str
    SET $stat("%row%") = ind
    PATCH_IF index BEGIN
      PATCH_IF ind=index BEGIN
        REMOVE_2DA_ROW row 2
        found=1
        PATCH_IF delete=0 BEGIN
          INSERT_2DA_ROW row 2 ~%index% %id%~
        END
        row=stats
      END
    END ELSE BEGIN
      PATCH_IF ~%str%~ STRING_EQUAL_CASE ~%id%~ BEGIN
        stat_ind=ind
        found=1
     /* row=stats */ // don't stop looking, the same ID may be assigned to a greater index, which takes priority when compiling
      END
    END
  END

  PATCH_IF found=0 BEGIN
    stat_ind=min_new
    PHP_EACH stat AS row => ind BEGIN
      PATCH_IF found=0 && (row+1 < stats) BEGIN // not at the end of file
        next_row = row+1
        next_ind = EVAL $stat("%next_row%")
        PATCH_IF index BEGIN
          PATCH_IF index<next_ind && index>ind BEGIN
            INSERT_2DA_ROW next_row 2 ~%index% %id%~
            found=1
          END
        END ELSE BEGIN
          PATCH_IF stat_ind<next_ind BEGIN
            PATCH_IF ind<stat_ind BEGIN
              INSERT_2DA_ROW next_row 2 ~%stat_ind% %id%~
              found=1
            END ELSE BEGIN
              stat_ind+=1
              PATCH_IF stat_ind<next_ind BEGIN
                INSERT_2DA_ROW next_row 2 ~%stat_ind% %id%~
                found=1
              END
            END
          END
        END
      END ELSE BEGIN // at the end of file
        PATCH_IF found=0 BEGIN
          PATCH_IF index BEGIN
            INSERT_2DA_ROW stats 2 ~%index% %id%~
          END ELSE BEGIN
            PATCH_IF stat_ind>ind BEGIN
              INSERT_2DA_ROW stats 2 ~%stat_ind% %id%~
            END ELSE BEGIN
              stat_ind+=1
              INSERT_2DA_ROW stats 2 ~%stat_ind% %id%~
            END
          END
        END
      END
    END // PHP_EACH
  END

END

//////////////////////////////////////////////////////////////////////////////////////
// make spells that grant/remove instant uninterruptible casting
//
// we do this via sectype for backwards-compatibility
//////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION innate_casting_effects BEGIN

   LAF check_label STR_VAR label=dw#innate_casting RET value END
   ACTION_IF !value BEGIN
      LAF make_label STR_VAR label=dw#innate_casting END

      ADD_SECTYPE Innate_Casting ""


      ACTION_CLEAR_ARRAY patch_data
      ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
         secondary=>~%Innate_Casting%~
         add_basic_ability=>null
         add_effect_inline'0=>~opcode=>221 timing=>1 target=>1 parameter1=>10 parameter2=>"%Innate_Casting%"~
         add_effect_inline=>~opcode=>101 timing=>1 target=>1 parameter2=>38 at_end=>1~
         add_effect_inline'=>~opcode=>101 timing=>1 target=>1 parameter2=>60 at_end=>1~
         add_effect_inline''=>~opcode=>101 timing=>1 target=>1 parameter2=>80 at_end=>1~
         add_effect_inline'''=>~opcode=>189 timing=>1 target=>1 parameter1=>10 at_end=>1~
      END
      LAF make_spell STR_VAR spell=dw#innat edits=patch_data END

      ACTION_CLEAR_ARRAY patch_data
      ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
         add_basic_ability=>null
         add_effect_inline'0=>~opcode=>221 timing=>1 target=>1 parameter1=>10 parameter2=>"%Innate_Casting%"~
      END
      LAF make_spell STR_VAR spell=dw#inrem edits=patch_data END

  END
END

//////////////////////////////////////////////////////////////////////////////////////
// make spells that grant/remove instant see-invisible
//
// we do this via sectype for backwards-compatibility
//////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION innate_see_invisible_effects BEGIN

   LAF check_label STR_VAR label=dw#innate_see_invisible RET value END
   ACTION_IF !value BEGIN
      LAF make_label STR_VAR label=dw#innate_see_invisible END

      ADD_SECTYPE Innate_See_Invisible ""


      ACTION_CLEAR_ARRAY patch_data
      ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
         secondary=>~%Innate_See_Invisible%~
         add_basic_ability=>null
         add_effect_inline'0=>~opcode=>221 timing=>1 target=>1 parameter1=>10 parameter2=>"%Innate_See_Invisible%"~
         add_effect_inline=>~opcode=>101 timing=>1 target=>1 parameter2=>38 at_end=>1~
      END
      LAF make_spell STR_VAR spell=dw#sinat edits=patch_data END

      ACTION_CLEAR_ARRAY patch_data
      ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
         add_basic_ability=>null
         add_effect_inline'0=>~opcode=>221 timing=>1 target=>1 parameter1=>10 parameter2=>"%Innate_See_Invisible%"~
      END
      LAF make_spell STR_VAR spell=dw#sirem edits=patch_data END

  END
END

//////////////////////////////////////////////////////////////////////////////////////
// make spells that modify hitpoints
//
// we do this via sectype for backwards-compatibility
//////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION hitpoint_boost_effects
        INT_VAR mult=100
                remove_only=0
        STR_VAR spell=""
        RET sectype
BEGIN
      ADD_SECTYPE Hitpoint_Bonus ""
      OUTER_SPRINT sectype "%Hitpoint_Bonus%"
      ACTION_IF !remove_only BEGIN
       ACTION_CLEAR_ARRAY patch_data
       ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
         secondary=>~%Hitpoint_Bonus%~
         add_basic_ability=>null
         add_effect_inline=>~opcode=>221 timing=>1 target=>1 parameter1=>10 parameter2=>"%Hitpoint_Bonus%"~
         add_effect_inline'=>~opcode=>18 timing=>0 duration=>60 target=>1 parameter1=>%mult% parameter2=>2 at_end=>1~
       END
       LAF make_spell STR_VAR spell edits=patch_data END
      END
      ACTION_IF !FILE_EXISTS_IN_GAME "dw#hprem.spl" BEGIN
        ACTION_CLEAR_ARRAY patch_data
        ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
           add_basic_ability=>null
           add_effect_inline=>~opcode=>221 timing=>1 target=>1 parameter1=>10 parameter2=>"%Hitpoint_Bonus%"~
        END
        LAF make_spell STR_VAR spell=dw#hprem edits=patch_data END
      END


END

//////////////////////////////////////////////////////////////////////////////////////
// make spells that modify resistance
//
// we do this via sectype for backwards-compatibility
//////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION resistance_boost_effects
        INT_VAR bonus=50
        STR_VAR spell=""
BEGIN

      ADD_SECTYPE Resistance_Bonus ""


      ACTION_CLEAR_ARRAY patch_data
      ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
         secondary=>~%Resistance_Bonus%~
         add_basic_ability=>null
         add_effect_inline=>~opcode=>221 timing=>1 target=>1 parameter1=>10 parameter2=>"%Resistance_Bonus%"~
         add_effect_inline'1=>~opcode=>86 timing=>0 duration=>999999 target=>1 parameter1=>%bonus% parameter2=>1 at_end=>1~
         add_effect_inline'2=>~opcode=>87 timing=>0 duration=>999999 target=>1 parameter1=>%bonus% parameter2=>1 at_end=>1~
         add_effect_inline'3=>~opcode=>88 timing=>0 duration=>999999 target=>1 parameter1=>%bonus% parameter2=>1 at_end=>1~
         add_effect_inline'4=>~opcode=>89 timing=>0 duration=>999999 target=>1 parameter1=>%bonus% parameter2=>1 at_end=>1~
      END
      LAF make_spell STR_VAR spell edits=patch_data END
      ACTION_IF !FILE_EXISTS_IN_GAME "dw#rerem.spl" BEGIN
        ACTION_CLEAR_ARRAY patch_data
        ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
           add_basic_ability=>null
           add_effect_inline=>~opcode=>221 timing=>1 target=>1 parameter1=>10 parameter2=>"%Resistance_Bonus%"~
        END
        LAF make_spell STR_VAR spell=dw#rerem edits=patch_data END
      END


END


//////////////////////////////////////////////////////////////////////////////////////
// Update difficulty widget; set difficulty variable
//////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION define_difficulty 
    STR_VAR type=""
    RET difficulty_variable
BEGIN
    // read the table to get out the actual difficulty variable
    LAF read_table_entry_external INT_VAR colnum=1 STR_VAR file="difficulty_controls.2da" locbase=lib row="%type%" RET difficulty_variable=value END
    // check to see if it's been installed in the dlg file already
    LAF check_label STR_VAR label="dw#%type%_difficulty" RET value END
    ACTION_IF !value BEGIN
      // if not, install it, and make a marker to say so
      LAF make_label STR_VAR label="dw#%type%_difficulty" END
      LAF swap_text STR_VAR files="dw#diffi.dlg" swaps=~Global("DW_%type%_disable","GLOBAL",99)=>null~ END
    END
END



