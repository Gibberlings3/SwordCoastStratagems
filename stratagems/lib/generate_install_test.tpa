DEFINE_ACTION_FUNCTION generate_install_test
    INT_VAR test_component_number=0
            maximum=10000
    STR_VAR
            root=stratagems
            tp2="setup-stratagems"
            bat="stratagems_test"
BEGIN
   OUTER_SPRINT comp_list ""
   COPY "%root%/%tp2%.tp2" "%workspace%"
      REPLACE_TEXTUALLY "DESIGNATED +[0-9]+ NO_LOG_RECORD" ""
      REPLACE_TEXTUALLY "DESIGNATED +[0-9]+ DEPRECATED" ""
      REPLACE_EVALUATE
      "DESIGNATED +\([0-9]+\) +GROUP +@[0-9]+ +SUBCOMPONENT @\([0-9]+\)"
      BEGIN
        PATCH_IF !VARIABLE_IS_SET $multicomponent("%MATCH2%") BEGIN
           SPRINT $multicomponent("%MATCH2%") ""
           SPRINT replace "DESIGNATED %MATCH1%"
        END ELSE BEGIN
           SPRINT replace ""
        END
      END
      "%replace%"
      REPLACE_EVALUATE
      "DESIGNATED +\([0-9]+\)" 
      BEGIN
        PATCH_IF !(MATCH1=test_component_number) && MATCH1<maximum BEGIN
          SPRINT comp_list "%comp_list% %MATCH1%"
        END
      END
      ""
   BUT_ONLY

   ACTION_IF FILE_EXISTS "%tp2%.exe" BEGIN
      OUTER_SPRINT exe "%tp2%"
   END ELSE BEGIN
      OUTER_SPRINT exe "setup-%tp2%"
   END
<<<<<<<< .../bat_template.bat
%exe% --language 0 --uninstall --force-install-list %comp_list%
>>>>>>>>
   DELETE + "%bat%.bat"
   COPY + ".../bat_template.bat" "%bat%.bat" EVALUATE_BUFFER


END