////////////////////////////////////////////////////////
////////////////////////////////////////////////////////
//// 	Things to do only once
////////////////////////////////////////////////////////
////////////////////////////////////////////////////////

ACTION_IF !VARIABLE_IS_SET ~initialise~ BEGIN

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN FAIL @27 END // Modmerge check

  CLEAR_EVERYTHING            // occasionally, e.g. after installing IWD spells, we call this again. Make sure we get a fresh start.
  SILENT
  OUTER_SPRINT MOD_VERSION @0
  OUTER_SPRINT scsroot "stratagems"
  OUTER_SPRINT ini_file "stratagems.ini"
  OUTER_SPRINT user_ini_file "stratagems_user.ini"
  OUTER_SET initialise=1


////////////////////////////////////////////////////////
//// general variables, macros and functions
////////////////////////////////////////////////////////

   INCLUDE "%scsroot%/lib/alter_script.tpa"
   INCLUDE ~%scsroot%/sfo/install_sfo.tpa~
   INCLUDE ~%scsroot%/ds/ds.tph~


   SILENT
////////////////////////////////////////////////////////
//// Make sure basic marker file exists
////////////////////////////////////////////////////////

  ACTION_IF !FILE_EXISTS "override/dw#marker_file.itm" BEGIN
     COPY "%scsroot%/lib/resource/misc02.itm" "override/dw#marker_file.itm"
  END


////////////////////////////////////////////////////////
//// sort out languages (doing it a slightly nonstandard way to keep 
//// the main mod directory immutable)
////////////////////////////////////////////////////////

  ACTION_IF debug_variable=3 BEGIN
     PRINT "Beginning charset handling: press a key to continue"
     ACTION_READLN blook
     SILENT
  END

 LAF check_ini STR_VAR ini=force_tra_rebuild RET value END
 OUTER_SET force_tra_rebuild=value
 LAF handle_charsets_immutably INT_VAR force_tra_rebuild RET scs_tra_loc END
  LOAD_TRA "%scs_tra_loc%/english/setup.tra" "%scs_tra_loc%/english/shared.tra" "%scs_tra_loc%/%LANGUAGE%/setup.tra" "%scs_tra_loc%/%LANGUAGE%/shared.tra"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// install ToB Extender(Windows only)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_IF (!enhanced_edition && !FILE_EXISTS "tobex_ini/tobextweak.ini") BEGIN

    OUTER_SPRINT ~TOBEX_MOD_DIRECTORY~ ~%scsroot%~
    LOAD_TRA ~%scsroot%/tobex_redist/tobex.tra~
    INCLUDE ~%scsroot%/tobex_redist/tobex.tpa~
    SILENT

END

////////////////////////////////////////////////////////
//// detect my and third-party mods
////////////////////////////////////////////////////////

        // Detect Ascension

        ACTION_IF FILE_EXISTS_IN_GAME "fingrom.cre" BEGIN
           OUTER_SET ascension=1
        END ELSE BEGIN
           OUTER_SET ascension=0
        END

        // Detect Spell Revisions

        ACTION_IF FILE_EXISTS_IN_GAME ~dvimhere.mrk~ THEN BEGIN
           OUTER_SET demivrgvs=1
        END ELSE BEGIN
           OUTER_SET ~demivrgvs~=0
        END

        // detect Item Revisions
        
        ACTION_IF MOD_IS_INSTALLED "item_rev.tp2" 0 BEGIN
           OUTER_SET item_revisions=1
        END ELSE BEGIN
           OUTER_SET item_revisions=0
        END

        // detect Refinements HLAs

        ACTION_IF FILE_EXISTS_IN_GAME ~tg#come.spl~ BEGIN
           OUTER_SET ~refinements~=1
        END ELSE BEGIN
           OUTER_SET ~refinements~=0
        END
        
        // detect Talents of Faerun HLAs [TD]
        
        OUTER_SET talents_of_faerun_hlas=0

        // detect IWD spellsystem
        
        LAF check_label STR_VAR label=dw#iwdspells_arcane RET dw_iwd_arcane=value END
        ACTION_IF ( (MOD_IS_INSTALLED "iwdification.tp2" 30) || dw_iwd_arcane || is_iwd ) BEGIN
            OUTER_SET iwd_arcane=1
        END ELSE BEGIN
            OUTER_SET iwd_arcane=0
        END
        LAF check_label STR_VAR label=dw#iwdspells_divine RET dw_iwd_divine=value END
        ACTION_IF ( (MOD_IS_INSTALLED "iwdification.tp2" 40) || dw_iwd_divine || is_iwd ) BEGIN
            OUTER_SET iwd_divine=1
        END ELSE BEGIN
            OUTER_SET iwd_divine=0
        END

        // detect Rogue Rebalancing poisoned weapons
        
        ACTION_IF FILE_EXISTS_IN_GAME "fl#tas02.spl" BEGIN
           OUTER_SET rr_poison=1
        END ELSE BEGIN
           OUTER_SET rr_poison=0
        END

        // detect Infinity Animations

        ACTION_IF MOD_IS_INSTALLED ~setup-infinityanimations.tp2~ ~0~ THEN BEGIN
           OUTER_SET ~infinity_animations~=1
        END ELSE BEGIN
           OUTER_SET ~infinity_animations~=0
        END

        // set relevant base folders
        ACTION_IF GAME_IS iwdee BEGIN
           OUTER_SPRINT base_folder_spells_mage iwdtrue
           OUTER_SPRINT base_folder_spells_priest iwdtrue
        END ELSE BEGIN
         ACTION_IF demivrgvs BEGIN
           OUTER_SPRINT base_folder_spells_mage demivrgvs
           OUTER_SPRINT base_folder_spells_priest demivrgvs
         END ELSE BEGIN
          ACTION_IF iwd_arcane BEGIN
           OUTER_SPRINT base_folder_spells_mage iwd
          END ELSE BEGIN
           OUTER_SPRINT base_folder_spells_mage vanilla
          END
          ACTION_IF iwd_divine BEGIN
           OUTER_SPRINT base_folder_spells_priest iwd
          END ELSE BEGIN
           OUTER_SPRINT base_folder_spells_priest vanilla
          END
         END
        END

        ACTION_IF talents_of_faerun_hlas BEGIN
             OUTER_SPRINT base_folder_hlas tof
        END ELSE
        ACTION_IF refinements BEGIN
             OUTER_SPRINT base_folder_hlas refinements
        END ELSE BEGIN
             OUTER_SPRINT base_folder_hlas vanilla
        END


///////////////////////////////////////////////////
// Add Spell_Rev spell.ids entries
///////////////////////////////////////////////////

    ACTION_IF demivrgvs BEGIN
             LAF check_label STR_VAR label=dw#spell_rev_ids RET value END
             ACTION_IF !value BEGIN
                CLEAR_IDS_MAP
                LAF sort_ids STR_VAR ids=spell END
             END
   END

///////////////////////////////////////////////////
// Get resources (need to do AFTER SR/Refinements detected)
///////////////////////////////////////////////////

  ACTION_IF !VARIABLE_IS_SET read_resources BEGIN
     OUTER_SET read_resources=1
  END

  ACTION_IF debug_variable=3 BEGIN
     PRINT "Beginning resource collection: press a key to continue"
     ACTION_READLN blook
     SILENT
  END



  // Construct HLA table if it doesn't exist

    MKDIR "%data_loc_root%/dw_shared"
    ACTION_IF !FILE_EXISTS "%data_loc_root%/dw_shared/dw#hla_feat.2da" BEGIN
       LAF include STR_VAR file=initialise_hla_feat.tph locbase=lib END
       SILENT
       LAF initialise_hla_feat END
    END

    APPEND_OUTER "%data_loc_root%/dw_shared/dw#hla_feat.2da" "WIZARD_IMPROVED_ALACRITY_NPC dw#qalac grant" UNLESS "WIZARD_IMPROVED_ALACRITY_NPC dw#qalac grant"// helpful to have this present right away, to avoid false-positive error messages



  ACTION_IF read_resources BEGIN
   ACTION_FOR_EACH macro IN spellcodes saving_throws hit_point_levels stats PCs thac0 BEGIN
     ACTION_IF debug_variable=3 BEGIN
         PRINT "Launching the read_in_%macro% macro: press a key to continue"
         ACTION_READLN blook
         SILENT
     END
     PRINT ~Reading in data: %macro%~
     SILENT
     LAUNCH_ACTION_MACRO ~read_in_%macro%~
   END
  END

     ACTION_IF debug_variable=3 BEGIN
         PRINT "All macros read in: press a key to continue"
         ACTION_READLN blook
     END


  // markers to detect certain spells

  OUTER_SPRINT ITEM_MM_SEEN dw#mmsee
  OUTER_SPRINT ITEM_PSIONIC_SEEN dw#pssee
  OUTER_SPRINT ITEM_ANTIWEB_SEEN dw#wbsee
  OUTER_SPRINT IMPRISONMENT_MARKER dw#mkimp
  OUTER_SPRINT MAZE_MARKER dw#mkmaz



////////////////////////////////////////////////////////
//// stratagems-specific macros and functions
////////////////////////////////////////////////////////

     ACTION_IF debug_variable=3 BEGIN
         PRINT "Starting install of shared functions: press a key to continue"
         ACTION_READLN blook
         SILENT
     END
  INCLUDE ~%scsroot%/lib/stratagems_shared.tph~
  INCLUDE "%scsroot%/lib/kit_hardcode.tpa"
  ACTION_IF enhanced_edition BEGIN
     LAF build_kit_spell_enforcer STR_VAR kit="ARCHER ASSASIN BERSERKER BARBARIAN KENSAI SWASHBUCKLER WIZARDSLAYER BLACKGUARD" END
  END ELSE BEGIN
     LAF build_kit_spell_enforcer STR_VAR kit="ARCHER ASSASIN BERSERKER BARBARIAN KENSAI SWASHBUCKLER WIZARDSLAYER" END
  END
     ACTION_IF debug_variable=3 BEGIN
         PRINT "Stratagems_shared now installed: press a key to continue"
         ACTION_READLN blook
         SILENT
     END

////////////////////////////////////////////////////////
//// make sure dir.ids is present
////////////////////////////////////////////////////////

   COPY "%scsroot%/lib/dir.ids" override
   COPY_EXISTING "action.ids" override
         REPLACE_TEXTUALLY "Face\*)" "Face*DIR)"
   BUT_ONLY

////////////////////////////////////////////////////////
//// mark some functions as not-to-be-parsed
////////////////////////////////////////////////////////

OUTER_SPRINT $SFO_do_not_parse_arguments("enforce_mage") ""
OUTER_SPRINT $SFO_do_not_parse_arguments("enforce_clericmage") ""
OUTER_SPRINT $SFO_do_not_parse_arguments("enforce_priest") ""
OUTER_SPRINT $SFO_do_not_parse_arguments("enforce_charclass") ""
OUTER_SPRINT $SFO_do_not_parse_arguments("genai") ""
OUTER_SPRINT $SFO_do_not_parse_arguments("fiend_general") ""

///////////////////////////////////////////////////////////////
///     Crossplatform content
///////////////////////////////////////////////////////////////

   OUTER_SPRINT component_loc lib
   INCLUDE ~%scsroot%/%component_loc%/crossplatform.tpa~


///////////////////////////////////////////////////////////////
///	Possibly-missing IDS entries
///////////////////////////////////////////////////////////////


  COPY_EXISTING ~SPELL.IDS~ ~override~
    REPLACE_TEXTUALLY ~PSIONIC _SUPERIOR_INVISIBILITY~ ~PSIONIC_SUPERIOR_INVISIBILITY~
  BUT_ONLY

  APPEND ~STATE.IDS~ ~0x8010202D STATE_DISABLED~
  UNLESS ~0x8010202D STATE_DISABLED~  /// includes Helpless, Stunned, Panic, Sleeping, Charmed, Feebleminded, Confused

  APPEND ~STATE.IDS~ ~0x00000029 STATE_IMMOBILE~
  UNLESS ~0x00000029 STATE_IMMOBILE~  /// includes Helpless, Stunned, Sleeping

  APPEND ~STATE.IDS~ ~0x00400010 STATE_NOT_TARGETABLE~
  UNLESS ~0x00400010 STATE_NOT_TARGETABLE~  /// includes Improved Invisibility and plain Invisibility

  APPEND ~STATE.IDS~ ~0x00000FC0 STATE_REALLY_DEAD~
  UNLESS ~0x00000FC0 STATE_REALLY_DEAD~   

///////////////////////////////////////////////////
// NPC summon fiend spell names
///////////////////////////////////////////////////

  OUTER_SPRINT WIZARD_SUMMON_FIEND_SCS "dw#sumfi"
  OUTER_SPRINT WIZARD_CACOFIEND_SCS "dw#cacof"
  OUTER_SPRINT WIZARD_GATE_SCS "dw#gatem"
  OUTER_SPRINT CLERIC_GATE_SCS "dw#gatep"

///////////////////////////////////////////////////
// String localisation
///////////////////////////////////////////////////

ACTION_IF GAME_IS "bgee" BEGIN
   OUTER_SPRINT trigger_string 25101
   OUTER_SPRINT minor_sequencer_string 26469
   OUTER_SPRINT sequencer_string 24618
   OUTER_SPRINT contingency_string 26577
   OUTER_SPRINT chain_contingency_string "-1"
   OUTER_SPRINT polymorphed_string 25124
END ELSE
ACTION_IF GAME_IS "iwdee" BEGIN
   OUTER_SPRINT trigger_string 26243
   OUTER_SPRINT minor_sequencer_string 25889
   OUTER_SPRINT sequencer_string 25951
   OUTER_SPRINT contingency_string 25942
   OUTER_SPRINT chain_contingency_string 26328
   OUTER_SPRINT polymorphed_string 37703
END ELSE BEGIN
   OUTER_SPRINT trigger_string 26243
   OUTER_SPRINT minor_sequencer_string 5013
   OUTER_SPRINT sequencer_string 25951
   OUTER_SPRINT contingency_string 25942
   OUTER_SPRINT chain_contingency_string 26328
   OUTER_SPRINT polymorphed_string 339
END



OUTER_SET SFO_unusable_string_tra_ref=100400
OUTER_SET SFO_usable_string_tra_ref=100401

 ACTION_IF !("%LANGUAGE%" STRING_EQUAL_CASE "EMPTY_LANGUAGE") BEGIN
   OUTER_SPRINT SFO_reserved_previously_string @100300 // " (cast previously)"
 END

////////////////////////////////////////////////////////
//// Disable HLAs and sequencers/triggers on BG1-only install; disable HLAs on IWD install
////////////////////////////////////////////////////////

ACTION_IF !is_bg2  BEGIN
   OUTER_SET disable_hlas=1
END ELSE BEGIN
   OUTER_SET disable_hlas=0
END

ACTION_IF !(is_bg2 || is_iwd)  BEGIN
   OUTER_SET disable_sequencers=1
END ELSE BEGIN
   OUTER_SET disable_sequencers=0
END

///////////////////////////////////////////////////////////////
// Set SSL library files and SSL variables
///////////////////////////////////////////////////////////////

   OUTER_SPRINT ssllibrary "%scsroot%/ssl/library %scsroot%/ssl/difficulty"

   ACTION_IF ascension BEGIN
      OUTER_SPRINT sslvariables "%sslvariables%&IsAscension=True"
   END

   ACTION_IF enhanced_edition BEGIN
        OUTER_SPRINT sslvariables ~%sslvariables%&EnhancedEdition=True~
        OUTER_SPRINT ssllibrary "%ssllibrary% %scsroot%/ssl/library_ee %scsroot%/ssl/libwpn_ee"
   END ELSE BEGIN
        OUTER_SPRINT ssllibrary "%ssllibrary% %scsroot%/ssl/libwpn_vanilla"
   END
   
   ACTION_IF is_iwd BEGIN
      OUTER_SPRINT sslvariables ~%sslvariables%&IsIWD=True~
   END

   ACTION_IF dw_iwd_arcane BEGIN
        OUTER_SPRINT sslvariables "%sslvariables%"^"&"^"SCSIWDArcane=True"
   END
   ACTION_IF iwd_arcane && !demivrgvs BEGIN
        OUTER_SPRINT sslvariables "%sslvariables%"^"&"^"IsIWDArcane=True"
   END

   ACTION_IF dw_iwd_divine BEGIN
        OUTER_SPRINT sslvariables "%sslvariables%"^"&"^"SCSIWDDivine=True"
        OUTER_SPRINT ssllibrary "%ssllibrary% %scsroot%/ssl/library_iwd_divine"
   END
   ACTION_IF iwd_divine && !demivrgvs BEGIN
        OUTER_SPRINT sslvariables "%sslvariables%"^"&"^"IsIWDDivine=True"
        OUTER_SPRINT ssllibrary "%ssllibrary% %scsroot%/ssl/library_iwd_divine"
   END

   ACTION_IF demivrgvs BEGIN
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Demivrgvs=True~
           OUTER_SPRINT ssllibrary ~%ssllibrary% %scsroot%/ssl/library_sr~
   END
   ACTION_IF item_revisions BEGIN
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~ItemRevisions=True~
           OUTER_SPRINT ssllibrary ~%ssllibrary% %scsroot%/ssl/library_ir~
   END ELSE BEGIN
           OUTER_SPRINT ssllibrary ~%ssllibrary% %scsroot%/ssl/library_notir~
   END

   ACTION_IF refinements BEGIN
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~Refinements=True~
   END

  ACTION_IF GAME_IS bgt BEGIN
     OUTER_SPRINT sslvariables ~%sslvariables%&IsTrilogy=True~
  END
  
  ACTION_IF rr_poison BEGIN
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&~^~RogueRebalancingPoison=True~
  END

  ACTION_IF !is_bg2 BEGIN
     OUTER_SPRINT sslvariables ~%sslvariables%&PlusUnknownSafe=PlusNullSafe&MultipleDamageTypeSafe=NullSafe&IsBG1Only=True~
  END

        // check for safe version of imprisonment

        ACTION_IF FILE_EXISTS_IN_GAME ~tb#impri.spl~ OR FILE_EXISTS_IN_GAME ~dvimhere.mrk~ BEGIN
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&SafeImprisonment=True~
        END


  // check for external library (must be last)

  LAF check_ini STR_VAR ini=library_override RET value END
  ACTION_IF ("%value%" STRING_COMPARE_CASE none && "%value%" STRING_COMPARE_CASE "0") BEGIN
      OUTER_SPRINT list "%value%"
      OUTER_WHILE "%list%" STRING_COMPARE_CASE "" BEGIN
         LAF return_first_entry STR_VAR list separator="|" RET entry list END
         OUTER_SPRINT ssllibrary "%ssllibrary% %data_loc%/%entry%.slb"
      END
  END


////////////////////////////////////////////////////////
//// 	Check if Free Action protects from Stun (hardcoded, painfully slow otherwise)
////////////////////////////////////////////////////////

      ACTION_IF debug_variable=3 BEGIN
         PRINT "Checking for Free Action/Stun: press a key to continue"
         ACTION_READLN blook
         SILENT
     END
   LAF check_label STR_VAR label="dw#freeaction_stun" RET stun=value END
   LAF check_label STR_VAR label="dw#freeaction_nostun" RET nostun=value END
   ACTION_IF !(stun || nostun) BEGIN
     COPY_EXISTING "sppr403.spl" override
        GET_OFFSET_ARRAY2 fx_array (LONG_AT 0x64) SPL_V10_HEAD_EFFECTS
        PHP_EACH fx_array AS null=>fx_off BEGIN
               PATCH_IF ((SHORT_AT fx_off)=101 && (SHORT_AT (fx_off+0x8))=45) BEGIN
                  SET stun=1
               END
        END
     BUT_ONLY
     ACTION_IF stun BEGIN
         LAF make_label STR_VAR label="dw#freeaction_stun" END
      END ELSE BEGIN
         LAF make_label STR_VAR label="dw#freeaction_nostun" END
      END
   END
   ACTION_IF stun THEN BEGIN
       OUTER_SPRINT ssllibrary "%ssllibrary% %scsroot%/ssl/library_freeactionstun"
   END

///////////////////////////////////////////////////
// load in HLAs (it gets done a zillion times, may as well do it once up-front)
///////////////////////////////////////////////////

ACTION_IF !disable_hlas BEGIN
   LAF include STR_VAR file=hla.tph locbase=lib END
   LAM read_in_hla_choices
END


/////////////////////////////////////////////////////////
///  Create testing item
////////////////////////////////////////////////////////

ACTION_IF !FILE_EXISTS_IN_GAME ~dw#kilsw.itm~ BEGIN
   LAF include STR_VAR file=killsword.tph  END
   LAF killsword END
END

VERBOSE
END   // end of do-once part

////////////////////////////////////////////////////////
////////////////////////////////////////////////////////
//// 	Things to do every time
////////////////////////////////////////////////////////
////////////////////////////////////////////////////////

  INCLUDE "%scsroot%/lib/alter_effect.tpa" // these overwrite standard functions, which get reset each time


   LOAD_TRA "%scs_tra_loc%/english/setup.tra" "%scs_tra_loc%/english/shared.tra" "%scs_tra_loc%/%LANGUAGE%/setup.tra" "%scs_tra_loc%/%LANGUAGE%/shared.tra" // do this again just in case


  LAF check_ini STR_VAR ini=God_Does_Not_Play_Dice RET value END
  
  ACTION_IF value BEGIN
     RANDOM_SEED 3
  END ELSE BEGIN
     RANDOM_SEED 3.14
  END

///////////////////////////////////////////////////
// detect whether Truesight cures blindness
///////////////////////////////////////////////////

    LAF check_label STR_VAR label="dw#truesightblindness" RET value END
    ACTION_IF (value || demivrgvs ) BEGIN
           OUTER_SPRINT ~sslvariables~ ~%sslvariables%~^~&TruesightCuresBlindness=True~
    END
    
     ACTION_IF debug_variable=3 BEGIN
         FAIL "SCS setup complete; exiting, since this is just speedtest code"
     END

    


