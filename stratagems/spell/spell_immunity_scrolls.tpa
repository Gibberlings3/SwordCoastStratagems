
DEFINE_ACTION_FUNCTION spell_immunity_scrolls BEGIN



<<<<<<<< .../stratagems-inline/immunity.2da
spell	scroll		strref
SPWI590	dw#si590	31671
SPWI591	dw#si591	31673
SPWI592	dw#si592	31677
SPWI593	dw#si593	31678
SPWI594	dw#si594	31675
SPWI595	dw#si595	31676
SPWI596	dw#si596	31672
SPWI597	dw#si597	31674
>>>>>>>>

    LAF process_table STR_VAR table="immunity.2da" inline=yes function=make_immunity_scroll ext_args="old_title=~%old_title%~ old_desc=~%old_desc%~ addon_1=~%addon_1%~ addon_2=~%addon_2%~"  END



    OUTER_SPRINT store ""
    
    ACTION_IF is_iwd BEGIN
       COPY_EXISTING "edion.sto" override
        ADD_STORE_ITEM ~%WIZARD_SPELL_IMMUNITY_SCROLL%~   AFTER  ~%WIZARD_SUN_FIRE_SCROLL%~ #1 #0 #0 ~IDENTIFIED~ #1

    END

    COPY_EXISTING_REGEXP ".*\.sto" override
        PATCH_IF INDEX_BUFFER ("%WIZARD_SPELL_IMMUNITY_SCROLL%") >=0 BEGIN
           SPRINT store "%store% %SOURCE_RES%"
        END
    BUT_ONLY
    MAKE_PATCH
       match=>"item_resource=%WIZARD_SPELL_IMMUNITY_SCROLL%"
       number_to_add=>8
       item_resource=>"entry_index from [dw#si590 dw#si591 dw#si592 dw#si593 dw#si594 dw#si595 dw#si596 dw#si597]"
    END

    ACTION_IF "%store%" STRING_COMPARE "" BEGIN
       LAF edit_store STR_VAR store editstring="clone_item=>patch_data" END
    END

END

DEFINE_ACTION_FUNCTION make_immunity_scroll
    STR_VAR spell=""
            scroll=""
            strref=""
BEGIN
     COPY_EXISTING "%WIZARD_SPELL_IMMUNITY%.spl" override
       LPF SPL_name RET old_title=value END
       LPF SPL_description RET old_desc=value END
     BUT_ONLY
     OUTER_SPRINT addon_1 @207
     OUTER_SPRINT addon_2 @208
     COPY_EXISTING "%spell%.spl" override
         LPF SPL_name RET new_title=value END
     BUT_ONLY
     OUTER_SPRINT new_desc "%old_desc%"
     LAF substitute_string STR_VAR input="%old_desc%" arguments="~%old_title%~=>~%new_title%~" RET new_desc=output END
     OUTER_SPRINT new_desc "%new_desc%%addon_1%%new_title%%addon_2%"
     MAKE_PATCH
        set_description=>"%new_desc%"
     END
     LAF edit_spell STR_VAR spell edits=patch_data END
     MAKE_PATCH
        patch_effect_inline=>"match=>~opcode is_in [146 147]~ resource=>%spell%"
        set_name=>"%new_title%"
        set_description=>"%new_desc%"
     END
     LAF clone_item STR_VAR item="%WIZARD_SPELL_IMMUNITY_SCROLL%=>%scroll%" edits=patch_data END
END


