DEFINE_ACTION_FUNCTION modify_breach BEGIN


// on non-EE (or SR) breach needs to be school: abjuration, sectype: 0
// on EE breach needs its own bespoke sectype, which spell shield should block


    ACTION_IF enhanced_edition && !demivrgvs BEGIN
		LAF add_silent_sectype STR_VAR sectype="Breach" RET sectype_value END
		COPY_EXISTING "%WIZARD_BREACH%.spl" override
			WRITE_BYTE 0x27 sectype_value
		COPY_EXISTING 
			"%WIZARD_SPELL_SHIELD%.spl" override
			"dw#isssb.spl" override // impervious sanctity of mind
			LPF CLONE_EFFECT INT_VAR match_opcode=226 parameter2=sectype_value END
		IF_EXISTS
    END ELSE BEGIN
		COPY_EXISTING "%WIZARD_BREACH%.spl" override
			WRITE_BYTE 0x25 1
			WRITE_BYTE 0x27 0
    END
   
    // externalise the spells cast by the Wand of Spell Striking

	spl.copy[%WIZARD_PIERCE_MAGIC%=>dw#wndss]
	[
		m.ab.delete{s_level>1}
		m.ab_fx.alter{s_parameter1="-20"|match="s_opcode=166"}
	]
	
	itm.edit[wand18|allow_missing:i=1]
	[
		m.ab.alter{s_projectile=0 s_primary_type=0 s_secondary_type=0}
		m.ab_fx.delete{"~%p_icon%~ STR_EQ spwi513b OR ~%p_icon%~ STR_EQ spwi608b"}
		m.ab_fx.add{s_opcode=146 s_parameter2=1 s_resource:=%WIZARD_BREACH% s_target=2 s_timing=1|match_parent="~%p_icon%~ STR_EQ spwi513b"}
		m.ab_fx.add{s_opcode=146 s_parameter2=1 s_resource:=dw#wndss s_target=2 s_timing=1|match_parent="~%p_icon%~ STR_EQ spwi608b"}
	]


	ACTION_IF is_bg2 || 1 BEGIN
	
	ACTION_IF 0 BEGIN 
		spl.edit[%WIZARD_BREACH%]
		[
			m.ab_fx.alter{s_power=10}
			m.ab_fx.add{s_power=5 s_opcode=0 s_target=2 s_timing=0|insert_point:i=0}
		]
		
	END ELSE BEGIN// the old version 
		// we let Breach affect (demi)liches and Rakshasas by removing their immunity to L5 spells and then explicitly giving them immunity
		// to all L5 spells other than breach

		// find a list of relevant spells
			ACTION_CLEAR_ARRAY l5_spells
			COPY_EXISTING_REGEXP - "\(SPPR\|SPWI\|DWWP\DWPW\|DWPI\|DWWI\)5[0-9a-z][0-9a-z].spl" nowhere
				proceed=0
				PATCH_MATCH "%SOURCE_RES%" WITH spwi513 dwwp513 dwwi513 BEGIN END
				DEFAULT			
					// check sectype
					READ_BYTE 0x27 sectype
					PATCH_MATCH sectype WITH 1 2 3 6 7 8 13 // non-attack spells
					BEGIN END
					DEFAULT
						GET_OFFSET_ARRAY ab_arr SPL_V10_HEADERS
						PATCH_IF VARIABLE_IS_SET $ab_arr(0) BEGIN
							GET_OFFSET_ARRAY2 fx_arr ab_arr_0 SPL_V10_HEAD_EFFECTS
							PHP_EACH fx_arr AS fx_ind=>fx_off BEGIN
								PATCH_IF BYTE_AT (0x3+fx_off)=5 && !(BYTE_AT (0x2+fx_off)=1) BEGIN
									proceed=1
								END
							END
							PATCH_IF VARIABLE_IS_SET $fx_arr(0) BEGIN
								PATCH_IF BYTE_AT (0x3+fx_arr_0)=5 BEGIN // power=5
									SPRINT $l5_spells("%SOURCE_RES%") ""
								END
							END
						END
					END
				END
				PATCH_IF proceed BEGIN
					SPRINT $l5_spells("%SOURCE_RES%") ""
				END
			ACTION_IF FILE_EXISTS_IN_GAME spwi523d.spl BEGIN
				OUTER_SPRINT $l5_spells("spwi523d") ""
			END
			OUTER_SPRINT $l5_spells("%WIZARD_SECRET_WORD%") ""
				
			itm.edit[lich rakring demilich|allow_missing:i=1]
			[
				PHP_EACH l5_spells AS l5_spell=>discard BEGIN
					m.fx.add{s_opcode=206 s_parameter1="-1" s_timing=2 s_target=1 s_resource:="%l5_spell%"}
				END
				m.fx.add{s_opcode=83 s_parameter2=186 s_timing=2 s_target=1} // cloudkill wand
				m.fx.delete{s_opcode=102 AND s_parameter1=5}
			]
			
			spl.edit[dw#licim|allow_missing:i=1]
			[
				PHP_EACH l5_spells AS l5_spell=>discard BEGIN
					m.ab_fx.add{s_opcode=206 s_parameter1="-1" s_timing=2 s_target=1 s_resource:="%l5_spell%"}
				END
				m.ab_fx.add{s_opcode=83 s_parameter2=186 s_timing=2 s_target=1} // cloudkill wand
				m.ab_fx.delete{s_opcode=102 AND s_parameter1=5}
			
			]


		END
	
	END




END


