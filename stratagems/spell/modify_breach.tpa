DEFINE_ACTION_FUNCTION modify_breach BEGIN


// on non-EE breach needs to be school: abjuration, sectype: 0
// on EE breach needs its own bespoke sectype, which spell shield should block


   ACTION_IF enhanced_edition BEGIN
      ADD_SECTYPE Breach ""
      LAF edit_spell STR_VAR spell=WIZARD_BREACH editstring="school=>1 secondary=>%Breach%" END
      MAKE_PATCH
         clone_effect_inline=>"match=>~opcode=226~ parameter2=>%Breach%"
      END
      LAF edit_spell STR_VAR spell=SPELL_SHIELD edits=patch_data END
   END ELSE BEGIN
      LAF edit_spell STR_VAR spell=WIZARD_BREACH editstring="school=>1 secondary=>0" END
   END
   
    // externalise the spells cast by the Wand of Spell Striking

	MAKE_PATCH
		delete_ability=>"ability_min_level>1"
		patch_effect_inline=>"match=>opcode=166 parameter1=>~-20~"
	END
	LAF clone_spell STR_VAR spell="%WIZARD_PIERCE_MAGIC%=>dw#wndss" edits=patch_data END
	
    MAKE_PATCH
       patch_ability_inline=>"projectile=>1"
       delete_effect=>"ability_icon is_in [spwi513b spwi608b]"
       add_effect_inline=>"match=>~ability_icon=spwi513b~ opcode=>146 parameter2=>1 resource=>%WIZARD_BREACH% target=>2 timing=>1"
       add_effect_inline'=>"match=>~ability_icon=spwi608b~ opcode=>146 parameter2=>1 resource=>dw#wndss target=>2 timing=>1"
    END
    LAF edit_item INT_VAR allow_missing=1 STR_VAR item=wand18 edits=patch_data END


   ACTION_IF is_bg2 BEGIN
// we let Breach affect (demi)liches and Rakshasas by removing their immunity to L5 spells and then explicitly giving them immunity
// to all L5 spells other than breach


ACTION_IF FILE_EXISTS_IN_GAME "spwi523d.spl" BEGIN // EE (and SR) Sunfire payload
	OUTER_SPRINT sunfire_spell spwi523d
END ELSE BEGIN
	OUTER_SPRINT sunfire_spell spwi523
END
MAKE_PATCH
 add_effect_global_inline=>~
   opcode=>206
   parameter1=>"-1"
   timing=>2
   target=>1
   var_list=>"[SPDR501 SPWI508 SPWI503 SPWI506 SPWI514 %sunfire_spell% SPPR503 SPPR517]"
   number_to_add=>"length list"
   resource=>"entry_index from list" ~
  add_effect_global_inline'=>~
   opcode=>83
   parameter2=>186
   timing=>2
   target=>1~ // for Cloudkill wand
   delete_effect_global=>"opcode=102 and parameter1=5"
END
LAF edit_item INT_VAR allow_missing=1 STR_VAR item="lich rakring demilich" edits=patch_data END

MAKE_PATCH
 add_effect_inline=>~
   opcode=>206
   parameter1=>"-1"
   timing=>9
   target=>1
   var_list=>"[SPDR501 SPWI508 SPWI503 SPWI506 SPWI514 %sunfire_spell% SPPR503 SPPR517]"
   number_to_add=>"length list"
   resource=>"entry_index from list" ~
  add_effect_inline'=>~
   opcode=>83
   parameter2=>186
   timing=>1
   target=>1~
   delete_effect_global=>"opcode=102 and parameter1=5"
END
LAF edit_spell INT_VAR allow_missing=1 STR_VAR spell="dw#licim" edits=patch_data END

END




END


