DEFINE_ACTION_FUNCTION pro_normal_missiles
BEGIN


    LAF log_this INT_VAR repeat=0 STR_VAR file="scs_modified_spells.2da" input=WIZARD_PROTECTION_FROM_NORMAL_MISSILES END

	ACTION_FOR_EACH arrow IN arow05 arow08 arow09 bolt03 bolt04 BEGIN
		OUTER_SET count=0
		COPY_EXISTING - ~%tutu_var%%arrow%.itm~ nowhere
			READ_LONG 0x64 ab_off
			READ_SHORT 0x2a + ab_off pro_num
			SET pro_num -= 1
			SET $extra_pros("%count%")=pro_num
			++count
		BUT_ONLY
	END


    OUTER_SPRINT desc_add @100

	spl.edit[%WIZARD_PROTECTION_FROM_NORMAL_MISSILES% spra303 ohwi311|allow_missing:i=1 edit_strrefs_in_place:i=1]
	[
		m.ab_fx.clone{s_parameter2=$extra_pros("%entry_index%")|number:i=count match="s_opcode=83 && s_parameter2=4"}
		SPRINT m_description "%m_description%%desc_add%"
	]
	itm.edit[%tutu_scriptm%agebrac|allow_missing:i=1]
	[
		m.ab_fx.clone{s_parameter2=$extra_pros("%entry_index%")|number:i=count match="s_opcode=83 && s_parameter2=4"}
	]

     // Flame Arrow spell needs its own projectile, and Physical Mirror/Entropy Shield need to know about that projectile

	pro_num=pro.copy[arrowflm=>dw#flam][]
	  
	ACTION_IF demivrgvs BEGIN
		OUTER_SPRINT spell "%WIZARD_FLAME_ARROW%E"
	END ELSE BEGIN
		OUTER_SPRINT spell "%WIZARD_FLAME_ARROW%"
	END
	spl.edit[%spell%]
	[
		m.ab.alter{s_projectile:=dw#flam}
	]



	ACTION_IF FILE_EXISTS_IN_GAME "%CLERIC_PHYSICAL_MIRROR%.spl" BEGIN
	  COPY_EXISTING "%CLERIC_PHYSICAL_MIRROR%.spl" override
		LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=197 match_parameter2=3 parameter2=pro_num END  // silent because some versions of game - BGEE, TUTU - don't protect against Flame Arrow projectile to start with
	  BUT_ONLY
	END	  

	ACTION_IF FILE_EXISTS_IN_GAME "%CLERIC_ENTROPY_SHIELD%.spl" BEGIN
	  COPY_EXISTING "%CLERIC_ENTROPY_SHIELD%.spl" override
		LPF CLONE_EFFECT INT_VAR match_opcode=83 match_parameter2=3 parameter2=pro_num END
	  BUT_ONLY
	END

END