DEFINE_ACTION_FUNCTION entropy_shield_abjuration BEGIN

   LAF log_this STR_VAR file="scs_modified_spells_iwd.2da" input=CLERIC_ENTROPY_SHIELD repeat=no END


   // if SCS version not installed, debug
   
   ACTION_IF !dw_iwd_arcane BEGIN
          LAF edit_spell STR_VAR spell=CLERIC_ENTROPY_SHIELD editstring=~delete_effect=>"opcode=83 and parameter2=54"~ END
   END
   
   // core edits
   
   ACTION_IF is_iwd BEGIN
      OUTER_SET icon=194
   END ELSE BEGIN
      OUTER_SET icon=196
   END

   ACTION_IF is_iwd BEGIN
      OUTER_SPRINT immune_list "[%WIZARD_BREACH% %WIZARD_DISPEL_MAGIC% %WIZARD_IMPRISONMENT%]"
   END ELSE BEGIN
      OUTER_SPRINT immune_list "[%WIZARD_BREACH% %WIZARD_DISPEL_MAGIC% %WIZARD_IMPRISONMENT% %DEMILICH_TRAP_SOUL%]"
   END

   MAKE_PATCH
       secondary=>1 // SPELLPROTECTIONS, not COMBATPROTECTIONS
       clone_effect_inline=>~match=>"opcode=142" number_to_add=>"length immune_list" opcode=>318 parameter2=>0 parameter1=>0 resource=>"entry_index from %immune_list%"~ // spell immunity: abjuration
       clone_effect_inline'=>~match=>"opcode=142" opcode=>282 parameter1=>1 parameter2=>6~ // DS scripting state
       entropy_shield_description=>null
   END
   LAF edit_spell STR_VAR spell=CLERIC_ENTROPY_SHIELD edits=patch_data END

   // ds
   
   ACTION_FOR_EACH id IN SI_ABJURATION PRIORITY_DISPEL BEGIN
      LAF ds_make_detectable STR_VAR id resource=CLERIC_ENTROPY_SHIELD END
   END


END

DEFINE_PATCH_FUNCTION entropy_shield_description BEGIN
   READ_LONG 0x50 tlk_entry
   GET_STRREF tlk_entry desc
   SPRINT desc_add @40700
   SPRINT desc "%desc%%desc_add%"
   INNER_ACTION BEGIN
      STRING_SET_EVALUATE tlk_entry "%desc%"
   END
END