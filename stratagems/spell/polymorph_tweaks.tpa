DEFINE_ACTION_FUNCTION polymorph_tweaks BEGIN

   LAF log_this STR_VAR file="scs_modified_spells.2da" input=WIZARD_POLYMORPH_OTHER repeat=no END
   LAF log_this STR_VAR file="scs_modified_spells.2da" input=WIZARD_REMOVE_CURSE repeat=no END
   LAF log_this STR_VAR file="scs_modified_spells.2da" input=CLERIC_REMOVE_CURSE repeat=no END


   // if we don't have the 'curse' icon effect, copy it over
   
   ACTION_IF !FILE_EXISTS_IN_GAME ohbcwou1.eff BEGIN
      COPY "%MOD_FOLDER%/spell/resource/ohbcwou1.eff" override
   END


   // move non-BG2EE installs over to the BG2EE method
   
   ACTION_IF !(GAME_IS ~bg2ee eet~) BEGIN

      ACTION_FOR_EACH file IN "squirp.itm" "isquirl.bam" BEGIN
         COPY "%MOD_FOLDER%/%component_loc%/resource/%file%" override
      END

      COPY_EXISTING "%WIZARD_POLYMORPH_OTHER%.spl" override
          LPF DELETE_EFFECT INT_VAR check_globals=0 END
          LPF polymorph_patch INT_VAR duration=300000 power=4 END
          LPF ADD_SPELL_EFFECT INT_VAR opcode=174 target=2 power=4 timing=1 STR_VAR resource=eff_p07 END
          LPF ADD_SPELL_EFFECT INT_VAR opcode=141 target=2 power=4 parameter2=4 timing=1 END
      BUT_ONLY

      ACTION_IF FILE_EXISTS_IN_GAME "%WIZARD_SPHERE_OF_CHAOS%.spl" BEGIN
        COPY_EXISTING "%WIZARD_SPHERE_OF_CHAOS%.spl" override
          LPF DELETE_EFFECT INT_VAR match_opcode=135 END
          LPF DELETE_EFFECT INT_VAR match_opcode=111 END
          LPF polymorph_patch INT_VAR duration=9 power=7 probability1=10 STR_VAR savetype=spell END
        BUT_ONLY
      END
      // wand of polymorphing

      ACTION_IF FILE_EXISTS_IN_GAME "%tutu_var%wand09.itm" BEGIN
        COPY_EXISTING wand09.itm override
           LPF DELETE_EFFECT INT_VAR check_globals=0 END
           LPF polymorph_patch INT_VAR power=4 STR_VAR savetype=wand type=item END
        BUT_ONLY
      END

   END
   
   // even on EE, a couple of spells/items aren't fully handled
   
   ACTION_IF FILE_EXISTS_IN_GAME "%tutu_var%bolt05.itm" BEGIN
     COPY_EXISTING "%tutu_var%bolt05.itm" override
       PATCH_FOR_EACH match_opcode IN 111 135 0 BEGIN
          LPF DELETE_EFFECT INT_VAR match_opcode END
       END
       LPF polymorph_patch INT_VAR duration=300000 power=0 STR_VAR type=item END
     BUT_ONLY
   END
   
   ACTION_IF FILE_EXISTS_IN_GAME "%MAJOR_POLYMORPH%.spl" BEGIN
     COPY_EXISTING "%MAJOR_POLYMORPH%.spl" override
       LPF DELETE_EFFECT INT_VAR check_globals=0 END
       LPF polymorph_patch INT_VAR power=4 savebonus="-4" END
       LPF ADD_SPELL_EFFECT INT_VAR opcode=174 target=2 power=4 timing=1 STR_VAR resource=eff_p07 END
       LPF ADD_SPELL_EFFECT INT_VAR opcode=141 target=2 power=4 parameter2=4 timing=1 END
     BUT_ONLY
   END

   ACTION_IF FILE_EXISTS_IN_GAME "%DECK_MAGICIAN%.spl" BEGIN
     COPY_EXISTING "%DECK_MAGICIAN%.spl" override
       LPF DELETE_EFFECT INT_VAR check_globals=0 END
       LPF polymorph_patch STR_VAR item="dw#rabbi" savetype=none  END
       LPF ADD_SPELL_EFFECT INT_VAR opcode=174 target=2 power=4 timing=1 STR_VAR resource=eff_p07 END
       LPF ADD_SPELL_EFFECT INT_VAR opcode=141 target=2 power=4 parameter2=4 timing=1 END
     BUT_ONLY
   END

   COPY_EXISTING "spwm183.spl" override
       PATCH_FOR_EACH match_opcode IN 177 144 BEGIN
           LPF DELETE_EFFECT INT_VAR match_opcode END
       END
       LPF polymorph_patch STR_VAR item="squirp"  END
       LPF ADD_SPELL_EFFECT INT_VAR opcode=174 target=2 power=4 timing=1 STR_VAR resource=eff_p07 END
       LPF ADD_SPELL_EFFECT INT_VAR opcode=141 target=2 power=4 parameter2=4 timing=1 END
   BUT_ONLY

   // edit the 'squirrel' item

   OUTER_SET poly_strref= GAME_IS BGEE? 25124 : 339
   COPY_EXISTING "squirp.itm" override
       WRITE_ASCII 0x3a "isquirl" (8) // typo in original means no inventory icon
       LPF ADD_ITEM_EQEFFECT INT_VAR opcode=139 target=2 timing=1 parameter1=poly_strref END
       LPF ADD_ITEM_EQEFFECT INT_VAR opcode=126 target=2 parameter1=10 parameter2=1 timing=2 END
       LPF ADD_ITEM_EQEFFECT INT_VAR opcode=144 target=2 parameter2=9 timing=2 END
       LPF ADD_ITEM_EQEFFECT INT_VAR opcode=144 target=2 parameter2=11 timing=2 END
       LPF ADD_ITEM_EQEFFECT INT_VAR opcode=144 target=2 parameter2=12 timing=2 END
       LPF ADD_ITEM_EQEFFECT INT_VAR opcode=144 target=2 parameter2=7 timing=2 END
       LPF ADD_ITEM_EQEFFECT INT_VAR opcode=142 target=2 parameter2=124 timing=2 END
       LPF DELETE_EFFECT INT_VAR match_opcode=18 END // hp modifier
       LPF DELETE_EFFECT INT_VAR match_opcode=44 END // STR modifier
       LPF DELETE_EFFECT INT_VAR match_opcode=142 match_parameter2=83 END // remove redundant 'casting failure' icon
       PATCH_IF enhanced_edition BEGIN
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode=181 target=2 timing=2 special=RESOLVE_STR_REF (@300) parameter1=9 parameter2=1 END
       END

  // clone the 'squirrel' item as a 'rabbit' item for the Deck of Many Things card


   COPY_EXISTING "squirp.itm" "override/dwrabp.itm"
 //     LPF ALTER_EFFECT INT_VAR match_opcode=107 match_parameter2=0 STR_VAR resource="%portrait_small%" END
 //     LPF ALTER_EFFECT INT_VAR match_opcode=107 match_parameter2=0 STR_VAR resource="%portrait_large%" END
      LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR resource=rabbit END


   // edit the 'squirrel' and 'rabbit' creature file

   ACTION_FOR_EACH creature IN squirr rabbit BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "%creature%.cre" BEGIN
      COPY_EXISTING "%creature%.cre" override
        WRITE_BYTE 0x238 2
        WRITE_BYTE 0x23c 15
        WRITE_BYTE 0x23d 9
      BUT_ONLY
    END
   END

  // remove encumberance penalty at STR=2 (yes, this is hacky)

    COPY_EXISTING strmod.2da override
       SET_2DA_ENTRY 2 4 5 1000

  // let Remove Curse remove polymorph
  
  COPY_EXISTING
      "%CLERIC_REMOVE_CURSE%.spl" override
      "%WIZARD_REMOVE_CURSE%.spl" override
           LPF CLONE_EFFECT INT_VAR match_opcode=26 opcode=112 STR_VAR resource=squirp END
           LPF CLONE_EFFECT INT_VAR match_opcode=26 opcode=112 STR_VAR resource=dwrabp END


  // adjust descriptions to explain this

  COPY_EXISTING "%WIZARD_POLYMORPH_OTHER%.spl" override
      READ_LONG 0x50 desc_str
      READ_STRREF 0x50 desc
  BUT_ONLY
  OUTER_SPRINT old @302
  OUTER_SPRINT new @301
  OUTER_INNER_PATCH_SAVE desc "%desc%" BEGIN
         REPLACE_TEXTUALLY "%old%" "%new%"
  END
  STRING_SET_EVALUATE desc_str "%desc%"

  OUTER_SPRINT extra @303

  COPY_EXISTING "%WIZARD_REMOVE_CURSE%.spl" override
      READ_LONG 0x50 desc_str
      READ_STRREF 0x50 desc
  BUT_ONLY
  OUTER_SPRINT desc "%desc% %extra%"
  STRING_SET_EVALUATE desc_str "%desc%"
  COPY_EXISTING "%CLERIC_REMOVE_CURSE%.spl" override
      READ_LONG 0x50 desc_str
      READ_STRREF 0x50 desc
  BUT_ONLY
  OUTER_SPRINT desc "%desc% %extra%"
  STRING_SET_EVALUATE desc_str "%desc%"


  COPY_EXISTING "speldesc.2da" "%workspace%"
       REPLACE_EVALUATE CASE_INSENSITIVE "%CLERIC_REMOVE_CURSE%\( \|%TAB%\)+\([0-9]+\)\(%WNL%\|%MNL%\|%LNL%\)" BEGIN
          SPRINT strref "%MATCH2%"
          GET_STRREF strref string
       END
       ""
  BUT_ONLY
  OUTER_SPRINT string "%string% %extra%"
  STRING_SET_EVALUATE strref "%string%"

   // ensure all polymorph effects are detectable by stores

   ADD_SECTYPE PolymorphCurse ""

   MAKE_PATCH
      add_basic_ability=>null
      secondary=>"%PolymorphCurse%"
      add_effect_inline=>"opcode=>283 target=>2 timing=>1 parameter2=>2 resource=>ohbcwou1"
   END
   LAF make_spell STR_VAR spell=dwplycu edits=patch_data END


   ACTION_FOR_EACH object IN
   "%MAJOR_POLYMORPH%.spl"
   "%WIZARD_POLYMORPH_OTHER%.spl"
   "%DECK_MAGICIAN%.spl"
   "spwm183.spl"
   "%tutu_var%bolt05.itm"
   "%tutu_var%wand09.itm"
   BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME "%object%" BEGIN
         COPY_EXISTING "%object%" override
          LPF CLONE_EFFECT INT_VAR match_opcode=111 opcode=146 parameter2=1 timing=9 STR_VAR resource=dwplycu END
      END
   END

   // we need to make sure this curse effect gets stripped any time the polymorph does
   
   COPY_EXISTING_REGEXP 
    ".*\.\(spl\|itm\)" override
	PATCH_TRY
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=111 opcode=221 timing=1 parameter2=PolymorphCurse STR_VAR resource="" END
	WITH DEFAULT
			LPF regexp_warning STR_VAR parent=polymorph_tweaks END
	END
   BUT_ONLY
   

END // end of main function

////////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION polymorph_patch
   INT_VAR duration=0
           savebonus=0
           power=0
           probability1=100
           probability2=0
   STR_VAR savetype=polymorph
           type=spell
           item=squirp
BEGIN
          PATCH_MATCH "%savetype%" WITH
             polymorph BEGIN
                save_vs_polymorph=1
                save_vs_wand=0
                save_vs_spell=0
             END
             wand BEGIN
                save_vs_polymorph=0
                save_vs_wand=1
                save_vs_spell=0
             END
             spell BEGIN
                save_vs_polymorph=0
                save_vs_wand=0
                save_vs_spell=1
             END
             none BEGIN
                save_vs_polymorph=0
                save_vs_wand=0
                save_vs_spell=0
             END
          DEFAULT
             PATCH_FAIL "unrecognised save type %savetype%"
          END
          TO_UPPER type
          LPF DELETE_EFFECT INT_VAR match_opcode=111 STR_VAR match_resource=squirp END
          LPF "ADD_%type%_EFFECT" INT_VAR type=99 opcode=111 target=2 power parameter1=1 duration probability1 probability2 save_vs_wand save_vs_polymorph save_vs_spell savebonus STR_VAR resource="%item%" END
          PATCH_FOR_EACH resource IN SPIN122 SPIN123 SPIN124 SPIN150 SPIN151 SPIN160 SPIN529 SPIN667 SPIN718 SPIN794 SPINHUM SPMDSLAY SPWI489 SPWI490 SPWI491 BEGIN
              LPF DELETE_EFFECT INT_VAR match_opcode=172 STR_VAR match_resource="%resource%" END
              LPF CLONE_EFFECT INT_VAR match_opcode=111 opcode=172 timing=1 STR_VAR resource END
          END
END
