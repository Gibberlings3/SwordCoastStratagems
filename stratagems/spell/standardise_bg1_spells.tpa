DEFINE_ACTION_FUNCTION standardise_bg1_spells
  STR_VAR version="bg1"
BEGIN
  LAF "standardise_spells_%version%" END
  
END

DEFINE_ACTION_FUNCTION standardise_spells_bg1 BEGIN

     LAF make_label STR_VAR label=dw#standardise_no_bg2 END
     LAF edit_spell STR_VAR spell=~spwi120 spwi125 spwi220 spwi221 spwi223
                                   spwi224 spwi302 spwi318 spwi319 spwi320
                                   spwi321 spwi322 spwi324 spwi325 spwi403
                                   spwi404 spwi408 spwi409 spwi410 spwi417
                                   spwi418 spwi419 spwi420 spwi421 spwi423 
                                   spwi424 spwi425 spwi510 spwi511 spwi512
                                   spwi513 spwi514 spwi515 spwi516 spwi517
                                   spwi518 spwi519 spwi520 spwi521 spwi522
                                   spwi523~
                             editstring=~level=>9~
     END

<<<<<<<< .../stratagems-inline/spells.2da
2DA V1.0
0
            1       2       3       4       5   6   7
MAGE        19      19      17      11      8   50  50
PRIEST      10      14      12      8       6   50  50
>>>>>>>>

    COPY ~.../stratagems-inline/spells.2da~ ~override~

    LAF edit_creature STR_VAR creature=~jaheir jaheir2 jaheir4 jaheir6 faldor faldor5~
                              tv=1
                              editstring=~set_priest_spells=>druid~
                      END

    LAF edit_creature STR_VAR creature=~tiax tiax4 tiax6 branwe branwe5 yeslic yeslic5 quayle quayle4 quayle6 viconi viconi4 viconi6~
                              tv=1
                              editstring=~set_priest_spells=>cleric~
                      END


END 

DEFINE_ACTION_FUNCTION standardise_spells_bg2 BEGIN

      LAF add_scrolls_to_store STR_VAR spells=~REFLECTED_IMAGE DEAFNESS GLITTERDUST~
                                       store=inn3351
                                       location=~LAST~
                                       tv=1
      END

      LAF add_scrolls_to_store STR_VAR spells=~SPOOK POWER_WORD_SLEEP RAY_OF_ENFEEBLEMENT DEAFNESS~
                                       store= ~%tutu_scripth%ighhedg~
                                       location= ~AFTER %tutu_var%scrl98~
      END

      LAF add_scrolls_to_store STR_VAR spells=~MINOR_SPELL_DEFLECTION PROTECTION_FROM_FIRE PROTECTION_FROM_COLD HOLD_UNDEAD ~
                                       store= ~%tutu_scripth%ighhedg~
                                       location= ~AFTER %tutu_var%scrl1q~
      END

      LAF add_scrolls_to_store STR_VAR spells=~POWER_WORD_SLEEP RAY_OF_ENFEEBLEMENT~
                                       store= sto0703
                                       location= ~AFTER %tutu_var%scrl99~
                                       tv=1
      END

      LAF add_scrolls_to_store STR_VAR spells=~CONTAGION SECRET_WORD FARSIGHT~
                                       store= sto0703
                                       location= ~AFTER %tutu_var%scrl5m~
                                       tv=1
      END
      LAF add_scrolls_to_store STR_VAR spells=~SPELL_IMMUNITY PROTECTION_FROM_NORMAL_WEAPONS PROTECTION_FROM_ELECTRICITY BREACH LOWER_RESISTANCE 
                                               ORACLE CONJURE_LESSER_FIRE_ELEMENTAL CONJURE_LESSER_EARTH_ELEMENTAL CONJURE_LESSER_WATER_ELEMENTAL PROTECTION_FROM_ACID 
                                               PHANTOM_BLADE SPELL_SHIELD MINOR_SPELL_TURNING SUN_FIRE~
                                       store=sto0703
                                       location= ~AFTER %tutu_var%scrl5p~
                                       tv=1
      END


ACTION_DEFINE_ASSOCIATIVE_ARRAY cre_to_scroll BEGIN
alai=>MINOR_SEQUENCER
andris=>DISPEL_MAGIC
davaeo=>STONE_SKIN
davaeo'=>DISPEL_MAGIC
deathk=>GLITTERDUST
denak=>TELEPORT_FIELD
dezkie=>STONE_SKIN
halaca=>DETECT_ILLUSION
kysus=>FIRE_SHIELD_RED
niemai=>SPIDER_SPAWN
nimbul=>FIND_FAMILAR
resar=>FIRE_SHIELD_BLUE
tranzi=>GLITTERDUST
venkt=>SPELL_THRUST
END

ACTION_PHP_EACH cre_to_scroll AS cre=>spell BEGIN
  OUTER_INNER_PATCH_SAVE cre "%cre%" BEGIN
     REPLACE_TEXTUALLY "'" ""
  END
  COPY_EXISTING ~%tutu_var%%cre%.cre~ ~override~
     SPRINT scroll EVALUATE_BUFFER ~%WIZARD_%spell%_SCROLL%~
     ADD_CRE_ITEM ~%scroll%~ #0 #0 #0 ~NONE~ ~INV~
  BUT_ONLY
END

END

DEFINE_ACTION_FUNCTION add_scrolls_to_store STR_VAR spells="" location="" tv="no" store=""
BEGIN
      OUTER_SPRINT items ~~
      OUTER_WHILE ~%spells%~ STRING_COMPARE ~~ BEGIN
         LAF return_first_entry STR_VAR list= ~%spells%~ RET spell=entry spells=list END
         ACTION_IF VARIABLE_IS_SET  ~%WIZARD_%spell%_SCROLL%~ BEGIN
            OUTER_SPRINT items  EVALUATE_BUFFER ~%items% %WIZARD_%spell%_SCROLL%~
         END
      END
      LAF add_items_to_store STR_VAR store items location tv END
END

DEFINE_PATCH_FUNCTION set_priest_spells
     STR_VAR arguments=""
BEGIN
   LPF CRE_remove_spells STR_VAR arguments=priest END
   LPF CRE_read_level RET level=value END
   PATCH_MATCH ~%arguments%~ WITH
   cleric BEGIN
     SET priest_var=0x8000
   END
   druid BEGIN
     SET priest_var=0x4000
   END
   DEFAULT
     SET priest_var=0
   END
   PATCH_DEFINE_ARRAY spell_max_array BEGIN
      10 14 12 8 6
   END
   FOR (spl_lev=0;spl_lev*2<level;spl_lev=spl_lev+1) BEGIN
        SET ind_max =$spell_max_array(~%spl_lev%~)
        FOR (i=1;i<=ind_max;i=i+1) BEGIN
           SET ind=i+100*(spl_lev+1)
           SPRINT spell ~SPPR%ind%~
           INNER_ACTION BEGIN
                COPY_EXISTING ~%spell%.spl~ ~override~
                      LPF SPL_read_priest_type RET type=value END
                BUT_ONLY
           END
           PATCH_IF type=priest_var BEGIN
                 SPRINT input ~ADD_KNOWN_SPELL %spell% #%spl_lev% priest~
                 LPF patch_reinclude_this STR_VAR input END
           END
        END
   END


END
