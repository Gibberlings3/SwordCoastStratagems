LAF include STR_VAR locbase=caster_shared file=caster_shared.tph END
DEFINE_ACTION_FUNCTION innate_triggers BEGIN


   ACTION_DEFINE_ASSOCIATIVE_ARRAY spell_trigger_array BEGIN
      CONTINGENCY=>null
      CHAIN_CONTINGENCY=>null
      MINOR_SEQUENCER=>null
      SPELL_SEQUENCER=>null
      SPELL_TRIGGER=>null
   END


   LAM read_in_spells_per_level
   OUTER_SPRINT kit_edit_mage ""
   OUTER_SPRINT kit_edit_bard ""
   OUTER_SPRINT scroll_list ""
   ACTION_PHP_EACH spell_trigger_array AS spell=>discard BEGIN
      OUTER_SPRINT spell "WIZARD_%spell%"
      LAF edit_spell STR_VAR spell editstring="spell_type_num=>4 not_in_combat=>1 patch_ability_inline=>~ability_icon_loc=>4~" END
      OUTER_SET level=EVAL "%%spell%_LEVEL%"  + 1 // because of how we code level, 0-8
      OUTER_SET clev = $spell_level_to_caster_level("mage" "%level%")
      OUTER_SPRINT kit_edit_mage "%kit_edit_mage% grant_power=>~%spell% %clev%~"
      ACTION_IF VARIABLE_IS_SET $spell_level_to_caster_level("bard" "%level%") BEGIN
         OUTER_SET clev = $spell_level_to_caster_level("bard" "%level%")
         OUTER_SPRINT kit_edit_bard "%kit_edit_bard% grant_power=>~%spell% %clev%~"
      END
      OUTER_SPRINT resref EVAL "%%spell%%"
      ACTION_IF enhanced_edition BEGIN
          APPEND "hidespl.2da" "%resref% 1 0"
      END ELSE BEGIN
          APPEND "hidespl.2da" "%resref% ****"
      END

   END

   // patch innates into kits

   LAF edit_all_kits STR_VAR parent_class=mage editstring="%kit_edit_mage%" END
   LAF edit_all_kits STR_VAR parent_class=bard editstring="%kit_edit_bard%" END
   ACTION_IF enhanced_edition BEGIN
      LAF edit_kit STR_VAR kit=dragon_disciple editstring="%kit_edit_mage%" END
   END

   // get rid of all store copies

   COPY_EXISTING_REGEXP ".*\.sto" override
       PHP_EACH spell_trigger_array AS spell=>discard BEGIN
            SPRINT spell "WIZARD_%spell%"
            SPRINT scroll EVAL "%%spell%_SCROLL%"
            REMOVE_STORE_ITEM "%scroll%"
       END
   BUT_ONLY

  // get rid of all other copies on my install

  ACTION_IF is_bg2 BEGIN
     LAF edit_area STR_VAR area=ar1202 editstring="delete_item=>~item_resource=scrl6p~" END
     ACTION_IF enhanced_edition BEGIN
        LAF edit_creature STR_VAR creature=ohbjoker editstring="remove_items=>~scrl8l scrl9d~" END
        LAF edit_area STR_VAR area=oh6500 editstring="delete_item=>~item_resource=scrl9d~" END
     END
     LAF edit_area STR_VAR area="ar0705 ar1103" editstring="delete_item=>~item_resource=scrl7u~" END
     LAF edit_creature STR_VAR creature=vvshad1 editstring="remove_items=>scrl7u" END

     COPY_EXISTING "rndscrol.2da" override
          REPLACE_TEXTUALLY "scrl8l" "scrl8g" // sequencer->ruby ray
          REPLACE_TEXTUALLY "scrl9d" "scrl9a" // trigger->pierce shield
     BUT_ONLY
  END
  ACTION_IF is_bg1 BEGIN
     LAF edit_creature STR_VAR creature=alai tv=1 editstring="remove_items=>scrl6p" END
     ACTION_IF FILE_EXISTS_IN_GAME "bd0130.are" BEGIN
        LAF edit_area STR_VAR area=bd0130 editstring="delete_item=>~item_resource=scrl6p~" END
     END
  END
  
  // copy some other scroll over the sequencer ones, in case a mod adds them
  
  ACTION_DEFINE_ASSOCIATIVE_ARRAY spell_scroll_swap BEGIN
      CONTINGENCY=>PROTECTION_FROM_MAGIC_WEAPONS
      CHAIN_CONTINGENCY=>SPELL_STRIKE
      SPELL_SEQUENCER=>RUBY_RAY_OF_REVERSAL
      SPELL_TRIGGER=>IMPROVED_MANTLE
      MINOR_SEQUENCER=>SECRET_WORD
  END
  ACTION_PHP_EACH spell_scroll_swap AS old=>new BEGIN
     OUTER_SPRINT old_ids "WIZARD_%old%"
     OUTER_SPRINT old_scroll EVAL "%%old_ids%_SCROLL%"
     OUTER_SPRINT new_ids "WIZARD_%new%"
     OUTER_SPRINT new_scroll EVAL "%%new_ids%_SCROLL%"
     ACTION_IF FILE_EXISTS "%new_scroll%.itm" BEGIN
          COPY_EXISTING "%new_scroll%.itm" "override/%old_scroll%.itm"
     END
  END


END