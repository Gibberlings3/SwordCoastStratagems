/////////////////////////////////////////////////////////////////////////////
//// How this will work:
////
//// - it's *enemy-only*
////
//// Creatures (basically, mages) that go invisible have SPLSTATE states that cause the standard invisibility spells to
//// also cast DW#HDANI, the animation-hiding spell
////
//// Divinations apply DW#BLHDA, which (just) blocks/removes DW#HDANI.
//// Breaks-invisibility spells apply DW#BLHDA as an effect (though perhaps if DW#HDANI is an illusion, this would be moot)
////
//// We need to break invisibility manually when attacking (albeit invisible mages *shouldn't* attack, so maybe it's moot)
//// If we make DW#HDANI an illusion, hopefully it'll be removed by Detect Illusion thief power





DEFINE_ACTION_FUNCTION invisibility_animations BEGIN

   LAF invisibility_animation_core_spells END
   LAF invisibility_animation_invisibility_spells END
   LAF invisibility_animation_spell_mods END
   LAF spellcasting_breaks_invisibility END


END




////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION invisibility_animation_core_spells BEGIN

  MAKE_PATCH
     school=>illusion
     add_basic_ability=>null
     add_effect_inline=>~opcode=>291 parameter2=>1 duration=>7200 target=>2~     // disable visual effect
  //   add_effect_inline'=>~opcode=>146 target=>2 duration=>1 timing=>4 parameter1=>0 parameter2=>1 resource=>dw#hdani~
  END
  LAF make_spell STR_VAR spell=dw#hdani edits=patch_data  END
//   LAF make_casting_effect INT_VAR target=1 STR_VAR effect=dw#hdani spell= dw#hdani END

   MAKE_PATCH
       add_basic_ability=>null
   //    add_effect_inline=>~opcode=>206 target=>1 parameter1=>"-1" duration=>1 resource=>dw#hdani~ // suppress DW#HDANI
    add_effect_inline=>~opcode=>321 target=>1 parameter1=>0 duration=>0 resource=>dw#hdani~ // suppress DW#HDANI
   END
   LAF make_spell STR_VAR spell=dw#blhda edits=patch_data END


END

DEFINE_ACTION_FUNCTION invisibility_animation_invisibility_spells BEGIN

   LAF ds_resolve_stat INT_VAR ids=1 STR_VAR id=DW_SCS_CASTER RET stat_ind END
   MAKE_PATCH
     add_effect_inline=>~opcode=>318
                         parameter1=>%stat_ind%
                         parameter2=>111
                         target=>2
                         duration=>1
                         resource=>dw#hdani
                         at_end=>1~
     add_effect_inline'=>~opcode=>326
                         parameter1=>0
                         parameter2=>43
                         target=>2
                         duration=>0
                         timing=>1
                         resource=>dw#hdani
                         at_end=>1~
     clone_effect_inline=>~match=>"opcode=20"
                          opcode=>326
                          parameter1=>0
                          parameter2=>43
                          target=>2
                          timing=>4
                          resource=>dw#blhda~
  END
  LAF edit_spell STR_VAR spell="WIZARD_INVISIBILITY WIZARD_SHADOW_DOOR WIZARD_INVISIBILITY_10_FOOT WIZARD_IMPROVED_INVISIBILITY WIZARD_MASS_INVISIBILITY WIZARD_MISLEAD" edits=patch_data END
  LAF edit_item STR_VAR item="%tutu_var%potn10 dw#ptn10" allow_missing=1  edits=patch_data END



END

DEFINE_ACTION_FUNCTION invisibility_animation_spell_mods BEGIN

   <<<<<<<< .../stratagems-inline/hidden_animations.2da
   spell                            sectype                      animation1       animation2      match       check
   WIZARD_FIRE_SHIELD_RED           FireShieldRedAnimation       SPFIRESR         SPFIREST        opcode      30
   WIZARD_FIRE_SHIELD_BLUE          FireShieldBlueAnimation      SPFIRESA         SPFIRESB        opcode      28
   CLERIC_AURA_OF_FLAMING_DEATH     FireShieldRedAnimation       SPAURAFF         SPAURAFB        opcode      30
   WIZARD_AEGIS                     AegisAnimation               LI#AMSH          null            opcode      87
   WIZARD_SPELL_SHIELD              SpellShieldAnimation         SPTURNI2         null            opcode      142
   >>>>>>>>
   COPY ~.../stratagems-inline/hidden_animations.2da~ ~%workspace%~
        READ_2DA_ENTRIES_NOW anim_data 6
        FOR (i=1;i<anim_data;i+=1) BEGIN
           READ_2DA_ENTRY_FORMER anim_data i 0 spellname
           SPRINT spell EVALUATE_BUFFER ~%%spellname%%~
           FOR(j=1;j<6;j+=1) BEGIN
              READ_2DA_ENTRY_FORMER anim_data i j val
              READ_2DA_ENTRY_FORMER anim_data 0 j var
              SPRINT  ~%var%~ ~%val%~
           END
           INNER_ACTION BEGIN
              LAF hide_animation_for_this_spell STR_VAR spell sectype animation1 animation2 match check END
           END
        END
   BUT_ONLY


END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION hide_animation_for_this_spell
STR_VAR spell=""
        animation1=""
        animation2="null"
        match=""
        check=""
BEGIN
   ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.spl~ BEGIN
     // get the new spellname
        OUTER_INNER_PATCH ~%spell%~ BEGIN
           READ_ASCII 0x0 type (4)
           TO_UPPER type
           PATCH_MATCH ~%type%~ WITH
             SPWI BEGIN
                SPRINT prefix w
             END
             SPPR BEGIN
                SPRINT prefix p
             END
             SPIN BEGIN
                SPRINT prefix i
             END
             DEFAULT
                PATCH_PRINT ~warning, hide_animation_for_this_spell can't think of a name~
             END
           READ_ASCII 0x4 code (3)
        END
        
   // modify existing spell
   MAKE_PATCH
       delete_opcodes=>215
       clone_entry_inline=>~match=>%match% check=>%check% type=>effect opcode=>272 power=>0 parameter1=>1 parameter2=>3 resource=>dw#%prefix%%code%a~
       clone_entry_inline'=>~match=>%match% check=>%check% type=>effect opcode=>206 parameter1=>"-1" power=>0 parameter1=>0 parameter2=>0 resource=>dw#%prefix%%code%b~
   END
   LAF edit_spell STR_VAR spell edits=patch_data END
   
   // make the animation-caster

  LAF make_casting_effect INT_VAR target=1 STR_VAR effect= ~dw#%prefix%%code%a~ spell= ~dw#%prefix%%code%a~ END

   MAKE_PATCH
      add_basic_ability=>null
      add_effect_inline=>~opcode=>215  target=>1 parameter2=>1 resist_dispel=>0 duration=>1500 resource=>%animation1%~   // actual animations
      add_effect_inline''=>~opcode=>272 target=>1 parameter1=>1 parameter2=>3 duration=>1500 resource=>dw#%prefix%%code%b at_end=>1~ // cast dispel-self code
      add_effect_inline'''=>~opcode=>206 target=>1 parameter1=>"-1" duration=>1500 resource=>dw#%prefix%%code%a at_end=>1~
   END
   ACTION_IF ~%animation2%~ STRING_COMPARE_CASE ~null~ BEGIN
      OUTER_SPRINT $patch_data(~add_effect_inline'~) ~opcode=>215 target=>1 parameter2=>1 resist_dispel=>0 duration=>1500 resource=>%animation2% insert_point=>0~
   END

   LAF make_spell STR_VAR spell= ~dw#%prefix%%code%a~ edits=patch_data END

   // make the animation-remover
 
  LAF make_casting_effect INT_VAR target=1 STR_VAR effect= ~dw#%prefix%%code%b~ spell= ~dw#%prefix%%code%b~ END

   MAKE_PATCH
      add_basic_ability=>null
      add_effect_inline=>~opcode=>321 target=>1 resource=>dw#%prefix%%code%a~
   END
   LAF make_spell STR_VAR spell=~dw#%prefix%%code%b~ edits=patch_data END

   // add to dw#hdani
  MAKE_PATCH
     add_effect_inline=>~opcode=>206 target=>2 parameter1=>"-1" timing=>1 resource=>dw#%prefix%%code%a~ // immunity to the animation
      add_effect_inline'=>~opcode=>321 target=>2 resource=>dw#%prefix%%code%a~  // zap the animation
  END
  LAF edit_spell STR_VAR spell=dw#hdani edits=patch_data END
 END



END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION spellcasting_breaks_invisibility BEGIN

    OUTER_SPRINT spell ~~
    COPY_EXISTING_REGEXP GLOB ~.*\.spl~ ~override~
             SPRINT filename ~%SOURCE_RES%~
          SET found=0
          READ_BYTE 0x27 sectype
          PATCH_MATCH sectype WITH
              4 10 11
          BEGIN 
             SET found =1
          END
          DEFAULT
             LPF SPL_read_breaks_invisibility RET found=value END
          END
          PATCH_IF found BEGIN
              LPF ADD_SPELL_CFEFFECT 
                  INT_VAR opcode=321
                          target=1
                  STR_VAR resource=dw#hdani
              END
              LPF ADD_SPELL_CFEFFECT
                  INT_VAR opcode=146
                          target=1
                          parameter1=1
                          parameter2=1
                  STR_VAR resource=dw#blhda
              END
            // PUSH spell ~%filename%~
          END
    BUT_ONLY

  /*    hardcoded for speed
  
  MAKE_PATCH
       type=>effect_global
       opcode=>321
       target=>1
       parameter1=>0
       probability1=>100
       resource=>dw#hdani
    END
    ACTION_CLEAR_ARRAY patch_data2
    ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data2 BEGIN
       type=>effect_global
       opcode=>146
       target=>1
       parameter1=>1
       parameter2=>1
       probability1=>100
       resource=>dw#blhda
    END
   LAF edit_spell STR_VAR spell editstring=~add_entry=>patch_data add_entry=>patch_data2~ END
   
   */
 END



