INCLUDE ~%MOD_FOLDER%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION undead BEGIN

	LAF lich END
	LAF wights END
	LAF mummies END


END

DEFINE_ACTION_FUNCTION lich BEGIN

	COPY_EXISTING "lich2.cre" override
		SPRINT filename "%SOURCE_RES%"
		LPF enforce_mage STR_VAR script=udlich1 END
		
	LAF extend STR_VAR script=udlich1 top=udlichtop location=script END

	COPY_EXISTING "lich2.cre" "override/lich.cre"
		WRITE_LONG 0x14 0
		LPF ADD_CRE_EFFECT INT_VAR opcode=328 target=2 timing=9 parameter2=IDS_OF_SYMBOL (splstate CONTINGENCY_TRIGGERED) special=1 END
		GET_OFFSET_ARRAY spl_mem_array 0x2b0 4 0x2b4 4 0 0 0xc
		PHP_EACH spl_mem_array AS spl_mem_ind=>spl_mem_off BEGIN
			READ_ASCII spl_mem_off spell
			PATCH_MATCH "%spell%" WITH
			"dw#sq.*"
			"dw#ms.*"
			BEGIN
				WRITE_ASCII spl_mem_off "deleteme"
			END
			DEFAULT
			END
		END
		REMOVE_MEMORIZED_SPELL deleteme
		




END

DEFINE_ACTION_FUNCTION wights BEGIN

   // energy-draining weapon
   
   COPY_EXISTING "s1-4.itm" "override/dw#wight.itm"
     LPF ADD_ITEM_EFFECT INT_VAR opcode=216 target=2 parameter1=1 timing=1 END
     LPF ADD_ITEM_EFFECT INT_VAR opcode=139 target=2 parameter1=35498 timing=1 END
     LPF ADD_ITEM_EFFECT INT_VAR opcode=141 target=2 parameter2=1 END
   BUT_ONLY
   
   // give out weapon; systematise cold resistance

   MAKE_PATCH
      add_item=>"dw#wight(weapon1)"
      immunity=>cold
   END
   LAF edit_creature STR_VAR creature="ktwgt1 ktwgti1 lwight udwight wight wight2 wight3" edits=patch_data END

   // barrow wights get cold resistance but no energy drain
   
   LAF edit_creature STR_VAR creature="rbibarr wightba" editstring="immunity=>cold" END

   // imbued wight script
   
   LAF ssl_to_bcs STR_VAR script=ktwgtim1 location=ssl END
   
   // temples need to carry Restoration
   
   OUTER_SPRINT temples ""
   COPY_EXISTING_REGEXP ".*\.sto" override  
      PATCH_IF INDEX_BUFFER ("%CLERIC_RESURRECTION%") >= 0 BEGIN
         SPRINT temples "%temples% %SOURCE_RES%"
      END
   BUT_ONLY
   MAKE_PATCH
      cure=>"%CLERIC_RESTORATION%"
      cure_price=>750
      at_end=>1
   END
   LAF edit_store STR_VAR store="%temples%" editstring="add_cure=>patch_data" END

                                                          
END

DEFINE_ACTION_FUNCTION mummies BEGIN

     // aura of fear

     OUTER_SET aura_string=RESOLVE_STR_REF (@1)
     MAKE_PATCH
        name1_string=>"-1"
        add_effect_inline=>"at_end=>1 opcode=>318 target=>2 duration=>60 resource=>dw#feaur"
        patch_effect_inline=>"match=>~opcode=139~ parameter1=>%aura_string%"
        delete_effect=>"opcode=215"
     END
     LAF install_spell STR_VAR spell=dw#feaur location=resource edits=patch_data END
     LAF make_aura STR_VAR spell=dw#mumau payload=dw#feaur END

     LAF install STR_VAR file="dw#mumau.baf" location=resource END

     // mummy rot

	spl.make[dw#mumcw]
	[
		m_name:=@3
		m.ab.add{}
		m.ab_fx.add{s_opcode=101 s_target=2 s_timing=9 s_parameter2=17}
		PATCH_FOR_EACH resource IN CLWOUNH CMWOUNH CSWOUNH CCWOUNH HEALH SPHEALIN BEGIN
			m.ab_fx.add{s_opcode=296 s_target=2 s_timing=9 s_resource:="%resource%"}
		END
		m.ab_fx.add{s_opcode=142 s_target=2 s_timing=9 s_parameter2=7}
		m.ab_fx.add{s_opcode=267 s_target=2 s_timing=9 s_parameter1=14022}
		m.ab_fx.add{s_opcode=324 s_target=2 s_timing=9 s_resource:=dw#mumcw}
	]     
     
     OUTER_SET rot_string = RESOLVE_STR_REF (@2)
     MAKE_PATCH
        delete_effect=>"opcode=78"
        add_effect_inline=>"opcode=>326 target=>2 timing=>1 resource=>dw#mumcw"
        patch_effect_inline=>"match=>~opcode=139~ parameter1=>%rot_string%"
     END
     LAF edit_item STR_VAR item=mum1-12 edits=patch_data END

     COPY_EXISTING_REGEXP ".*\.spl" override
          SET cures=0
          GET_OFFSET_ARRAY ab_arr SPL_V10_HEADERS
          PHP_EACH ab_arr AS ind=>ab_off BEGIN
             GET_OFFSET_ARRAY2 fx_arr ab_off SPL_V10_HEAD_EFFECTS
             PHP_EACH fx_arr AS ind2=>fx_off BEGIN
                READ_SHORT fx_off opcode
                PATCH_IF opcode=79 BEGIN
                   SET cures=1
                END
             END
          END
          PATCH_IF cures BEGIN
             LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 timing=1 insert_point=0 STR_VAR resource=dw#mumcw END
          END
     BUT_ONLY

     COPY_EXISTING_REGEXP ".*\.itm" override
          SET cures=0
          GET_OFFSET_ARRAY ab_arr ITM_V10_HEADERS
          PHP_EACH ab_arr AS ind=>ab_off BEGIN
             GET_OFFSET_ARRAY2 fx_arr ab_off ITM_V10_HEAD_EFFECTS
             PHP_EACH fx_arr AS ind2=>fx_off BEGIN
                READ_SHORT fx_off opcode
                PATCH_IF opcode=79 BEGIN
                   SET cures=1
                END
             END
          END
          PATCH_IF cures BEGIN
             LPF ADD_ITEM_EFFECT INT_VAR opcode=321 target=2 timing=1 insert_point=0 STR_VAR resource=dw#mumcw END
          END
     BUT_ONLY

     // creature edits

     MAKE_PATCH
        script_override=>dw#mumau
        resist_fire=>"-50"
     END
     LAF edit_creature STR_VAR creature="ktmum1 mummy cpmumg mummyg" edits=patch_data END

END