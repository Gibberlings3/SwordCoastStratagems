// overall breakdown
//
//
//   counter        result
//   -1              Initial defenders
//   0,1,2,3         Disorganised groups
//   4,5,6,7         Serious war-parties
//   8               First mage elite group
//   9               war-party
//   10              first priest elite group
//   11              war-party
//   12              second mage elite group
//   13              war-party
//   14              final elite group
//
//   Insane: 0-14
//   Hard: 0-6,12-14
//   Core: 0-4,8,10,14
//   Easy: 0-3,10
//   Easiest: 0,1,10

// drow counter increment

IF
   GlobalGT("SPRITE_IS_DEADdw#dwcty","GLOBAL",1)
THEN
    RESPONSE #100
         IncrementGlobal("SPRITE_IS_DEADdw#dwcty","GLOBAL",-1)
         IncrementGlobal("DMWWDrowNumbers","GLOBAL",-1)
END

IF TRIGGER
        TriggerBlock(Hard)
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	Global("DMWWDrowCityCounter","GLOBAL",7) // if we've had the first three war parties
THEN DO
                Action(Literal)
		SetGlobal("DMWWDrowCityCounter","GLOBAL",12) // skip to the third elite group
END

IF TRIGGER
	TriggerBlock(Core)
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	Global("DMWWDrowCityCounter","GLOBAL",5) // if we've had the first war party
THEN DO
                Action(Literal)
		SetGlobal("DMWWDrowCityCounter","GLOBAL",8) // skip to the first elite group
END

IF TRIGGER
	TriggerBlock(Core)
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	Global("DMWWDrowCityCounter","GLOBAL",9) // if we've had the first elite group
THEN DO
                Action(Literal)
		SetGlobal("DMWWDrowCityCounter","GLOBAL",10) // skip to the second elite group
END

IF TRIGGER
	TriggerBlock(Core)
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	Global("DMWWDrowCityCounter","GLOBAL",11) // if we've had the second elite group
THEN DO
                Action(Literal)
		SetGlobal("DMWWDrowCityCounter","GLOBAL",14) // skip to the last elite group
END

IF TRIGGER
	TriggerBlock(Easy)
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	Global("DMWWDrowCityCounter","GLOBAL",4) // if we've had the last casual group
THEN DO
                Action(Literal)
		SetGlobal("DMWWDrowCityCounter","GLOBAL",10) // skip to the second elite group
END

IF TRIGGER
	TriggerBlock(Easy)
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	Global("DMWWDrowCityCounter","GLOBAL",11) // if we've had the second elite group
THEN DO
                Action(Literal)
		SetGlobal("DMWWDrowCityCounter","GLOBAL",15) // call it a day
END

IF TRIGGER
	TriggerBlock(Easiest)
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	Global("DMWWDrowCityCounter","GLOBAL",2) // if we've had the second casual group
THEN DO
                Action(Literal)
		SetGlobal("DMWWDrowCityCounter","GLOBAL",10) // skip to the second elite group
END

IF TRIGGER
	TriggerBlock(Easiest)
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	Global("DMWWDrowCityCounter","GLOBAL",11) // if we've had the second elite group
THEN DO
                Action(Literal)
		SetGlobal("DMWWDrowCityCounter","GLOBAL",15) // call it a day
END


// Initial drow battlegroups

IF
	Global("DrowCityHostile","GLOBAL",1)
	Global("DMWWInitializeDrowCity","GLOBAL",0)
	OR(2)
		Global("DemonWalks","GLOBAL",0)
		Global("DemonWalks","GLOBAL",5)
THEN
	RESPONSE #100
		SetGlobal("DMWWInitializeDrowCity","GLOBAL",1)
		SetGlobalTimer("DMWWDrowStartTimer","GLOBAL",6)	
END

IF
	Global("DrowCityHostile","GLOBAL",1)
	!Global("DMWWInitializeDrowCity","GLOBAL",2)
	!GlobalTimerNotExpired("DMWWDrowStartTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("DMWWInitializeDrowCity","GLOBAL",2)
		SetGlobalTimer("DMWWDrowCityTimer","GLOBAL",48)
		// first battlegroup
		CreateCreature("uddrow27",[1841.2665],0)
		CreateCreature("drow01",[1794.2640],0)
		CreateCreature("uddrow28",[1780.2692],0)
		CreateCreature("drow05",[1675.2665],0)
		CreateCreature("drow06",[1613.2675],0)
		// second battlegroup - 
		CreateCreature("uddrow27",[1108.3540],0)
		CreateCreature("drow01",[1045.3524],0)
		CreateCreature("drow04",[1012.3601],0)
END

// First phase. Drow are disorganised; only occasional enemies show up

IF
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	!GlobalTimerNotExpired("DMWWDrowCityTimer","GLOBAL")
	Global("DMWWInitializeDrowCity","GLOBAL",2)
	Global("DMWWDrowCityCounter","GLOBAL",0)
	!GlobalGT("DMWWDrowNumbers","GLOBAL",15)
THEN 
	RESPONSE #100
		IncrementGlobal("DMWWDrowCityCounter","GLOBAL",1)
		IncrementGlobal("DMWWDrowNumbers","GLOBAL",7)
		SetGlobalTimer("DMWWDrowCityTimer","GLOBAL",30)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct2",0)
		CreateCreatureOffScreen("dw#drct2",0)
		CreateCreatureOffScreen("dw#drct3",0)
END

IF
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	!GlobalTimerNotExpired("DMWWDrowCityTimer","GLOBAL")
	Global("DMWWDrowCityCounter","GLOBAL",1)
	!GlobalGT("DMWWDrowNumbers","GLOBAL",15)
THEN 
	RESPONSE #100
		IncrementGlobal("DMWWDrowCityCounter","GLOBAL",1)
		IncrementGlobal("DMWWDrowNumbers","GLOBAL",5)
		SetGlobalTimer("DMWWDrowCityTimer","GLOBAL",24)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct2",0)
		CreateCreatureOffScreen("dw#drct1",0)
END

IF
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	!GlobalTimerNotExpired("DMWWDrowCityTimer","GLOBAL")
	Global("DMWWDrowCityCounter","GLOBAL",2)
	!GlobalGT("DMWWDrowNumbers","GLOBAL",15)
THEN 
	RESPONSE #100
		IncrementGlobal("DMWWDrowCityCounter","GLOBAL",1)
		IncrementGlobal("DMWWDrowNumbers","GLOBAL",7)
		SetGlobalTimer("DMWWDrowCityTimer","GLOBAL",24)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct2",0)
		CreateCreatureOffScreen("dw#drct2",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct4",0)
END

IF
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	!GlobalTimerNotExpired("DMWWDrowCityTimer","GLOBAL")
	Global("DMWWDrowCityCounter","GLOBAL",3)
	!GlobalGT("DMWWDrowNumbers","GLOBAL",15)
THEN 
	RESPONSE #100
		IncrementGlobal("DMWWDrowCityCounter","GLOBAL",1)
		IncrementGlobal("DMWWDrowNumbers","GLOBAL",5)
		SetGlobalTimer("DMWWDrowCityTimer","GLOBAL",24)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drctf",0)
END

// now the serious war parties turn up; the elites are hanging back, waiting for a softening-up chance
	
IF
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	!GlobalTimerNotExpired("DMWWDrowCityTimer","GLOBAL")
	OR(7)
		Global("DMWWDrowCityCounter","GLOBAL",4)
		Global("DMWWDrowCityCounter","GLOBAL",5)
		Global("DMWWDrowCityCounter","GLOBAL",6)
		Global("DMWWDrowCityCounter","GLOBAL",7)
		Global("DMWWDrowCityCounter","GLOBAL",9)
		Global("DMWWDrowCityCounter","GLOBAL",11)
		Global("DMWWDrowCityCounter","GLOBAL",13)
	!GlobalGT("DMWWDrowNumbers","GLOBAL",15)
THEN 
	RESPONSE #100
		IncrementGlobal("DMWWDrowCityCounter","GLOBAL",1)
		IncrementGlobal("DMWWDrowNumbers","GLOBAL",15)
		SetGlobalTimer("DMWWDrowCityTimer","GLOBAL",24)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct1",0)
		CreateCreatureOffScreen("dw#drct2",0)
		CreateCreatureOffScreen("dw#drct2",0)
		CreateCreatureOffScreen("dw#drct2",0)
		CreateCreatureOffScreen("dw#drct2",0)
		CreateCreatureOffScreen("dw#drct5",0)
		CreateCreatureOffScreen("dw#drct4",0)
		CreateCreatureOffScreen("dw#drct8",0)
		CreateCreatureOffScreen("dw#drctf",0)
		CreateCreatureOffScreen("dw#drct9",0)
END

// Now for the uber groups. Anyone still in Ust Natha is either dead, uber, or very cheesy...

IF
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	!GlobalTimerNotExpired("DMWWDrowCityTimer","GLOBAL")
	Global("DMWWDrowCityCounter","GLOBAL",8)
	!GlobalGT("DMWWDrowNumbers","GLOBAL",15)
THEN 
	RESPONSE #100
		IncrementGlobal("DMWWDrowCityCounter","GLOBAL",1)
		IncrementGlobal("DMWWDrowNumbers","GLOBAL",7)
		SetGlobalTimer("DMWWDrowCityTimer","GLOBAL",30)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct7",0)
		CreateCreatureOffScreen("dw#drct7",0)
		CreateCreatureOffScreen("dw#drct7",0)
END

IF
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	!GlobalTimerNotExpired("DMWWDrowCityTimer","GLOBAL")
	Global("DMWWDrowCityCounter","GLOBAL",10)
	!GlobalGT("DMWWDrowNumbers","GLOBAL",15)
THEN 
	RESPONSE #100
		IncrementGlobal("DMWWDrowCityCounter","GLOBAL",1)
		IncrementGlobal("DMWWDrowNumbers","GLOBAL",8)
		SetGlobalTimer("DMWWDrowCityTimer","GLOBAL",30)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct9",0)
		CreateCreatureOffScreen("dw#drct9",0)
		CreateCreatureOffScreen("dw#drcta",0)
		CreateCreatureOffScreen("dw#drct8",0)
		CreateCreatureOffScreen("demglab2",0)
		CreateCreatureOffScreen("demnab02",0)
		CreateCreatureOffScreen("demnab02",0)

END

IF
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	!GlobalTimerNotExpired("DMWWDrowCityTimer","GLOBAL")
	Global("DMWWDrowCityCounter","GLOBAL",12)
	!GlobalGT("DMWWDrowNumbers","GLOBAL",15)
THEN 
	RESPONSE #100
		IncrementGlobal("DMWWDrowCityCounter","GLOBAL",1)
		IncrementGlobal("DMWWDrowNumbers","GLOBAL",4)
		SetGlobalTimer("DMWWDrowCityTimer","GLOBAL",30)
		CreateCreatureOffScreen("dw#drctc",0)
		CreateCreatureOffScreen("dw#drct8",0)
		CreateCreatureOffScreen("dw#drctf",0)
		CreateCreatureOffScreen("dw#drctf",0)
		CreateCreatureOffScreen("dw#drctg",0)
		CreateCreatureOffScreen("dw#drctg",0)
		CreateCreatureOffScreen("dw#drctg",0)
		CreateCreatureOffScreen("dw#drctg",0)
END

IF
	Global("DrowCityHostile","GLOBAL",1) // sanity check
	!GlobalTimerNotExpired("DMWWDrowCityTimer","GLOBAL")
	Global("DMWWDrowCityCounter","GLOBAL",14)
	!GlobalGT("DMWWDrowNumbers","GLOBAL",15)
THEN 
	RESPONSE #100
		IncrementGlobal("DMWWDrowCityCounter","GLOBAL",1)
		IncrementGlobal("DMWWDrowNumbers","GLOBAL",8)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drct6",0)
		CreateCreatureOffScreen("dw#drctb",0)
		CreateCreatureOffScreen("dw#drct9",0)
		CreateCreatureOffScreen("dw#drct5",0)
		CreateCreatureOffScreen("dw#drct9",0)
		CreateCreatureOffScreen("dw#drct5",0)
		CreateCreatureOffScreen("dw#drct4",0)
		CreateCreatureOffScreen("dw#drctx",0)
		CreateCreatureOffScreen("dw#drctx",0)
		CreateCreatureOffScreen("dw#drcty",0)
		CreateCreatureOffScreen("dw#drcty",0)
		CreateCreatureOffScreen("dw#drctz",0)
		CreateCreatureOffScreen("dw#drctz",0)

END
