INCLUDE ~%scsroot%/lib/ai_wrap.tph~
DEFINE_ACTION_FUNCTION drow
STR_VAR version=""
BEGIN

        ACTION_FOR_EACH func IN ambush temple jaellat ghaunadan exit misc city BEGIN
           LAF ~drow_%func%~ END
        END
END


DEFINE_ACTION_FUNCTION drow_ambush BEGIN

	// the drow ambush in the underdark. It's basically fine, except that there's no reason the wizards shouldn't have
	// elven chain

        LAF edit_creature STR_VAR creature=drow06 editstring=~add_items=>dwchan01~ END

 	// the drow ambush on the bridge. We'll keep to the assumption that it's not got any uber-powerful creatures in,
	// but we'll up the numbers by adding secondary priests, more fighters, and the mage who looks as if he was
	// forgotten about, and we'll give the drow fighters some reasonable ability scores

        MAKE_PATCH
           add_items=>helmnoan
           str=>17
           dex=>19
        END
        LAF edit_creature STR_VAR creature=~uddrow27 uddrow28~ edits=patch_data END
        
        LAF edit_creature STR_VAR creature=uddrow30 editstring=~add_items=>dwchan01~ END

        LAF swap_text STR_VAR files=cut44a.bcs
                              swaps=~CreateCreatureDoor("uddrow27",\[4099.1668\],0)=>CreateCreatureDoor("uddrow27",[4099.1668],0)CreateCreatureDoor("uddrow27",[4115.1616],0)
		                     CreateCreatureDoor("uddrow28",\[3945.1915\],8)=>CreateCreatureDoor("uddrow28",[3945.1915],8)CreateCreatureDoor("uddrow28",[4064.1624],0)
		                     CreateCreatureDoor("uddrow29",\[4094.1632\],0)=>CreateCreatureDoor("uddrow29",[4094.1632],8)CreateCreatureDoor("uddrow29",[4014.1620],0)
		                     CreateCreatureDoor("uddrow29",\[3893.1944\],8)=>CreateCreatureDoor("uddrow29",[3893.1944],8)CreateCreatureDoor("uddrow29",[4197.1675],0)
		                     CreateCreatureDoor("uddrow31",\[3914.1875\],8)=>CreateCreatureDoor("uddrow31",[3914.1875],8)CreateCreatureDoor("drow04",[3799.2008],8)CreateCreatureDoor("uddrow30",[4201.1639],8)~
        END

END

DEFINE_ACTION_FUNCTION drow_temple BEGIN

	// The guards in the temple - again, all we do is add some abilities

        MAKE_PATCH
           add_items=>helmnoan
           str=>18
           str_ex=>76
           dex=>19
        END
        LAF edit_creature STR_VAR creature=~uddrow01 uddrow02 uddrow03~  edits=patch_data END

        MAKE_PATCH
           level=>10
           add_items=>helmnoan
           str=>18
           str_ex=>76
           dex=>19
           enforce_charclass=>null
        END
        LAF edit_creature STR_VAR creature=~uddrow30~  edits=patch_data END

        // Qilue becomes a L20 priest

	MAKE_PATCH
	   level=>20
	   enforce_charclass=>null
	END
        LAF edit_creature STR_VAR creature=~daqilue~  edits=patch_data END

END

DEFINE_ACTION_FUNCTION drow_jaellat BEGIN

	/////// the Jaellats

	// staged attack scripts

<<<<<<<< .../stratagems-inline/dw#jael1.baf

IF
	NumTimesTalkedTo(0)
	See([PC])
THEN
	RESPONSE #100
		SetGlobal("DMWWJaellatAll","GLOBAL",1)
		StartDialogueNoSet([PC])
END

>>>>>>>>

<<<<<<<< .../stratagems-inline/dw#jael2.baf

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("DMWWJaellatAll","GLOBAL",1)
THEN
	RESPONSE #100
		NoAction()
END

>>>>>>>>

        LAF install STR_VAR files=~dw#jael1.baf dw#jael2.baf~ inline=yes END

	// Hindra has extra bit of script

        LAF edit_creature STR_VAR creature=jael01 editstring=~insert_script_high=>dw#jael1~ END

        // ordinary troops need to be given better stats etc

        MAKE_PATCH
           str=>17
           dex=>19
           add_items=>helmnoan
           insert_script_high=>dw#jael2
        END
        LAF edit_creature STR_VAR creature=jael04  edits=patch_data END

	// Ist'tar likewise

        MAKE_PATCH
           str=>18
           str_ex=>76
           dex=>19
           add_items=>helmnoan
        END
        LAF edit_creature STR_VAR creature=jael04  edits=patch_data END



	// Every noble house needs a mage-trained son as well as a weapon master

        MAKE_PATCH
           allegiance=>ENEMY
           say_both_names=>3100
           script_override=>shout
        END
        LAF clone_creature STR_VAR creature=~dadrow2=>dw#drja1~ edits=patch_data END

	// minor spellcasters

        MAKE_PATCH
           allegiance=>ENEMY
           say_both_names=>3101
           script_override=>shout
        END
        LAF clone_creature STR_VAR creature=~dadrow1=>dw#drja2~ edits=patch_data END

        MAKE_PATCH
           allegiance=>ENEMY
           say_both_names=>3102
           script_override=>shout
        END
        LAF clone_creature STR_VAR creature=~drow05=>dw#drja3~ edits=patch_data END

	// the area script

        LAF extend_area_script STR_VAR area=ar2209 bottom=ar2209 location=resource END

END

DEFINE_ACTION_FUNCTION drow_ghaunadan BEGIN

	///// Ghaunadan priests

        LAF swap_text STR_VAR files=~ar2200.bcs~ swaps=~CreateCreature("daghaun2",\[4225.3508\],6)=>CreateCreature("daghaun2",[4571.3437],6)CreateCreature("daghaun2",[4230.3600],6)CreateCreature("daghaun2",[4190.3590],6)CreateCreature("daghaun2",[4360.3640],6)CreateCreature("daghaun2",[4225.3508],6)~ END

        MAKE_PATCH
           level=>16
           enforce_charclass=>null
        END
        LAF edit_creature STR_VAR creature=dagmag01  edits=patch_data END

END

DEFINE_ACTION_FUNCTION drow_exit BEGIN

	////// drow battlegroup at Underdark exit

        LAF edit_creature STR_VAR creature=uddoor06 editstring=~add_items=>"dwplat01 dwblun01"~ END
        LAF edit_creature STR_VAR creature=uddoor05 editstring=~add_items=>"dwchan01"~ END


        ACTION_FOR_EACH demon_number IN 1 2 BEGIN
        MAKE_PATCH
           dv=>~dw#drxg%demon_number%~
           script_override=>gpshout
           script_class=>dwgates1
           specifics=>129
           allegiance=>NEUTRAL
        END
        LAF clone_creature STR_VAR creature= ~demosum3=>dw#drxg%demon_number%~ edits=patch_data END
        END

        MAKE_PATCH
           script_override=>gpshout
           script_class=>dwgates1
           allegiance=>NEUTRAL
           specifics=>129
        END
        LAF clone_creature STR_VAR creature=~golsto01=>uddoor07~ edits=patch_data END

        MAKE_PATCH
           insert_script=>~dwgates1 after gpshout~
           allegiance=>NEUTRAL
           dv=>dw#drxmg
        END
        LAF clone_creature STR_VAR creature=~drow06=>dw#drxmg~ edits=patch_data END

        MAKE_PATCH
           insert_script=>~dwgates1 after gpshout~
           allegiance=>NEUTRAL
           dv=>dw#drxpr
        END
        LAF clone_creature STR_VAR creature=~drow05=>dw#drxpr~ edits=patch_data END

        LAF install STR_VAR files=~dw#adban.spl adalon_mod.d~ location=resource END
        LAF edit_spell STR_VAR spell=dw#adban editstring=~name1_string=>61358 name2_string=>61358~ END

        LAF extend STR_VAR file=dragsilv top=adalon_add location=resource END
        LAF extend STR_VAR file=ar2402 bottom=ar2402 location=resource END

	COPY_EXISTING ~cut44c.bcs~ ~override~
		DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~\[1129.488\]~ ~[1340.543]~
		REPLACE_TEXTUALLY ~\[1114.538\]~ ~[1322.609]~
		REPLACE_TEXTUALLY ~\[1184.560\]~ ~[1393.562]~
		REPLACE_TEXTUALLY ~\[1260.533\]~ ~[1431.580]~
		REPLACE_TEXTUALLY ~\[1200.496\]~ ~[1463.555]~
		REPLACE_TEXTUALLY ~\[1332.580\]~ ~[1534.600]~
		REPLACE_TEXTUALLY ~\[1203.541\]~ ~[1257.582]~
		END

END


DEFINE_ACTION_FUNCTION drow_misc BEGIN

	// the drow duelist
	
	MAKE_PATCH
	   level=>16
	   enforce_charclass=>null
	END
	LAF edit_creature STR_VAR creature=uddrow17  edits=patch_data END
	
END

DEFINE_ACTION_FUNCTION ~drow_city~ BEGIN


	//// drow city defences

	/// we have:

	// dw#drct1 - melee fighters, based on uddrow27
	// dw#drct2 - archers, based on uddrow29
	// dw#drct3 - mid-level mage based on dadrow1
	// dw#drct4 - midlevel priest based on uddrow31
	// dw#drct5 - higher-level priest based on drow07
	// dw#drct6 - higher-level drow soldier based on uddrow01
	// dw#drct7 - higher-level mage based on dadrow2
	// dw#drct8 - mid-high level mage based on drow06
	// dw#drct9 - highish level priest based on drow05
	// dw#drcta - high-level priest based on jael01
	// dw#drctb - ultra-high-level priest based on uddoor06
	// dw#drctc - ultra-high-level mage based on uddoor05
	// dw#drctf - mid-level mage based on uddrow30

        MAKE_PATCH
           script_override=>dw#dwcty
           strip_scs_scripts=>null
           insert_script_low=>wtasight
           dv=>dw#dwcty
           patch_item_inline=>"undroppable=>1"
        END
        LAF clone_creature STR_VAR creature=~uddrow27=>dw#drct1 uddrow29=>dw#drct2~ edits=patch_data END

        MAKE_PATCH
           script_override=>dw#dwcty
           allegiance=>ENEMY
           name1_string=>50900
           name2_string=>50900
           dv=>dw#dwcty
           patch_item_inline=>"undroppable=>1"
        END
        LAF clone_creature STR_VAR creature=~dadrow1=>dw#drct3~ edits=patch_data END

        MAKE_PATCH
           script_override=>dw#dwcty
           dv=>dw#dwcty
           name1_string=>165
           name2_string=>165
           patch_item_inline=>"undroppable=>1"
        END
        LAF clone_creature STR_VAR creature=~uddrow31=>dw#drct4 drow07=>dw#drct5~ edits=patch_data END

        MAKE_PATCH
           script_override=>dw#dwcty
           strip_scs_scripts=>null
           insert_script_low=>wtasight
           dv=>dw#dwcty
           name1_string=>50900
           name2_string=>50900
           allegiance=>ENEMY
           patch_item_inline=>"undroppable=>1"
        END
        LAF clone_creature STR_VAR creature=~uddrow01=>dw#drct6~ edits=patch_data END

        MAKE_PATCH
           script_override=>dw#dwcty
           dv=>dw#dwcty
           name1_string=>50900
           name2_string=>50900
           allegiance=>enemy
        END
        LAF clone_creature STR_VAR creature=~dadrow2=>dw#drct7~ edits=patch_data END

        MAKE_PATCH
           script_override=>dw#dwcty
           dv=>dw#dwcty
        END
        LAF clone_creature STR_VAR creature=~drow06=>dw#drct8 drow05=>dw#drct9 uddrow30=>dw#drctf~ edits=patch_data END

        MAKE_PATCH
           script_override=>dw#dwcty
           dv=>dw#dwcty
           allegiance=>enemy
           say_both_names=>3103
        END
        LAF clone_creature STR_VAR creature=~jael01=>dw#drcta~ edits=patch_data END

        MAKE_PATCH
           script_override=>dw#dwcty
           dv=>dw#dwcty
           allegiance=>enemy
           say_both_names=>3104
        END
        LAF clone_creature STR_VAR creature=~uddoor06=>dw#drctb~ edits=patch_data END

        MAKE_PATCH
           script_override=>dw#dwcty
           dv=>dw#dwcty
           allegiance=>enemy
           say_both_names=>3105
        END
        LAF clone_creature STR_VAR creature=~uddoor05=>dw#drctc~ edits=patch_data END

        MAKE_PATCH
           insert_script_high=>dw#dwcty
        END
        LAF clone_creature STR_VAR creature="telbal1=>dw#drctx demgla01=>dw#drcty demnab02=>dw#drctz golsto01=>dw#drctg" edits=patch_data END

	// the area scripts

        LAF define_difficulty STR_VAR type=drow RET difficulty_variable END
        LAF extend_area_script STR_VAR area=ar2200 bottom=ar2200add location=ssl ssl=yes END
        LAF extend_area_script STR_VAR area=ar2100 bottom=ar2100add location=resource END

	LAF install STR_VAR files=~dw#dwcty.baf~ location=resource END
	
	LAF swap_text STR_VAR files=~udardul.dlg uddemon.dlg udphae01.dlg~ swaps=~SetGlobalTimer("udescape","GLOBAL",120)=>SetGlobalTimer("udescape","GLOBAL",36)~ END

END


