/////////////////////////////////////////////////////////////////////////////////
////////////////////  General fiend patch
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION fiend_general
STR_VAR arguments="" BEGIN
   SET tanarri=0
   SET baatezu=0
   SET spellcaster=0
   WHILE "%arguments%" STRING_COMPARE "" BEGIN
      LPF return_first_entry STR_VAR list="%arguments%" RET entry arguments=list END
      SET "%entry%"=1
   END
   PATCH_MAKE_PATCH
      strip_scs_scripts=>"if spellcaster then no_change else null"
      strip_script=>wtasight
      delete_effect=>~opcode=193~
      add_effect_inline=>~opcode=>193 parameter2=>1~
      class=>"if enhanced_edition then no_change else FIGHTER_MAGE_CLERIC"
      delete_opcodes=>~16 292~
      immunity_to_opcode=>25 // poison immunity
      add_effect_inline''=>~opcode=>173 parameter1=>100~
   END
   TO_LOWER arguments
   PATCH_IF tanarri BEGIN
      SPRINT $patch_data(~resist_fire~) 50
      SPRINT $patch_data(~immunity~) electricity
      SPRINT $patch_data(~resist_cold~) 50
      SPRINT $patch_data(~resist_acid~) 0
      SPRINT $patch_data(~alignment~) CHAOTIC_EVIL

   END
   PATCH_IF baatezu BEGIN
      SPRINT $patch_data(~immunity~) fire
      SPRINT $patch_data(~resist_cold~) 50
      SPRINT $patch_data(~resist_acid~) 50
      SPRINT $patch_data(~alignment~) LAWFUL_EVIL
   END
   LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END


/////////////////////////////////////////////////////////////////////////////////
////////////////////  Shared resources
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION shared_fiend_resources
BEGIN
   LAF check_label STR_VAR label=dw#fiend_shared_resources RET value END
   ACTION_IF !value BEGIN
    LAF make_label STR_VAR label=dw#fiend_shared_resources END
    OUTER_SPRINT component_loc fiend
    ////////////////
    // difficulty
    ////////////////
    
    LAF innate_casting_effects END
    LAF check_ini STR_VAR ini=fiend_staying_power RET mult=value END
    LAF hitpoint_boost_effects INT_VAR mult STR_VAR spell=dw#fiehp END

    ////////////////
    /// spells
    ////////////////

    LAF run STR_VAR file=fiend_spells_basic END // the ones used by Smarter Mages

    MAKE_PATCH
          SUCCUBUS_TELEPORT=>INNATE_FIEND_TELEPORT
          WIZARD_DISPEL_MAGIC=>INNATE_FIEND_DISPEL_MAGIC
          WIZARD_MIRROR_IMAGE=>INNATE_FIEND_MIRROR_IMAGE
          CLERIC_UNHOLY_BLIGHT=>INNATE_FIEND_UNHOLY_BLIGHT
          WIZARD_CONFUSION=>INNATE_FIEND_CONFUSION
          CLERIC_BLADE_BARRIER=>INNATE_FIEND_BLADE_BARRIER
          CLERIC_CURE_CRITICAL_WOUNDS=>INNATE_FIEND_CURE_CRITICAL_WOUNDS
          WIZARD_INVISIBILITY=>INNATE_FIEND_INVISIBILITY
          WIZARD_PRISMATIC_SPRAY=>INNATE_FIEND_PRISMATIC_SPRAY
          WIZARD_SLOW=>INNATE_FIEND_SLOW
          WIZARD_DIRE_CHARM=>INNATE_FIEND_MASS_CHARM
          WIZARD_BLUR=>INNATE_FIEND_BLUR
    END
    ACTION_PHP_EACH patch_data AS key=>value BEGIN
            LAF make_innate_repeating_spell INT_VAR allow_missing=1 STR_VAR arguments="%key%=>%value%" END
    END
    MAKE_PATCH
          patch_effect_inline=>"match=>opcode=124 timing=>1 duration=>0"
    END
    LAF edit_spell STR_VAR spell=INNATE_FIEND_TELEPORT edits=patch_data END

    LAF make_innate_repeating_spell INT_VAR overwrite=1 allow_missing=1 STR_VAR arguments=ERINYES_CHARM END
    LAF make_innate_repeating_spell INT_VAR cooldown=18 STR_VAR arguments="WIZARD_STONE_SKIN=>INNATE_FIEND_STONE_SKIN" END

  END

END

/////////////////////////////////////////////////////////////////////////////////
////////////////////  Balors
////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION balor
BEGIN
    PATCH_MAKE_PATCH
       animation=>TANARRI
       fiend_general=>tanarri
       immunity=>fire
       immunity'=>energy_drain
       level_all=>24
       add_items=>~balor(weapon1) dw#balwp(shield)~
       remove_spells=>all
       add_spells=>"INNATE_FIEND_DOMINATION INNATE_FIEND_STONE_SKIN CLERIC_FIRE_STORM INNATE_FIEND_DISPEL_MAGIC WIZARD_BREACH(3) CLERIC_IMPLOSION WIZARD_NPC_SYMBOL_STUN(3) WIZARD_NPC_SYMBOL_DEATH(3) INNATE_FIEND_TELEPORT"
       movement=>11
       ac=>~-8~
       resist_magicGT=>70
       strGT=>21
       dexGT=>18
       conGT=>20
       intGT=>20
       wisGT=>16
       chaGT=>25
       attacks=>2 // we'll follow atweaks and give them dual-wielding
       set_proficiencies=>~LONGSWORD=>5 2WEAPON=>3 FLAILMORNINGSTAR=>3~

    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END

END

/////////////////////////////////////////////////////////////////////////////////
////////////////////  Glabrezu
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION glabrezu
	STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       hp_current=>"if hp_current<100 then hp_current else 70"
       fiend_general=>"tanarri %arguments%"
       animation=>"if (enhanced_edition or infinity_animations) then GLABREZU else IC_GLAB"
       remove_spells=>all     
	  add_spells=>"INNATE_FIEND_TRUE_SIGHT INNATE_FIEND_MIRROR_IMAGE INNATE_FIEND_DISPEL_MAGIC INNATE_FIEND_CONFUSION INNATE_FIEND_UNHOLY_BLIGHT INNATE_FIEND_TELEPORT WIZARD_POWER_WORD_STUN(7)"
       level_all=>17
       thac0LT=>7
       movement=>11
       ac=>~-7~
       strGT=>20
       dexGT=>18
       conGT=>20
       intGT=>16
       wisGT=>13
       chaGT=>22
       resist_magic=>50
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

/////////////////////////////////////////////////////////////////////////////////
////////////////////  Nabassu
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION nabassu
STR_VAR arguments=""
BEGIN

    PATCH_MAKE_PATCH
       fiend_general=>tanarri
       animation=>TANARRI
       level_all=>12
       ac=>~-5~
       movement=>9
       remove_spells=>all
       add_spells=>"INNATE_FIEND_TELEPORT TANARI_SILENCE TANARI_VAMPIRIC_TOUCH TANARI_PARALYZE"
       strGT=>19
       dexGT=>14
       conGT=>19
       intGT=>14
       wisGT=>15
       chaGT=>18
       resist_magic=>50
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END

    REPLACE_CRE_ITEM ~BL3-12M4~ #0 #0 #0 NONE WEAPON1
    // change color for EE fiends
    PATCH_IF enhanced_edition BEGIN
		LPF check_ini STR_VAR ini=no_nabassu_color_change RET value END
		PATCH_IF !value BEGIN
			WRITE_LONG 0x28 IDS_OF_SYMBOL (animate FIEND_GREEN)
		END
	END
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Mariliths
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION marilith
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>tanarri
       animation=>"if (enhanced_edition or infinity_animations) then MARILITH else IC_MERILITH"
       level_all=>20
       delete_effect=>1
       attacks=>4
       set_proficiencies=>~2WEAPON=>3 SCIMITARWAKISASHININJATO=>5~
       remove_spells=>all
       add_spells=>"INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC INNATE_FIEND_TRUE_SIGHT INNATE_FIEND_BLADE_BARRIER INNATE_FIEND_STONE_SKIN MARILITH_PROTECTION_FROM_MAGIC_WEAPONS MARILITH_ANIMATE_DEAD MARILITH_CLOUDKILL"
       add_items=>~marili(weapon1) marili(shield) ipsion(amulet)~
       strGT=>19
       dexGT=>18
       conGT=>12
       intGT=>18
       wisGT=>15
       chaGT=>20
       gender=>FEMALE
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Succubi
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION succubus
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>tanarri
       level_all=>12
       movement=>9
       remove_spells=>all
       add_spells=>"INNATE_FIEND_TELEPORT SUCCUBUS_ETHEREAL INNATE_FIEND_CURE_CRITICAL_WOUNDS"
       strGT=>15
       dexGT=>17
       conGT=>12
       intGT=>16
       wisGT=>14
       chaGT=>18
       resist_magic=>30
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Yochlols
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION yochlol
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>tanarri
       level_all=>15
       remove_spells=>all
       add_spells=>"INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC INNATE_FIEND_DOMINATION INNATE_YOCHLOL_SPIDER_SPAWN INNATE_YOCHLOL_FLAME_STRIKE INNATE_FIEND_IMPROVED_INVISIBILITY CLERIC_HEAL(3)"
       strGT=>15
       dexGT=>17
       conGT=>12
       intGT=>16
       wisGT=>14
       chaGT=>18
       resist_magic=>50
       replace_items=>"dwplat01 ringdemn immune2 dwsw1h01"
       patch_item_inline=>"undroppable=>1"
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Cambions
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION cambion
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>"tanarri spellcaster"
       movement=>11
       strGT=>18
       str_exGT=>76
       dexGT=>16
       conGT=>17
       intGT=>16
       wisGT=>8
       chaGT=>18
       resist_magicGT=>30
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END


/////////////////////////////////////////////////////////////////////////////////
//////////   Maurezhi
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION maurezhi
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>tanarri
       level_all=>10
       movement=>9
       resist_magic=>34
       remove_spells=>all
       add_spells=>"CLERIC_CLOAK_OF_FEAR WIZARD_HOLD_PERSON WIZARD_ANIMATE_DEAD"
       strGT=>14
       dexGT=>15
       conGT=>12
       int=>16
       wis=>8
       cha=>9
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Quasits
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION quasit
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>tanarri
       level_all=>8
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END


/////////////////////////////////////////////////////////////////////////////////
//////////   Demon knights
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION demon_knight
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>null
       level_all=>18
       class=>FIGHTER_MAGE
       add_items=>ringdemn
       remove_spells=>all
       attacks=>3
       add_spells=>~DEATHKNIGHT_FIREBALL(3) WIZARD_DISPEL_MAGIC(2) POWER_WORD_STUN POWER_WORD_BLIND POWER_WORD_KILL WIZARD_NPC_SYMBOL_FEAR WIZARD_NPC_SYMBOL_DEATH~
       alignment=>CHAOTIC_EVIL
       set_proficiencies=>~TWOHANDEDSWORD=>5 2HANDED=>3~
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

//////////////////////////////////////////
/////	Alu-fiends
///////////////////////////////////////////

DEFINE_PATCH_FUNCTION alu_fiend
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>tanarri
       level_all=>12
       strGT=>15
       dexGT=>17
       conGT=>14
       intGT=>18
       wisGT=>12
       chaGT=>16
       resist_magic=>30
       remove_spells=>all
       add_spells=>"WIZARD_MIRROR_IMAGE(2) WIZARD_SECRET_WORD(2) WIZARD_HASTE"
       gender=>FEMALE
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

//////////////////////////////////////////
/////	Pit fiends
///////////////////////////////////////////

DEFINE_PATCH_FUNCTION pit_fiend
STR_VAR arguments=""
BEGIN
    PATCH_IF iwd_divine BEGIN
       SPRINT symbol CLERIC_SYMBOL_PAIN
    END ELSE BEGIN
       SPRINT symbol WIZARD_SYMBOL_FEAR
    END
    PATCH_MAKE_PATCH
       fiend_general=>baatezu
       hp_current=>"if hp_current<150 then hp_current else 100"
       animation=>RAVER
       remove_spells=>all
       add_spells=>"INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC INNATE_FIEND_IMPROVED_INVISIBILITY INNATE_FIEND_FIREBALL %symbol% WIZARD_METEOR_SWARM CLERIC_UNHOLY_WORD"
       level_all=>24
       resist_magic=>50
       strGT=>18
       str_exGT=>100
       dexGT=>16
       conGT=>16
       intGT=>18
       wisGT=>16
       chaGT=>25
       movement=>11
       delete_opcodes=>~16~
       immunity=>energy_drain

    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Bone fiends
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION bone_fiend
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>baatezu
       animation=>"if enhanced_edition then SKELETON_FIEND else if infinity_animations then SKELETONB else IC_SKELETONB"
       level_all=>12
       add_items=>~bonefd(weapon1)~
       remove_spells=>all
       add_spells=>"INNATE_FIEND_TELEPORT WIZARD_CONE_OF_COLD(3) INNATE_FIEND_IMPROVED_INVISIBILITY"
       strGT=>16
       dexGT=>14
       conGT=>15
       intGT=>12
       wisGT=>12
       chaGT=>10
       resist_magic=>30

    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Cornugons
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION cornugon
STR_VAR arguments=""
RET value
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>baatezu
       immunity=>electricity
       animation=>"if (enhanced_edition or infinity_animations) then CORNUGON else IC_CORNUGONSKI"
       level_all=>17
       remove_spells=>all
       add_spells=>"INNATE_FIEND_TELEPORT WIZARD_FIREBALL(3) WIZARD_LIGHTNING_BOLT(3)"
       strGT=>18
       str_exGT=>100
       dexGT=>17
       conGT=>17
       intGT=>16
       wisGT=>14
       chaGT=>16
       resist_magic=>50
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
    SET value=1
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Erinyes
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION erinyes
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>baatezu
       level_all=>12
       remove_spells=>all
       add_spells=>"INNATE_FIEND_TELEPORT INNATE_FIEND_UNHOLY_BLIGHT INNATE_FIEND_INVISIBILITY WIZARD_PROTECTION_FROM_MAGIC_WEAPONS ERINYES_CHARM"
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END


//////////////////////////////////////////
////	Imps
//////////////////////////////////////////

DEFINE_PATCH_FUNCTION imp
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>baatezu
       level_all=>8
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Solars
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION solar
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>null
       level_all=>35
       resist_fire=>100
       resist_acid=>0
       remove_spells=>all
       add_spells=>"INNATE_FIEND_TRUE_SIGHT INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC INNATE_SOLAR_VOCALIZE INNATE_SOLAR_HEAL INNATE_SOLAR_DEATH_SPELL WIZARD_CONFUSION(3) CLERIC_EARTHQUAKE(3) CLERIC_CREEPING_DOOM WIZARD_FINGER_OF_DEATH CLERIC_UNHOLY_WORD(3) INNATE_FIEND_IMPROVED_INVISIBILITY WIZARD_DOMINATION(3) CLERIC_FIRE_STORM WIZARD_PRISMATIC_SPRAY"
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Aurumach Rilmani
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION aurumach
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>null
       level_all=>25
       add_effect_inline=>~opcode=>310 target=>1 timing=>9 parameter2=>1~ // immunity to timestop
       remove_spells=>all
       add_spells=>"INNATE_RILMANI_LAY_HANDS(3) WIZARD_SYMBOL_STUN WIZARD_SYMBOL_DEATH WIZARD_SYMBOL_FEAR INNATE_FIEND_TRUE_SIGHT INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC INNATE_FIEND_SLOW INNATE_FIEND_MASS_CHARM INNATE_SOLAR_DEATH_SPELL INNATE_FIEND_IMPROVED_INVISIBILITY INNATE_FIEND_MIRROR_IMAGE INNATE_FIEND_PRISMATIC_SPRAY"
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Ferrumach Rilmani
////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION ferrumach
STR_VAR arguments=""
BEGIN
    PATCH_MAKE_PATCH
       fiend_general=>null
       level_all=>15
       add_effect_inline=>~opcode=>310 target=>1 timing=>9 parameter2=>1~ // immunity to timestop
       remove_spells=>all
       add_spells=>"INNATE_FIEND_TELEPORT TANARI_SILENCE INNATE_FIEND_BLUR CLERIC_CURE_CRITICAL_WOUNDS WIZARD_BREACH(3)"
       replace_items=>HALB12 // +3 halberd
       immunity_to_spell=>"TANARI_SILENCE"
    END
    LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END


