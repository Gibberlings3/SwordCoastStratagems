/////////////////////////////////////////////////////////////////////////////////
////////////////////  General fiend patch
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO tanarri BEGIN
	LPM fiend_general_2e
	m_resist_fire=50
	m_resist_cold=50
	m_resist_magic_fire=50
	m_resist_magic_cold=50
	m_resist_acid=0
	m.resist_electricity=100
	m_alignment:=CHAOTIC_EVIL
END

DEFINE_PATCH_MACRO baatezu BEGIN
	LPM fiend_general_2e
	m_resist_fire=50
	m_resist_cold=50
	m_resist_magic_fire=50
	m_resist_magic_cold=50
	m_resist_acid=0
	m.resist_electricity=100
	m_alignment:=CHAOTIC_EVIL
END

DEFINE_PATCH_MACRO fiend_general_2e BEGIN
	LOCAL_SPRINT script ""
	LOCAL_SPRINT scr ""
	IF {!IS_AN_INT spellcaster} {{spellcaster=0}}
	
	PATCH_FOR_EACH script IN override class race general default BEGIN
		scr:="m_%script%"
		PATCH_MATCH EVAL "%scr%" WITH
		"dw[#123].*" BEGIN
			PATCH_IF !spellcaster BEGIN
				SPRINT $m("%script%") ""
			END
		END
		wtasight "_tasight" BEGIN
			SPRINT $m("%script%") ""
		END		
		DEFAULT
		END
	END
	
	
	m.fx.delete{s_opcode=16 OR s_opcode=193 OR s_opcode=292}
	m.fx.add{s_opcode=193 s_parameter2=1}
	m.immunity_effect{poison}

END


DEFINE_PATCH_FUNCTION fiend_general
STR_VAR arguments="" BEGIN
   SET tanarri=0
   SET baatezu=0
   SET spellcaster=0
   WHILE "%arguments%" STRING_COMPARE "" BEGIN
      LPF return_first_entry STR_VAR list="%arguments%" RET entry arguments=list END
      SET "%entry%"=1
   END
   PATCH_MAKE_PATCH
      strip_scs_scripts=>"if spellcaster then no_change else null"
      strip_script=>wtasight
      delete_effect=>~opcode=193~
      add_effect_inline=>~opcode=>193 parameter2=>1~
      class=>"if enhanced_edition then no_change else FIGHTER_MAGE_CLERIC"
      delete_opcodes=>~16 292~
      immunity_to_opcode=>25 // poison immunity
      add_effect_inline''=>~opcode=>173 parameter1=>100~
   END
   TO_LOWER arguments
   PATCH_IF tanarri BEGIN
      SPRINT $patch_data(~resist_fire~) 50
      SPRINT $patch_data(~immunity~) electricity
      SPRINT $patch_data(~resist_cold~) 50
      SPRINT $patch_data(~resist_acid~) 0
      SPRINT $patch_data(~alignment~) CHAOTIC_EVIL

   END
   PATCH_IF baatezu BEGIN
      SPRINT $patch_data(~immunity~) fire
      SPRINT $patch_data(~resist_cold~) 50
      SPRINT $patch_data(~resist_acid~) 50
      SPRINT $patch_data(~alignment~) LAWFUL_EVIL
   END
   LPF apply_patches STR_VAR filename file_ext=CRE edits=patch_data END
END


/////////////////////////////////////////////////////////////////////////////////
////////////////////  Shared resources
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION shared_fiend_resources
BEGIN
   LAF check_label STR_VAR label=dw#fiend_shared_resources RET value END
   ACTION_IF !value BEGIN
    LAF make_label STR_VAR label=dw#fiend_shared_resources END
    OUTER_SPRINT component_loc fiend
    ////////////////
    // difficulty
    ////////////////
    
    LAF innate_casting_effects END
    LAF check_ini STR_VAR ini=fiend_staying_power RET mult=value END
    LAF hitpoint_boost_effects INT_VAR mult STR_VAR spell=dw#fiehp END

    ////////////////
    /// spells
    ////////////////

    LAF run STR_VAR file=fiend_spells_basic locbase=spell END // the ones used by Smarter Mages

	// install Demon Fear if absent
	

	ACTION_IF !FILE_EXISTS_IN_GAME "%DEMON_FEAR%.spl" BEGIN
		COPY "%MOD_FOLDER%/%component_loc%/resource/%DEMON_FEAR%.spl" override
	END

    MAKE_PATCH
          SUCCUBUS_TELEPORT=>INNATE_FIEND_TELEPORT
          WIZARD_DISPEL_MAGIC=>INNATE_FIEND_DISPEL_MAGIC
          WIZARD_MIRROR_IMAGE=>INNATE_FIEND_MIRROR_IMAGE
          CLERIC_UNHOLY_BLIGHT=>INNATE_FIEND_UNHOLY_BLIGHT
          WIZARD_CONFUSION=>INNATE_FIEND_CONFUSION
          CLERIC_BLADE_BARRIER=>INNATE_FIEND_BLADE_BARRIER
          CLERIC_CURE_CRITICAL_WOUNDS=>INNATE_FIEND_CURE_CRITICAL_WOUNDS
          WIZARD_INVISIBILITY=>INNATE_FIEND_INVISIBILITY
          WIZARD_PRISMATIC_SPRAY=>INNATE_FIEND_PRISMATIC_SPRAY
          WIZARD_SLOW=>INNATE_FIEND_SLOW
          WIZARD_DIRE_CHARM=>INNATE_FIEND_MASS_CHARM
          WIZARD_BLUR=>INNATE_FIEND_BLUR
    END
    ACTION_PHP_EACH patch_data AS key=>value BEGIN
            LAF spl_make_innate_repeating_spell INT_VAR allow_missing=0 STR_VAR arguments="%key%=>%value%" END
    END

    MAKE_PATCH
          patch_effect_inline=>"match=>opcode=124 timing=>1 duration=>0"
    END
    LAF edit_spell STR_VAR spell=INNATE_FIEND_TELEPORT edits=patch_data END

    LAF spl_make_innate_repeating_spell INT_VAR overwrite=1 allow_missing=1 STR_VAR arguments=ERINYES_CHARM END
    LAF spl_make_innate_repeating_spell INT_VAR cooldown=18 STR_VAR arguments="WIZARD_STONE_SKIN=>INNATE_FIEND_STONE_SKIN" END

  END

END

/////////////////////////////////////////////////////////////////////////////////
////////////////////  Balors
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO balor
BEGIN
	m_animation:=TANARRI
	LPM tanarri
	m_resist_fire=100
	m_resist_magic_fire=100
	m.immunity_effect{energy_drain}
	m_level_all=24
	m.cre_min_stats{21 18 20 20 16 25}
	m_attacks=2
	m_ac="-8"
	m_resist_magic=(m_resist_magic<70?70:m_resist_magic)
	m.cre_add_items{balor(weapon1) dw#balwp(shield)}
	m.cre_remove_spells{all}	
	m.cre_add_spells{INNATE_FIEND_DOMINATION INNATE_FIEND_STONE_SKIN CLERIC_FIRE_STORM INNATE_FIEND_DISPEL_MAGIC WIZARD_BREACH(3) CLERIC_IMPLOSION WIZARD_NPC_SYMBOL_STUN(3) WIZARD_NPC_SYMBOL_DEATH(3) INNATE_FIEND_TELEPORT}

END


/////////////////////////////////////////////////////////////////////////////////
////////////////////  Glabrezu
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO glabrezu
BEGIN
	m_hp=(m_hp_current<100)?100:m_hp_current
	LPM tanarri
	IF {infinity_animations OR enhanced_edition} 
	{{ m_animation:=GLABREZU }}
	ELSE
	{{ m_animation:=IC_GLAB }}
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_FIEND_TRUE_SIGHT INNATE_FIEND_MIRROR_IMAGE INNATE_FIEND_DISPEL_MAGIC INNATE_FIEND_CONFUSION INNATE_FIEND_UNHOLY_BLIGHT INNATE_FIEND_TELEPORT WIZARD_POWER_WORD_STUN(7)}
	m_level_all=17
	m_thac0=(m_thac0>7?7:m_thac0)
	m_ac="-7"
	m.cre_min_stats{20 18 20 16 13 22}
	m_resist_magic=50
END

/////////////////////////////////////////////////////////////////////////////////
////////////////////  Nabassu
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO nabassu
BEGIN
	LOCAL_SET value=1
	LPM tanarri
	m_level_all=12
	m_animation:=TANARRI
	m_ac="-5"
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_FIEND_TELEPORT TANARI_SILENCE TANARI_VAMPIRIC_TOUCH TANARI_PARALYZE}
	m.cre_min_stats{19 14 19 14 15 18}
	m_resist_magic=50
	m.cre_add_items{BL3-12M4(weapon1)|replace:i=1}
    // change color for EE fiends
	IF {enhanced_edition} 
	{{
		LPF check_ini STR_VAR ini=no_nabassu_color_change RET value END
		IF {!value} {{m_animation:=FIEND_GREEN}}
	}}
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Mariliths
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO marilith
BEGIN
		m.fx.delete{}
		LPM tanarri
		IF {enhanced_edition OR infinity_animations}
		{{ m_animation:=MARILITH}}
		ELSE
		{{m_animation:=IC_MERILITH}}
		m_level_all=20
		m_attacks=4
		m.cre_set_proficiencies{2WEAPON=>3 SCIMITARWAKISASHININJATO=>5}
		m.cre_remove_spells{all}
		m.cre_add_spells{INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC INNATE_FIEND_TRUE_SIGHT INNATE_FIEND_BLADE_BARRIER INNATE_FIEND_STONE_SKIN MARILITH_PROTECTION_FROM_MAGIC_WEAPONS MARILITH_ANIMATE_DEAD MARILITH_CLOUDKILL}
		m.cre_add_items{marili(weapon1) marili(shield) ipsion(amulet)|replace:i=1}
		m_gender:=FEMALE
		m.cre_min_stats{19 18 12 18 15 20}
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Succubi
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO succubus BEGIN
	LPM tanarri
	m_level_all=12
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_FIEND_TELEPORT SUCCUBUS_ETHEREAL INNATE_FIEND_CURE_CRITICAL_WOUNDS}
	m.cre_min_stats{15 17 12 16 14 22}
	m_resist_magic=30
	
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Yochlols
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO yochlol BEGIN
	LPM tanarri
	m_level_all=15
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC INNATE_FIEND_DOMINATION INNATE_YOCHLOL_SPIDER_SPAWN INNATE_YOCHLOL_FLAME_STRIKE INNATE_FIEND_IMPROVED_INVISIBILITY CLERIC_HEAL(3)}
	m.cre_min_stats{15 17 12 16 14 18}
	m_resist_magic=50
	m.cre_add_items{dwplat01 ringdemn immune2 dwsw1h01|replace:i=1}
	m.item.alter{s_undroppable=1 s_unstealable=1}
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Cambions
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO cambion BEGIN
	spellcaster=1
	LPM tanarri
	m.cre_min_stats{18/76 16 17 16 8 18}
	m_resist_magic=(m_resist_magic<30?30:m_resist_magic)
END


/////////////////////////////////////////////////////////////////////////////////
//////////   Maurezhi
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO maurezhi
BEGIN
	LPM tanarri
	m_level_all=10
	m_resist_magic=34
	m.cre_remove_spells{all}
	m.cre_add_spells{CLERIC_CLOAK_OF_FEAR WIZARD_HOLD_PERSON WIZARD_ANIMATE_DEAD}
	m.cre_min_stats{14 15 12 16 8 9}
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Quasits
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO quasit
BEGIN
	LPM tanarri
	m_level_all=8
END


/////////////////////////////////////////////////////////////////////////////////
//////////   Demon knights
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO demon_knight
BEGIN
	LPM tanarri
	m_level_all=18
	m_class:=FIGHTER_MAGE
	m.cre_add_items{ringdemn}
	m_attacks=3
	m.cre_remove_spells{all}
	m.cre_add_spells{DEATHKNIGHT_FIREBALL(3) WIZARD_DISPEL_MAGIC(2) INNATE_FIEND_TELEPORT POWER_WORD_STUN POWER_WORD_BLIND POWER_WORD_KILL WIZARD_NPC_SYMBOL_FEAR WIZARD_NPC_SYMBOL_DEATH}
	m.cre_set_proficiencies{TWOHANDEDSWORD=>5 2HANDED=>3}
END

//////////////////////////////////////////
/////	Alu-fiends
///////////////////////////////////////////

DEFINE_PATCH_MACRO alu_fiend
BEGIN
	LPM tanarri
	m_level_all=12
	m.cre_min_stats{15 17 14 18 12 16}
	m_resist_magic=30
	m.cre_remove_spells{all}
	m.cre_add_spells{WIZARD_MIRROR_IMAGE(2) WIZARD_SECRET_WORD(2) WIZARD_HASTE}
	m_gender:=FEMALE
END

//////////////////////////////////////////
/////	Pit fiends
///////////////////////////////////////////

DEFINE_PATCH_MACRO pit_fiend
BEGIN
	LOCAL_SPRINT symbol ""
    PATCH_IF iwd_divine BEGIN
       SPRINT symbol CLERIC_SYMBOL_PAIN
    END ELSE BEGIN
       SPRINT symbol WIZARD_SYMBOL_FEAR
    END
	LPM baatezu
	m_hp=(m_hp_current<150)?150:m_hp_current
	m_animation:=RAVER
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC INNATE_FIEND_IMPROVED_INVISIBILITY INNATE_FIEND_FIREBALL %symbol% WIZARD_METEOR_SWARM CLERIC_UNHOLY_WORD}
	m_level_all=24
	m_resist_magic=50
	m.cre_min_stats{18/100 16 16 18 16 25}
	m.immunity_effect{energy_drain}
	m.fx.delete{s_opcode=16}
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Bone fiends
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO bone_fiend BEGIN
	LPM baatezu
	IF {enhanced_edition}
	{{
	m_animation:=SKELETON_FIEND
	}}
	ELSE 
	IF {infinity_animations}
	{{
	m_animation:=SKELETONB
	}}
	ELSE
	{{
	m_animation:=IC_SKELETONB
	}}
	m_level=12
	m.cre_add_items{bonefd(weapon)}
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_FIEND_TELEPORT WIZARD_CONE_OF_COLD(3) INNATE_FIEND_IMPROVED_INVISIBILITY}
	m.cre_min_stats{16 14 15 12 12 10}
	m_resist_magic=30
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Cornugons
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO cornugon
BEGIN
	LPM baatezu
	m_resist_electricity=100
	IF {enhanced_edition OR infinity_animations}
	{{
	m_animation:=CORNUGON
	}}
	ELSE
	{{
	m_animation:=IC_CORNUGON
	}}
	m_level_all=17
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_FIEND_TELEPORT WIZARD_FIREBALL(3) WIZARD_LIGHTNING_BOLT(3)}
	m.cre_min_stats{18/100 17 17 16 14 16}
	m_resist_magic=50
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Erinyes
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO erinyes
BEGIN
	LPM baatezu
	m_level_all=12
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_FIEND_TELEPORT INNATE_FIEND_UNHOLY_BLIGHT INNATE_FIEND_INVISIBILITY WIZARD_PROTECTION_FROM_MAGIC_WEAPONS ERINYES_CHARM}
END


//////////////////////////////////////////
////	Imps
//////////////////////////////////////////

DEFINE_PATCH_MACRO imp
BEGIN
	LPM baatezu
	m_level_all=8
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Planetars
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO planetar
BEGIN
	LPM fiend_general_2e
	m.cre_strip_scripts{dw#sumei}
	m.cre_remove_spells{}
	m.cre_add_spells{INNATE_FIEND_TRUE_SIGHT INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC CLERIC_EARTHQUAKE CLERIC_FIRE_STORM CLERIC_HEAL CLERIC_FLAME_STRIKE(3) CLERIC_HOLY_WORD WIZARD_NPC_SYMBOL_STUN}
END

DEFINE_PATCH_MACRO planetar_evil
BEGIN
	LPM fiend_general_2e
	m.cre_strip_scripts{dw#sumei}
	m.cre_remove_spells{}
	m.cre_add_spells{INNATE_FIEND_TRUE_SIGHT INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC CLERIC_EARTHQUAKE CLERIC_FIRE_STORM CLERIC_HEAL CLERIC_FLAME_STRIKE(3) CLERIC_UNHOLY_WORD WIZARD_NPC_SYMBOL_STUN}
END


/////////////////////////////////////////////////////////////////////////////////
//////////   Devas
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO deva
BEGIN
	LPM fiend_general_2e
	m.cre_strip_scripts{dw#sumei}
	m.cre_remove_spells{}
	m.cre_add_spells{INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC CLERIC_HEAL CLERIC_NEUTRALIZE_POISON(3) CLERIC_CURE_CRITICAL_WOUNDS(3) CLERIC_REMOVE_FEAR(3) CLERIC_REMOVE_CURSE(3) CLERIC_LESSER_RESTORATION(3)}
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Solars
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO solar
BEGIN
	LPM fiend_general_2e
	m_level_all=35
	m_resist_fire=100
	m_resist_magic_fire=100
	m_resist_acid=0
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_FIEND_TRUE_SIGHT INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC INNATE_SOLAR_VOCALIZE INNATE_SOLAR_HEAL INNATE_SOLAR_DEATH_SPELL WIZARD_CONFUSION(3) CLERIC_EARTHQUAKE(3) CLERIC_CREEPING_DOOM WIZARD_FINGER_OF_DEATH CLERIC_UNHOLY_WORD(3) INNATE_FIEND_IMPROVED_INVISIBILITY WIZARD_DOMINATION(3) CLERIC_FIRE_STORM WIZARD_PRISMATIC_SPRAY}
 END

/////////////////////////////////////////////////////////////////////////////////
//////////   Aurumach Rilmani
////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_MACRO aurumach
BEGIN
	LPM fiend_general_2e
	m_level_all=25
	m.fx.add{s_opcode=310 s_target=2 s_timing=9 s_parameter2=1} //	immunity to timestop
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_RILMANI_LAY_HANDS(3) WIZARD_SYMBOL_STUN WIZARD_SYMBOL_DEATH WIZARD_SYMBOL_FEAR INNATE_FIEND_TRUE_SIGHT INNATE_FIEND_TELEPORT INNATE_FIEND_DISPEL_MAGIC INNATE_FIEND_SLOW INNATE_FIEND_MASS_CHARM INNATE_SOLAR_DEATH_SPELL INNATE_FIEND_IMPROVED_INVISIBILITY INNATE_FIEND_MIRROR_IMAGE INNATE_FIEND_PRISMATIC_SPRAY}
END

/////////////////////////////////////////////////////////////////////////////////
//////////   Ferrumach Rilmani
////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_MACRO ferrumach
BEGIN
	LPM fiend_general_2e
	m_level_all=15
	m.fx.add{s_opcode=310 s_target=2 s_timing=9 s_parameter2=1} //	immunity to timestop
	m.cre_remove_spells{all}
	m.cre_add_spells{INNATE_FIEND_TELEPORT TANARI_SILENCE INNATE_FIEND_BLUR CLERIC_CURE_CRITICAL_WOUNDS WIZARD_BREACH(3)}
	m.cre_add_items{halb12|replace:i=1}
	m.item.alter{s_undroppable=1}
	m.immunity_spell{TANARI_SILENCE}
END


