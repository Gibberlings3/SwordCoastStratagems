//////////////////////////////////////////////////////////////////////////////////////
///	Succubus combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)


BEGIN_ACTION_DEFINITION
	Name(AttackVictim)
	TRIGGER
		ActionListEmpty()
		!GlobalTimerNotExpired("seekvictim","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("seekvictim","LOCALS",30)
		AttackReevaluate(scstarget,180)
END


INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

//////////////////////////////////////////////////////////////////////////////////////
///	Minor Spell Turning at start
//////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("spellturning","LOCALS")
	!AreaCheck("ar3004") // dead magic
THEN
	RESPONSE #100
		SetGlobalTimer("spellturning","LOCALS",1000)
		ApplySpell(Myself,WIZARD_MINOR_SPELL_TURNING)
		Continue()
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!GlobalTimerNotExpired("teleport","LOCALS")
	Target(WorstAC)
	TargetBlock(PCsInReverseOrder)
	TriggerBlock(Plus2Safe|Enemy|Helpless|LevelDrain|EasyPlus)
	!Range(scstarget,15)
	OR(3)
		CheckStatGT(scstarget,-6,ARMORCLASS)
		HPPercentLT(scstarget,30)
		Class(scstarget,MAGE_ALL)
	OR(3)
		!See(LastTargetedBy(Myself))
		!Range(LastTargetedBy(Myself),8)
		!HPPercentLT(LastTargetedBy(Myself),30)
	OR(3)
		!See(LastTargetedBy(Myself))
		!Range(LastTargetedBy(Myself),8)
		!Class(LastTargetedBy(Myself),MAGE_ALL)
THEN DO
	Combine()
	SetGlobalTimer("teleport","LOCALS",16)
	Action(Spell,INNATE_FIEND_TELEPORT|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Flee and heal if injured
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,50)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(EasyPlus)
THEN DO
	Action(SpellMyself,SUCCUBUS_ETHEREAL)
END

IF TRIGGER
	HPPercentLT(Myself,90)
	StateCheck(Myself,STATE_INVISIBLE)
	Range(NearestEnemyOf(Myself),20)
THEN DO
	Action(Literal)
	RunAwayFrom(NearestEnemyOf(Myself),60)
END

IF TRIGGER
	HPPercentLT(Myself,90)
	StateCheck(Myself,STATE_INVISIBLE)
THEN DO
	Action(SpellMyself,INNATE_FIEND_CURE_CRITICAL_WOUNDS)
END

IF
	StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

///////////////////////////////////////////////////////////////////////////////
////	Seduce weak-willed
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([PC.0.0.0.0.MALE])
	Target(SecondNearest([PC.0.0.0.0.MALE]))
	Target(ThirdNearest([PC.0.0.0.0.MALE]))
	Target(FourthNearest([PC.0.0.0.0.MALE]))
	TriggerBlock(Enemy|Disabled|Charm|MR)
THEN DO
	Action(SpellNoDec,SUCCUBUS_CHARM_MALE)
END

IF TRIGGER
	Target([PC.0.0.0.0.FEMALE])
	Target(SecondNearest([PC.0.0.0.0.FEMALE]))
	Target(ThirdNearest([PC.0.0.0.0.FEMALE]))
	Target(FourthNearest([PC.0.0.0.0.FEMALE]))
	TriggerBlock(Enemy|Disabled|Charm|MR)
THEN DO
	Action(SpellNoDec,SUCCUBUS_CHARM_FEMALE)
END

//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////
///	Basic succubus attack strategy:
///
///	(1) Look for someone close who's vulnerable to level drain
///	(2) If you can't find one, and you can find someone further away who's
///	vulnerable to level drain, spend 1 round trying to get to them. Don't try
///	this again for 5 rounds.
///	(3) Kill near enemies
///     (4) Finish off charmed creatures
///	(5) Segue to the standard Move and Chase scripts
///////////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////
////	(1)
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|Plus2Safe|LevelDrain)
	Range(scstarget,10)
THEN DO
	Action(AttackIfEmpty)
END


///////////////////////////////////////////////////////////////////////
////	(2a)
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        Global("victimroute","LOCALS",0)
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Disabled|Plus2Safe|LevelDrain)
THEN DO
	Action(AttackVictim)
END

///////////////////////////////////////////////////////////////////////
////	(3)
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        Global("secondary","LOCALS",0)
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Disabled|Plus2Safe)
	Range(scstarget,10)
THEN DO
	Action(AttackIfEmpty)
END

///////////////////////////////////////////////////////////////////////
////  (4) Finish off charmed creatures
///////////////////////////////////////////////////////////////////////

IF TRIGGER
   Allegiance(Myself,ENEMY)
   !See(NearestEnemyOf(Myself))
   TargetBlock(PlayersInRandomOrder)
THEN DO
     Action(Attack)
END




///////////////////////////////////////////////////////////////////////
////	(5)
///////////////////////////////////////////////////////////////////////

 //////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(MyWeaponStrength||2)
BEGIN LOOP(MyWeaponDamageType||Null)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
END LOOP
END LOOP



