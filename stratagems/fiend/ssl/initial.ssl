///////////////////////////////////////////////////////////////////////////////////////
///	Difficulty adjustments
///////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
   RequireBlock(ImprovedFiends)
   TriggerBlock(CorePlus)
   Global("innate_casting","LOCALS",0)
THEN DO
     Action(Literal)
     ApplySpellRES("dw#innat",Myself)
     SetGlobal("innate_casting","LOCALS",1)
     Continue()
END

IF TRIGGER
   RequireBlock(ImprovedFiends)
   TriggerBlock(EasyMinus)
   Global("innate_casting","LOCALS",1)
THEN DO
     Action(Literal)
     ApplySpellRES("dw#inrem",Myself)
     SetGlobal("innate_casting","LOCALS",0)
     Continue()
END

IF TRIGGER
   RequireBlock(ImprovedFiends)
   TriggerBlock(HardPlus)
   Global("staying_power","LOCALS",0)
THEN DO
     Action(Literal)
     ApplySpellRES("dw#fiehp",Myself)
     SetGlobal("staying_power","LOCALS",1)
     Continue()
END

IF TRIGGER
   RequireBlock(ImprovedFiends)
   TriggerBlock(CoreMinus)
   Global("staying_power","LOCALS",1)
THEN DO
     Action(Literal)
     ApplySpellRES("dw#hprem",Myself)
     SetGlobal("staying_power","LOCALS",0)
     Continue()
END


///////////////////////////////////////////////////////////////////////////////////////
///	Remove pre-summoning ring
///////////////////////////////////////////////////////////////////////////////////////

IF
	RequireBlock(IsSummoned)
        HasItem("dw#sumfi",Myself)
THEN
	RESPONSE #100
		DestroyItem("dw#sumfi")
		Continue()
END

//////////////////////////////////////////////////////////////////////////////////
////  Look for illusion-detecting rogues
//////////////////////////////////////////////////////////////////////////////////

 INCLUDE FILE(%scsroot%/lib/ssl/thief_detect_illusion.ssl)

//////////////////////////////////////////////////////////
////	When to attack the bad guys (borrowed from Weimer Tactics)
////
////
////////////////////////////////////////////////////////////

IF
    IgnoreBlock(IsSummoned)
    !Global("inafight","LOCALS",1)
    OR(6)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
THEN
  RESPONSE #100
    Enemy()
    SetGlobal("inafight","LOCALS",1)
	SetGlobalTimer("stayontarget","LOCALS",12)
	SetGlobalTimer("seedefences","LOCALS",6)
    Continue()
END

IF
	!Global("inafight","LOCALS",1)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("inafight","LOCALS",1)
		SetGlobalTimer("stayontarget","LOCALS",12)
		SetGlobalTimer("seedefences","LOCALS",6)
		Continue()
END

////////////////////////////////////////////////////////////
////	This is a rest-and-recover step for when
////	enemies leave you alone...
////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%\lib\ssl\common_rest.ssl)

///////////////////////////////////////////////////
////	If attacked by an unseen foe, mill around
////	in confusion. You never know, you might
////	bump into them!
//////////////////////////////////////////////////

IF
	Allegiance(Myself,ENEMY)
    OR(12)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
      SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
      SpellCastOnMe([PC],0)
      SpellCastOnMe([FAMILIAR],0)
      SpellCastOnMe([ALLY],0)
      SpellCastOnMe([CONTROLLED],0)
      SpellCastOnMe([CHARMED],0) 
!GlobalTimerNotExpired("whatthe","LOCALS")
  !See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    SetGlobalTimer("whatthe","LOCALS",12)
    Continue()
END

IF
  !See(NearestEnemyOf(Myself))
  GlobalTimerNotExpired("whatthe","LOCALS")
THEN
  RESPONSE #100
    RandomWalkContinuous()
END

/////////////////////////////////////////////////////
///	Cancel the "chase" move so you actually stop and shoot
/////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!Global("chasePC","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("chasePC","LOCALS",0)
		ClearActions(Myself)
END

/////////////////////////////////////////////////////
///	Return to summoner
/////////////////////////////////////////////////////////////


IF
  RequireBlock(ImprovedFiends)
  RequireBlock(IsSummoned)
  !See(NearestEnemyOf(Myself))
  !Range(LastSummonerOf(Myself),30)
  !StateCheck(LastSummonerOf(Myself),STATE_REALLY_DEAD)
  !StateCheck(Myself,STATE_REALLY_DEAD)
THEN
    RESPONSE #100
         ReallyForceSpell(LastSummonerOf(Myself),INNATE_FIEND_TELEPORT)
         Wait(2)
END

IF
  IgnoreBlock(ImprovedFiends)
  RequireBlock(IsSummoned)
  !See(NearestEnemyOf(Myself))
  !Range(LastSummonerOf(Myself),30)
  !StateCheck(LastSummonerOf(Myself),STATE_REALLY_DEAD)
  !StateCheck(Myself,STATE_REALLY_DEAD)
THEN
    RESPONSE #100
         ReallyForceSpell(LastSummonerOf(Myself),SUCCUBUS_TELEPORT)
         Wait(2)
END


IF
	!Allegiance(Myself,GOODBUTRED)
  !See(NearestEnemyOf(Myself))
  RequireBlock(IsSummoned)
  Range(LastSummonerOf(Myself),30)
  !Range(LastSummonerOf(Myself),10)
  !StateCheck(LastSummonerOf(Myself),STATE_REALLY_DEAD)
THEN
    RESPONSE #100
         MoveToObject(LastSummonerOf(Myself))
END

IF
  !See(NearestEnemyOf(Myself))
  RequireBlock(IsSummoned)  
THEN
    RESPONSE #100
         RandomWalk()
END

///////////////////////////////////////////////////////////////////////////////////////
///	Leave clouds
///////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scscloudmelee||!See([PC]))
BEGIN LOOP(scscloudresponse||RunAwayFromNoLeaveArea(LastHeardBy(Myself),30))
INCLUDE FILE(%scsroot%\genai\ssl\fleecloud.ssl)
END LOOP
END LOOP

BEGIN LOOP(scscloudmelee||See([PC])!Range([PC],8))
BEGIN LOOP(scscloudresponse||MoveToObject([PC]))
INCLUDE FILE(%scsroot%/genai/ssl/fleecloud.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/cloud_stop.ssl)
END LOOP
END LOOP

///////////////////////////////////////////////////////////
///	CPU saver
///////////////////////////////////////////////////////////

IF
	!Detect(NearestEnemyOf(Myself))
	!Gender(Myself,SUMMONED_DEMON)
	!Global("inafight","LOCALS",1)
	!Allegiance(Myself,GOODCUTOFF)
	ActionListEmpty()
THEN
	RESPONSE #100
		NoAction()
END
		

INCLUDE FILE(%scsroot%/genai/ssl/chase_top.ssl)