/*

Vanilla fallen solar:

- heal 1/5 rds
- vocalise at will
- free action at will (used to avoid web/entangle/slow - we'll change this to innate free action)
- truesight 1/10 rds - make this at-will, it's simpler
- death spell 1/5 rds
- confusion three times total
- earthquake three times total
- creeping doom once
- remove magic at will
- finger of death once
- fire storm once
- unholy word three times
- imprisonment once
- improved invisibility at will
- "nalmissra_charm" three times (save at -2; other minor effects) - for simplicity swap for domination
- prismatic spray once

Also casts startlingly fast, has permanent haste, immune to basically any movement-blocking attacks.
170-odd hp, regenerates at 40 hp/round (maybe faster given haste, not sure). Immune to all damage
types except fire; 50% resistance to physical damage; 85% magic resistance.

Immune to +3 weapons. Armed with a bow whose arrows have a dispel effect (and sometimes kill on 
contact) and a vorpal mace.


Ascension one replaces Nalmissra charm with Erinyes charm (save at -5)

We'll go with the Ascension version, but we'll replace Imprisonment (which is ridiculously lethal
if cast instantly) with a "Disjunction" power that strips all magic defences and is usable once per 4 rounds. We'll also allow teleportation.

We'll de-chunkify the mace (as with Balors), shift the resistances to be 50% vs electricity/acid
and 100% vs fire. We'll not bother with vocalise, on the grounds that probably we've got instacasting
installed.

*/

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)
VARIABLE(ImprovedFiends=True)
BEGIN_ACTION_DEFINITION
	Name(AttackRanged)
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		EquipRanged()
		Attack(scstarget)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Death agonies
//////////////////////////////////////////////////////////////////////////////////////

IF
	Die()
THEN
	RESPONSE #100
		CreateVisualEffectObject("SPUNHOLY",Myself)
		DestroySelf()
END

//////////////////////////////////////////////////////////////////////////////////////
///	Spawn rat
//////////////////////////////////////////////////////////////////////////////////////

IF
	!Global("spawnedrat","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("spawnedrat","LOCALS",1)
		CreateCreatureObject("dw#solra",Myself,0,0,0)
		Continue()
END


//////////////////////////////////////////////////////////////////////////////////////
///	Initialise
//////////////////////////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)


IF
	Detect(NearestEnemyOf(Myself))
	!Global("lostparty","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("lostparty","LOCALS",0)
		Continue()
END


//////////////////////////////////////////////////////////////////////////////////////
///	If injured, heal (we won't waste it on allies)
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,60)
THEN DO
	Action(SpellMyself,CLERIC_HEAL|100|50)
END

IF TRIGGER
	HPPercentLT(Myself,30)
THEN DO
	Action(SpellMyself,CLERIC_HEAL|100|50)
END

//////////////////////////////////////////////////////////////////////////////////////
///	If no truesights active, consider invisibility. 
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	TriggerBlock(TruesightSafe|CorePlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_IMPROVED_INVISIBILITY|100|100)
	SetGlobalTimer("invis","LOCALS",5)
END

IF 
	GlobalTimerNotExpired("invis","LOCALS")
	StateCheck(Myself,STATE_INVISIBLE)
THEN 
	RESPONSE #100
		RandomWalkContinuous()
END

//////////////////////////////////////////////////////////////////////
//	Consider running away
////////////////////////////////////////////////////////////////////

IF TRIGGER
	!GlobalTimerNotExpired("castspell","LOCALS")
	ActionListEmpty()
	TargetBlock(PCsInOrder)
	Range(scstarget,8)
	!Range("dw#solra",20)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Action(Literal|100|100)
	ForceSpell("dw#solra",INNATE_FIEND_TELEPORT)
	SetGlobalTimer("castspell","LOCALS",6)
END

////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/dispel.ssl)

//////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
///	High-powered magic
//////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////
///	Creeping Doom
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	TriggerBlock(MR100NotSR|SIConjuration|Enemy|Helpless|CorePlus)
THEN DO
	Action(Spell,CLERIC_CREEPING_DOOM|200|50)
END

/////////////////////////////////////////////////////////////////////////////
/////  Earthquake (may hurt allies too, but c'est la vie)
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See([PC])
	!GlobalTimerNotExpired("quake","LOCALS")
	TriggerBlock(CorePlus)
THEN DO
	Action(SpellMyself,CLERIC_EARTHQUAKE|100|100)
	SetGlobalTimer("quake","LOCALS",24)
END

/////////////////////////////////////////////////////////////////////////////
/////  Fire Storm
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(CorePlus)
THEN DO
	Combine()
	Action(Spell,CLERIC_FIRE_STORM|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
///	Attack defences
//////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////////
/////  Disjunction
///////////////////////////////////////////////////////////////////////////
// first priority: defence buffs

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SIAbjuration|Enemy|Helpless|PriorityBreach)
	TriggerBlock(EasyPlus)
	!GlobalTimerNotExpired("disjunction","LOCALS")
THEN DO
	Action(SpellRES,"dw#soldi"|200|100)
	SetGlobalTimer("disjunction","LOCALS",24)
END

// then antimagic

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellProtectionTarget|Enemy|Helpless)
	TriggerBlock(CorePlus)
	!GlobalTimerNotExpired("disjunction","LOCALS")
THEN DO
	Action(SpellRES,"dw#soldi"|200|100)
	SetGlobalTimer("disjunction","LOCALS",24)
END


/////////////////////////////////////////////////////////////////////////////
/////  Truesight
///////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(TruesightAlreadyCast|IllusionTarget|Enemy|SIDivination|EasyPlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_TRUE_SIGHT|150|150)
END

//////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
///	Lower-powered magic
//////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
////	KILL SUMMONS
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(Summons)
	CheckStatGT(scstarget,10,Level)
	OR(2)
		Gender(scstarget,SUMMONED)
		Gender(scstarget,20)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,INNATE_SOLAR_DEATH_SPELL|150|50)
END

///////////////////////////////////////////////////////////////////////
///	Unholy word
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	Range(scstarget,10)
	TriggerBlock(MR|Enemy|Helpless|SIConjuration)
	Alignment(scstarget,MASK_GOOD)
	!Alignment(Myself,MASK_GOOD)
THEN DO
	Combine()
	Action(Spell,CLERIC_UNHOLY_WORD|200|50)
END

///////////////////////////////////////////////////////////////////////////
////	Charm
///////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Helpless|MR|SpellDeflect|Charm)
	CheckStatGT(scstarget,0,SAVEVSSPELL)
THEN DO
	Action(Spell,WIZARD_DOMINATION|200|100)
END

///////////////////////////////////////////////////////////////////////
///	Prismatic spray
///////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled)
	Range(scstarget,20)
THEN DO
	Combine()
	Action(Spell,WIZARD_PRISMATIC_SPRAY|100|50)
END


///////////////////////////////////////////////////////////////////////////
////	Confusion
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|Confused|SIEnchantment|Enemy|Helpless|ConfuseProof)
THEN DO
	Combine()
	Action(Spell,WIZARD_CONFUSION|100|100)
END

/////////////////////////////////////////////////////////////////////////////
/////  Finger of Death
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SpellTurn|Slay|SINecromancy|Enemy)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Spell,WIZARD_FINGER_OF_DEATH|150|50)
END


///////////////////////////////////////////////////////////////////
///	Reset targetting
////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!Range(NearestEnemyOf(Myself),8)
	GlobalTimerExpired("stayontarget","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("stayontarget","LOCALS",4)
		ClearActions(Myself)
END



//////////////////////////////////////////////////////////////////////
//	Melee
////////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/genai/ssl/easy_ranged.ssl)
BEGIN LOOP(MyWeaponStrength||5)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
END LOOP
END LOOP

//////////////////////////////////////////////////////////////////////
////	Take down magical defences
//////////////////////////////////////////////////////////////////////

IF TRIGGER 
	TargetBlock(PCsInOrder)
	TargetBlock(Celestials)
	TriggerBlock(Enemy|Disabled|ArrowSafe|MR|NoInsects|BreachTarget)
	!CheckStatGT(scstarget,2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	Combine()
	Action(AttackRanged|100|50)
END

///////////////////////////////////////////////////////////////////////
////	Just shoot stuff
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(Celestials)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled|ArrowSafe)
	!CheckStatGT(scstarget,2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	Action(AttackRanged)
END

IF TRIGGER
	TargetBlock(PlayersInRandomOrder)
	TriggerBlock(ArrowSafe)
	!CheckStatGT(scstarget,2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	Action(AttackRanged)
END

IF
	!Detect(NearestEnemyOf(Myself))
	Global("lostparty","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("lostparty","LOCALS",1)
		SetGlobalTimer("losttimer","LOCALS",4)
END

IF 
	!Detect(NearestEnemyOf(Myself))
	Global("lostparty","LOCALS",1)
	GlobalTimerNotExpired("losttimer","LOCALS")
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	!Detect(NearestEnemyOf(Myself))
	GlobalGT("lostparty","LOCALS",0)
	!GlobalTimerNotExpired("losttimer","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("lostparty","LOCALS",2)
		MoveToObject(Player1)
END
