//////////////////////////////////////////////////////////////////////
////	Pit Fiend summoned script
////    For PC summons, and installs without Smarter Mages
////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)

IF
	Die()
THEN
	RESPONSE #100
		DestroySelf()
END

IF	
	!StateCheck(Myself,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		ApplySpellRES("dw#silin",Myself)
END

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||CHAOTIC_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

////////////////////////////////////////////////////////////////////////////
///	Begin fight with fear aura
////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("fearaura","LOCALS")
        !GlobalTimerNotExpired("DMWWTimeStopRunning","GLOBAL")
THEN 
	RESPONSE #100
		SetGlobalTimer("fearaura","LOCALS",6)
		ReallyForceSpell(Myself,DEMON_FEAR)
		Continue()
END

//////////////////////////////////////////////////////////////////////////
///	Stay Improved Invisible where possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TriggerBlock(TruesightSafe|CorePlus)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN DO	
	Action(SpellMyself,INNATE_FIEND_IMPROVED_INVISIBILITY|200|50)
END

/////////////////////////////////////////////////////////////////////////////////
///	Ultramagic
/////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([ANYONE])
	TriggerBlock(Enemy|Disabled|MR100|SIEvocation|ResistFire|CorePlus)
THEN DO
	Action(Spell,WIZARD_METEOR_SWARM|100|100)
END

IF TRIGGER
	Target([ANYONE])
	Alignment(scstarget,MASK_GOOD)
	Range(scstarget,10)
	TriggerBlock(EasyPlus)
THEN DO
	Action(SpellMyself,CLERIC_UNHOLY_WORD|100|100)
END

///////////////////////////////////////////////////////////////////////
///	Fireball
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([ANYONE])
	Target(SecondNearest([ANYONE]))
	TriggerBlock(Enemy|Disabled|MR100|SIEvocation|ResistFire)
THEN DO
	Combine()
	Action(Spell,INNATE_FIEND_FIREBALL|100|100)
END


///////////////////////////////
////	Symbol
///////////////////////////////

IF TRIGGER
	Target([ANYONE])
	Target(SecondNearest([ANYONE]))
	TriggerBlock(MR|SIConjuration|Enemy|Helpless)
THEN DO
	Action(Spell,CLERIC_SYMBOL_PAIN|100|100)
END

IF TRIGGER
	Target([ANYONE])
	Target(SecondNearest([ANYONE]))
	TriggerBlock(Enemy|Disabled|MR|Panic|SIConjuration|EasyPlus)
THEN DO
	Combine()
	Action(SpellNoDec,WIZARD_SYMBOL_FEAR)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////

IF
	See([ANYONE])
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END
