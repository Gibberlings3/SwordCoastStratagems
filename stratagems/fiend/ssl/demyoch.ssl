//////////////////////////////////////////////////////////////////////////////////////
///	Yochlol combat script
//////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////
///	Powers:
///     Improved Invisibility, at will
///     Domination, at will
///     Spider Spawn, once per 3 rounds
///     Teleport and Remove Magic, as per usual
///     Heal, 3/day
///     Flame Strike, once per 5 rounds
//////////////////////////////////////////////////////////////////////////////////////


INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////////////////
///	Keep improved invisibility running
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	TriggerBlock(TruesightSafe|CorePlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_IMPROVED_INVISIBILITY|100|100)
END

/////////////////////////////////////////////////////////////////////////
///    Healing Touch
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
          HPPercentLT(Myself,40)
THEN DO
          Action(SpellMyself,CLERIC_HEAL)
END

IF TRIGGER
          TargetBlock(VillainsInOrder)
          Range(scstarget,15)
          HPPercentLT(scstarget,50)
THEN DO
     Action(Spell,CLERIC_HEAL|100|100)
END

IF TRIGGER
          TargetBlock(VillainsInOrder)
          !Range(scstarget,15)
          HPPercentLT(scstarget,50)
          HaveSpell(CLERIC_HEAL)
THEN DO
     Action(Spell,INNATE_FIEND_TELEPORT|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/dispel.ssl)

////////////////////////////////////////////////////////////
////	Spiders
//////////////////////////////////////////////////////////////

IF TRIGGER
   TargetBlock(PCsPreferringWeak)
   TriggerBlock(Enemy|CorePlus)
   !NumCreatureGT([ENEMY.0.SPIDER],5)
THEN DO
     Combine()
     Action(Spell,INNATE_YOCHLOL_SPIDER_SPAWN|100|100)
END

////////////////////////////////////////////////////////////
////	Flame Strike
//////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Disabled|MR|ResistFire|Enemy|SIEvocation|EasyPlus)
THEN DO
	Combine()
	Action(Spell,INNATE_YOCHLOL_FLAME_STRIKE|80|20)
END


/////////////////////////////////////////////////////////////////////////
//	Domination
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Charm|MR|SIEnchantment|SpellDeflect|Enemy|Helpless|EasyPlus)
THEN DO
	Action(Spell,INNATE_FIEND_DOMINATION|100|100)
END

/////////////////////////////////////////////////////////////////////////
//	Burnthru
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(Charm|MR|SIEnchantment|Enemy|Helpless|EasyPlus)
THEN DO
	Action(Spell,INNATE_FIEND_DOMINATION|100|100)
END

//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on stunned victims
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(NearestEnemyOfType([PC]))
	StateCheck(scstarget,STATE_STUNNED)
	TriggerBlock(Enemy|Plus3Safe|SlashingSafe)
THEN DO
	Action(Attack|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////



BEGIN LOOP(MyWeaponStrength||3)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
