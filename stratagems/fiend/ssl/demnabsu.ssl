//////////////////////////////////////////////////////////////////////////////////////
///	Summons script - used for PC-summoned fiend, and for NPC-summoned
///     fiends if Smarter Mages is not installed
//////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
///	Work around death animation bug
//////////////////////////////////////////////////////////////////////////////////////////

IF
        IgnoreBlock(EnhancedEdition)
	Die()
THEN
	RESPONSE #100
		DestroySelf()
END


//////////////////////////////////////////////////////////////////////////////////////////
///	Redefine spellcasting to include death gazes
//////////////////////////////////////////////////////////////////////////////////////////
INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)


BEGIN_ACTION_DEFINITION
	Name(Spell)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
		ForceSpell([ANYONE],TANARI_DEATH_GAZE)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
		ForceSpell(SecondNearest([ANYONE]),TANARI_DEATH_GAZE)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
		ForceSpell(ThirdNearest([ANYONE]),TANARI_DEATH_GAZE)
END


//////////////////////////////////////////////////////////////////////////////////////
///	Nabassu combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////////////////
///	Silence mages and clerics (we trade the at-will PnP version for the -5-to-saves
///	BG2 version)
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([0.0.0.MAGE_ALL])
	Target([0.0.0.CLERIC_ALL])
	Target([0.0.0.DRUID_ALL])
	Target([0.0.0.BARD])
	TriggerBlock(Disabled|Silence|EasyPlus)
	!GlobalTimerNotExpired("silence","LOCALS")
THEN DO
	Combine()
	SetGlobalTimer("silence","LOCALS",1000)
	Action(Spell,TANARI_SILENCE|200|300)
END

//////////////////////////////////////////////////////////////////////////////////////////
///	Paralysis
//////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([ANYONE])
THEN DO
	Combine()
	Action(Spell,TANARI_PARALYZE|100|300)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Vampiric touch
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(EnemiesInOrder;HPPercentLT(scstarget,30))
	TargetBlock(EnemiesInOrder)
	Target([ANYONE])
	Range(scstarget,10)
THEN DO
	Combine()
	Action(Spell,TANARI_VAMPIRIC_TOUCH)
END

//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on paralysed victims
///////////////////////////////////////////////////////////////////////


IF
	See([ANYONE])
	StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
	RESPONSE #100
	         Continue()
END

///////////////////////////////////////////////////////////////////////
////	Kill anything in sight
///////////////////////////////////////////////////////////////////////


IF
	See([ANYONE])
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END


