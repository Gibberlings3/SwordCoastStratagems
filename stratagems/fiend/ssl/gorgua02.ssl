////////////////////////////////////////////////////////////////////
///
///   Ferrumach Rilmani
///
///   Inspired by the version in Planescape MC2 (of which the vanilla
///   rilmani is a surprisingly faithful realisation)
///
///   For simplicity, we'll give the Rilmani most of its spells 9/day
///   rather than as innate repeating spells. It gets:
///
///   At will: blur, teleport, breach (1/3 rds)
///
///   1/day: silence, Cure Critical wounds
///
///
///   Special: immunity to time stop
////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////
///    Death throes
/////////////////////////////////////////////////////////////////////////

IF
	Die()
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,ILLUSION_DEATH) // SPIN835.SPL (No such index)
		Wait(1)
		DestroySelf()
END

/////////////////////////////////////////////////////////////////////////
///    Healing Touch
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
          HPPercentLT(Myself,60)
THEN DO
          Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)
END

IF TRIGGER
          TargetBlock(VillainsInOrder)
          Range(scstarget,15)
          HPPercentLT(scstarget,50)
THEN DO
     Action(Spell,CLERIC_CURE_CRITICAL_WOUNDS|100|100)
END

/////////////////////////////////////////////////////////////////////////////////////
///	Blur
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_BLUR)
	TriggerBlock(TruesightSafe|CorePlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_BLUR|100|100)
END
//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/jump.ssl)

///////////////////////////////////////////////////////////////////////////////////////
///	Breach enemy defenses
///////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        !GlobalTimerNotExpired("breach","LOCALS")
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
	TriggerBlock(PriorityBreach)
	TriggerBlock(EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_BREACH|150|150)
	SetGlobalTimer("breach","LOCALS",12)
END

IF TRIGGER
        !GlobalTimerNotExpired("breach","LOCALS")
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
	TriggerBlock(BreachTarget|AvoidDispel)
	TriggerBlock(EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_BREACH|150|150)
	SetGlobalTimer("breach","LOCALS",12)
 END
 
 //////////////////////////////////////////////////////////////////////////////////////
///	Silence mages and clerics (we trade the at-will PnP version for the -5-to-saves
///	BG2 version)
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Disabled|Silence)
THEN DO
	Combine()
	Action(Spell,TANARI_SILENCE|200|300)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||3)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%scsroot%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
