VARIABLE(IsSummoned=True)

//////////////////////////////////////////////////////////////////////////////////////
///	Summons script - used for non-summoner-aligned fiend
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

//////////////////////////////////////////////////////////////////////////////////////
///	Renew its mirror image
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See([ANYONE])
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	TriggerBlock(TruesightSafe|CorePlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_MIRROR_IMAGE|100|100)
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

/// INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/jump.ssl)  excluded for summoned demon

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

/// INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/dispel.ssl) excluded for summoned demon (not least because dispelling the Pro/Evil is cheating)

//////////////////////////////////////////////////////////////////////////////////////
///	Use offensive magic
//////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
////  Power Word: Stun
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([0.0.0.MAGE_ALL])
	Target([0.0.0.CLERIC_ALL])
	Target([0.0.0.BARD])
	Target([0.0.0.DRUID_ALL])
	Target([0.0.0.THIEF_ALL])
	Target([0.0.0.FIGHTER_ALL])
	TriggerBlock(MR|SpellTurn|Stun|Helpless|SIConjuration|SRPowerWord)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|100|100)
END

////////////////////////////////////////////////////////////////////////////////////
///	Unholy Blight
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([ANYONE])
	TriggerBlock(SINecromancy|ResistMagicDamage|EasyPlus)
	Alignment(scstarget,MASK_GOOD)
THEN DO
	Action(Spell,INNATE_FIEND_UNHOLY_BLIGHT|200|100)
END

///////////////////////////////////////////////////////////////////////////
////	Confusion - skipped as I'm not sure who it targets
///////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on stunned victims
///////////////////////////////////////////////////////////////////////


IF
	See([ANYONE])
	StateCheck(LastSeenBy(Myself),STATE_STUNNED)
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
	RESPONSE #100
	         Continue()
END

///////////////////////////////////////////////////////////////////////
////	Kill anything in sight
///////////////////////////////////////////////////////////////////////


IF
	See([ANYONE])
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END

