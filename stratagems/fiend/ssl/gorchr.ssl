//////////////////////////////////////////////////////////////////////////////////////
///	Chromatic demon combat script
//////////////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////
///	Shapeshift
//////////////////////////////////////////////////////////////////////////////////////

IF
	!GlobalTimerNotExpired("shapeshift","LOCALS")
	!GlobalTimerNotExpired("castspell","LOCALS")
THEN
	RESPONSE #250
		SetGlobalTimer("shapeshift","LOCALS",6)
		ReallyForceSpell(Myself,CHROMATIC_DEMON_RED)
		SetGlobal("shape","LOCALS",1)
		Polymorph(TANARRI)
	RESPONSE #250
		SetGlobalTimer("shapeshift","LOCALS",6)
		ReallyForceSpell(Myself,CHROMATIC_DEMON_YELLOW)
		SetGlobal("shape","LOCALS",2)
		Polymorph(ELEMENTAL_AIR)
	RESPONSE #250
		SetGlobalTimer("shapeshift","LOCALS",6)
		ReallyForceSpell(Myself,CHROMATIC_DEMON_WHITE)
		SetGlobal("shape","LOCALS",3)
		Polymorph(%ice_golem_animation%)
	RESPONSE #250
		SetGlobalTimer("shapeshift","LOCALS",6)
		ReallyForceSpell(Myself,CHROMATIC_DEMON_GREEN)
		SetGlobal("shape","LOCALS",4)
		Polymorph(SHAMBLING_MOUND)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Vanilla stuff
//////////////////////////////////////////////////////////////////////////////////////

IF
	NumTimesTalkedTo(0)
	See([PC])
THEN
	RESPONSE #100
		StartDialogueNoSet([PC])
END

IF
	Global("ScepterKeys","GLOBAL",1)
	Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		StartDialogueNoSet([PC])
END

IF
	Global("ScepterKeys","GLOBAL",0)
THEN
	RESPONSE #100
		NoAction()
END


INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.



//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/dispel.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Use offensive magic
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Global("shape","LOCALS",1)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|ResistFire|SIEvocation|MinorGlobe|Enemy)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(SpellNoDec,DEATHKNIGHT_FIREBALL)
END

IF
	Global("shape","LOCALS",2)
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("castspell","LOCALS")
THEN
	RESPONSE #1000
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpellRES("dw#airbu",NearestEnemyOf(Myself))
	RESPONSE #1000
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpell(NearestEnemyOf(Myself),WIZARD_CHAIN_LIGHTNING)
END

IF TRIGGER
	Global("shape","LOCALS",4)
	TargetBlock(EnemiesInOrder)
	Range(scstarget,5)
	TriggerBlock(MR100|ResistAcid|Enemy)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(SpellNoDecRES,"dw#chrsl")
END

IF TRIGGER
	Global("shape","LOCALS",3)
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|MR100|ResistCold|SIEvocation)
THEN DO
	Action(SpellNoDecRES,"dw#chrco")
END




//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////



BEGIN LOOP(MyWeaponStrength||3)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
