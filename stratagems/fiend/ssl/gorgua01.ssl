////////////////////////////////////////////////////////////////////
///
///   Aurumach Rilmani
///
///   Inspired by the version in Planescape MC2 (of which the vanilla
///   rilmani is a surprisingly faithful realisation)
///
///
///   At will: mirror image, slow, prismatic spray, improved invisibility,
///   death spell, mass charm, true sight, dispel magic
///
///   1/day: each symbol
///   
///   3/day: lay on hands, combining Regenerate, Heal and Restoration
///
///   Special: a time stop power, lasting 1 round, through which the Rilmani
///   can attack. This gets used:
///   - every 2 rounds on Insane
///   - every 3 rounds on Hardcore
///   - every 4 rounds on Tactical
///   - never below Tactical
////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

BEGIN_ACTION_DEFINITION
       Name(RilmaniTimeStop)
       TRIGGER
		Global("initialise_timestop","LOCALS",1)
                !GlobalTimerNotExpired("castspell","LOCALS")
                !GlobalTimerNotExpired("timestop","LOCALS")
                !CheckSpellState(Myself,TIME_STOP)
                See(NearestEnemyOf(Myself))
       ACTION
                RESPONSE #scsprob1
                SetGlobalTimer("timestop","LOCALS",scsargument1)
		SetGlobalTimer("castspell","LOCALS",6)
                ReallyForceSpellRES("dw#rilts",Myself)
END

/////////////////////////////////////////////////////////////////////////
///    Death throes
/////////////////////////////////////////////////////////////////////////

IF
	Die()
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,ILLUSION_DEATH) // SPIN835.SPL (No such index)
		Wait(1)
		DestroySelf()
END

/////////////////////////////////////////////////////////////////////////
///    Timestop power
/////////////////////////////////////////////////////////////////////////

IF
  Global("initialise_timestop","LOCALS",0)
THEN
    RESPONSE #100
             SetGlobal("initialise_timestop","LOCALS",1)
             SetGlobalTimer("timestop","LOCALS",6)
             Continue()
END

IF TRIGGER
   TriggerBlock(Insane)
THEN DO
   Action(RilmaniTimeStop,12)
END


IF TRIGGER
   TriggerBlock(Hard)
THEN DO
   Action(RilmaniTimeStop,18)
END


IF TRIGGER
   TriggerBlock(Core)
THEN DO
   Action(RilmaniTimeStop,24)
END

/////////////////////////////////////////////////////////////////////////
///    Renew MI and invisibility
/////////////////////////////////////////////////////////////////////////


IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	TriggerBlock(TruesightSafe|CorePlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_MIRROR_IMAGE|100|100)
END

IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	TriggerBlock(TruesightSafe|CorePlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_IMPROVED_INVISIBILITY|100|100)
END

/////////////////////////////////////////////////////////////////////////
///    Healing Touch
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
          HPPercentLT(Myself,40)
THEN DO
          Action(SpellMyself,INNATE_RILMANI_LAY_HANDS)
END

IF TRIGGER
          TargetBlock(VillainsInOrder)
          Range(scstarget,15)
          HPPercentLT(scstarget,50)
THEN DO
     Action(Spell,INNATE_RILMANI_LAY_HANDS|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Truesight
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	Gender(scstarget,ILLUSIONARY)
	!Allegiance(scstarget,ENEMY)
	TriggerBlock(TruesightAlreadyCast|Enemy|SIDivination|EasyPlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_TRUE_SIGHT)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/dispel.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Kill summons
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(Summons)
	OR(2)
		Gender(scstarget,SUMMONED)
		Gender(scstarget,20)
	TriggerBlock(Enemy|Helpless|EasyPlus)
THEN DO
	Combine()
	Action(Spell,INNATE_SOLAR_DEATH_SPELL|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Offensive magic
//////////////////////////////////////////////////////////////////////////////////////

/////////////////////////////
///   Symbol: Stun
//////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Stun|MR100|SIConjuration|Helpless|AoEHelpless|EasyPlus)
	/*slowspell*/
THEN DO
	Action(Spell,WIZARD_SYMBOL_STUN|100|300)
END

/////////////////////////////
///   Symbol: Fear
//////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Panic|MR100|SIConjuration|Enemy|Helpless|AoEHelpless|EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_SYMBOL_FEAR|100|200)
END

///////////////////////////////
////	Symbol: Death
///////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Slay|MR|SIConjuration|AoE|EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_SYMBOL_DEATH|100|200)
END

///////////////////////////////////////////////////
///   Prismatic Spray
///////////////////////////////////////////////////

IF TRIGGER
   IgnoreBlock(Demivrgvs)
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   TriggerBlock(MR|SIConjuration|Helpless|EasyPlus)
THEN DO
   Action(Spell,WIZARD_PRISMATIC_SPRAY|200|100)
END


IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   RequireBlock(Demivrgvs)
   TriggerBlock(MR|SIAlteration|Helpless|EasyPlus)
THEN DO
   Action(Spell,WIZARD_PRISMATIC_SPRAY|200|100)
END

///////////////////////////////////////////////////
///   Mass charm
///////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Charm|MR100|MR|Helpless|Enemy|SpellTurn|AoE|EasyPlus)
	CheckStatGT(scstarget,0,SAVEVSSPELL)
THEN DO
	Combine()
        Action(Spell,INNATE_FIEND_MASS_CHARM|200|100)
END

///////////////////////////////////////////////////
///   Slow
///////////////////////////////////////////////////


IF TRIGGER
	IgnoreBlock(Demivrgvs)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR100|Slow|Enemy|MinorGlobe|AoEHelpless|EasyPlus)
	!CheckStatLT(scstarget,0,SAVEVSSPELL)
	!StateCheck(scstarget,STATE_SLOWED)
THEN DO
	Combine()
	Action(Spell,INNATE_FIEND_SLOW|150|50)
END

IF TRIGGER
	RequireBlock(Demivrgvs)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR100|Slow|Enemy|MinorGlobe|AoEHelpless|EasyPlus)
	!CheckStatLT(scstarget,0,SAVEVSPOLY)
	!StateCheck(scstarget,STATE_SLOWED)
THEN DO
	Combine()
	Action(Spell,INNATE_FIEND_SLOW|150|50)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
