//////////////////////////////////////////////////////////////////////////////////////
///	Demon Knight combat script - variant attack-all-comers version for Demivrgvs
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

//////////////////////////////////////////////////////////////////////////////////////
////	Dispel Magic - EXCLUDED for summons
//////////////////////////////////////////////////////////////////////////////////////


/////////////////////////////
///   Symbol: Fear
//////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Panic|MR100|SIConjuration|Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_NPC_SYMBOL_FEAR|100|200)
END

///////////////////////////////
////	Symbol: Death
///////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Slay|MR|SIConjuration)
THEN DO
	Combine()
	Action(Spell,WIZARD_NPC_SYMBOL_DEATH|100|200)
END

////////////////////////////////////////////
///	Fireball
////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!GlobalTimerNotExpired("fireball","LOCALS")
	TriggerBlock(MR100|ResistFire|SIEvocation|MinorGlobe|Enemy)
	!HasItem("clck26",scstarget)
THEN DO
	SetGlobalTimer("fireball","LOCALS",1000)
	Action(SpellNoDec,DEATHKNIGHT_FIREBALL|100|100)
END

/////////////////////////////////////////////////////////////////////////
///// Power Word: Kill
////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(EnemiesInOrder;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	OR(3)
		HPPercentLT(scstarget,50)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	TriggerBlock(MR100|SpellTurn|Slay|SIConjuration|Enemy)
	!HasItem("clck26",scstarget)
THEN DO
	Combine()
	Action(Spell,WIZARD_POWER_WORD_KILL|100|100)
END


////////////////////////////////////////
////	Power Word: Blind
///////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(MR|SIConjuration)
THEN DO
	Combine()
	Action(Spell,WIZARD_POWER_WORD_BLIND|100|100)
END

////////////////////////////////////////////////////////////////////////
////  Power Word: Stun
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(EnemiesInOrder;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SpellTurn|Stun|Helpless|SIConjuration)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||Unknown)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
