//////////////////////////////////////////////////////////////////////
////	Pit Fiend combat script
////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)

IF
	Die()
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,RED_HOLY_MIGHT)
		DestroySelf()
END

IF	
	!StateCheck(Myself,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		ApplySpellRES("dw#silin",Myself)
END

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Tanar'ri
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||CHAOTIC_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

////////////////////////////////////////////////////////////////////////////
///	Begin fight with fear aura
////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("fearaura","LOCALS")
        Global("FIN_TIME_STOP_RUNNING","GLOBAL",0)
THEN 
	RESPONSE #100
		SetGlobalTimer("fearaura","LOCALS",6)
		ReallyForceSpell(Myself,DEMON_FEAR)
		Continue()
END

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/dispel.ssl)

//////////////////////////////////////////////////////////////////////////
///	Stay Improved Invisible where possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TriggerBlock(TruesightSafe|CorePlus)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN DO	
	Action(SpellMyself,WIZARD_IMPROVED_INVISIBILITY|100|100)
END

/////////////////////////////////////////////////////////////////////////////////
///	Ultramagic
/////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsSummoned)
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled|MR100NotSR|SIEvocation|ResistFire|CorePlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_METEOR_SWARM|200|100)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	Alignment(scstarget,MASK_GOOD)
	Range(scstarget,10)
	TriggerBlock(EasyPlus)
THEN DO
	Combine()
	Action(SpellMyself,CLERIC_UNHOLY_WORD|200|100)
END

///////////////////////////////////////////////////////////////////////
///	Fireball
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsSummoned)
	TargetBlock(DistantEnemies)
	TriggerBlock(Enemy|Disabled|MR100|SIEvocation|ResistFire|AoEFireball)
THEN DO
	Combine()
	Action(Spell,INNATE_FIEND_FIREBALL|200|100)
END

///////////////////////////////
////	Symbol
///////////////////////////////

IF TRIGGER
        RequireBlock(IsIWDDivine)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(AoEHelpless|MR|SIConjuration|Enemy|Helpless)
THEN DO
	Action(Spell,CLERIC_SYMBOL_PAIN|100|100)
END

IF TRIGGER
        IgnoreBlock(IsIWDDivine)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(AoEHelpless|MR|Panic|SIConjuration|EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_SYMBOL_FEAR)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////



BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Piercing)
INCLUDE FILE(%scsroot%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
