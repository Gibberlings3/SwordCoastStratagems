//////////////////////////////////////////////////////////////////////////////////////////
///	Work around death animation bug
//////////////////////////////////////////////////////////////////////////////////////////

IF
	Die()
		OR(2)
			AreaCheck("ar6200")
			AreaCheck("ar2900")
THEN
	RESPONSE #100
		ReallyForceSpellDeadRES("baldead",Myself)
END

IF
	Die()
	IgnoreBlock(EnhancedEdition)
THEN
	RESPONSE #100
		DestroySelf()
END


//////////////////////////////////////////////////////////////////////////////////////
///	Balor combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

//////////////////////////////////////////////////////////////////////////////////////
///	Begin stoneskinned & with fire aura
//////////////////////////////////////////////////////////////////////////////////////

IF
  !Global("firewall","LOCALS",1)
THEN
    RESPONSE #100
           SetGlobal("firewall","LOCALS",1)
           ApplySpellRES("finbalor",Myself) // Aura of Flaming Death
           Continue()
END

IF
	See(NearestEnemyOf(Myself))
	!AreaCheck("ar3004") // dead magic
	!GlobalTimerNotExpired("initialbuff","LOCALS")
THEN
	RESPONSE #100
	        SetGlobalTimer("initialbuff","LOCALS",1000)
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		Continue()
END

IF TRIGGER
	See(NearestEnemyOf(Myself))
	GlobalTimerNotExpired("initialbuff","LOCALS")
	TriggerBlock(StoneskinFailing)
	TriggerBlock(CorePlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_STONE_SKIN)
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////////////////////
///	Wail (demon lord only)
//////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        !GlobalTimerNotExpired("wails","LOCALS")
        RequireBlock(IsDemonLord)
	TargetBlock(PCsInOrder)
	Range(scstarget,10)
	TriggerBlock(Slay|MR100|Enemy|Helpless|SINecromancy|DirectDamageSafe)
THEN DO
	Action(SpellMyself,WIZARD_WAIL_OF_THE_BANSHEE|150|50)
	SetGlobalTimer("wails","LOCALS",18)
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Fire Storm will usually be first action, but we don't use it for the Suldanesselar
///	Balor that's chewing on elves, or for Irenicus's flunkies, or when summoned,
///     and the Lesser Demon Lord uses WotB instead
/////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsSummoned)
        IgnoreBlock(IsDemonLord)
	TargetBlock(PCsInOrder)
	TriggerBlock(CorePlus)
	!AreaCheck("AR2800") // Suldanesselar
	!AreaCheck("AR2900") // Hell
THEN DO
	Action(Spell,CLERIC_FIRE_STORM)
END


//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////
VARIABLE(FirstDispel=True)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/dispel.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Truesight (demon lord only)
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(IsDemonLord)
	TargetBlock(EnemiesInOrder)
	Gender(scstarget,ILLUSIONARY)
	!Allegiance(scstarget,ENEMY)
	TriggerBlock(TruesightAlreadyCast|Enemy|SIDivination|EasyPlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_TRUE_SIGHT)
END

///////////////////////////////////////////////////////////////////////////////////////
///	Breach enemy defenses
///////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        !GlobalTimerNotExpired("breach","LOCALS")
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
	TriggerBlock(PriorityBreach)
	TriggerBlock(EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_BREACH|150|150)
	SetGlobalTimer("breach","LOCALS",12)
END

IF TRIGGER
        !GlobalTimerNotExpired("breach","LOCALS")
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
	TriggerBlock(BreachTarget|AvoidDispel)
	TriggerBlock(EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_BREACH|150|150)
	SetGlobalTimer("breach","LOCALS",12)
END
//////////////////////////////////////////////////////////////////////////////////////////
///	Blow someone up (ToB: Hard+; SoA: Insane)
//////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsDemonLord)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Insane)
	!GlobalGT("chapter","GLOBAL",%bg2_chapter_7%)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
THEN DO
	Action(SpellRES,"%CLERIC_IMPLOSION%"|100|100)
END

IF TRIGGER
        IgnoreBlock(IsDemonLord)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(HardPlus)
	GlobalGT("chapter","GLOBAL",%bg2_chapter_7%)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(SpellTurn|Enemy|Helpless)
THEN DO
	Action(SpellRES,"%CLERIC_IMPLOSION%"|100|100)
END

IF TRIGGER
        RequireBlock(IsDemonLord)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(CorePlus)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(SpellTurn|Enemy|Helpless)
THEN DO
	Action(SpellRES,"dw#abyfs"|100|100)
END



//////////////////////////////////////////////////////////////////////////////////////////
///	Telekinesis - get rid of nearby warrior types
//////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled)
	TriggerBlock(CorePlus)
	OR(4)
		Class(scstarget,FIGHTER_ALL)
		Class(scstarget,PALADIN_ALL)
		Class(scstarget,RANGER_ALL)
		Class(scstarget,MONK)
	CheckStatGT(scstarget,-10,SAVEVSSPELL)
	Range(scstarget,5)
THEN DO
	Combine()
	Action(SpellNoDecRES,"dw#batel"|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////////
///	Telekinesis - pull in spellcasters
//////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled)
	TriggerBlock(CorePlus)
	OR(5)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
		Class(scstarget,CLERIC)
		Class(scstarget,CLERIC_MAGE)
		Class(scstarget,SORCERER)
	CheckStatGT(scstarget,-10,SAVEVSSPELL)
	!Range(scstarget,15)
THEN DO
	Combine()
	Action(SpellNoDecRES,"dw#bate2"|100|200)
END


//////////////////////////////////////////////////////////////////////////////////////
///	Use offensive magic
//////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////
///   Power Word: Kill (demon lord only)
//////////////////////////////

IF TRIGGER
        RequireBlock(IsDemonLord)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	OR(2)
		HPPercentLT(scstarget,75)
		!CheckStatGT(scstarget,12,Level)
	OR(3)
		HPPercentLT(scstarget,50)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	TriggerBlock(MR100|SpellTurn|Slay|SIConjuration|Enemy|SRPowerWord)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Combine()
	Action(Spell,WIZARD_POWER_WORD_KILL|150|50)
END

//////////////////////////////
///   Symbol: Stun
//////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(EasyPlus)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(Stun|MR|SIConjuration|Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_NPC_SYMBOL_STUN|100|100)
END

///////////////////////////////
////	Symbol: Death
///////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(EasyPlus)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(Slay|MR|SIConjuration)
THEN DO
	Combine()
	Action(Spell,WIZARD_NPC_SYMBOL_DEATH|100|100)
END

//////////////////////////////////////////////////////////////////////////
////	Domination
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR|Charm|SIEnchantment|Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,INNATE_FIEND_DOMINATION)
END


//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on stunned victims
///////////////////////////////////////////////////////////////////////

IF
	RequireBlock(IsDemonLord)
	Global("DemonHatesAll","GLOBAL",1)
        See([ANYONE])
	StateCheck(LastSeenBy(Myself),STATE_STUNNED)
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
	RESPONSE #100
	         Continue()
END

IF TRIGGER
	Target(NearestEnemyOfType([PC]))
	StateCheck(scstarget,STATE_STUNNED)
	TriggerBlock(Enemy|Plus5Safe)
THEN DO
	Action(Attack|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////

IF
        RequireBlock(IsDemonLord)
	Global("DemonHatesAll","GLOBAL",1)
        See([ANYONE])
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END


BEGIN LOOP(MyWeaponStrength||5)
BEGIN LOOP(MyWeaponDamageType||MultipleDamageType)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
