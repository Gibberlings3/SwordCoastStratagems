//////////////////////////////////////////////////////////////////////////////////////////
///	Work around death animation bug
//////////////////////////////////////////////////////////////////////////////////////////

IF
	Die()
		OR(2)
			AreaCheck("ar6200")
			AreaCheck("ar2900")
THEN
	RESPONSE #100
		ReallyForceSpellDeadRES("baldead",Myself)
END

IF
	Die()
	IgnoreBlock(EnhancedEdition)
THEN
	RESPONSE #100
		DestroySelf()
END


//////////////////////////////////////////////////////////////////////////////////////
///	Balor combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

//////////////////////////////////////////////////////////////////////////////////////
///	Begin stoneskinned
//////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!AreaCheck("ar3004") // dead magic
	!GlobalTimerNotExpired("initialbuff","LOCALS")
THEN
	RESPONSE #100
	        SetGlobalTimer("initialbuff","LOCALS",1000)
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		Continue()
END

IF TRIGGER
	See(NearestEnemyOf(Myself))
	GlobalTimerNotExpired("initialbuff","LOCALS")
	TriggerBlock(StoneskinFailing)
	TriggerBlock(CorePlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_STONE_SKIN)
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

/////////////////////////////////////////////////////////////////////////////////////////
///	Fire Storm will usually be first action, but we don't use it for the Suldanesselar
///	Balor that's chewing on elves, or for Irenicus's flunkies, or when summoned
/////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsSummoned)
	TargetBlock(PCsInOrder)
	TriggerBlock(CorePlus)
	!AreaCheck("AR2800") // Suldanesselar
	!AreaCheck("AR2900") // Hell
	!AreaCheck("AR2906") // Hell
THEN DO
	Action(Spell,CLERIC_FIRE_STORM)
END


//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////
VARIABLE(FirstDispel=True)
INCLUDE FILE(%scsroot%/fiend/ssl/dispel.ssl)

///////////////////////////////////////////////////////////////////////////////////////
///	Breach enemy defenses
///////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        !GlobalTimerNotExpired("breach","LOCALS")
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
	TriggerBlock(PriorityBreach)
	TriggerBlock(EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_BREACH|150|150)
	SetGlobalTimer("breach","LOCALS",12)
END

IF TRIGGER
        !GlobalTimerNotExpired("breach","LOCALS")
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
	TriggerBlock(BreachTarget|AvoidDispel)
	TriggerBlock(EasyPlus)
THEN DO
	Combine()
	Action(Spell,WIZARD_BREACH|150|150)
	SetGlobalTimer("breach","LOCALS",12)
END
//////////////////////////////////////////////////////////////////////////////////////////
///	Blow someone up (ToB: Hard+; SoA: Insane)
//////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Insane)
	!GlobalGT("chapter","GLOBAL",7)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(SpellDeflect|Enemy|Helpless)
THEN DO
	Action(Spell,CLERIC_IMPLOSION|100|100)
END

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(HardPlus)
	GlobalGT("chapter","GLOBAL",7)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(SpellTurn|Enemy|Helpless)
THEN DO
	Action(Spell,CLERIC_IMPLOSION|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////////
///	Telekinesis - get rid of nearby warrior types
//////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled)
	TriggerBlock(CorePlus)
	OR(4)
		Class(scstarget,FIGHTER_ALL)
		Class(scstarget,PALADIN_ALL)
		Class(scstarget,RANGER_ALL)
		Class(scstarget,MONK)
	CheckStatGT(scstarget,-10,SAVEVSSPELL)
THEN DO
	Combine()
	Action(SpellNoDecRES,"dw#batel"|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Use offensive magic
//////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////
///   Symbol: Stun
//////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(EasyPlus)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(Stun|MR|SIConjuration|Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_NPC_SYMBOL_STUN|100|100)
END

///////////////////////////////
////	Symbol: Death
///////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(EasyPlus)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(Slay|MR|SIConjuration)
THEN DO
	Combine()
	Action(Spell,WIZARD_NPC_SYMBOL_DEATH|100|100)
END

//////////////////////////////////////////////////////////////////////////
////	Domination
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR|Charm|SIEnchantment|Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,INNATE_FIEND_DOMINATION)
END


//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on stunned victims
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(NearestEnemyOfType([PC]))
	StateCheck(scstarget,STATE_STUNNED)
	TriggerBlock(Enemy|Plus5Safe)
THEN DO
	Action(Attack|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||5)
BEGIN LOOP(MyWeaponDamageType||MultipleDamageType)
INCLUDE FILE(%scsroot%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
