//////////////////////////////////////////////////////////////////////////////////////
///	Maurezhi combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions.ssl)


IF	
	!StateCheck(Myself,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
	!AreaCheck("ar3004") // dead magic
THEN
	RESPONSE #100
		ApplySpellRES("dw#silin",Myself)
END

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

//////////////////////////////////////////////////////////////////////////////////////
///	Blur and II at start
//////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("prep","LOCALS")
	!AreaCheck("ar3004") // dead magic
THEN
	RESPONSE #100
		SetGlobalTimer("prep","LOCALS",1000)
		ApplySpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
		ApplySpell(Myself,WIZARD_BLUR)
		Continue()
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////
///	Hold Person
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR100|SpellDeflect|Hold|SIEnchantment|Enemy|Helpless)
	Target(NearestEnemyOf(Myself))
THEN DO
	Combine()
	Action(Spell,WIZARD_HOLD_PERSON|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Animate Dead
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(NearestEnemyOf(Myself))
THEN DO
	Action(Spell,WIZARD_ANIMATE_DEAD|100|100)
END

///////////////////////////////////////////////////////////////////////////////
////	Cloak of Fear
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(NearestEnemyOf(Myself))
	Range(scstarget,5)
THEN DO
	Action(Spell,CLERIC_CLOAK_OF_FEAR)
END

//////////////////////////////////////////////////////////////////////////////
/////	Combat script
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||3)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/chase.ssl)
END LOOP
END LOOP
