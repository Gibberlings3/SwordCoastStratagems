//////////////////////////////////////////////////////////////////////////////////////////
///	Work around death animation bug
//////////////////////////////////////////////////////////////////////////////////////////

IF
	Die()
THEN
	RESPONSE #100
		DestroySelf()
END


//////////////////////////////////////////////////////////////////////////////////////////
///	Redefine spellcasting to include death gazes
//////////////////////////////////////////////////////////////////////////////////////////
INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)


BEGIN_ACTION_DEFINITION
	Name(SpellNoDec)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!AreaCheck("ar3004") // dead magic
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
		ForceSpell([PC],TANARI_DEATH_GAZE)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
		ForceSpell(SecondNearest([PC]),TANARI_DEATH_GAZE)
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
		ForceSpell(ThirdNearest([PC]),TANARI_DEATH_GAZE)
END


//////////////////////////////////////////////////////////////////////////////////////
///	Nabassu combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////
VARIABLE(FirstDispel=True)
INCLUDE FILE(%scsroot%/fiend/ssl/dispel.ssl)


//////////////////////////////////////////////////////////////////////////////////////
///	Silence mages and clerics (we trade the at-will PnP version for the -5-to-saves
///	BG2 version)
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Disabled|Silence)
	!GlobalTimerNotExpired("silence","LOCALS")
THEN DO
	Combine()
	SetGlobalTimer("silence","LOCALS",1000)
	Action(SpellNoDec,TANARI_SILENCE|200|300)
END

//////////////////////////////////////////////////////////////////////////////////////////
///	Paralysis
//////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR|Hold|SIEnchantment|Enemy|Helpless)
	CheckStatGT(scstarget,5,SAVEVSSPELL)
THEN DO
	Combine()
	Action(SpellNoDec,TANARI_PARALYZE|100|300)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Vampiric touch
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsInOrder;HPPercentLT(scstarget,30))
	TargetBlock(PCsInOrderShort)
	TriggerBlock(Enemy|Disabled|MR|SINecromancy|SpellTurn|Helpless)
	Range(scstarget,10)
THEN DO
	Combine()
	Action(SpellNoDecRES,"dw#demvt")
END

//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on paralysed victims
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(NearestEnemyOfType([PC]))
	StateCheck(scstarget,STATE_HELPLESS)
	TriggerBlock(Enemy|Plus4Safe)
THEN DO
	Action(Attack|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////



BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
