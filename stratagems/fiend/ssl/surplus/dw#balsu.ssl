///// Summoned block - only used if Smarter Mages not installed
//////////////////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////////
///	Work around death animation bug
//////////////////////////////////////////////////////////////////////////////////////////

IF
	Die()
		OR(2)
			AreaCheck("ar6200")
			AreaCheck("ar2900")
THEN
	RESPONSE #100
		ReallyForceSpellDeadRES("baldead",Myself)
END

IF
	IgnoreBlock(EnhancedEdition)
        Die()
THEN
	RESPONSE #100
		DestroySelf()
END


INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

//////////////////////////////////////////////////////////////////////////////////////
///	Begin stoneskinned and aflame
//////////////////////////////////////////////////////////////////////////////////////

IF
  !Global("firewall","LOCALS",1)
THEN
    RESPONSE #100
           SetGlobal("firewall","LOCALS",1)
           ApplySpellRES("finbalor",Myself) // Aura of Flaming Death
           Continue()
END

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("stoneskin","LOCALS")
	!AreaCheck("ar3004") // dead magic
THEN
	RESPONSE #100
		SetGlobalTimer("stoneskin","LOCALS",36)
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		Continue()
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/bloodwar.ssl)
END LOOP

/////////////////////////////////////////////////////////////////////////////////////////
///	Fire Storm will usually be first action, but we don't use it for the Suldanesselar
///	Balor that's chewing on elves, or for Irenicus's flunkies, or when summoned
/////////////////////////////////////////////////////////////////////////////////////////

/*IF TRIGGER
	TargetBlock(PCsInOrder)
	!GlobalTimerNotExpired("firestorm","LOCALS")
	!AreaCheck("AR2800") // Suldanesselar
	!AreaCheck("AR2900") // Hell
	!AreaCheck("AR2906") // Hell
	!HasItem("dw#basum",Myself)
THEN DO
	SetGlobalTimer("firestorm","LOCALS",1000)
	Action(SpellNoDec,CLERIC_FIRE_STORM)
END
*/

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

//  INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/jump.ssl) - not for summons

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////
// VARIABLE(FirstDispel=True)
//  INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/dispel.ssl)  - not for summons

///////////////////////////////////////////////////////////////////////////////////////
///	Breach enemy defenses
///////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([ANYONE])
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellTurn|Helpless)
	OR(5)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		CheckStatGT(scstarget,0,SCRIPTINGSTATE2)
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,2,STONESKINS)
	!GlobalTimerNotExpired("DMWW_breach","LOCALS")
THEN DO
	Combine()
	SetGlobalTimer("DMWW_breach","LOCALS",18)
	Action(SpellNoDec,WIZARD_BREACH|150|150)
END

//////////////////////////////////////////////////////////////////////////////////////////
///	Blow someone up (ToB only)
//////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock([ANYONE])
	GlobalGT("chapter","GLOBAL",%bg2_chapter_7%")
	!GlobalTimerNotExpired("implosion","LOCALS")
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(SpellTurn|Helpless)
THEN DO
	SetGlobalTimer("implosion","LOCALS",1000)
	Action(SpellNoDec,CLERIC_IMPLOSION|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////////
///	Telekinesis - get rid of nearby warrior types
//////////////////////////////////////////////////////////////////////////////////////////
/*       - skip for summons
IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled)
	OR(4)
		Class(scstarget,FIGHTER_ALL)
		Class(scstarget,PALADIN_ALL)
		Class(scstarget,RANGER_ALL)
		Class(scstarget,MONK)
	CheckStatGT(scstarget,-10,SAVEVSSPELL)
THEN DO
	Combine()
	Action(SpellNoDecRES,"dw#batel"|100|100)
END
*/
//////////////////////////////////////////////////////////////////////////////////////
///	Use offensive magic
//////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////
///   Symbol: Stun
//////////////////////////////

IF TRIGGER
        Target([ANYONE])
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(Stun|MR|SIConjuration|Helpless)
THEN DO
	Combine()
	Action(SpellNoDec,WIZARD_NPC_SYMBOL_STUN|100|100)
END

///////////////////////////////
////	Symbol: Death
///////////////////////////////

IF TRIGGER
        Target([ANYONE])
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	!Gender(scstarget,20)
	TriggerBlock(Slay|MR|SIConjuration)
THEN DO
	Combine()
	Action(SpellNoDec,WIZARD_NPC_SYMBOL_DEATH|100|100)
END

//////////////////////////////////////////////////////////////////////////
////	Domination
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR|Charm|SIEnchantment|SpellTurn|Enemy|Helpless)
THEN DO
	Combine()
	Action(SpellNoDec,WIZARD_DOMINATION)
END


//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on stunned victims
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([ANYONE])
	StateCheck(scstarget,STATE_STUNNED)
	TriggerBlock(Enemy|Plus4Safe)
THEN DO
	Action(Attack|100|100)
END

///////////////////////////////////////////////////////////////////////
////	Kill anything in sight
///////////////////////////////////////////////////////////////////////


IF
	See([ANYONE])
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END
