VARIABLE(IsSummoned=True)

//////////////////////////////////////////////////////////////////////////////////////////
///	Used by summoned Glabrezu who are summoner-friendly
//////////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions_summoned.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Initialise
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

///////////////////////////////////////////////////////////////////////////////////////
///	Remove pre-summoning ring
///////////////////////////////////////////////////////////////////////////////////////

IF
	HasItem("dw#sumfi",Myself)
THEN
	RESPONSE #100
		DestroyItem("dw#sumfi")
		Continue()
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////////////////
///	Renew its mirror image
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(ImprovedFiends)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	TriggerBlock(TruesightSafe|CorePlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_MIRROR_IMAGE|100|100)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Truesight
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	Gender(scstarget,ILLUSIONARY)
	!Allegiance(scstarget,ENEMY)
	TriggerBlock(TruesightAlreadyCast|Enemy|SIDivination|EasyPlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_TRUE_SIGHT)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

VARIABLE(FirstDispel=True)
INCLUDE FILE(%scsroot%/fiend/ssl/dispel.ssl)


//////////////////////////////////////////////////////////////////////////////////////
///     Stun someone
//////////////////////////////////////////////////////////////////////////////////////


// stun someone

IF TRIGGER
        IgnoreBlock(Demivrgvs)
        TargetBlock(EnemiesInOrder)
	OR(4)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
		CheckStatLT(scstarget,12,LEVEL)
	!CheckStatGT(scstarget,0,WIZARD_SPELL_TRAP)
	TriggerBlock(MR|Stun|Enemy|Helpless|SIConjuration|SRPowerWord)
	OR(2)
	        HPGT(Myself,90)
	        !CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
THEN DO
     Action(Spell,WIZARD_POWER_WORD_STUN|200|100)
END

IF TRIGGER
        RequireBlock(Demivrgvs)
        TargetBlock(EnemiesInOrder)
	OR(4)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
		CheckStatLT(scstarget,12,LEVEL)
	TriggerBlock(MR|Stun|Enemy|Helpless|SIConjuration|SRPowerWord|SpellTurn)
THEN DO
     Action(Spell,WIZARD_POWER_WORD_STUN|200|100)
END

////////////////////////////////////////////////////////////////////////////////////
///	Unholy Blight
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(ImprovedFiends)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(MR100|Enemy|Helpless|SINecromancy|ResistMagicDamage|EasyPlus)
	Alignment(scstarget,MASK_GOOD)	
THEN DO
	Action(Spell,INNATE_FIEND_UNHOLY_BLIGHT|200|100)
END

///////////////////////////////////////////////////////////////////////////
////	Confusion
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(ImprovedFiends)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|Confused|SIEnchantment|Enemy|Helpless|ConfuseProof|EasyPlus)
THEN DO
	Combine()
	Action(SpellNoDec,INNATE_FIEND_CONFUSION|200|100)
END

// eat nearby victims

IF TRIGGER
          RequireBlock(EnhancedEdition)
          TargetBlock(EnemiesInOrderShort)
          StateCheck(scstarget,STATE_HELPLESS)
          WeaponEffectiveVs(scstarget,MAINHAND)
	  WeaponCanDamage(scstarget,MAINHAND)
          Range(scstarget,10)
THEN DO
         Action(Attack)
END

IF TRIGGER
          IgnoreBlock(EnhancedEdition)
          TargetBlock(EnemiesInOrderShort)
          StateCheck(scstarget,STATE_HELPLESS)
          !CheckStatGT(scstarget,4,ENCHANTMENT_IMMUNITY)
          !CheckStat(scstarget,4,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
          Range(scstarget,10)
THEN DO
         Action(Attack)
END

BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Piercing)
INCLUDE FILE(%scsroot%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
