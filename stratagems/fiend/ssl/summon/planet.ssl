INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions_summoned.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)

///////////////////////////////
////	Death effects
///////////////////////////////

IF
	Die()
	Alignment(Myself,MASK_EVIL)
THEN
	RESPONSE #100
		CreateVisualEffectObject("SPUNHOLY",Myself)
		DestroySelf()
END

IF
	Die()
	!Alignment(Myself,MASK_EVIL)
THEN
	RESPONSE #100
		CreateVisualEffectObject("SPHOLY",Myself)
		DestroySelf()
END

///////////////////////////////
////	Heal myself
///////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,40)
THEN DO
	Action(SpellMyself,CLERIC_HEAL)
END

///////////////////////////////
////	Become invisible
///////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	TriggerBlock(TruesightSafe)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN DO
	Action(SpellMyself,WIZARD_IMPROVED_INVISIBILITY|100|50)
END

///////////////////////////////
////    Firestorm
///////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|AoEFire)
THEN DO
	Action(Spell,CLERIC_FIRE_STORM)
END

///////////////////////////////
////    Remove fear
///////////////////////////////

IF TRIGGER
	TargetBlock(VillainsInOrder)
	Allegiance(Myself,ENEMY)
	CheckStatGT(scstarget,14,Level)
	StateCheck(scstarget,STATE_PANIC)
THEN DO
	Action(Spell,CLERIC_REMOVE_FEAR)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	!Allegiance(Myself,ENEMY)
	CheckStatGT(scstarget,14,Level)
	StateCheck(scstarget,STATE_PANIC)
THEN DO
	Action(Spell,CLERIC_REMOVE_FEAR)
END

///////////////////////////////
////    Globe of blades
///////////////////////////////

IF TRIGGER
	TriggerBlock(ILackBladeBarrier)
	Range(NearestEnemyOf(Myself),5)
THEN DO
	Action(SpellMyself,CLERIC_GLOBE_OF_BLADES)
END

///////////////////////////////
////    High-powered healing
///////////////////////////////

IF TRIGGER
	TargetBlock(VillainsInOrder)
	Allegiance(Myself,ENEMY)
	CheckStatGT(scstarget,14,Level)
	HPPercentLT(scstarget,50)
	Range(scstarget,16)
THEN DO
	Action(Spell,CLERIC_HEAL|100|50)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	!Allegiance(Myself,ENEMY)
	HPPercentLT(scstarget,50)
	Range(scstarget,16)
THEN DO
	Action(Spell,CLERIC_HEAL|100|50)
END

///////////////////////////////
////    Insect plague
///////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100NotSR|SIConjuration|Enemy|NoInsects|CorePlus)
THEN DO
	Action(Spell,CLERIC_INSECT_PLAGUE|100|50)
END

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100NotSR|SIConjuration|NoInsects|CorePlus)
	!Allegiance(Myself,ENEMY)
THEN DO
	Action(Spell,CLERIC_INSECT_PLAGUE|100|50)
END

///////////////////////////////
////    (Un)holy word
///////////////////////////////

IF
  See(Myself)
  False()
THEN
   RESPONSE #100
    Continue()
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	Range(scstarget,10)
	TriggerBlock(MR100|SIConjuration|Enemy|EasyPlus)
THEN DO
	Combine()
	Action(SpellMyself,CLERIC_UNHOLY_WORD|100|50)
	Action(SpellMyself,CLERIC_HOLY_WORD|100|50)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	!Allegiance(Myself,ENEMY)
        Allegiance(scstarget,ENEMY) // needed b/c Combine()
	Range(scstarget,10)
	TriggerBlock(MR100|SIConjuration|EasyPlus)
THEN DO
	Combine()
	Action(SpellMyself,CLERIC_UNHOLY_WORD|100|50)
	Action(SpellMyself,CLERIC_HOLY_WORD|100|50)
END



IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|TruesightAlreadyCast|IllusionTarget|SIDivination|EasyPlus)
THEN DO
	Combine()
	Action(SpellMyself,WIZARD_TRUE_SIGHT|100|50)
END

///////////////////////////////
////	Symbol: Fear
///////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Panic|MR|SIConjuration|Enemy|AoEHelpless|EasyPlus)
THEN DO
	Combine()
	Action(Spell,CLERIC_SYMBOL_FEAR|100|50)
END

///////////////////////////////
////	Symbol: Death
///////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Slay|MR|SIConjuration|Enemy|AoE|EasyPlus)
THEN DO
	Combine()
	Action(Spell,CLERIC_SYMBOL_DEATH|100|50)
END

//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIEvocation|Enemy)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

/////////////////////////////////////////////////////////////////////////
////    Flame Arrows 
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;HPPercentLT(scstarget,40)&!CheckStatGT(scstarget,50,RESISTMAGIC)&!CheckStatGT(scstarget,50,RESISTFIRE))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&!CheckStatGT(scstarget,50,RESISTFIRE))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR100|Enemy|ArrowSafe|SpellTurn|MinorGlobe|MirrorImage|Helpless|SIConjuration)
THEN DO
	Action(Spell,WIZARD_FLAME_ARROW|100|50)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR100|ArrowSafe|SpellTurn|MinorGlobe|MirrorImage|Helpless|SIConjuration)
	!Allegiance(Myself,ENEMY)
THEN DO
	Action(Spell,WIZARD_FLAME_ARROW|100|50)
END

//////////////////////
////	Chaos
/////////////////////////////////
IF TRIGGER
        IgnoreBlock(Demivrgvs)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|Confused|SIEnchantment|Helpless|Enemy)
        Allegiance(Myself,ENEMY)
THEN DO
	Combine()
	Action(Spell,WIZARD_CHAOS|100|50)
END

IF TRIGGER
        RequireBlock(Demivrgvs)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|Confused|SIEnchantment|Helpless|Enemy)
        Allegiance(Myself,ENEMY)
THEN DO
	Combine()
	Action(SpellReplace,WIZARD_CONFUSION,WIZARD_CHAOS|100|50)
END

IF TRIGGER
        IgnoreBlock(Demivrgvs)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|Confused|SIEnchantment|Helpless)
        !Allegiance(Myself,ENEMY)
        Allegiance(scstarget,ENEMY) // needed b/c Combine()
THEN DO
	Combine()
	Action(Spell,WIZARD_CHAOS|100|50)
END

IF TRIGGER
        RequireBlock(Demivrgvs)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|Confused|SIEnchantment|Helpless)
        !Allegiance(Myself,ENEMY)
        Allegiance(scstarget,ENEMY) // needed b/c Combine()
THEN DO
	Combine()
	Action(SpellReplace,WIZARD_CONFUSION,WIZARD_CHAOS|100|50)
END

/////////////////////////////////////////////////////////////////////////
///	Flame Strike
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&!CheckStatGT(scstarget,50,RESISTFIRE))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|Enemy|MirrorImage|SpellTurn)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Spell,CLERIC_FLAME_STRIKE|100|100)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|MirrorImage|SpellTurn)
	TriggerBlock(DirectDamageSafe)
	!Allegiance(Myself,ENEMY)
THEN DO
	Action(Spell,CLERIC_FLAME_STRIKE|100|100)
END

/////////////////////////////////////////////////////////////////////////
////	Magic Missile
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|Enemy|MagicMissile|L1safe)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Spell,WIZARD_MAGIC_MISSILE|150|50)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|MagicMissile|L1safe)
	TriggerBlock(DirectDamageSafe)
	!Allegiance(Myself,ENEMY)
THEN DO
	Action(Spell,WIZARD_MAGIC_MISSILE|150|50)
END

BEGIN LOOP(MyWeaponStrength||3)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
