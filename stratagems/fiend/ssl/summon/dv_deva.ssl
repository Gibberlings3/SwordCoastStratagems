INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions_summoned.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)

IF
	Die()
	Alignment(Myself,MASK_EVIL)
THEN
	RESPONSE #100
		CreateVisualEffectObject("SPUNHOLY",Myself)
		DestroySelf()
END

IF
	Die()
	!Alignment(Myself,MASK_EVIL)
THEN
	RESPONSE #100
		CreateVisualEffectObject("SPHOLY",Myself)
		DestroySelf()
END
/// Blade barrier

IF TRIGGER
        TriggerBlock(ILackBladeBarrier)
	Range(NearestEnemyOf(Myself),10)
THEN DO
	Action(SpellMyself,NPC_BLADE_BARRIER|100|50)
END


/// Remove Fear (don't waste on low-powered scum)

IF TRIGGER
	TargetBlock(HumanoidVillains)
	TriggerBlock(Enemy)
	StateCheck(scstarget,STATE_PANIC)
	CheckStatGT(scstarget,8,Level)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,CLERIC_REMOVE_FEAR)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	StateCheck(scstarget,STATE_PANIC)
	CheckStatGT(scstarget,8,Level)
	!Allegiance(Myself,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,CLERIC_REMOVE_FEAR)
END


///////////////////////////////
////    (Un)holy word
///////////////////////////////


IF TRIGGER
	TargetBlock(PCsInOrder)
	Range(scstarget,10)
	TriggerBlock(MR100|SIConjuration|Enemy)
THEN DO
	Combine()
	Action(Spell,CLERIC_UNHOLY_WORD|100|50)
	Action(Spell,CLERIC_HOLY_WORD|100|50)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	!Allegiance(Myself,ENEMY)
	Range(scstarget,10)
	TriggerBlock(MR100|SIConjuration)
THEN DO
	Combine()
	Action(Spell,CLERIC_UNHOLY_WORD|100|50)
	Action(Spell,CLERIC_HOLY_WORD|100|50)
END


/// Truesight

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(TruesightAlreadyCast|IllusionTarget|Enemy|SIDivination)
THEN DO
	Action(SpellMyself,CLERIC_TRUE_SIGHT|100|100)
END

// Doom

      IF TRIGGER
         TargetBlock(PlayersInOrder)
         TriggerBlock(Enemy|Disabled|SpellTurn|MinorGlobe|Doomed)
         !GlobalTimerNotExpired("doomed","LOCALS")
      THEN DO
           Action(Spell,CLERIC_DOOM|100|200)
           SetGlobalTimer("doomed","LOCALS",12)
      END

      IF TRIGGER
         TargetBlock(EnemiesInOrderShort)
         TriggerBlock(Disabled|SpellTurn|MinorGlobe|Doomed)
         !GlobalTimerNotExpired("doomed","LOCALS")
         !Allegiance(Myself,ENEMY)
      THEN DO
           Action(Spell,CLERIC_DOOM|100|200)
           SetGlobalTimer("doomed","LOCALS",12)
      END


// Silence

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIConjuration|Silence)
	TriggerBlock(Enemy|Helpless)
	!StateCheck(scstarget,STATE_SILENCED)

THEN DO
	Action(Spell,CLERIC_SILENCE_15_FOOT|150|50)
END

// Greater Command

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Disabled|Sleep|MR|Enemy)
THEN DO
	Action(Spell,CLERIC_GREATER_COMMAND|80|20)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Disabled|Sleep|MR)
         !Allegiance(Myself,ENEMY)
THEN DO
	Action(Spell,CLERIC_GREATER_COMMAND|80|20)
END

// Harm 

IF TRIGGER
	TargetBlock(PCsInOrderShort)
	Range(scstarget,10)
	TriggerBlock(MRTouch|Enemy|SINecromancy|ResistMagicDamage|ProtectionFromMagicWeapons)
	Global("shapeshift","LOCALS",0)
THEN DO
	Action(SpellMyself,CLERIC_HARM|100|50)
END

// Hold Person

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Disabled|Hold|MinorGlobe|MR|Enemy)
THEN DO
	Action(Spell,CLERIC_HOLD_PERSON|70|30)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Disabled|Hold|MinorGlobe|MR)
	!Allegiance(Myself,ENEMY)
THEN DO	
	Action(Spell,CLERIC_HOLD_PERSON|70|30)
END

// Flame Strike

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Disabled|MR|ResistFire|Enemy|SIEvocation)
THEN DO
	Action(Spell,CLERIC_FLAME_STRIKE|80|20)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Disabled|MR|ResistFire|SIEvocation)
	!Allegiance(Myself,ENEMY)
THEN DO
	Action(Spell,CLERIC_FLAME_STRIKE|80|20)
END

// Poison

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	Range(scstarget,15)
	TriggerBlock(SpellTurn|MR100|Enemy|SINecromancy)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Spell,CLERIC_POISON|100|50)
	Action(Spell,CLERIC_RIGID_THINKING|100|50) // actually Contagion
END

// Unholy Blight

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	!Alignment(Myself,MASK_GOOD)
	Alignment(scstarget,MASK_GOOD)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR100|SINecromancy|ResistMagicDamage|Enemy)
THEN DO
	Action(Spell,CLERIC_UNHOLY_BLIGHT|200|100)
END


BEGIN LOOP(MyWeaponStrength||Unknown)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/chase.ssl)
END LOOP
END LOOP

