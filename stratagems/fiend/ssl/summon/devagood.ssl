INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions_summoned.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)

IF
	Die()
	Alignment(Myself,MASK_EVIL)
THEN
	RESPONSE #100
		CreateVisualEffectObject("SPUNHOLY",Myself)
		DestroySelf()
END

IF
	Die()
	!Alignment(Myself,MASK_EVIL)
THEN
	RESPONSE #100
		CreateVisualEffectObject("SPHOLY",Myself)
		DestroySelf()
END

IF TRIGGER
	HPPercentLT(Myself,40)
	!GlobalTimerNotExpired("freeheal","LOCALS")
THEN DO
	Action(SpellNoDecRESMyself,"devaheal")
	SetGlobalTimer("freeheal","LOCALS",1000)
END

IF TRIGGER
	TargetBlock(VillainsInOrder)
	TriggerBlock(Enemy)
	CheckStatGT(scstarget,14,Level)
	StateCheck(scstarget,STATE_PANIC)
THEN DO
	Action(Spell,CLERIC_REMOVE_FEAR)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	!Allegiance(Myself,ENEMY)
	CheckStatGT(scstarget,14,Level)
	StateCheck(scstarget,STATE_PANIC)
THEN DO
	Action(Spell,CLERIC_REMOVE_FEAR)
END


IF TRIGGER
        TriggerBlock(ILackBladeBarrier)
	Range(NearestEnemyOf(Myself),5)
THEN DO	
	Action(SpellMyself,CLERIC_GLOBE_OF_BLADES)
END


IF TRIGGER
	TargetBlock(HumanoidVillains)
	TriggerBlock(Enemy)
	Target(Myself)
	CheckStatGT(scstarget,12,Level)
	HPPercentLT(scstarget,50)
	Range(scstarget,15)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_CURE_CRITICAL_WOUNDS|100|50)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	!Allegiance(Myself,ENEMY)
	Target(Myself)
	CheckStatGT(scstarget,12,Level)
	HPPercentLT(scstarget,50)
	Range(scstarget,15)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_CURE_CRITICAL_WOUNDS|100|50)
END


//////////////////////
////	Chaos
/////////////////////////////////

IF TRIGGER
        IgnoreBlock(Demivrgvs)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|Confused|SIEnchantment|Helpless)
THEN DO
	Action(Spell,WIZARD_CHAOS|100|100)
END

IF TRIGGER
        RequireBlock(Demivrgvs)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|Confused|SIEnchantment|Helpless)
THEN DO
	Action(SpellReplace,WIZARD_CONFUSION,WIZARD_CHAOS|100|100)
END

BEGIN LOOP(MyWeaponStrength||3)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP

