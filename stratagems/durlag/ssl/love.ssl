VARIABLE(NeedsBattleCry=True)
INCLUDE FILE(%scsroot%/genai/ssl/genai_definitions.ssl)


BEGIN_ACTION_DEFINITION
	Name(Spell)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!CheckStatGT(scstarget,0,SANCTUARY)
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpell(scstarget,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(SpellTimer)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument1","LOCALS")
		!CheckStatGT(scstarget,0,SANCTUARY)
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpell(scstarget,scsargument1)
		SetGlobalTimer("scsargument1","LOCALS",scsargument2)
END

BEGIN_ACTION_DEFINITION
	Name(SpellNoDecMyself)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpell(Myself,scsargument1)
END


IF

	Global("LoveFight","GLOBAL",0)
	IgnoreBlock(EnhancedEdition)
THEN
	RESPONSE #100
		CreateCreature("%tutu_var%PRIDEM",[1719.1217],0) // Pride
		CreateCreature("%tutu_scripta%VARICEM",[1875.1079],0) // Avarice
		CreateCreature("%tutu_var%FEARM",[2114.1246],0) // Fear
		ActionOverride("pridem",MoveToPoint([1955.1378]))
		ActionOverride("avaricem",MoveToPoint([1955.1378]))
		ActionOverride("fearm",MoveToPoint([1955.1378]))
		SetGlobal("LoveFight","GLOBAL",1)
END


IF
	See([PC])
	GlobalLT("LoveFight","GLOBAL",2)
	!GlobalTimerNotExpired("loveprep","LOCALS")
	!StateCheck(Myself,STATE_DISABLED)
THEN
	RESPONSE #100
		ApplySpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
		ApplySpell(Myself,CLERIC_PHYSICAL_MIRROR)
		SetGlobalTimer("loveprep","LOCALS",24)
END

IF TRIGGER
        TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR|Charm|SpellTurn|Enemy|Helpless|MinorGlobe)
	!CheckStatLT(scstarget,0,SAVEVSSPELL)
	Allegiance(Myself,ENEMY)
THEN DO
       Action(SpellTimer,WIZARD_DIRE_CHARM,12|70|30)
END

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR100|Slow|Enemy|MinorGlobe|Helpless)
	!CheckStatLT(scstarget,0,SAVEVSSPELL)
	!StateCheck(scstarget,STATE_SLOWED)
THEN DO
	Action(SpellTimer,WIZARD_SLOW,12|150|50)
END

