INCLUDE ~%scsroot%/lib/ai_wrap.tph~
LAF include STR_VAR file=fiend_shared.tph locbase=fiend END

DEFINE_ACTION_FUNCTION durlag BEGIN

     LAF define_difficulty STR_VAR type=durlag RET difficulty_variable END
     LAF durlag_lovefight END
     LAF durlag_doppelgangers END
     LAF durlag_chess END
     LAF durlag_mirror END



END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION durlag_lovefight BEGIN

   // love
   
   MAKE_PATCH
      hitpoints=>120
      morale_break=>1
      add_items=>~immune1 ring95~
      immunity_to_spell=>~%tutu_var%sw1h01 %WIZARD_CLOUDKILL%~
   END
   LAF edit_creature STR_VAR creature=lovem tv=1 edits=patch_data END
   LAF ssl_to_bcs STR_VAR script=love location=~ssl~ rename_to=~%tutu_var%love~ END

   // fear
   
   MAKE_PATCH
      add_items=>~trollreg(boots)~
      morale_break=>1
      immunity_to_spell=>~%WIZARD_CLOUDKILL%~
   END
   LAF edit_creature STR_VAR creature=fearm tv=1 edits=patch_data END

   // avarice

   MAKE_PATCH
      add_items=>~immune1 ring95~
      morale_break=>1
      level=>12
      level2=>12
      hitpoints=>90
      immunity_to_spell=>~%WIZARD_CLOUDKILL%~
   END
   LAF edit_creature STR_VAR creature=~%tutu_scripta%varicem~ edits=patch_data END
   LAF ssl_to_bcs STR_VAR script=greed location=~durlag/ssl~ rename_to=~%tutu_var%greed~ END

   // pride

   MAKE_PATCH
      add_items=>~immune1 ring95~
      morale_break=>1
      hitpoints=>100
      level=>12
      level2=>12
      attacks=>2
      class=>FIGHTER_CLERIC
      immunity_to_spell=>~%tutu_var%ax1h01 %WIZARD_CLOUDKILL%~
   END
   LAF edit_creature STR_VAR creature=pridem tv=1 edits=patch_data END
   LAF ssl_to_bcs STR_VAR script=pride location=~durlag/ssl~ rename_to=~%tutu_var%pride~ END


END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION durlag_doppelgangers BEGIN



/// handle odd failure of the Kiel setpiece battle

    LAF edit_creature STR_VAR creature=islann tv=1 editstring=~insert_script_high=>dw#islan~ END
    LAF edit_creature STR_VAR creature=fuerne tv=1 editstring=~insert_script_high=>dw#fuern~ END
    LAF install STR_VAR files=~dw#islan.baf dw#fuern.baf~ location=resource END

    LAF swap_text STR_VAR files= ~%tutu_var%dopdur3.dlg~
                          swaps=~ReallyForceSpell("islanne",WIZARD_CLOUDKILL)=>ReallyForceSpellPoint([1806.895],WIZARD_CLOUDKILL)
                                 ReallyForceSpell("fuernebol",WIZARD_STINKING_CLOUD)=>ReallyForceSpellPoint([1638.749],WIZARD_STINKING_CLOUD)
                                 ReallyForceSpell(Myself,WIZARD_STINKING_CLOUD)=>ReallyForceSpellPoint([2082.580],WIZARD_STINKING_CLOUD)Wait(1)~
    END

    COPY_EXISTING "%tutu_scriptd%opspell.bcs" override
    EXTEND_BOTTOM ~%tutu_var%dopdop.bcs~ ~override/%tutu_scriptd%opspell.bcs~

    // doppleganger weapons don't break

    LAF edit_creature STR_VAR creature=~dopdur dopdur1 dopdur2 dopdur3~ tv=1 editstring=~immunity_to_spell=>%tutu_var%ax1h01~ END

    ACTION_FOR_EACH script IN dopdop dopdur dopfue dopisl dopkie BEGIN
        LAF ssl_to_bcs STR_VAR script location=~ssl~ rename_to= ~%tutu_var%%script%~ END
    END


    MAKE_PATCH
       add_items=>dw#nopre
       alignment=>NEUTRAL_EVIL
       script_race=>~%tutu_var%mage5~
       enforce_charclass=>is_bg1
    END
    LAF edit_creature STR_VAR creature=dopisl tv=1 edits=patch_data END

    MAKE_PATCH
       add_items=>dw#nopre
       alignment=>NEUTRAL_EVIL
       immunity_to_spell=>~%tutu_var%blun02~
       script_race=>~%tutu_var%priest5~
       enforce_charclass=>is_bg1
    END
    LAF edit_creature STR_VAR creature=dopkie tv=1 edits=patch_data END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION durlag_chess BEGIN

       LAF ssl_to_bcs STR_VAR script=~dw#ches1 dw#ches2 dw#ches3~ location=~ssl~ END

       // pawns

       MAKE_PATCH
          resist_electricity=>100
          hitpoints=>30
          add_items=>~immune1 ring95~
          immunity_to_spell=>~cdbreak1 cdbreak9 %tutu_var%hamm01~
       END
       LAF edit_creature STR_VAR creature=~%tutu_scriptbg%pawn~ edits=patch_data END

       // knights
       
       MAKE_PATCH
          resist_electricity=>100
          insert_script=>~dw#ches1 below %tutu_scriptk%ingdead~
          add_items=>~immune1 ring95(amulet)~
          immunity_to_spell=>~cdbreak1 cdbreak6 %tutu_var%sw1h04~
          dv=>KNIGHT
       END
       LAF edit_creature STR_VAR creature=~%tutu_scriptbg%knight~ edits=patch_data END

       // rooks
       
       MAKE_PATCH
          resist_electricity=>100
          insert_script=>~dw#ches2 below %tutu_scriptk%ingdead~
          add_items=>~immune1 ring95(amulet)~
          immunity_to_spell=>~cdbreak1 %tutu_var%sw1h04~
          dv=>ROOK
       END
       LAF edit_creature STR_VAR creature=~%tutu_scriptbg%rook~ edits=patch_data END

       // bishops

      MAKE_PATCH
          resist_electricity=>100
          insert_script=>~dw#ches3 below %tutu_scriptk%ingdead~
          add_items=>~immune1 ring95(amulet) dw#nopre~
          immunity_to_spell=>~%tutu_var%blun04~
          dv=>BISHOP
      END
      LAF edit_creature STR_VAR creature=~%tutu_scriptbg%bishop~  edits=patch_data END      // king

      MAKE_PATCH
          resist_electricity=>100
          class=>FIGHTER_MAGE
          add_items=>dw#move0
          strip_script=>king
          enforce_charclass=>is_bg1
      END
      LAF edit_creature STR_VAR creature=~%tutu_scriptbg%king~  edits=patch_data END

      //queen

      MAKE_PATCH
          resist_electricity=>100
          class=>MAGE
          level=>8
          insert_script=>~dw#ches3 below %tutu_scriptk%ingdead~
          enforce_charclass=>is_bg1
      END
      LAF edit_creature STR_VAR creature=~%tutu_scriptbg%queen~ edits=patch_data END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION durlag_mirror BEGIN

      // resources
      
      LAF shared_fiend_resources END

      // the knight
      
      MAKE_PATCH
         hitpoints=>150
         demon_knight=>null
         strip_scs_scripts=>null
         swap_script=>"%tutu_scriptd%eathkni=>dw#death"
      END
      LAF edit_creature STR_VAR creature=~%tutu_var%deathk~ edits=patch_data END // %tutu_scriptg%ooddeat=>dw#goodd~
      LAF edit_creature STR_VAR creature=~%tutu_scriptg%ooddeat~ edits=patch_data END
      LAF ssl_to_bcs STR_VAR script="dw#death killgood"  location=~ssl~ END

      // Generate REAL copies of the party via a modified Simulacrum spell

      LAF get_area_script STR_VAR area= ~%DurlagsTower_DemonknightsChamber%~ RET script END
      LAF swap_text STR_VAR files=~%script%.bcs~
                            swaps=~CreateCreature("%tutu_scriptg%ooddeat",\[692.637\],0)=>null
                                   Exists(Player1)=>Exists(Player1)GlobalLT("%difficulty_variable%","GLOBAL",4)OR(2)DifficultyLT(HARD)!Global("%difficulty_variable%","GLOBAL",0)

                                   Exists(Player2)=>Exists(Player2)GlobalLT("%difficulty_variable%","GLOBAL",4)OR(2)DifficultyLT(HARD)!Global("%difficulty_variable%","GLOBAL",0)
                                   Exists(Player3)=>Exists(Player3)GlobalLT("%difficulty_variable%","GLOBAL",4)OR(2)DifficultyLT(HARD)!Global("%difficulty_variable%","GLOBAL",0)
                                   Exists(Player4)=>Exists(Player4)GlobalLT("%difficulty_variable%","GLOBAL",4)OR(2)DifficultyLT(HARD)!Global("%difficulty_variable%","GLOBAL",0)
                                   Exists(Player5)=>Exists(Player5)GlobalLT("%difficulty_variable%","GLOBAL",4)OR(2)DifficultyLT(HARD)!Global("%difficulty_variable%","GLOBAL",0)
                                   Exists(Player6)=>Exists(Player6)GlobalLT("%difficulty_variable%","GLOBAL",4)OR(2)DifficultyLT(HARD)!Global("%difficulty_variable%","GLOBAL",0)~
      END
      LAF extend STR_VAR file=~%script%~ top=mirror location=ssl ssl=yes END
      LAF player_clones END

END
