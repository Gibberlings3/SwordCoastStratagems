DEFINE_PATCH_FUNCTION make_vampire BEGIN

   LPF make_basic_vampire END
   LPF CRE_read_level RET level=value END
   
   PATCH_IF level<9 BEGIN
        LPF vampire_0 END
   END ELSE BEGIN
      PATCH_MATCH level WITH
      9 BEGIN
        LPF vampire_9 END
      END
      10 11 BEGIN
        LPF vampire_10 END
      END
      12 13 BEGIN
        LPF vampire_12 END
      END
      DEFAULT
        LPF vampire_14 END
      END
   END
END



/////////////////////////////////////////////////////////
//  basic piece
/////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION make_basic_vampire BEGIN

    PATCH_MAKE_PATCH
       resist_coldGT=>50
       resist_electricityGT=>50
       swap_script=>~dw2mc2gw=>dw#vmran~
       strip_scs_scripts=>"if (class=MAGE or class=FIGHTER_MAGE) then no_change else null"
       class=>"if class=VAMPIRE then FIGHTER_THIEF else no_change"
       remove_items=>~immune2 vampreg~
       add_items=>~immune2 vampreg dw#vmwol(weapon3) dw#vmbat(weapon4)~
    END
    LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END
    LPF CRE_read_level RET arguments=value END
    LPF CRE_level2 STR_VAR arguments END
    LPF CRE_read_item_in_slot STR_VAR arguments=weapon1 RET weapon=value END
    TO_LOWER weapon
    INNER_PATCH_SAVE weapon_new ~%weapon%~ BEGIN
       REPLACE_TEXTUALLY vamp dw#bldd
    END
    PATCH_IF ~%weapon_new%~ STRING_COMPARE_CASE ~%weapon%~ BEGIN // sanity check - all vampires on a clean install satisfy this
      LPF CRE_add_items STR_VAR arguments= ~%weapon_new%(weapon2)~ END
    END
END

/////////////////////////////////////////////////////////
//  the strongest vampires - L13+
/////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION vampire_14 BEGIN
    PATCH_MAKE_PATCH
       strGT=>22
       str_ex=>0
       dexGT=>21
       conGT=>20
       thac0LT=>5
       attacksGT=>3
       hp_currentGT=>120
       hp_maxGT=>120
       remove_items=>~immune2 vampreg~
       add_items=>~immune3 vampreg2~
    END
    LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END
END

/////////////////////////////////////////////////////////
//  tough vampires - L12-13
/////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION vampire_12 BEGIN
   PATCH_MAKE_PATCH
       strGT=>20
       str_ex=>0
       dexGT=>20
       conGT=>19
       thac0LT=>7
       attacksGT=>2
       hp_currentGT=>100
       hp_maxGT=>100
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END

/////////////////////////////////////////////////////////
//  fairly typical vampires - L10-11
/////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION vampire_10 BEGIN
   PATCH_MAKE_PATCH
       strGT=>19
       str_ex=>0
       dexGT=>19
       conGT=>18
       thac0LT=>9
       hp_currentGT=>80
       hp_maxGT=>80
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END

/////////////////////////////////////////////////////////
//  basic vampires - L9
/////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION vampire_9 BEGIN
   PATCH_MAKE_PATCH
       strGT=>18
       str_ex=>100
       dexGT=>19
       conGT=>17
       thac0LT=>10
       hp_currentGT=>70
       hp_maxGT=>70
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END

/////////////////////////////////////////////////////////
//  fledgling vampires - L<9
/////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION vampire_0 BEGIN
   PATCH_MAKE_PATCH
       strGT=>18
       str_ex=>76
       dexGT=>19
       conGT=>16
       thac0LT=>11
       hp_currentGT=>60
       hp_maxGT=>60
       remove_items=>immune1
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END

END



