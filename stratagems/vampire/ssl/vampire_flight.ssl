// batform 1 is prowling
// 2 is pulling back
// 3 is running away

IF
	IgnoreBlock(PermaDeath)
        Die()
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,VAMPIRE_GAS_FORM_CHANGE)
END

IF
        IgnoreBlock(PermaDeath)
        HPPercentLT(Myself,10)
	AreaType(OUTDOOR) // this is because vampires in Bodhi's lair will cause trouble if they go off in bat form
	!AreaCheck("AR1600")  // Brynnlaw
THEN
	RESPONSE #100
		SelectWeaponAbility(SLOT_WEAPON3,0)
		DisplayString(Myself,@14507)
		EscapeArea()
END


IF TRIGGER
       TriggerBlock(CorePlus)
       !GlobalTimerNotExpired("pullback","LOCALS")
       Global("batform","LOCALS",0)
       HPPercentLT(Myself,50)
       RandomNum(2,1)
THEN DO
             Action(Literal)
             SetGlobalTimer("pullback","LOCALS",6)
             Continue()
END

IF TRIGGER
       TriggerBlock(CorePlus)
       !GlobalTimerNotExpired("pullback","LOCALS")
       Global("batform","LOCALS",0)
       HPPercentLT(Myself,50)
       RandomNum(2,2)
THEN DO
             Action(Literal)
             SetGlobalTimer("pullback","LOCALS",6)
             SetGlobal("batform","LOCALS",2)
             SelectWeaponAbility(SLOT_WEAPON3,0)
		DisplayString(Myself,@14507) 
             RunAwayFromNoLeaveArea(NearestEnemyOf(Myself),60)
END

IF
           Global("batform","LOCALS",2)
           HPPercentGT(Myself,65)
THEN
    RESPONSE #100
             SetGlobal("batform","LOCALS",1)
END

IF
   Global("batform","LOCALS",2)
   Range(NearestEnemyOf(Myself),20)
THEN
    RESPONSE #100
             RunAwayFromNoLeaveArea(NearestEnemyOf(Myself),30)
END

IF
   Global("batform","LOCALS",2)
   !Range(NearestEnemyOf(Myself),20)
THEN
    RESPONSE #100
             RandomWalk()
END


IF
	TimeGT(5)
	TimeLT(DUSK_END)
	AreaType(OUTDOOR)
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,VAMPIRE_BAT_FORM_CHANGE)
END


