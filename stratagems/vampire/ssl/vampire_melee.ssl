////////////////////////////////////////////////////////////////////////////
////	If I'm not a moron and I'm attacking something I can't hit, STOP!
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(EnhancedEdition)
        TriggerBlock(EasyPlus)
        Target(LastTargetedBy(Myself))
        Global("validtarget","LOCALS",1)
        OR(2)
            !WeaponEffectiveVs(scstarget,MAINHAND)
            !WeaponCanDamage(scstarget,MAINHAND)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END


IF TRIGGER
        IgnoreBlock(EnhancedEdition)
        TriggerBlock(EasyPlus)
	Target(LastTargetedBy(Myself))
	CheckStat(Myself,0,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	!CheckStat(scstarget,0,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

IF TRIGGER
        IgnoreBlock(EnhancedEdition)
        TriggerBlock(EasyPlus)
	Target(LastTargetedBy(Myself))
	CheckStatLT(Myself,3,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

IF TRIGGER
        IgnoreBlock(EnhancedEdition)
        TriggerBlock(EasyPlus)
	Target(LastTargetedBy(Myself))
	CheckStat(Myself,3,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	CheckStatGT(scstarget,1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

IF TRIGGER
        IgnoreBlock(EnhancedEdition)
        TriggerBlock(EasyPlus)
	Target(LastTargetedBy(Myself))
	CheckStat(Myself,4,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	CheckStatGT(scstarget,2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

IF TRIGGER
        IgnoreBlock(EnhancedEdition)
        TriggerBlock(EasyPlus)
	Target(LastTargetedBy(Myself))
	!CheckStat(Myself,0,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	CheckStat(scstarget,4,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

////////////////////////////////////////////////////////////////////////////
///	Basic vampire attack strategy:
///
///	(1) Look for someone close who's vulnerable to level drain
///	(2) If you can't find one, and you can find someone further away who's
///	vulnerable to level drain, spend 1 round trying to get to them, either in Bat form or directly. Don't try
///	this again for 5 rounds.
///	(3) Kill near enemies even if they are protected, using either wolf form or blood drain
///      (4) If no-one else is around, drink the blood of any charmed creatures
///	(5) Segue to the standard Move and Chase scripts
///////////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////
////	(1)
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        GlobalTimerNotExpired("seedefences","LOCALS")
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|Plus4Safe|CorePlus)
	InWeaponRange(scstarget)
THEN DO
	Action(AttackIfEmpty)
END


IF TRIGGER
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|Plus4Safe|LevelDrain|CorePlus)
	InWeaponRange(scstarget)
THEN DO
	Action(AttackIfEmpty)
END

IF TRIGGER
        GlobalTimerNotExpired("seedefences","LOCALS")
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|Plus4Safe|EasyMinus)
	InWeaponRange(scstarget)
THEN DO
	Action(AttackEasyIfEmpty)
END


IF TRIGGER
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|Plus4Safe|LevelDrain|EasyMinus)
	InWeaponRange(scstarget)
THEN DO
	Action(AttackEasyIfEmpty)
END


///////////////////////////////////////////////////////////////////////
////	(2a)
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        TriggerBlock(EasyPlus)
        Global("victimroute","LOCALS",0)
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Disabled|Plus4Safe|LevelDrain)
THEN DO
	Action(AttackVictim)
END

///////////////////////////////////////////////////////////////////////
////	(2b)
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        TriggerBlock(EasyPlus)
        Global("victimroute","LOCALS",1)
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Disabled|Plus4Safe|LevelDrain)
THEN DO
	Action(AttackVictimBat)
END


///////////////////////////////////////////////////////////////////////
////	(3)
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        TriggerBlock(CorePlus)
        Global("secondary","LOCALS",0)
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
        TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|Plus4Safe)
	InWeaponRange(scstarget)
THEN DO
	Action(AttackBloodIfEmpty)
END

IF TRIGGER
        TriggerBlock(CorePlus)
        Global("secondary","LOCALS",1)
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
        TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|Plus4Safe)
	InWeaponRange(scstarget)
THEN DO
	Action(AttackWolfIfEmpty)
END

IF TRIGGER
        TriggerBlock(Easy)
	Target(WorstAC)
	TargetBlock(PCsPreferringWeak)
        TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|Plus4Safe)
	InWeaponRange(scstarget)
THEN DO
	Action(AttackWolfIfEmpty)
END

IF TRIGGER
        TriggerBlock(Easiest)
	Target(WorstAC)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy)
	InWeaponRange(scstarget)
THEN DO
	Action(AttackIfEmpty)
END

///////////////////////////////////////////////////////////////////////
////  (4) Finish off charmed creatures
///////////////////////////////////////////////////////////////////////

IF TRIGGER
   TriggerBlock(CorePlus)
   Allegiance(Myself,ENEMY)
   !See(NearestEnemyOf(Myself))
   TargetBlock(PlayersInRandomOrder)
THEN DO
     Action(AttackBlood)
END




///////////////////////////////////////////////////////////////////////
////	(5)
///////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Null)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
END LOOP
END LOOP



