//////// Fear and Mesmerism ///////////////

// Vampires of L14+ can always do both tricks

IF
  Global("initialise","LOCALS",0)
  CheckStatGT(Myself,13,Level)
THEN
    RESPONSE #100
       SetGlobal("causesfear","LOCALS",1)
       SetGlobal("hypnotises","LOCALS",1)
       Continue()
END

// Vampires of L12-13 can do one of them

IF
  Global("initialise","LOCALS",0)
  !CheckStatGT(Myself,13,Level)
  CheckStatGT(Myself,11,Level)
THEN
    RESPONSE #100
       SetGlobal("causesfear","LOCALS",1)
       Continue()
    RESPONSE #100
       SetGlobal("hypnotises","LOCALS",1)
       Continue()
END

// Vampires of L10-11 can do one of them half the time

IF
  Global("initialise","LOCALS",0)
  !CheckStatGT(Myself,11,Level)
  CheckStatGT(Myself,9,Level)
THEN
    RESPONSE #100
       SetGlobal("causesfear","LOCALS",1)
       Continue()
    RESPONSE #100
       SetGlobal("hypnotises","LOCALS",1)
       Continue()
    RESPONSE #200
       Continue()
END

//////// Summon wolves, bats, rats ////////

// vampires of L10+ can always do one of them

IF
  Global("initialise","LOCALS",0)
  CheckStatGT(Myself,9,Level)
  RandomNum(3,1)
THEN
    RESPONSE #50
             SetGlobal("summonsbats","LOCALS",1)
             Continue()
END

IF
  Global("initialise","LOCALS",0)
  CheckStatGT(Myself,9,Level)
  RandomNum(3,2)
THEN
    RESPONSE #50
             SetGlobal("summonswolves","LOCALS",1)
             Continue()
END

IF
  Global("initialise","LOCALS",0)
  CheckStatGT(Myself,9,Level)
  RandomNum(3,3)
THEN
    RESPONSE #50
             SetGlobal("summonsrats","LOCALS",1)
             Continue()
END

// vampires of L9 can do one of them half the time


IF
  Global("initialise","LOCALS",0)
  CheckStatGT(Myself,9,Level)
  RandomNum(6,1)
THEN
    RESPONSE #50
             SetGlobal("summonsbats","LOCALS",1)
             Continue()
END

IF
  Global("initialise","LOCALS",0)
  CheckStatGT(Myself,9,Level)
  RandomNum(6,2)
THEN
    RESPONSE #50
             SetGlobal("summonswolves","LOCALS",1)
             Continue()
END

IF
  Global("initialise","LOCALS",0)
  CheckStatGT(Myself,9,Level)
  RandomNum(6,3)
THEN
    RESPONSE #50
             SetGlobal("summonsrats","LOCALS",1)
             Continue()
END

//////// Take rat form, fade into mist ////

// Vampires of L10+ have a 1 in 3 chance of knowing either skill

IF
  Global("initialise","LOCALS",0)
  CheckStatGT(Myself,9,Level)
  OR(2)
     RandomNum(6,1)
     RandomNum(6,2)
THEN
   RESPONSE #50
             SetGlobal("takesratform","LOCALS",1)
             Continue()
END

IF
  Global("initialise","LOCALS",0)
  CheckStatGT(Myself,9,Level)
  OR(2)
     RandomNum(6,3)
     RandomNum(6,4)
THEN
   RESPONSE #50
             SetGlobal("takesmistform","LOCALS",1)
             Continue()
END


//// only go round once

IF
  Global("initialise","LOCALS",0)
THEN
    RESPONSE #100                
             SetGlobal("initialise","LOCALS",1)
             Continue()
END


IF
  See(NearestEnemyOf(Myself))
  !GlobalTimerNotExpired("picksecondary","LOCALS")
THEN
    RESPONSE #100
       SetGlobalTimer("picksecondary","LOCALS",24)
       SetGlobal("secondary","LOCALS",0)
       Continue()
    RESPONSE #50
       SetGlobalTimer("picksecondary","LOCALS",24)
       SetGlobal("secondary","LOCALS",1)
       Continue()
END

IF
  See(NearestEnemyOf(Myself))
  !GlobalTimerNotExpired("pickvictimroute","LOCALS")
THEN
    RESPONSE #100
       SetGlobalTimer("pickvictimroute","LOCALS",24)
       SetGlobal("victimroute","LOCALS",0)
       Continue()
    RESPONSE #100
       SetGlobalTimer("pickvictimroute","LOCALS",24)
       SetGlobal("victimroute","LOCALS",1)
       Continue()
END



