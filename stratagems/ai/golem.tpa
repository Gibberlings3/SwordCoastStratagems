LAF include STR_VAR file=caster_shared.tph locbase=caster_shared END
LAUNCH_ACTION_MACRO read_in_instant_prebuff_spells


DEFINE_ACTION_FUNCTION golem BEGIN
        
        LAF define_difficulty STR_VAR type=genai RET difficulty_variable END

        LAF golem_creature_patch END
        LAF golem_spells END
        LAF golem_scripts END
        LAF golem_weapons END
        LAF golem_spawns END
        LAF iron_golem_cosmetic END

END

////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////// // make golem spells innate
////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION golem_spells BEGIN

    LAF add_spell_to_ids STR_VAR typename=innate filename="GOLEM_CONE_OF_COLD" RET GOLEM_CONE_OF_COLD=newname END
    LAF make_innate_repeating_spell INT_VAR cooldown=18 STR_VAR arguments="WIZARD_CONE_OF_COLD=>GOLEM_CONE_OF_COLD" END
    LAF edit_spell STR_VAR spell=GOLEM_CONE_OF_COLD editstring="patch_ability_inline=>~casting_time=>0~" END
END

////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////// // check if a golem script is installed
////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION has_golem_script RET value BEGIN
       SET value=0
       PATCH_FOR_EACH arguments IN golcly01 golsto01 goliro01 golice01 golbra01 BEGIN
          LPF CRE_match_script STR_VAR arguments RET value_here=value END
          SET value=value=1? 1: value_here
       END
END


////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////// // grant immunity to poison and lower-resistance effects; strip unneeded scripts
////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION golem_creature_patch BEGIN

    // collect golems (hardcoded for speed)

    OUTER_SPRINT creature ~~
    COPY_EXISTING_REGEXP GLOB ~.*\.cre~ ~override~

         PATCH_IF BYTE_AT 0x272=144 BEGIN
           LPF CRE_read_class RET class=value END
           PATCH_IF ~%class%~ STRING_COMPARE_CASE ~ELEMENTAL_FIRE~ BEGIN // not Burning Men
            PUSH creature ~%SOURCE_RES%~
           END
         END
    BUT_ONLY

    MAKE_PATCH
       immunity_to_spell=>~%WIZARD_LOWER_RESISTANCE% %WIZARD_PIERCE_MAGIC% %WIZARD_PIERCE_SHIELD%~
       immunity_to_opcode=>25
       add_effect_inline=>~opcode=>173 parameter1=>100~
       apply_patches_inline=>~match=>"has_golem_script=1" strip_scs_scripts=>null~
    END
    LAF edit_creature STR_VAR creature edits=patch_data END
END

////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////// weapons
////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION golem_weapons BEGIN

    LAF edit_item STR_VAR item="golbra golcla golfle goliro golsto" editstring=~patch_ability_inline=>"ability_rangeGT=>2"~ END
    LAF check_ini STR_VAR ini=no_cursed_golem_wounds RET value END
    ACTION_IF !value BEGIN
       LAF edit_item STR_VAR item=golcla editstring="add_effect_inline=>~match=>ability_type=1 opcode=>146 target=>2 parameter2=>1 timing=>1 resource=>dw#curse~" END
    END


END

////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////// scripting
////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION ~golem_scripts~ BEGIN

     LAF ssl_to_bcs STR_VAR script=golem location=ssl variables=~IsMoron=True&IsIronGolem=True~ rename_to=goliro01 END
     LAF ssl_to_bcs STR_VAR script=golem location=ssl variables=~IsMoron=True&IsStoneGolem=True~ rename_to=golsto01 END
     LAF ssl_to_bcs STR_VAR script=golem location=ssl variables=~IsMoron=True&IsClayGolem=True~ rename_to=golcly01 END
     LAF ssl_to_bcs STR_VAR script=golem location=ssl variables=~IsMoron=True&IsIceGolem=True~ rename_to=golice01 END
     LAF ssl_to_bcs STR_VAR script=golem location=ssl variables=~IsBrainGolem=True~ rename_to=golbra01 END

END

////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////// spawning
////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION golem_spawns BEGIN

    // Iron and adamantite golems shouldn't spawn where they don't physically fit

    LAF clone_script STR_VAR script=~spwngol=>dw#spng~ END
    LAF swap_text STR_VAR files=dw#spng.bcs swaps=~goliro01=>golcla01 golada01=>goljug01~ END
    LAF swap_text STR_VAR files=~ar0411.are ar0413.are ar1202.are~ swaps=~spwngol=>dw#spng~ END
END

////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////// cosmetic [legacy code]
////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION iron_golem_cosmetic BEGIN


  COPY_EXISTING ~spin979.spl~ ~override~   // for cosmetic reasons, iron golem gas cloud casting time increases
     READ_LONG 0x64 ~ab_off~
     READ_SHORT 0x68 ~ab_num~
     FOR (i=0;i<~ab_num~;i=i+1) BEGIN
        WRITE_SHORT 0x12+0x28*~i~+~ab_off~ 3
     END

     ACTION_IF !FILE_EXISTS ~override/migog22.bam~ BEGIN // it's faintly possible something else has done this

	COPY_EXISTING ~migog22.bam~ ~override\migog25.bam~
			READ_ASCII 0x0 ~type~ (4)
			PATCH_IF ~%type%~ STRING_EQUAL_CASE ~BAMC~ THEN BEGIN
				READ_LONG 0x8 ~uncompressed_length~
				SET ~compressed_length~=~%SOURCE_SIZE%~-0xc
				DECOMPRESS_REPLACE_FILE 0xc ~%compressed_length%~ ~%uncompressed_length%~
			END
			READ_SHORT 0x8 ~framecount~
			READ_LONG 0xc ~frame_off~
			SET ~cycle_off~=~frame_off~+0xc*~framecount~
			FOR (i=0;i<9;i=i+1) BEGIN
			   READ_LONG ~cycle_off~+0x4*(18+~i~) ~cycledata_long~
			   READ_LONG ~cycle_off~+0x4*~i~ ~cycledata_short~
			   WRITE_LONG ~cycle_off~+0x4*~i~ ~cycledata_long~
                           WRITE_LONG ~cycle_off~+0x4*(18+~i~) ~cycledata_short~
			END

         COPY_EXISTING ~migog25.bam~ ~override/migog26.bam~

	COPY_EXISTING ~migog21.bam~ ~override\migog22.bam~
			READ_ASCII 0x0 ~type~ (4)
			PATCH_IF ~%type%~ STRING_EQUAL_CASE ~BAMC~ THEN BEGIN
				READ_LONG 0x8 ~uncompressed_length~
				SET ~compressed_length~=~%SOURCE_SIZE%~-0xc
				DECOMPRESS_REPLACE_FILE 0xc ~%compressed_length%~ ~%uncompressed_length%~
			END
			READ_SHORT 0x8 ~framecount~
			READ_LONG 0xc ~frame_off~
			SET ~cycle_off~=~frame_off~+0xc*~framecount~
			FOR (i=0;i<9;i=i+1) BEGIN
			   READ_LONG ~cycle_off~+0x4*(9+~i~) ~cycledata_long~
			   READ_LONG ~cycle_off~+0x4*(18+~i~) ~cycledata_short~
			   WRITE_LONG ~cycle_off~+0x4*(18+~i~) ~cycledata_long~
                           WRITE_LONG ~cycle_off~+0x4*(9+~i~) ~cycledata_short~
			END
   END

END

