// basically dw#melee

VARIABLE(IsMainAI=True)
VARIABLE(IgnoreCloud=True)
VARIABLE(MyWeaponStrength=4)
VARIABLE(MyWeaponDamageType=Crushing)

INCLUDE FILE(%MOD_FOLDER%/lib/ssl/shared_definitions.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/initial.ssl)

BEGIN_ACTION_DEFINITION
	Name(GolemSpell)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("golem_power","LOCALS")
		!StateCheck(scstarget,STATE_NOT_TARGETABLE)
		!CheckStatGT(scstarget,0,SANCTUARY)
		CheckStatLT(Myself,60,SPELLFAILUREMAGE)
		!CheckSpellState(scstarget,DO_NOT_TARGET_SPELLS)
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("golem_power","LOCALS",scsargument2)
		ForceSpell(scstarget,scsargument1)
END

IF TRIGGER
      RequireBlock(IsBrainGolem)
      TargetBlock(EnemiesInOrderShort)
      HPPercentLT(Myself,50)
THEN DO
     Action(GolemSpell,BRAIN_PSIONIC_BLAST,1000)
END

IF TRIGGER
      RequireBlock(IsIronGolem)
      TargetBlock(EnemiesInOrderShort)
      Range(scstarget,15)
THEN DO
     Action(GolemSpell,GOLEM_GASCLOUD,18|100|100)
END

IF TRIGGER
      RequireBlock(IsStoneGolem)
      TargetBlock(EnemiesInOrderShort)
      Range(scstarget,15)
THEN DO
     Action(GolemSpell,GOLEM_SLOW,18|100|100)
END

IF
      RequireBlock(IsClayGolem)
      See(NearestEnemyOf(Myself))
      !Global("waitstart","LOCALS",1)
THEN
    RESPONSE #100
        SetGlobal("waitstart","LOCALS",1)
        SetGlobalTimer("wait","LOCALS",6)
END

IF TRIGGER
      RequireBlock(IsClayGolem)
      See(NearestEnemyOf(Myself))
      Target(Myself)
      !Global("waitstart","LOCALS",0)
      !GlobalTimerNotExpired("wait","LOCALS")
THEN DO
     Action(GolemSpell,GOLEM_HASTE,18)
END

IF
    RequireBlock(IsIceGolem)
    HitBy([ANYONE],FIRE)
THEN
	RESPONSE #100
		ForceSpell(Myself,ICE_GOLEM_STEAM)
END

IF TRIGGER
     RequireBlock(IsIceGolem)
     TargetBlock(EnemiesInOrderShort)
     Range(scstarget,10)
THEN DO
     Action(GolemSpell,GOLEM_CONE_OF_COLD,18|100|100)
END

INCLUDE FILE(%MOD_FOLDER%/genai/ssl/chase.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/chase.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/random.ssl)

