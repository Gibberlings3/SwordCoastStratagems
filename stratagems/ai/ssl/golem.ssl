// basically dw#melee

VARIABLE(IsMainAI=True)
VARIABLE(IgnoreCloud=True)
VARIABLE(MyWeaponStrength=4)
VARIABLE(MyWeaponDamageType=Crushing)

INCLUDE FILE(%scsroot%/lib/ssl/shared_definitions.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/initial.ssl)

IF TRIGGER
      RequireBlock(IsBrainGolem)
      TargetBlock(EnemiesInOrderShort)
      HPPercentLT(Myself,50)
THEN DO
     Action(GolemSpell,BRAIN_PSIONIC_BLAST,brain,1000)
END

IF TRIGGER
      RequireBlock(IsIronGolem)
      TargetBlock(EnemiesInOrderShort)
      Range(scstarget,15)
THEN DO
     Action(ForceSpell,GOLEM_GASCLOUD|100|100)
END

IF TRIGGER
      RequireBlock(IsStoneGolem)
      TargetBlock(EnemiesInOrderShort)
      Range(scstarget,15)
THEN DO
     Action(ForceSpell,GOLEM_SLOW|100|100)
END

IF
      RequireBlock(IsClayGolem)
      See(NearestEnemyOf(Myself))
      !Global("waitstart","LOCALS",1)
THEN
    RESPONSE #100
        SetGlobal("waitstart","LOCALS",1)
        SetGlobalTimer("wait","LOCALS",6)
END

IF TRIGGER
      RequireBlock(IsClayGolem)
      See(NearestEnemyOf(Myself))
      Target(Myself)
      !Global("waitstart","LOCALS",0)
      !GlobalTimerNotExpired("wait","LOCALS")
THEN DO
     Action(ForceSpell,GOLEM_HASTE)
END

IF
    RequireBlock(IsIceGolem)
    HitBy([ANYONE],FIRE)
THEN
	RESPONSE #100
		ForceSpell(Myself,ICE_GOLEM_STEAM)
END

IF TRIGGER
     RequireBlock(IsIceGolem)
     TargetBlock(EnemiesInOrderShort)
     Range(scstarget,10)
THEN DO
     Action(ForceSpell,GOLEM_CONE_OF_COLD|100|100)
END

INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/random.ssl)

