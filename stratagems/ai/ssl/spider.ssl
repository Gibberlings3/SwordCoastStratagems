//////////////////////////////////////////////////////////////////////
////         Definitions
//////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/lib/ssl/shared_definitions.ssl)


BEGIN_ACTION_DEFINITION
	Name(SpiderJump)
	TRIGGER
	        !StateCheck(scstarget,STATE_INVISIBLE)
	        !CheckStat(scstarget,1,SANCTUARY)
		!GlobalTimerNotExpired("castspell","LOCALS")
		HaveSpell(INNATE_PHASE_SPIDER_TELEPORT)
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",8)
		ForceSpell(scstarget,INNATE_PHASE_SPIDER_TELEPORT)
		Wait(1)
END

//////////////////////////////////////////////////////////////////////
////         Easy version
//////////////////////////////////////////////////////////////////////

IF TRIGGER
      TriggerBlock(Easiest)
      AttackedBy([GOODCUTOFF],DEFAULT)
      !Allegiance(Myself,ENEMY)
THEN DO
     Action(Literal)
     Enemy()
END

IF TRIGGER
        RequireBlock(IsPhaseSpider)
        TriggerBlock(Easiest)
	See(NearestEnemyOf(Myself))
	!Range(NearestEnemyOf(Myself),7)
	HaveSpell(INNATE_PHASE_SPIDER_TELEPORT)
THEN DO
                Action(Literal)
		ForceSpell(NearestEnemyOf(Myself),INNATE_PHASE_SPIDER_TELEPORT)
END

IF TRIGGER
        RequireBlock(IsPhaseSpider)
        TriggerBlock(EasyMinus)
	See(NearestEnemyOf(Myself))
	HaveSpell(INNATE_PHASE_SPIDER_TELEPORT)
THEN DO
        Action(Literal)
		ForceSpell(Player1,INNATE_PHASE_SPIDER_TELEPORT)
	RESPONSE #100
		ForceSpell(Player2,INNATE_PHASE_SPIDER_TELEPORT)
	RESPONSE #100
		ForceSpell(Player3,INNATE_PHASE_SPIDER_TELEPORT)
	RESPONSE #100
		ForceSpell(Player4,INNATE_PHASE_SPIDER_TELEPORT)
	RESPONSE #100
		ForceSpell(Player5,INNATE_PHASE_SPIDER_TELEPORT)
	RESPONSE #100
		ForceSpell(Player6,INNATE_PHASE_SPIDER_TELEPORT)
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

IF TRIGGER
      TriggerBlock(Easiest)
      See(NearestEnemyOf(Myself))
      InWeaponRange(LastSeenBy(Myself))
THEN DO
      Action(Literal)
      AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF TRIGGER
      TriggerBlock(Easiest)
      See(NearestEnemyOf(Myself))
THEN DO
      Action(Literal)
      MoveToObject(NearestEnemyOf(Myself))
END

IF TRIGGER
      TriggerBlock(Easiest)
	AttackedBy([ANYONE],DEFAULT)
THEN DO
      Action(Literal)
      Attack(LastAttackerOf(Myself))
END

IF TRIGGER
      TriggerBlock(Easiest)
THEN DO
     Action(EndScript)
END



//////////////////////////////////////////////////////////////////////
/////      Swap weapon
//////////////////////////////////////////////////////////////////////

IF TRIGGER
     HasItem("%tutu_var%spidgi1",Myself)
     TriggerBlock(HardPlus)
THEN DO
           Action(Literal)
           DestroyItem("%tutu_var%spidgi1")
           GiveItemCreate("dw#spbit",Myself,1,1,0)
           FillSlot(SLOT_WEAPON)
           EquipMostDamagingMelee()
           Continue()
END

IF TRIGGER
     HasItem("dw#spbit",Myself)
     TriggerBlock(CoreMinus)
THEN DO
           Action(Literal)
           DestroyItem("dw#spbit")
           GiveItemCreate("%tutu_var%spidgi1",Myself,1,1,0)
           FillSlot(SLOT_WEAPON)
           EquipMostDamagingMelee()
           Continue()
END



//////////////////////////////////////////////////////////////////////
////         Setup (basically from dw#melee)
//////////////////////////////////////////////////////////////////////

VARIABLE(scscloudmelee=!Range([PC],8))
INCLUDE FILE(%scsroot%/genai/ssl/initial.ssl)
VARIABLE(scscloudmelee=See([PC])!Range([PC],8))
VARIABLE(scscloudresponse=MoveToObject([PC]))
INCLUDE FILE(%scsroot%/genai/ssl/fleecloud.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/cloud_stop.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)

//////////////////////////////////////////////////////////////////////
////         Phase-spider part
//////////////////////////////////////////////////////////////////////

IF
        RequireBlock(IsPhaseSpider)
	!GlobalTimerNotExpired("DMWWphase","GLOBAL")
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobalTimer("DMWWphase","GLOBAL",100)
		Continue()
END

IF
        RequireBlock(IsPhaseSpider)
	!StateCheck(Myself,STATE_DISABLED)
	GlobalTimerNotExpired("DMWWphase","GLOBAL")
	!GlobalTimerNotExpired("heard","LOCALS")
	!See(NearestEnemyOf(Myself))
	HaveSpell(INNATE_PHASE_SPIDER_TELEPORT)
	BEGIN LOOP(scsloopvar||1;2;3;4;5;6)
		InMyArea(Playerscsloopvar)
		HPPercentGT(Playerscsloopvar,0)
		!StateCheck(Playerscsloopvar,STATE_INVISIBLE)
		!CheckStat(Playerscsloopvar,1,SANCTUARY)
	END LOOP
THEN
	BEGIN LOOP(scsloopvar||1;2;3;4;5;6)
		RESPONSE #50
			SetGlobalTimer("castspell","LOCALS",8)
			ForceSpell(Playerscsloopvar,INNATE_PHASE_SPIDER_TELEPORT)
	END LOOP
	RESPONSE #600
		SetGlobalTimer("heard","LOCALS",1000)
END

IF
        RequireBlock(IsPhaseSpider)
	!StateCheck(Myself,STATE_DISABLED)
	GlobalTimerNotExpired("DMWWphase","GLOBAL")
	!GlobalTimerNotExpired("heard","LOCALS")
	!See(NearestEnemyOf(Myself))
	HaveSpell(INNATE_PHASE_SPIDER_TELEPORT)
	BEGIN LOOP(scsloopvar||1;2;3)
		InMyArea(Playerscsloopvar)
		HPPercentGT(Playerscsloopvar,0)
		!StateCheck(Playerscsloopvar,STATE_INVISIBLE)
		!CheckStat(Playerscsloopvar,1,SANCTUARY)
	END LOOP
	BEGIN LOOP(scsloopvar||4;5;6)
	OR(4)
		!InMyArea(Playerscsloopvar)
		!HPPercentGT(Playerscsloopvar,0)
		StateCheck(Playerscsloopvar,STATE_INVISIBLE)
		CheckStat(Playerscsloopvar,1,SANCTUARY)
	END LOOP
THEN
	BEGIN LOOP(scsloopvar||1;2;3)
		RESPONSE #100
			SetGlobalTimer("castspell","LOCALS",8)
			ForceSpell(Playerscsloopvar,INNATE_PHASE_SPIDER_TELEPORT)
	END LOOP
	RESPONSE #600
		SetGlobalTimer("heard","LOCALS",1000)
END

IF
        RequireBlock(IsPhaseSpider)
	!StateCheck(Myself,STATE_DISABLED)
	GlobalTimerNotExpired("DMWWphase","GLOBAL")
	!GlobalTimerNotExpired("heard","LOCALS")
	!See(NearestEnemyOf(Myself))
	InMyArea(Player1)
	!StateCheck(Player1,STATE_INVISIBLE)
	!CheckStat(Player1,1,SANCTUARY)
	HaveSpell(INNATE_PHASE_SPIDER_TELEPORT)
	BEGIN LOOP(scsloopvar||2;3;4;5;6)
	OR(4)
		!InMyArea(Playerscsloopvar)
		!HPPercentGT(Playerscsloopvar,0)
		StateCheck(Playerscsloopvar,STATE_INVISIBLE)
		CheckStat(Playerscsloopvar,1,SANCTUARY)
	END LOOP
THEN
	RESPONSE #300
		SetGlobalTimer("castspell","LOCALS",8)
		ForceSpell(Player1,INNATE_PHASE_SPIDER_TELEPORT)
	RESPONSE #600
		SetGlobalTimer("heard","LOCALS",1000)
END

IF
        RequireBlock(IsPhaseSpider)
	!StateCheck(Myself,STATE_DISABLED)
	!GlobalTimerNotExpired("phase","LOCALS")
	!GlobalTimerExpired("phase","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("phase","LOCALS",350)
		Continue()
	RESPONSE #100
		SetGlobalTimer("phase","LOCALS",250)
		Continue()
	RESPONSE #100
		SetGlobalTimer("phase","LOCALS",150)
		Continue()
	RESPONSE #100
		SetGlobalTimer("phase","LOCALS",50)
		Continue()
END

IF
        RequireBlock(IsPhaseSpider)
	!StateCheck(Myself,STATE_DISABLED)
	!GlobalTimerNotExpired("castspell","LOCALS")
	See(NearestEnemyOf(Myself))
	!Range(NearestEnemyOf(Myself),7)
	HaveSpell(INNATE_PHASE_SPIDER_TELEPORT)
THEN
	RESPONSE #50
		SetGlobalTimer("castspell","LOCALS",8)
		ForceSpell(WorstAC,INNATE_PHASE_SPIDER_TELEPORT)
	RESPONSE #50
		SetGlobalTimer("castspell","LOCALS",8)
		ForceSpell(WeakestOf,INNATE_PHASE_SPIDER_TELEPORT)
	RESPONSE #50
		SetGlobalTimer("castspell","LOCALS",8)
		ForceSpell(MostDamagedOf,INNATE_PHASE_SPIDER_TELEPORT)
END

IF TRIGGER
        RequireBlock(IsPhaseSpider)
	Target(WorstAC)
	Target(WeakestOf)
	Target(MostDamagedOf)
	!Range(scstarget,5)
	!StateCheck(Myself,STATE_DISABLED)
	HPPercentGT(scstarget,0)
THEN DO
	Action(SpiderJump|100|800)
	OnContinue(SetGlobalTimer("castspell","LOCALS",8))
END


//////////////////////////////////////////////////////////////////////
////         Use webs et al
//////////////////////////////////////////////////////////////////////

IF TRIGGER
  RequireBlock(HasWeb)
  TriggerBlock(CorePlus)
  !StateCheck(Myself,STATE_DISABLED)
  HaveSpell(INNATE_SPIDER_WEB)
  See(NearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  !GlobalTimerNotExpired("web","LOCALS")
THEN DO
     Action(Literal)
    ForceSpell(NearestEnemyOf(Myself),INNATE_SPIDER_WEB)
    Continue()
  RESPONSE #100
    ForceSpell(SecondNearestEnemyOf(Myself),INNATE_SPIDER_WEB)
    Continue()
  RESPONSE #100
    ForceSpell(ThirdNearestEnemyOf(Myself),INNATE_SPIDER_WEB)
    Continue()
  RESPONSE #100
    ForceSpell(FourthNearestEnemyOf(Myself),INNATE_SPIDER_WEB)
    Continue()
  RESPONSE #300
    SetGlobalTimer("web","LOCALS",6)
    Continue()
END

IF TRIGGER
  RequireBlock(HasWeb)
  TriggerBlock(CorePlus)
  !StateCheck(Myself,STATE_DISABLED)
  HaveSpell(INNATE_SPIDER_WEB)
  See(NearestEnemyOf(Myself))
  !GlobalTimerNotExpired("web","LOCALS")
THEN DO
     Action(Literal)
    ForceSpell(NearestEnemyOf(Myself),INNATE_SPIDER_WEB)
    Continue()
    RESPONSE #100
    SetGlobalTimer("web","LOCALS",6)
    Continue()
END

IF
    RequireBlock(IsLasharra)
    See(NearestEnemyOf(Myself))
    !GlobalTimerNotExpired("castspell","LOCALS")
    !GlobalTimerNotExpired("summons","LOCALS")
THEN
    RESPONSE #100
       SetGlobalTimer("castspell","LOCALS",6)
       SetGlobalTimer("summons","LOCALS",1000)
       ApplySpell(Myself,WIZARD_SPELL_TURNING)
       ForceSpellRES("senspisu",Myself)
END

IF
   RequireBlock(IsLasharra)
   See(NearestEnemyOf(Myself))
   HPPercentLT(Myself,60)
   !GlobalTimerNotExpired("castspell","LOCALS")
   !Global("heal","LOCALS",2)
THEN 
   RESPONSE #100
      IncrementGlobal("heal","LOCALS",1)
      SetGlobalTimer("castspell","LOCALS",6)
      ForceSpellRES("senspihe",Myself)
END


IF
   RequireBlock(IsVortexSpider)
   Range(NearestEnemyOf(Myself),10)
   !GlobalTimerNotExpired("vortexweb","LOCALS")
    !GlobalTimerNotExpired("castspell","LOCALS")
THEN
    RESPONSE #100
    SetGlobalTimer("castspell","LOCALS",6)
    SetGlobalTimer("vortexweb","LOCALS",18)
    ReallyForceSpell(Myself,VORTEX_WEB)
    RESPONSE #50
    Continue()
END

IF TRIGGER
    RequireBlock(IsVortexSpider)
    TargetBlock(EnemiesInOrderShort)
    TriggerBlock(Enemy|Disabled)
    !StateCheck(Myself,STATE_DISABLED)
    HaveSpell(INNATE_SPIDER_WEB_AREA)
    !GlobalTimerNotExpired("castspell","LOCALS")
THEN DO
    Action(Literal|100|200)
    SetGlobalTimer("castspell","LOCALS",6)
    ForceSpell(scstarget,INNATE_SPIDER_WEB_AREA)
END




//////////////////////////////////////////////////////////////////////
////         Melee
//////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////
////         If anyone nearby is webbed and isn't already poisoned, bite'em
//////////////////////////////////////////////////////////////////////


IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   !StateCheck(scstarget,STATE_POISONED)
   CheckStat(scstarget,1,HELD)
   Range(scstarget,4)
THEN DO
     Action(Attack)
END

//////////////////////////////////////////////////////////////////////
////         Attack anyone else in range indiscriminately
//////////////////////////////////////////////////////////////////////


IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,4)
THEN DO
     Action(Attack)
END

//////////////////////////////////////////////////////////////////////
////         Chase down anyone in a web who's half-way close
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	Range(scstarget,20)
        CheckStat(scstarget,1,HELD)
THEN DO
	Action(MoveIfEmpty)
END

//////////////////////////////////////////////////////////////////////
////        Kill anyone hostile
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(NearestEnemyOf(Myself))
THEN DO
	Action(MoveIfEmpty)
END

//////////////////////////////////////////////////////////////////////
////        Kill PCs
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PlayersInRandomOrder)
	Allegiance(Myself,ENEMY)
THEN DO
	Action(Attack)
END

//////////////////////////////////////////////////////////////////////
////        Closedown
//////////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
