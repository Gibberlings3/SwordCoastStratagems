DEFINE_ACTION_FUNCTION spider BEGIN

     //// make spells

     // phase-spider jump

     LAF make_innate_repeating_spell STR_VAR arguments="PHASE_SPIDER_TELEPORT=>INNATE_PHASE_SPIDER_TELEPORT" END
     MAKE_PATCH
        patch_effect_inline=>"match=>~opcode=124~ timing=>1 duration=>0"
        patch_ability_inline=>"casting_time=>0"
     END
     LAF edit_spell STR_VAR spell=INNATE_PHASE_SPIDER_TELEPORT editstring=~patch_effect=>patch_data~ END

     // single-target web
     
     // start by doing some fixes to the FIGURINE_SPIDER_WEB spell (already done on BG2EE)
     
     ACTION_IF !GAME_IS ~eet bg2ee~ BEGIN
       MAKE_PATCH
           patch_effect_inline=>~duration=>"if duration>0 then 12 else no_change"~
           patch_effect_inline'=>~match=>opcode=142 parameter2=>129~
       END
     LAF edit_spell STR_VAR spell=FIGURINE_SPIDER_WEB edits=patch_data END
     END

     LAF make_innate_repeating_spell INT_VAR cooldown=12 STR_VAR arguments="FIGURINE_SPIDER_WEB=>INNATE_SPIDER_WEB" END
     MAKE_PATCH
        not_magical=>1
        patch_ability_inline=>"casting_time=>0"
        patch_effect_inline=>"match=>target=2 dispel=>0 savebonus=>0 save_vs_spell=>0 save_vs_breath=>1"

     END
     LAF edit_spell STR_VAR spell=INNATE_SPIDER_WEB edits=patch_data END

     // multitarget web
     
     LAF make_innate_repeating_spell INT_VAR cooldown=18 STR_VAR arguments="WIZARD_WEB=>INNATE_SPIDER_WEB_AREA" END
     MAKE_PATCH
        not_magical=>1
        patch_ability_inline=>"casting_time=>0"
        patch_effect_inline=>"match=>target=2 dispel=>0 savebonus=>~-4~ save_vs_spell=>0 save_vs_breath=>1"
     END
     LAF edit_spell STR_VAR spell=INNATE_SPIDER_WEB_AREA edits=patch_data END

     // modified bite

     LAF edit_item STR_VAR item="%tutu_var%spidgi1" editstring="category=>19" END // give spider bite a weapon category so FillSlot recognises it
     MAKE_PATCH
        delete_effect=>1
        add_effect_inline=>"opcode=>25 target=>2 parameter1=>3 parameter2=>3 duration=>37 save_vs_poison=>1"
        add_effect_inline'=>"opcode=>109 target=>2 parameter2=>2 duration=>37 save_vs_poison=>1"
        add_effect_inline'''=>"opcode=>142 target=>2 parameter2=>129 duration=>37 save_vs_poison=>1"
        add_effect_inline''''=>"opcode=>142 target=>2 parameter2=>6 duration=>37 save_vs_poison=>1"
        add_effect_inline'''''=>"opcode=>157 target=>2 duration=>37 save_vs_poison=>1"
     END
     LAF clone_item STR_VAR item="%tutu_var%spidgi1=>dw#spbit" edits=patch_data END

     // compile scripts
     
     LAF define_difficulty STR_VAR type=spider RET difficulty_variable END

     LAF ssl_to_bcs STR_VAR script=spider location=~ssl~  rename_to=dw#spids END
     LAF ssl_to_bcs STR_VAR script=spider location=~ssl~ variables=~HasWeb=True~ rename_to=dw#spidg END
     LAF ssl_to_bcs STR_VAR script=spider location=~ssl~ variables=~IsPhaseSpider=True~ rename_to= ~%tutu_var%pspider~ END

     ACTION_IF is_bg2 BEGIN
          LAF ssl_to_bcs STR_VAR script=spider location=~ssl~ variables=~IsPhaseSpider=True&IsVortexSpider=True&IsLasharra=True~ rename_to= ~senspi01~ END
          LAF ssl_to_bcs STR_VAR script=spider location=~ssl~ variables=~IsPhaseSpider=True&IsVortexSpider=True~ rename_to= ~spidvo01~ END
     END

     // give out web scripts and spells

     ACTION_IF is_bg1 BEGIN
       LAF edit_creature STR_VAR creature= ~%tutu_scripts%pidland %tutu_var%spidhu %tutu_var%spidsw~ editstring=~strip_script=>"%tutu_var%wtasight" strip_scs_scripts=>null strip_script=>dw#spids insert_script_low=>dw#spids~ END
       LAF edit_creature STR_VAR creature=~%tutu_scripts%pidphas %tutu_var%spidph~ editstring=~strip_script=>"%tutu_var%wtasight" strip_scs_scripts=>null add_spells=>INNATE_PHASE_SPIDER_TELEPORT~ END
       LAF edit_creature STR_VAR tv=1  creature=~spidgi spidwr~  editstring=~strip_script=>"%tutu_var%wtasight" strip_scs_scripts=>null strip_script=>dw#spidg insert_script_low=>dw#spidg add_spells=>INNATE_SPIDER_WEB~ END
     END
     ACTION_IF is_bg2 BEGIN
       LAF edit_creature STR_VAR creature=~sensmsp spidhu01 spidsm01 spidsm02 spidsw01 spspid07 spspider~ editstring=~strip_scs_scripts=>null insert_script_low=>dw#spids~ END
       LAF edit_creature STR_VAR creature=~spidph01 spspid06 senspi01~ editstring=~strip_script=>wtasight strip_scs_scripts=>null add_spells=>INNATE_PHASE_SPIDER_TELEPORT~ END
       LAF edit_creature STR_VAR creature=~spidvo01 smspid02~ editstring=~strip_script=>wtasight strip_scs_scripts=>null add_spells=>"INNATE_PHASE_SPIDER_TELEPORT INNATE_SPIDER_WEB_AREA"~ END
       LAF edit_creature STR_VAR creature=~spidgi01 spspid05 spidwr01~  editstring=~strip_script=>wtasight strip_scs_scripts=>null insert_script_low=>dw#spidg add_spells=>INNATE_SPIDER_WEB~ END
     END



     // BG1=specific

     ACTION_IF is_bg1 BEGIN
               
             LAF edit_creature STR_VAR creature=centeo tv=1 editstring=~remove_items=>antiweb add_items=>antiweb~ END


     
     END


END
