INCLUDE ~%scsroot%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION doppelganger BEGIN

     LAF define_difficulty STR_VAR type=doppelganger RET difficulty_variable END

     LAF doppelganger_core END
     LAF doppelganger_assassins END

END


DEFINE_ACTION_FUNCTION doppelganger_core BEGIN
     // lesser doppelgangers are based on DOPPLE


     MAKE_PATCH
        ac_naturalLT=>2
        ac_effectiveLT=>2
        save_vs_deathLT=>4
        save_vs_wandsLT=>6
        save_vs_polyLT=>5
        save_vs_breathLT=>6
        save_vs_spellLT=>4
        thac0LT=>9
        attacksGT=>2
        hp_currentGT=>31
        hp_maxGT=>31
        alignment=>NEUTRAL_EVIL
     END
     LAF clone_creature STR_VAR creature=~%tutu_var%doppss=>dw#dopss %tutu_var%dopzorl=>dw#dopzo~ edits=patch_data END

     // greater doppelgangers are based on DOPPGR, and get DOPSPELL

     MAKE_PATCH
        ac_naturalLT=>0
        ac_effectiveLT=>0
        save_vs_deathLT=>2
        save_vs_wandsLT=>2
        save_vs_polyLT=>2
        save_vs_breathLT=>2
        save_vs_spellLT=>3
        thac0LT=>7
        attacksGT=>4
        hp_currentGT=>65
        hp_maxGT=>65
        strip_script=>~%tutu_scriptd%opspell~
        insert_script_high=>~%tutu_scriptd%opspell~
        remove_items=>~mage04 mage06~
        add_items=>~mage04 mage06~
        alignment=>NEUTRAL_EVIL
     END
     LAF clone_creature STR_VAR creature=~%tutu_var%gdopp7=>dw#dop7
                                          %tutu_var%gdopp71=>dw#dop71 
                                          %tutu_var%gdopp72=>dw#dop72 
                                          %tutu_var%gdopp73=>dw#dop73 
                                          %tutu_var%gdopp74=>dw#dop74
                                          %tutu_var%gdopp75=>dw#dop75
                                          %tutu_var%gdopp7=>dw#hdp7
                                          %tutu_var%gdopp71=>dw#hdp71
                                          %tutu_var%gdopp72=>dw#hdp72
                                          %tutu_var%gdopp73=>dw#hdp73
                                          %tutu_var%gdopp74=>dw#hdp74
                                          %tutu_var%gdopp75=>dw#hdp75~ 
     edits=patch_data
     END

     // clone the doppleganger-making spells



     // DOPPLEGANGER_CHANGEX, for X in [3,8], makes gdopp7 through gdopp75. Only the "nobles" with Sarevok use this.

     LAF ssl_to_bcs STR_VAR script=noblpa location=ssl rename_to=~%tutu_var%noblpa~ END
     ACTION_FOR_EACH dopnum IN 3 4 5 6 7 8 BEGIN
        OUTER_SET dopnum2 = dopnum - 3
        ACTION_IF dopnum2= 0 BEGIN
           OUTER_SPRINT dopnum2 ~~
        END
        OUTER_SPRINT temp "DOPPLEGANGER_CHANGE%dopnum%"
        OUTER_SPRINT oldspell EVALUATE_BUFFER ~%%temp%%~
        OUTER_SPRINT newspell ~dw#dop7%dopnum2%~
        OUTER_SPRINT newspell2 ~dw#hdp7%dopnum2%~
        LAF clone_spell STR_VAR spell=~%oldspell%=>%newspell%~ END
        LAF clone_spell STR_VAR spell=~%oldspell%=>%newspell2%~ END
        LAF swap_text STR_VAR files=~%newspell%.spl~ swaps=~%tutu_var%gdopp7%dopnum2%=>dw#dop7%dopnum2%~ END
        LAF swap_text STR_VAR files=~%newspell2%.spl~ swaps=~%tutu_var%gdopp7%dopnum2%=>dw#hdp7%dopnum2%~ END
        LAF clone_script STR_VAR script=~%tutu_var%noblpa=>%tutu_var%noblpa%dopnum2%~ END
        LAF swap_text STR_VAR files=~%tutu_var%noblpa%dopnum2%.bcs~ swaps=~CHANGE3=>CHANGE%dopnum% dop7=>dop7%dopnum2% hdp7=>hdp7%dopnum2%~ END
     END

     // DOPPLEGANGER_CHANGE_DEFAULT_LESSER already makes DOPPLE, the full-strength "lesser" doppleganger, so can be left alone
     // DOPPLEGANGER_CHANGE_DEFAULT already makes DOPPGR, likewise
     //
     // DOPPLEGANGER CHANGE, used only in DOPPSS.bcs, makes weak dopplegangers DOPPSS

     LAF ssl_to_bcs STR_VAR script=doppss location=~ssl~ rename_to=~%tutu_var%doppss~ END

     LAF clone_spell STR_VAR spell=~%DOPPLEGANGER_CHANGE%=>dw#dopss~ END
     LAF swap_text STR_VAR files=dw#dopss.spl swaps=~%tutu_var%doppss=>dw#dopss~ END

     // DOPPLEGANGER CHANGE2, used only in ZORL and IRLENTRE, makes weak dopplegangers DOPZORL
     LAF ssl_to_bcs STR_VAR script=zorl location=ssl rename_to= ~%tutu_var%zorl~ END
     LAF ssl_to_bcs STR_VAR script=irlentre location=ssl rename_to= ~%tutu_scripti%rlentre~ END

     LAF clone_spell STR_VAR spell=~%DOPPLEGANGER_CHANGE2%=>dw#dopzo~ END
     LAF swap_text STR_VAR files=dw#dopzo.spl swaps=~%tutu_var%dopzorl=>dw#dopzo~ END

     LAF get_area_script STR_VAR area="%SWBaldursGate_MerchantLeague_L3%" RET script END
     LAF edit_script STR_VAR script editstring="patch=>delete_action_if(dopzorl)" END
     LAF extend STR_VAR script top=merchant_league_roof ssl=yes location=ssl END



END

DEFINE_ACTION_FUNCTION doppelganger_assassins BEGIN

     // make gdopp75 into a mage
     LAF check_label STR_VAR label=dw#mage RET value END
     ACTION_IF value BEGIN
      MAKE_PATCH
        level=>9
        class=>MAGE
        kit=>CONJURER
        enforce_mage=>~game_is_bg1 accept_level~
       strip_script=>~%tutu_scriptd%opspell~
        add_items=>dw#nopre
        strip_script=>~%tutu_var%sardopp~
      END
         OUTER_SPRINT $patch_data(say_both_names) 16042
      LAF edit_creature STR_VAR creature=dw#hdp75 edits=patch_data END
     END

     // make gdopp74 into a priest
     LAF check_label STR_VAR label=dw#priest RET value END
     ACTION_IF value BEGIN
      MAKE_PATCH
        level=>9
        class=>CLERIC
        enforce_priest=>~game_is_bg1 accept_level~
       strip_script=>~%tutu_scriptd%opspell~
        add_items=>dw#nopre
        strip_script=>~%tutu_var%sardopp~
      END
         OUTER_SPRINT $patch_data(say_both_names) 16043
      LAF edit_creature STR_VAR creature=dw#hdp74 edits=patch_data END
     END
     
     // make gdopp73 into a thief
     
     MAKE_PATCH
       level=>8
       class=>THIEF
       kit=>ASSASIN
       strip_scs_scripts=>null
       insert_script_low=>dw1melas
       strip_script=>~%tutu_scriptd%opspell %tutu_var%sardopp~
       add_items=>~%tutu_var%potn10(2,inv)~
     END
         OUTER_SPRINT $patch_data(say_both_names) 16044
     LAF edit_creature STR_VAR creature=~dw#hdp73~ edits=patch_data END
     ACTION_IF !FILE_EXISTS_IN_GAME ~dw1melas.bcs~ BEGIN
        LAF build_genai_script STR_VAR script=dw1melas END
     END

     // compile Sarevok-doppelganger script

     LAF ssl_to_bcs STR_VAR script=sardopp location=~ssl~ END

     // Flag Belt, Liia Jannath, and the Flaming Fist as GOODBUTBLUE, and the former 2 as confusion/hold-proof
     
     MAKE_PATCH
        add_items=>~ring05~
        immunity_to_opcode=>~5 241 24 148~
        allegiance=>GOODBUTBLUE
     END
     LAF edit_creature STR_VAR creature=~belt liia~ edits=patch_data tv=1 END
     LAF edit_creature STR_VAR creature=flam10 tv=1 editstring=~allegiance=>GOODBUTBLUE~ END

     // Belt and Liia need to be more forgiving
     
     LAF swap_text STR_VAR files=~belt.bcs liia.bcs~ tv=1 swaps=~AttackedBy(\[PC\],DEFAULT)=>False()~ END

END

