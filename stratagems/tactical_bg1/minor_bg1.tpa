INCLUDE ~%MOD_FOLDER%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION minor_bg1 BEGIN

    LAF define_difficulty STR_VAR type=genai RET difficulty_variable END


       ACTION_FOR_EACH function IN laurel cythandria slythe jenkal minor_misc BEGIN
		  LAF ~%function%~ END
       END



END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////  Makes the paladin's gibberling hunt slightly less ridiculous...
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION laurel BEGIN

     LAF install STR_VAR file=laureladd.d location=resource END
     LAF extend_area_script INT_VAR ssl=1 STR_VAR area= ~%BearRiver%~ bottom=laureladd location=ssl END

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////// Cythandria: she's too smart to get a couple of ogres as all her help against the party. Replace them with stone golems
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION cythandria BEGIN

   ACTION_IF enhanced_edition BEGIN
        LAF install STR_VAR file="golsto01.cre golstone.itm golsto.itm golsto01.bcs" location=resource END
   END
   MAKE_PATCH
           say_both_names=>21113
           dv=>arghh
           add_effect_inline=>"opcode=>120"
           remove_items=>immune2
   END
   LAF clone_creature STR_VAR creature=~golsto01=>%tutu_var%arghh~ edits=patch_data END
   MAKE_PATCH
           say_both_names=>21114
           dv=>ughh
           add_effect_inline=>"opcode=>120"
           remove_items=>immune2
   END
   LAF clone_creature STR_VAR creature=~golsto01=>%tutu_var%ughh~ edits=patch_data END

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////// Slythe: one more level gets him to quadruple-damage territory.
///////////// Also, he has a Potion of Magic Blocking and a cloak of non-detection
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION slythe BEGIN

       MAKE_PATCH
          level=>9
          add_items=>~%tutu_var%potn34 %tutu_var%clck06~
          enforce_charclass=>is_bg1
       END
       LAF edit_creature INT_VAR tv=1 STR_VAR creature=slythe edits=patch_data END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Jenkal (the halfling traitor): raise him from 4/4 to 6/6; let him flee the battle
// and turn up in the ogre mage's home, and in due course with the sewer ogre mage
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION jenkal BEGIN
       MAKE_PATCH
          level=>6
          level2=>6
          insert_script_high=>dw#jenka
          xp_value=>650
          enforce_charclass=>is_bg1
       END
       LAF edit_creature INT_VAR tv=1 STR_VAR creature=jenkal edits=patch_data END

       LAF install STR_VAR files=~dw#jenka.baf jenkal.d~ location=resource END
       
       LAF extend_area_script STR_VAR area=~%FirewineRuins%~ bottom=ar5201_jenkal location=resource END
       LAF extend_area_script STR_VAR area=~%ESewers%~ bottom=ar0226_jenkal location=resource END



       MAKE_PATCH
          match=>"trigger_dest=%FirewineRuins%"
          say_trigger_info=>22507
          trigger_type=>1
       END
       LAF edit_area STR_VAR area=~%Gullykin_JenkalsHouse_Cellar%~ editstring=~patch_trigger=>patch_data~ END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////  misc. level adjustments
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION minor_misc BEGIN


     // Raises Tamoko's level slightly
     LAF edit_creature INT_VAR tv=1 STR_VAR creature=tamoko  editstring=~level=>14 enforce_charclass=>is_bg1~ END

     // Sunin: is a self-defined "master mage", ought to be kinda more impressive....
     LAF edit_creature INT_VAR tv=1 STR_VAR creature=sunin editstring=~level=>11 enforce_charclass=>is_bg1~ END

     // Hairtooth and Gnarl become 5th level fighters: +18 hp, level 5 THAC0 16. And they get strength 18/95
     LAF edit_creature INT_VAR tv=1 STR_VAR creature=~hairto gnarl~ editstring=~level=>5 str=>18 str_ex=>95 enforce_charclass=>is_bg1~ END

     // Micro-tweak to the Amazons: raise the archers' levels to 5 to buy sleep immunity
     LAF edit_creature INT_VAR tv=1 STR_VAR creature=~maneir telka~ editstring=~level=>5 enforce_charclass=>is_bg1~ END


END


