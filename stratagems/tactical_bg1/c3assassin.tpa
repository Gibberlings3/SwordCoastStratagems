DEFINE_ACTION_FUNCTION c3assassin BEGIN

   LAF c3assassin_shared END

   ACTION_IF GAME_IS bgt BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME ~fw01230.bcs~ BEGIN
         OUTER_SPRINT version new
      END ELSE BEGIN
         OUTER_SPRINT version old
      END
   END ELSE BEGIN
         OUTER_SPRINT version new
   END
   
   LAF check_ini STR_VAR ini=Force_Old_Assassin_Spawn RET value END
   ACTION_IF value BEGIN
      OUTER_SPRINT version old
   END

   LAF ~c3assassin_%version%~ END

END

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION c3assassin_shared BEGIN


   // delete the base parties
   LAF edit_area STR_VAR area=~%Gullykin%~ editstring="delete_actor=>~actor_resource is_in [%tutu_var%drakar %tutu_var%halaca %tutu_var%morvin %tutu_var%molkar]~" END
   LAF edit_area STR_VAR area=~%ValleyoftheTombs%~ editstring="delete_actor=>~actor_resource is_in [%tutu_var%lamalh %tutu_var%zeela %tutu_var%telka %tutu_var%maneir]~" END

   // sort out allegiance

   LAF edit_creature INT_VAR tv=1 STR_VAR creature=~lamalh molkar~ editstring=~allegiance=>NEUTRAL strip_script=>"%tutu_var%shout dw#gpsht"~ END
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=~maneir telka zeela drakar halaca morvin~ editstring=~allegiance=>ENEMY strip_script=>"%tutu_var%shout dw#gpsht"~ END

   // script tweak

   LAF swap_text INT_VAR tv=1 STR_VAR files=~molkar.bcs lamalha.bcs~ swaps=~SmallWait(2)=>SmallWait(2)FaceObject([PC])~ END

   // create scripts for assassins
   
   LAF ssl_to_bcs STR_VAR script=dw#c3as1 location=ssl END
   LAF clone_script STR_VAR script=~dw#c3as1=>dw#c3as2~ END
   LAF swap_text STR_VAR files=dw#c3as2.bcs swaps=~dmwwC3asshostileA=>dmwwC3asshostileB~ END

   // install scripts

   LAF edit_creature INT_VAR tv=1 STR_VAR creature=~drakar halaca morvin~ editstring=~insert_script_high=>dw#c3as1~ END
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=molkar editstring=~insert_script=>"dw#c3as1 after %tutu_var%molkar"~ END
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=~maneir telka zeela~ editstring=~insert_script_high=>dw#c3as2~ END
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=lamalh editstring=~insert_script=>"dw#c3as2 after %tutu_var%lamalha"~ END


END

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION c3assassin_old BEGIN

   // sort out dialogs
   LAF swap_text STR_VAR files="%tutu_var%molkar.dlg %tutu_var%lamalh.dlg" swaps=~ActionOverride("\([^"]*\)",Enemy())=>CreateCreatureOffscreen("%tutu_var%\1",0)~ END

   // extend the main script
   LAF extend STR_VAR file=baldur bottom=assassinadd location=ssl ssl=yes variables="OldVersion=True" END

END

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION c3assassin_new BEGIN

<<<<<<<< .../stratagems-inline/assassin_data.2da
area                 xloc_1 yloc_1 orient_1 xloc_2 yloc_2 orient_2 xloc_3 yloc_3 orient_3 xloc_4 yloc_4 orient_4
Encounter_Plains1    495    310    0        779    308    0        340    528    0        939    563    0
Encounter_Plains2    923    255    0        533    279    0        346    625    0        816    742    0
Encounter_Canyon1    632    310    0        597    346    0        565    382    0        653    363    0
Encounter_Canyon2    456    193    0        513    253    0        330    335    0        617    326    0
Encounter_Cliff1     579    143    0        821    193    0        919    150    0        688    180    0
Encounter_Cliff2     814    202    0        899    226    0        714    262    0        570    198    0
Encounter_Grassland1 382    656    0        468    374    0        662    342    0        898    561    0
Encounter_Grassland2 575    260    0        371    575    0        451    714    0        646    733    0
Encounter_Forest2    710    478    0        569    592    0        884    525    0        824    684    0
Encounter_Road       382    656    0        468    374    0        662    342    0        898    561    0
>>>>>>>>
 
    DEFINE_ACTION_FUNCTION c3assassin_helper 
        STR_VAR area="" xloc_1="" yloc_1="" orient_1="" xloc_2="" yloc_2="" orient_2="" xloc_3="" yloc_3="" orient_3="" xloc_4="" yloc_4="" orient_4="" 
    BEGIN
       LAF extend_area_script STR_VAR area="%%area%%" top=assassin_random variables="OldVersion=False" location=resource END
    END

    LAF process_table STR_VAR table="assassin_data.2da" inline=yes function=c3assassin_helper END

   // extend the main script

   LAF extend STR_VAR file=baldur bottom=assassinadd location=ssl ssl=yes variables="OldVersion=False" END

END


