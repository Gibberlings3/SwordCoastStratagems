INCLUDE ~%scsroot%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION c2end BEGIN

     LAF run STR_VAR file=creature_swaps END
     LAF mulahey_flunkies END
     LAF mulahey_cre END
     LAF mulahey_scripting END

END

DEFINE_ACTION_FUNCTION mulahey_flunkies BEGIN

    // kobold7 starts neutral, to avoid upsetting the dialogue with Mulahey
    
   LAF edit_creature STR_VAR creature=kobold7 tv=1 editstring= ~allegiance=>neutral insert_script_high=>%tutu_var%helpmul~ END


   // tougher clones of kobold7
   
   MAKE_PATCH
      level=>4
      enforce_thac0=>at_worst
      enforce_hp=>at_worst
      enforce_saves=>at_worst
      genai=>all
      strip_script=>~%tutu_var%helpmul~
      insert_script_high=>helpmul
   END
   LAF diff_based_clone STR_VAR creature= ~%tutu_var%kobold7~ edits=patch_data hardname=dw#mulke END

   // copies of Kobold commandoes, Kobold archers, skeletons, and skeleton archers with "helpmul"


   MAKE_PATCH
      strip_script=>~%tutu_var%helpmul~
      insert_script_high=>~%tutu_var%helpmul~
      strip_script=>~dw#kobsp~
   END
   LAF clone_creature STR_VAR creature= ~%tutu_var%kobcomm=>dw#mulkc
                                                        %tutu_var%kobolda=>dw#mulka
                                                        %tutu_var%skelet=>dw#mulsk
                                                        %tutu_scripts%kelet_c=>dw#mulsa~
                              edits=patch_data
   END

END

DEFINE_ACTION_FUNCTION mulahey_cre BEGIN

   MAKE_PATCH
       level=>7
       enforce_charclass=>is_bg1
    END
   LAF diff_based_clone STR_VAR creature= ~%tutu_var%mulahe~ hardname=~dw#mulah~ edits=patch_data END

   MAKE_PATCH
      match=>"actor_name=mulahey"
      actor_x_coord=> 494
      actor_y_coord=> 858
   END
   LAF edit_area STR_VAR area=~%NashkelMines_MulaheysCave%~ editstring=~patch_actor=>patch_data~ END



END

DEFINE_ACTION_FUNCTION mulahey_scripting BEGIN

     // externalise mulahey's calls for help to the area script
<<<<<<<< .../stratagems-inline/mulreplace.d
ALTER_TRANS ~%tutu_var%mulahe~
BEGIN 2 6 8 END
BEGIN 0 END
BEGIN
"ACTION" ~Enemy() SetGlobal("CDMulahey","%NashkelMines_MulaheysCave%",3) SetGlobal("DMWWMulaheyFight","MYAREA",1)SetGlobal("DMWWMulaheyFlunky","MYAREA",1)~  // the CDMulahey is relevant only on BGEE
END
>>>>>>>>
     LAF install STR_VAR files=mulreplace.d inline=yes END
     LAF ssl_to_bcs STR_VAR script=helpmul location=~tactical_bg1/ssl~ rename_to= ~%tutu_var%helpmul~ END
     LAF extend_area_script STR_VAR area= ~%NashkelMines_MulaheysCave%~ bottom=mularea location=ssl ssl=yes END

END
