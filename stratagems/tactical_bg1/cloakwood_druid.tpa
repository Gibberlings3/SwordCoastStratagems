INCLUDE ~%scsroot%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION cloakwood_druid BEGIN

       ACTION_IF FILE_EXISTS_IN_GAME "dw#gpsht.bcs" BEGIN
          OUTER_SPRINT help_script dw#gpsht
       END ELSE BEGIN
          OUTER_SPRINT help_script shout
       END

       // make flunky-summoning script

<<<<<<<< .../stratagems-inline/dw#takiy.baf
IF
	!Global("SpawnedApprentices","LOCALS",1)
THEN
	RESPONSE #100
		CreateCreature("dw#takiy",[-1.-1],0)
		CreateCreature("dw#takiy",[-1.-1],0)
		SetGlobal("SpawnedApprentices","LOCALS",1)
END
>>>>>>>>

       LAF install STR_VAR file=dw#takiy.baf inline=yes END
       ACTION_FOR_EACH druid IN izefi amara BEGIN
          LAF clone_script STR_VAR script=~dw#takiy=>dw#%druid%~ END
          LAF swap_text STR_VAR files= ~dw#%druid%.bcs~ swaps=~takiy=>%druid%~ END
       END

       // takiyah: L9, DV, flunky script
       
       MAKE_PATCH
          level=>9
          enforce_charclass=>is_bg1
          insert_script_high=>"%help_script%"
          insert_script_high'=>dw#takiy
          specifics=>93
       END
       LAF edit_creature STR_VAR creature= ~%tutu_var%takiyah~ edits=patch_data editstring=~dv=>takiyah~ END

       // izefia: likewise

       MAKE_PATCH
          level=>9
          enforce_charclass=>is_bg1
          insert_script_high=>"%help_script%"
          insert_script_high'=>dw#izefi
          specifics=>93
       END
       LAF edit_creature STR_VAR creature= ~%tutu_var%izefia~ edits=patch_data editstring=~dv=>izefia~ END

       // amarande: 11th level, DV, flunky, shoutdlg

       MAKE_PATCH
          level=>11
          enforce_charclass=>is_bg1
          insert_script_high=>"%help_script%"
          insert_script_high'=>dw#amara
          specifics=>93
       END
       LAF edit_creature STR_VAR creature=~%tutu_var%amaran~ edits=patch_data editstring=~dv=>amara~ END

       // make remaining core druids 9th level

       MAKE_PATCH
          level=>9
          enforce_charclass=>is_bg1
       END
       LAF edit_creature STR_VAR creature= ~%tutu_var%corsone~ edits=patch_data END
       LAF edit_creature STR_VAR creature= ~%tutu_var%osmadi~ edits=patch_data END

       // flunkies
       
       MAKE_PATCH
          add_items=>dw#attac
          insert_script_high=>"%help_script%"
          specifics=>93
       END
       LAF clone_creature STR_VAR creature= ~%tutu_var%druid3=>dw#takiy %tutu_var%druid3=>dw#izefi~
                                  edits=patch_data
       END
       
       ACTION_FOR_EACH creature IN dw#takiy dw#izefi BEGIN
          LAF edit_creature STR_VAR creature editstring="dv=>%creature%" END
       END

       MAKE_PATCH
          add_items=>dw#attac
          insert_script_high=>"%help_script%"
          specifics=>93
          kit=>SHAPESHIFTER
          dv=>dw#amara
          add_items=>"dw#shnw1(WEAPON2)"
       END
       LAF clone_creature STR_VAR creature= ~%tutu_var%druid3=>dw#amara~
                                  edits=patch_data
       END



END

