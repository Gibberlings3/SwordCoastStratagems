INCLUDE ~%scsroot%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION c3end BEGIN

    ACTION_FOR_EACH function IN camp_cre camp_are camp_bcs tent_cre tent_are tent_bcs BEGIN
       LAF ~%function%~ END
    END





END

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION camp_cre BEGIN



/// the new bandits

     MAKE_PATCH
        strip_script=>~dw#prsht %tutu_var%hobgobc~
        insert_script_high'=>dw#prsht
        insert_script_high=>~%tutu_var%hobgobc~
        allegiance=>NEUTRAL
        dialog=>dw#hobgc
     END
     LAF clone_creature STR_VAR creature= ~%tutu_scripth%obelite=>dw#c3che~ edits=patch_data END

     MAKE_PATCH
        strip_script=>~dw#prsht %tutu_var%bandit dw#swpup~
        insert_script_high'=>dw#prsht
        insert_script_high=>~%tutu_var%bandit~
        allegiance=>NEUTRAL
        dialog=>~%tutu_var%bandit~
     END
     LAF clone_creature STR_VAR creature= ~%tutu_scripti%ronelit=>dw#c3cbt~ edits=patch_data END


// Give hobgoblins something to say

   LAF install STR_VAR file=dw#hobgc.d location=resource END
   LAF edit_creature STR_VAR creature= ~dw#c3che %tutu_var%hobgobc~ editstring=~dialog=>dw#hobgc~ END

// Block Credus's dialog if camp is hostile


   <<<<<<<< .../stratagems-inline/dw#credu.baf
      IF
        !Global("JoinedBandits","GLOBAL",1)
        Range([PC],15)
        See([PC])
	NumTimesTalkedTo(0)
THEN
	RESPONSE #100
		Dialogue([PC])
END
   >>>>>>>>

    LAF install STR_VAR files=~dw#credu.baf~ inline=yes END
    LAF edit_creature STR_VAR creature=credus tv=1 editstring=~swap_script=>"initdlg=>dw#credu"~ END

END

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION camp_are BEGIN // run after camp_cre
   

   MAKE_PATCH
   // party-required for tent entrance
   match=>"trigger_name is_in [Door8701 Door1901]"
   party_required=>1
   // new bandits
      add_actor_inline'1=>~actorname=>dw_camp_elite_1
                           actor_x_coord=>1713
                           actor_y_coord=>528
                           actor_orientation=>0
                           actor_resource=>dw#c3che
                           actor_defaults=>null~
      add_actor_inline'2=>~actorname=>dw_camp_elite_2
                           actor_x_coord=>1789
                           actor_y_coord=>723
                           actor_orientation=>0
                           actor_resource=>dw#c3che
                           actor_defaults=>null~
      add_actor_inline'3=>~actorname=>dw_camp_elite_3
                           actor_x_coord=>2025
                           actor_y_coord=>751
                           actor_orientation=>0
                           actor_resource=>dw#c3che
                           actor_defaults=>null~
      add_actor_inline'4=>~actorname=>dw_camp_elite_4
                           actor_x_coord=>1833
                           actor_y_coord=>841
                           actor_orientation=>0
                           actor_resource=>dw#c3che
                           actor_defaults=>null~
      add_actor_inline'5=>~actorname=>dw_camp_elite_5
                           actor_x_coord=>1415
                           actor_y_coord=>1053
                           actor_orientation=>0
                           actor_resource=>dw#c3cbt
                           actor_defaults=>null~
      add_actor_inline'6=>~actorname=>dw_camp_elite_6
                           actor_x_coord=>1715
                           actor_y_coord=>1214
                           actor_orientation=>0
                           actor_resource=>dw#c3cbt
                           actor_defaults=>null~
      add_actor_inline'7=>~actorname=>dw_camp_elite_7
                           actor_x_coord=>2448
                           actor_y_coord=>1102
                           actor_orientation=>0
                           actor_resource=>dw#c3cbt
                           actor_defaults=>null~
      add_actor_inline'8=>~actorname=>dw_camp_elite_8
                           actor_x_coord=>2052
                           actor_y_coord=>1185
                           actor_orientation=>0
                           actor_resource=>dw#c3cbt
                           actor_defaults=>null~
   END
   LAF edit_area STR_VAR area=~%BanditCamp%~ editstring="patch_trigger=>patch_data" END

   ACTION_IF GAME_IS bgt BEGIN
      OUTER_SPRINT doorname Door8701
   END ELSE BEGIN
      OUTER_SPRINT doorname Door1901
   END

 //  LAF extend_area_script STR_VAR area=~%BanditCamp%~ bottom=morebandits location=ssl ssl=yes END
   LAF extend_area_script STR_VAR area= ~%BanditCamp%~ top=sealtent location=ssl ssl=yes END

END



//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION camp_bcs BEGIN

      // slightly more reliable bandit go-hostile script
      LAF ssl_to_bcs STR_VAR script=bandit location=~tactical_bg1/ssl~ rename_to= ~%tutu_var%bandit~ END
      LAF extend STR_VAR file=hobgobc tv=1 bottom=bandit location=ssl ssl=yes END

      // extend it to a couple more bandits
      
      LAF edit_creature STR_VAR creature=~credus ardeno taugos~ tv=1 editstring= ~strip_script=>%tutu_var%bandit insert_script_high=>%tutu_var%bandit~ END

        // Taugos go-hostile block

        LAF ssl_to_bcs STR_VAR script=dw#taug location=~ssl~ END
        LAF edit_creature STR_VAR creature=taugos tv=1 editstring=~strip_script=>dw#taug insert_script_high=>dw#taug~ END

        // Taugos dialog

        LAF install STR_VAR file=dw#taug.d location=resource END

END

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION tent_bcs BEGIN
   LAF ssl_to_bcs STR_VAR script=dw#c3hst location=ssl END
   LAF extend_area_script STR_VAR area= ~%BanditCamp_RaemonsTent%~ bottom=tazoktent location=ssl ssl=yes END
END



//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION tent_cre BEGIN

   // britik

   MAKE_PATCH
      insert_script_high=>dw#c3hst
      level=>5
      class=>FIGHTER
      enforce_charclass=>is_bg1
      add_items=>~%tutu_var%potn12~
   END
   LAF edit_creature STR_VAR creature= ~%tutu_var%britik~ edits=patch_data END

   //  Raemon

   LAF install STR_VAR file=raemonadd.d location=resource END

   MAKE_PATCH
      swap_script=>~%tutu_var%initdlg=>shoutdlg~
      insert_script_high=>dw#c3hst
      dv=>raemon
   END
   LAF edit_creature STR_VAR creature=raemon tv=1 edits=patch_data END
   
   // Hakt, Venkt

   MAKE_PATCH
      insert_script_high=>dw#c3hst
   END
   LAF edit_creature STR_VAR creature=~hakt venkt~ tv=1 edits=patch_data END

   // non-hostile-at-start BTE and hobgobs
   
   MAKE_PATCH
      allegiance=>NEUTRAL
      insert_script_high'=>dw#c3hst
      insert_script_high''=>dw#prsht
   END
   LAF clone_creature STR_VAR creature= ~%tutu_scripth%obelite=>dw#c3hb1 %tutu_scripti%ronelit=>dw#c3bt1~ edits=patch_data END

   ACTION_FOR_EACH creature IN dw#c3hb1 dw#c3hb2 BEGIN
      COPY_EXISTING "dw#c3hb1.cre" "override/%creature%.cre"
          LPF CRE_dv STR_VAR arguments="%creature%" END
   END
   ACTION_FOR_EACH creature IN dw#c3bt1 dw#c3bt2 dw#c3bt3 BEGIN
      COPY_EXISTING "dw#c3bt1.cre" "override/%creature%.cre"
          LPF CRE_dv STR_VAR arguments="%creature%" END
   END
   
   // make sure they go hostile
   
   COPY_EXISTING "%tutu_var%raemon.dlg" override
         DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY
            ~ActionOverride("VENKT",Enemy())~
            ~ActionOverride("dw#c3hb1",Enemy())
            ActionOverride("dw#c3hb2",Enemy())
            ActionOverride("dw#c3bt1",Enemy())
            ActionOverride("dw#c3bt2",Enemy())
            ActionOverride("dw#c3bt3",Enemy())
            ActionOverride("VENKT",Enemy())~
         END
   BUT_ONLY

END


//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION tent_are BEGIN // run after tent_cre

   MAKE_PATCH
   // move Britik and Raemon back
      patch_actor_inline=>~match=>"actorname=britik"
                          actor_x_coord=>401
                          actor_y_coord=>273~
      patch_actor_inline'=>~match=>"actorname=raemon"
                          actor_x_coord=>350
                          actor_y_coord=>400~
   // add the new actors
      add_actor_inline'1=>~actorname=>black_talon_1
                           actor_x_coord=>486
                           actor_y_coord=>438
                           actor_orientation=>13
                           actor_resource=>dw#c3bt1
                           actor_defaults=>null~
      add_actor_inline'2=>~actorname=>black_talon_2
                           actor_x_coord=>550
                           actor_y_coord=>400
                           actor_orientation=>13
                           actor_resource=>dw#c3bt2
                           actor_defaults=>null~
      add_actor_inline'3=>~actorname=>black_talon_3
                           actor_x_coord=>604
                           actor_y_coord=>372
                           actor_orientation=>13
                           actor_resource=>dw#c3bt3
                           actor_defaults=>null~
      add_actor_inline'4=>~actorname=>chill_elite_1
                           actor_x_coord=>499
                           actor_y_coord=>352
                           actor_orientation=>13
                           actor_resource=>dw#c3hb1
                           actor_defaults=>null~
      add_actor_inline'5=>~actorname=>chill_elite_2
                           actor_x_coord=>551
                           actor_y_coord=>319
                           actor_orientation=>13
                           actor_resource=>dw#c3hb2
                           actor_defaults=>null~
   // make exit party-required
      patch_trigger_inline=>~match=>"trigger_type=2"
                            party_required=>1~

   END
   LAF edit_area STR_VAR area=~%BanditCamp_RaemonsTent%~ edits=patch_data END


END
