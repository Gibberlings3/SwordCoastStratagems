
INCLUDE FILE(%MOD_FOLDER%\genie\ssl\genie_definitions.ssl)
VARIABLE(IsNotMoron=True)
INCLUDE FILE(%MOD_FOLDER%\genie\ssl\genie_top.ssl)

IF
	HotKey(E)
	Global("ai_inactive","LOCALS",1) 
        Allegiance(Myself,GOODCUTOFF)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@100403)
		SetGlobal("ai_inactive","LOCALS",0)
END

IF
	HotKey(E)
	Global("ai_inactive","LOCALS",0)
	Allegiance(Myself,GOODCUTOFF)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@100402)
		SetGlobal("ai_inactive","LOCALS",1)
END



DEFAULT TRIGGER(OR(2)ActionListEmpty()!Allegiance(Myself,GOODCUTOFF)OR(2)Global("ai_inactive","LOCALS",0)!Allegiance(Myself,GOODCUTOFF))


IF
	StateCheck(Myself,STATE_INVISIBLE)
	GlobalTimerNotExpired("inviswander","LOCALS")
THEN
	RESPONSE #100
		RandomWalk()
END

IF
     StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("inviswander","LOCALS")
	GlobalTimerNotExpired("invisreturn","LOCALS")
THEN
	RESPONSE #100
		MoveToObject(Player1)
END

IF
       !See(NearestEnemyOf(Myself))
       InMyArea(LastSummonerOf(Myself))
       !Range(LastSummonerOf(Myself),20)
THEN
    RESPONSE #100
             MoveToObject(LastSummonerOf(Myself))
END


IF TRIGGER
	OnCreation()
	See(NearestEnemyOf(Myself))
	Target(Myself)
	TriggerBlock(EasyPlus)
	TriggerBlock(TruesightSafe)
	!Range(NearestEnemyOf(Myself),15)
	Allegiance(Myself,ENEMY)
THEN DO
	SetGlobalTimer("inviswander","LOCALS",4)
	SetGlobalTimer("invisreturn","LOCALS",10)
	Action(Spell,WIZARD_INVISIBILITY)
END


IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	Target(Myself)
	TriggerBlock(EasyPlus)
	TriggerBlock(TruesightSafe)
	OR(2)
	     Allegiance(Myself,ENEMY)
 !Hotkey(E)
THEN DO
	SetGlobalTimer("inviswander","LOCALS",4)
	Action(Spell,WIZARD_INVISIBILITY|100|200)
END

IF TRIGGER
        RequireBlock(Demivrgvs)
        RequireBlock(IsSummoned)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(ILackFireShieldRed)
THEN DO
	Action(SpellMyself,WIZARD_FIRE_SHIELD_RED|200|100)
END

IF TRIGGER
        RequireBlock(Demivrgvs)
        IgnoreBlock(IsSummoned)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(ILackFireShieldRed)
THEN DO
	        Action(Literal)
        	ApplySpell(Myself,WIZARD_FIRE_SHIELD_RED)
		Continue()
END


IF TRIGGER
        IgnoreBlock(Demivrgvs)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(ILackFireShieldRed)
THEN DO
	        Action(Literal)
		ApplySpell(Myself,WIZARD_FIRE_SHIELD_RED)
		Continue()
END

///////////////////////////////////////////////////////////////////////
///	Buffing and summons (Demi version only)
///////////////////////////////////////////////////////////////////////


IF TRIGGER
      RequireBlock(Demivrgvs)
      RequireBlock(SummonedVersion)
      TargetBlock(PCsPreferringWeak)
      TriggerBlock(Enemy)
      See(NearestEnemyOf(Myself))
THEN DO
     Action(SpellArea,WIZARD_CONJURE_LESSER_FIRE_ELEMENTAL|100|100)
END

IF TRIGGER
      RequireBlock(Demivrgvs)
      RequireBlock(SummonedVersion)
      TriggerBlock(ILackShield)
THEN DO
      Action(SpellMyself,WIZARD_SHIELD|200|100)
END


/////////////////////////////////////////////////////////////////////////////////////////
///	Fire Storm, if you've got it
/////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	Allegiance(Myself,ENEMY)
	TriggerBlock(EasyPlus)
THEN DO
	Action(SpellArea,CLERIC_FIRE_STORM)
END


///////////////////////////////////////////////////////////////////////////
////	Fireball
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|SIEvocation|MinorGlobe|Enemy)
	TriggerBlock(DirectDamageSafe)
	!NumCreatureGT([ENEMY.HUMANOID],0)
	Allegiance(Myself,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,WIZARD_FIREBALL|200|50)
END

/////////////////////////////////////////////////////////////////////////
///	Flame Strike
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&!CheckStatGT(scstarget,50,RESISTFIRE)&Allegiance(Myself,ENEMY))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|MirrorImage)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Spell,CLERIC_FLAME_STRIKE|100|100)
END

//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIEvocation|Enemy)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Action(Spell,WIZARD_MAGIC_MISSILE)
END


/////////////////////////////////////////////////////////////////////////
////    Flame Arrows 
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;HPPercentLT(scstarget,40)&!CheckStatGT(scstarget,50,RESISTMAGIC)&!CheckStatGT(scstarget,50,RESISTFIRE)&Allegiance(Myself,ENEMY))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&!CheckStatGT(scstarget,50,RESISTFIRE)&Allegiance(Myself,ENEMY))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR100|ArrowSafe|SpellTurn|Stoneskin|MinorGlobe|MirrorImage|Helpless|SIConjuration)
THEN DO
	Action(Spell,WIZARD_FLAME_ARROW|100|50)
END

/////////////////////////////////////////////////////////////////////////
////	Magic Missile
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40)&Allegiance(Myself,ENEMY))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&Allegiance(Myself,ENEMY))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|MagicMissile)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Spell,WIZARD_MAGIC_MISSILE|150|50)
END

/////////////////////////////////////////////////////////////////////////
////	Cut through ST
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
          TargetBlock(PCSpellcasters)
          Allegiance(Myself,ENEMY)
          !Allegiance(scstarget,ENEMY)
THEN DO
       Combine()
       Action(Spell,WIZARD_FLAME_ARROW)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/genai/ssl/chase_top.ssl)

BEGIN LOOP(MyWeaponStrength||0)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/chase.ssl)
END LOOP
END LOOP
