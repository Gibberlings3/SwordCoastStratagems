
INCLUDE FILE(%MOD_FOLDER%\genie\ssl\genie_definitions.ssl)
VARIABLE(IsNotMoron=True)
INCLUDE FILE(%MOD_FOLDER%\genie\ssl\genie_top.ssl)

IF
	HotKey(E)
	Global("ai_inactive","LOCALS",1) 
        Allegiance(Myself,GOODCUTOFF)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@100403)
		SetGlobal("ai_inactive","LOCALS",0)
END

IF
	HotKey(E)
	Global("ai_inactive","LOCALS",0)
	Allegiance(Myself,GOODCUTOFF)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@100402)
		SetGlobal("ai_inactive","LOCALS",1)
END


DEFAULT TRIGGER(OR(2)ActionListEmpty()!Allegiance(Myself,GOODCUTOFF)OR(2)Global("ai_inactive","LOCALS",0)!Allegiance(Myself,GOODCUTOFF))


IF TRIGGER
	OnCreation()
	!See(NearestEnemyOf(Myself))
THEN DO
	Action(SpellRESReplaceMyself,"dw#silin",WIZARD_INVISIBILITY)
	SetGlobalTimer("invisreturn","LOCALS",10)
	SetGlobalTimer("inviswander","LOCALS",4)
END

IF TRIGGER
	RequireBlock(Demivrgvs)
	RequireBlock(SummonedVersion)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	TriggerBlock(TruesightSafe)
	TriggerBlock(EasyPlus)
THEN DO
	SetGlobalTimer("inviswander","LOCALS",4)
	SetGlobalTimer("invisreturn","LOCALS",10)
	Action(SpellMyself,WIZARD_IMPROVED_INVISIBILITY|100|100)
END

IF TRIGGER
	IgnoreBlock(Demivrgvs)
	RequireBlock(SummonedVersion)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	TriggerBlock(TruesightSafe)
	TriggerBlock(EasyPlus)
THEN DO
	SetGlobalTimer("inviswander","LOCALS",4)
	SetGlobalTimer("invisreturn","LOCALS",10)
	Action(SpellMyself,WIZARD_SHADOW_DOOR|100|100)
END

IF TRIGGER
	IgnoreBlock(SummonedVersion)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	TriggerBlock(TruesightSafe)
	TriggerBlock(EasyPlus)
THEN DO
	SetGlobalTimer("inviswander","LOCALS",4)
	SetGlobalTimer("invisreturn","LOCALS",10)
	Action(SpellMyself,WIZARD_SHADOW_DOOR|100|100)
END

IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(TruesightSafe)
	TriggerBlock(EasyPlus)
	Target(Myself)
THEN DO
	SetGlobalTimer("inviswander","LOCALS",4)
	SetGlobalTimer("invisreturn","LOCALS",10)
	Action(SpellMyself,WIZARD_INVISIBILITY|100|100)
END


IF
	StateCheck(Myself,STATE_INVISIBLE)
	GlobalTimerNotExpired("inviswander","LOCALS")
THEN
	RESPONSE #100
		RandomWalk()
END

IF
	StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("inviswander","LOCALS")
	GlobalTimerNotExpired("invisreturn","LOCALS")
THEN
	RESPONSE #100
		MoveToObject(Player1)
END



IF TRIGGER
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),5)
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(ILackWeaponProtection|EasyPlus)
THEN DO
	Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
END

IF TRIGGER
	RequireBlock(Demivrgvs)
	RequireBlock(SummonedVersion)
	See(NearestEnemyOf(Myself))
	!CheckStatGT(Myself,0,MINORGLOBE)
	TriggerBlock(EasyPlus)
THEN DO
	Action(SpellMyself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
END

IF TRIGGER
	RequireBlock(Demivrgvs)
	IgnoreBlock(SummonedVersion)
	See(NearestEnemyOf(Myself))
	!CheckStatGT(Myself,0,MINORGLOBE)
        TriggerBlock(EasyPlus)
THEN DO
	Action(SpellMyself,WIZARD_GLOBE_OF_INVULNERABILITY)
END

IF TRIGGER
	IgnoreBlock(Demivrgvs)
	See(NearestEnemyOf(Myself))
	!CheckStatGT(Myself,0,MINORGLOBE)
        TriggerBlock(EasyPlus)	
THEN DO
	Action(SpellMyself,WIZARD_GLOBE_OF_INVULNERABILITY)
END

///////////////////////////////////////////////////////////////////////
///	Buffing and summons (Demi version only)
///////////////////////////////////////////////////////////////////////

IF TRIGGER
      RequireBlock(Demivrgvs)
      RequireBlock(SummonedVersion)
      !StateCheck(Myself,STATE_MIRRORIMAGE)
      See(NearestEnemyOf(Myself))
THEN DO
      Action(SpellMyself,WIZARD_MIRROR_IMAGE|200|100)
END

IF TRIGGER
      RequireBlock(Demivrgvs)
      RequireBlock(SummonedVersion)
      TargetBlock(PCsPreferringWeak)
      See(NearestEnemyOf(Myself))
THEN DO
     Action(SpellArea,WIZARD_CONJURE_LESSER_AIR_ELEMENTAL|100|100)
END

IF TRIGGER
      RequireBlock(Demivrgvs)
      RequireBlock(SummonedVersion)
      TriggerBlock(ILackShield)
      See(NearestEnemyOf(Myself))
THEN DO
      Action(SpellMyself,WIZARD_SHIELD|200|100)
END


///////////////////////////////////////////////////////////////////////
///	Cloud attacks
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	Allegiance(Myself,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,WIZARD_CLOUDKILL|100|50)
	Action(SpellArea,WIZARD_STINKING_CLOUD|100|100)
END

/////////////////////////////////////////////////////////////////////////
///	Lightning Bolt
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsInOrder;!CheckStatGT(scstarget,40,RESISTELECTRICITY)&!CheckStatGT(scstarget,40,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	CheckStatGT(Myself,0,MINORGLOBE)
	!NumCreatureGT([ENEMY.HUMANOID],0)
	Allegiance(Myself,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,WIZARD_LIGHTNING_BOLT|100|50)
END

/////////////////////////////////////////////////////////////////////////
////	Magic Missile
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|Enemy|MagicMissile)
	TriggerBlock(DirectDamageSafe)
	Allegiance(Myself,ENEMY)
THEN DO
	Action(Spell,WIZARD_MAGIC_MISSILE|150|50)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////


INCLUDE FILE(%MOD_FOLDER%/genai/ssl/chase_top.ssl)
BEGIN LOOP(MyWeaponStrength||0)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/chase.ssl)
END LOOP
END LOOP
