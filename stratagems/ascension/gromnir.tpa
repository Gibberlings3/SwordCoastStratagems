DEFINE_ACTION_FUNCTION gromnir BEGIN


     LAF include STR_VAR file=genai_shared.tph locbase=genai END
     LAF gromnir_cre END
     ACTION_IF FILE_EXISTS_IN_GAME "fingrom.cre" BEGIN
        LAF include STR_VAR file=ascension_shared.tpa END
        LAF update_vanilla_ascension END
        LAF gromnir_finale END
     END
     ACTION_IF FILE_EXISTS_IN_GAME "jumjum.cre" BEGIN
        LAF gromnir_tougher END
     END



END

DEFINE_ACTION_FUNCTION gromnir_cre BEGIN 

        // common changes to Gromnir's Cre file
    	// Enforce generic L24 berserker

	MAKE_PATCH
	   level=>24
	   kit=>BERSERKER
	   enforce_saves=>at_worst
	   enforce_thac0=>at_worst
	   enforce_hp=>at_worst
	   enforce_class=>null
	   genai=>~hla prof~
	END
	LAF edit_creature STR_VAR creature=~gromnir fingrom~ allow_missing=1 edits=patch_data END

END
DEFINE_ACTION_FUNCTION gromnir_tougher BEGIN

    	// Gromnir scripting

        LAF build_genai_script STR_VAR script=dw3ms5be END
	LAF clone_script STR_VAR script=~dw3ms5be=>gromnir~ END
	
 	// Berena Elkin is a wizard slayer; she also has spurious immunity to Slay, and regeneration

        MAKE_PATCH
           genai=>~prof~
           kit=>WIZARDSLAYER
	   enforce_class=>null
	   delete_effect=>~opcode=101 and parameter2=55~
	   delete_opcodes=>98
        END
        LAF edit_creature STR_VAR creature=gromg10 edits=patch_data END
        
        LAF build_genai_script STR_VAR script=dw3mp3ws END
        LAF clone_script STR_VAR script=~dw3mp3ws=>gpwslay~ END

	// new script for Lashar'ra, the medusa

        LAF ssl_to_bcs STR_VAR script=jumjum location=~ssl~ END
END

DEFINE_ACTION_FUNCTION gromnir_finale BEGIN

        LAF ascension_to_scs END

        LAF ssl_to_bcs STR_VAR script=fingrom location=~ssl~ END
        LAF build_genai_script STR_VAR script=dw3ms5be END
        LAF edit_creature STR_VAR creature=fingrom editstring=~insert_script_low=>dw3ms5be~ END

END

