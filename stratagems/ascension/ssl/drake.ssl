INCLUDE FILE(%scsroot%/lib/ssl/shared_definitions.ssl)


//////////////////////////////////////////////
//// remain inactive till the fight starts
//////////////////////////////////////////////

IF
  !Global("FinEnteredArea","MYAREA",2)
THEN
    RESPONSE #100
             NoAction()
END

//////////////////////////////////////////////
//// handle cloud effects
//////////////////////////////////////////////

BEGIN LOOP(scscloudmelee||See([PC]) !Range([PC],8))
BEGIN LOOP(scscloudresponse||MoveToObject([PC]))
INCLUDE FILE(%scsroot%/genai/ssl/fleecloud.ssl)
END LOOP
END LOOP

//////////////////////////////////////////////
//// protective spells
//////////////////////////////////////////////

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   TriggerBlock(Enemy|Disabled)
   CheckStatGT(scstarget,4,NUMBEROFATTACKS)
   !HasItem("eneblade",scstarget)
   !HasItem("melf",scstarget)
   !GlobalTimerNotExpired("invis","LOCALS") // no more than 1/3 rds
THEN DO
     Combine()
     Action(SpellMyself,WIZARD_INVISIBILITY|100|50)
     SetGlobalTimer("invis","LOCALS",30)
     SetGlobalTimer("inviswander","LOCALS",7)
END

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   HPPercentLT(Myself,50)
   TriggerBlock(Enemy|Disabled)
   !HasItem("eneblade",scstarget)
   !HasItem("melf",scstarget)
   !GlobalTimerNotExpired("invis","LOCALS") // no more than 1/3 rds
THEN DO
     Action(SpellMyself,WIZARD_INVISIBILITY|100|50)
     SetGlobalTimer("invis","LOCALS",30)
     SetGlobalTimer("inviswander","LOCALS",7)
END

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   TriggerBlock(Enemy|Disabled|StoneskinFailing)
   !GlobalTimerNotExpired("stoneskin","LOCALS") // no more than 1/5 rds
THEN DO
     Combine()
     Action(SpellMyself,DRAGON_STONE_SKIN|100|50)
     SetGlobalTimer("stoneskin","LOCALS",30)
END
//////////////////////////////////////////////
//// heal; if invisible, wander
//////////////////////////////////////////////

IF TRIGGER
   HPPercentLT(Myself,75)
   OR(2)
       StateCheck(Myself,STATE_INVISIBLE)
       !Range(NearestEnemyOf(Myself),15)
THEN DO
   Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)
END

IF
  StateCheck(Myself,STATE_INVISIBLE)
  GlobalTimerNotExpired("inviswander","LOCALS")
THEN
    RESPONSE #100
       RandomWalkContinuous()
END

IF
  StateCheck(Myself,STATE_INVISIBLE)
  HPPercentLT(Myself,75)
  HaveSpell(CLERIC_CURE_CRITICAL_WOUNDS)
THEN
    RESPONSE #100
       RandomWalkContinuous()
END

//////////////////////////////////////////////
//// Breath weapon!
//////////////////////////////////////////////


IF TRIGGER
     !GlobalTimerNotExpired("dragonbreath","LOCALS")
     RequireBlock(IsFireDrake)
     TargetBlock(EnemiesInOrderShort)
     Range(scstarget,7)
     TriggerBlock(Enemy|Helpless)
     !CheckStatGT(scstarget,59,RESISTFIRE)
THEN DO
     Combine()
     Action(ForceSpellRES,"drake01"|100|50)
     SetGlobalTimer("dragonbreath","LOCALS",12)
END

IF TRIGGER
     !GlobalTimerNotExpired("dragonbreath","LOCALS")
     RequireBlock(IsShadowDrake)
     TargetBlock(EnemiesInOrderShort)
     Range(scstarget,7)
     TriggerBlock(Enemy|Helpless)
     !CheckStatGT(scstarget,59,MAGICDAMAGERESISTANCE)
THEN DO
     Combine()
     Action(ForceSpellRES,"drake02"|100|50)
     SetGlobalTimer("dragonbreath","LOCALS",12)
END

IF TRIGGER
     !GlobalTimerNotExpired("dragonbreath","LOCALS")
     RequireBlock(IsIceDrake)
     TargetBlock(EnemiesInOrderShort)
     Range(scstarget,7)
     TriggerBlock(Enemy|Helpless)
     !CheckStatGT(scstarget,59,RESISTCOLD)
THEN DO
     Combine()
     Action(ForceSpellRES,"drake03"|100|50)
     SetGlobalTimer("dragonbreath","LOCALS",12)
END

IF TRIGGER
     !GlobalTimerNotExpired("dragonbreath","LOCALS")
     RequireBlock(IsEarthDrake)
     TargetBlock(EnemiesInOrderShort)
     Range(scstarget,7)
     TriggerBlock(Enemy|Helpless)
     !CheckStatGT(scstarget,59,RESISTACID)
THEN DO
     Combine()
     Action(ForceSpellRES,"drake04"|100|50)
     SetGlobalTimer("dragonbreath","LOCALS",12)
END

//////////////////////////////////////////////
//// Combat
//////////////////////////////////////////////

BEGIN LOOP(MyWeaponStrength||2)
BEGIN LOOP(MyWeaponDamageType||Piercing)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
END LOOP
END LOOP