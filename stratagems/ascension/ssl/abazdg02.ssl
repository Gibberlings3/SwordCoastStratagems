
IF
	Dead("Abazigal")
THEN
	RESPONSE #100
		ForceSpell(Myself,DRYAD_TELEPORT)
END

///////////////////////////////////////////////////////////////////////
///	Usual pre-battle stuff
///////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/dragon/ssl/dragondef.ssl)
INCLUDE FILE(%scsroot%/dragon/ssl/dragonsetup.ssl)

////////////////////////////////////////////////////////////////////////////
/// initial stoneskin
////////////////////////////////////////////////////////////////////////////


IF	
	!GlobalTimerNotExpired("initialstoneskin","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("initialstoneskin","LOCALS",1000)
		ApplySpell(Myself,DRAGON_STONE_SKIN)
		Continue()
END


//////////////////////////////////////////////////////////////////////////
////	Fire off a general trigger
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(Myself)
THEN DO
	Action(SequencerMyself,TAMAH_TRIGGER)
	ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
	SetGlobalTimer("dragonbreath","LOCALS",1)
	SetGlobalTimer("redefend","LOCALS",3)
END

//////////////////////////////////////////////////////////////////////////
///  Pursue!
///////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)


//////////////////////////////////////////////////////////////////////////
///	Use breath weapon as often as possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Stoneskin|AoE)
THEN DO
	Action(TamahDragonBreath,"abazdg02","dragonbreath",black05)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Stoneskin)
THEN DO
	Action(TamahDragonBreath,"abazdg02","dragonbreath",black05)
END


//////////////////////////////////////////////////////////////////////////
///	Use wing buffet to drive away anything at all dangerous
//////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%/dragon/ssl/wingbuffet.ssl)

/////////////////////////////////////////////////////////////////////////////
///	Renew defences
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Global("contingency","LOCALS",0)
	TriggerBlock(StoneskinFailing)
	Detect(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
                Action(Literal)
		DisplayStringHead(Myself,25942)
		SetGlobal("contingency","LOCALS",1)
		ReallyForceSpell(Myself,DRAGON_STONE_SKIN)
END

IF TRIGGER
	!CheckStatGT(Myself,0,STONESKINS)
	Detect(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	GlobalTimerExpired("redefend","LOCALS")
	Target(Myself)
THEN DO
	Action(SequencerMyself,TAMAH_SEQUENCER)
END

IF TRIGGER
	!GlobalTimerNotExpired("castspell","LOCALS")
	TriggerBlock(ILackWeaponProtection)
	!GlobalTimerNotExpired("redefend","LOCALS")
	Detect(NearestEnemyOf(Myself))
THEN DO
	Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END

IF TRIGGER
	!GlobalTimerNotExpired("castspell","LOCALS")
	TriggerBlock(StoneskinFailing)
	GlobalTimerExpired("redefend","LOCALS")
	Target(Myself)
THEN DO
	Action(Spell,DRAGON_STONE_SKIN|100|100)
END

/////////////////////////////////////////////////////////////////////////
///	Be invisible
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	TriggerBlock(TruesightSafe)
THEN DO
	Action(SpellMyself,PSIONIC_SUPERIOR_INVISIBILITY)
END	

/////////////////////////////////////////////////////////////////////////
///	Heal myself
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,65)
THEN DO
	Action(SpellMyself,ULITHARID_CURE_SERIOUS_WOUNDS)
END	

/////////////////////////////////////////////////////////////////////////
///	Psionic Blast
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Stun|Enemy|Helpless|PsiBlast|AoEPsiBlast)
THEN DO
	Combine()
	Action(SpellMarker,ULITHARID_PSIONIC_BLAST,ITEM_PSIONIC_SEEN|150|50)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Stun|Enemy|Helpless|PsiBlast)
THEN DO
	Combine()
	Action(SpellMarker,ULITHARID_PSIONIC_BLAST,ITEM_PSIONIC_SEEN|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Abi-Dalzim, if targets present themselves
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	OR(2)
		!Gender(scstarget,SUMMONED)
		HasItem("morsword",scstarget)
	TriggerBlock(ResistMagicDamage|MR|SINecromancy|Enemy|DirectDamageSafe|AoEMagicDamage)
THEN DO
	Combine()
	Action(Spell,WIZARD_ABI_DALZIMS_HORRID_WILTING|400|100)
END

///////////////////////////////////////////////////////////////////////////
////	Kill swords (don't worry about other summons)
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([GOODCUTOFF.0.SWORD])
	Gender(scstarget,SUMMONED)
	TriggerBlock(Enemy|Helpless)
	HasItem("morsword",scstarget)
THEN DO
	Combine()
	Action(Spell,WIZARD_DEATH_SPELL|150|50)
	Action(Sequencer,DRAGON_MAGIC_MISSILE_SEQUENCER)
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

//////////////////////////////////////////////////////////////////////////
///	Attack defences
//////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellDeflect|Enemy|Helpless|PriorityBreach)
THEN DO
	Combine()
	Action(Spell,WIZARD_BREACH|150|150)
END



IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(AvoidDispelIgnoreInsects|BreachTarget|SIAbjuration|Enemy)
THEN DO
	Combine()
	Action(Spell,WIZARD_DISPEL_MAGIC|100|50)
END



//////////////////////////////////////////////////////////////////////////
////	Domination
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	ConditionalTargetBlock(EnemiesInOrder;CheckStatGT(scstarget,7,Level)) // don't dominate weak creatures
	TriggerBlock(Enemy|Helpless|PsiDom|Domination)
	CheckStatGT(scstarget,3,SAVEVSSPELL)
THEN DO
	Action(Spell,MIND_FLAYER_DOMINATION,ITEM_PSIONIC_SEEN)
END

//////////////////////////////////////////////////////////////////////////////
////	Maze
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	ConditionalTargetBlock(EnemiesInOrder;CheckStatGT(scstarget,7,Level)) // don't dominate weak creatures
	TriggerBlock(Enemy|Helpless|Maze)
	CheckStatGT(scstarget,1,SAVEVSSPELL)
THEN DO
	Combine()
	Action(Spell,PSIONIC_MAZE,ITEM_PSIONIC_SEEN)
END

////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless|SpellShieldTarget)
THEN DO
	Combine()
	Action(SpellArea,WIZARD_SECRET_WORD)
	Action(SpellArea,WIZARD_SPELL_THRUST) 
        Action(SpellArea,WIZARD_RUBY_RAY_OF_REVERSAL)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless|SpellProtectionTarget)
THEN DO
	Combine()
	Action(SpellArea,WIZARD_RUBY_RAY_OF_REVERSAL|100|100)
	Action(SpellArea,WIZARD_SECRET_WORD|100|100)
	Action(SpellArea,WIZARD_SPELL_THRUST|100|100)
END



//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIEvocation|Enemy|MinorGlobe)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs 
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellDeflect|Enemy|L1safe)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

//////////////////////////////////////////////////////////////////////////////////
/////	Melee phase
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/dragon/ssl/melee.ssl)

////////////////////////////////////////////////////////
///	Closedown: avoid interaction with other scripts
////////////////////////////////////////////////////////////

IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END
