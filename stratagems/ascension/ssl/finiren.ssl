
//////////////////////////////////////////////////////////////////
///	For Ascension's Irenicus. He has a Robe of Vecna so he
///	needs to avoid slowspell.
//////////////////////////////////////////////////////////////////


VARIABLE(HasL1=True)
VARIABLE(HasL2=True)
VARIABLE(HasL3=True)
VARIABLE(HasL4=True)
VARIABLE(HasL5=True)
VARIABLE(HasL6=True)
VARIABLE(HasL7=True)
VARIABLE(HasL8=True)
VARIABLE(HasL9=True)
VARIABLE(IsOption3=True)
VARIABLE(HasNoPrep=True)
VARIABLE(HasNoContingency=True)
//////////////////////////////////////////////////////////////////////////////////
///
///   Define actions
///
////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsspellsubstitute1||!StateCheck(scstarget,STATE_NOT_TARGETABLE))
BEGIN LOOP(scsspellsubstitute2||!StateCheck(scstarget,STATE_INVISIBLE))
	INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)
END LOOP
END LOOP

//////////////////////////////////////////////////////////////////////////////////
//      Testing
//////////////////////////////////////////////////////////////////////////////////


IF
	Global("DMWWTestIrenicus","GLOBAL",1)
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Enemy()
END



//////////////////////////////////////////////////////////////////////////////////
//      Handle fear
//////////////////////////////////////////////////////////////////////////////////

IF
  StateCheck(Myself,STATE_PANIC)
THEN
    RESPONSE #100
             RandomWalkContinuous()
END
////////////////////////////////////////////////////////////////////////
/////////	triggers
////////////////////////////////////////////////////////////////////////

IF
	Global("setuptriggers","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("SpellTrigger","LOCALS",2)
		SetGlobal("SpellSequencer","LOCALS",3)
		SetGlobal("MinorSequencer","LOCALS",3)
		SetGlobal("setuptriggers","LOCALS",2)
		Continue()
END

////////////////////////////////////////////////////////////////////////
/////////	Contingency
////////////////////////////////////////////////////////////////////////

IF
	!Global("ChainContingencyFired","LOCALS",1)
THEN
	RESPONSE #100
			SetGlobal("ChainContingencyFired","LOCALS",1)
			DisplayStringHead(Myself,26328)
			ReallyForceSpellRES("%WIZARD_MISLEAD_INSTANT%",Myself)
			ReallyForceSpell(Myself,WIZARD_SIMULACRUM)
			ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_DIVINATION)
			SetGlobalTimer("redefend","LOCALS",3)
			SetGlobal("inafight","LOCALS",1)
			Continue()
END



//////////////////////////////////////////////////////////////////////////////////
////	If non-hostile, closedown - this script doesn't pretend to fight bad guys
//////////////////////////////////////////////////////////////////////////////////

IF
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		NoAction()
END
////////////////////////////////////////////////////////////////////////////
///	Contingencies renew defences
////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/contingency.ssl)

///////////////////////////////////////////////////////////////////////////
////	Flee Cloudkill spell
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/fleecloud.ssl)


////////////////////////////////////////////////////////////////////////////////////////
////	Kill the timer if we're using Alacrity
////////////////////////////////////////////////////////////////////////////////////////

IF
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	GlobalTimerNotExpired("castspell","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",0)
END

///////////////////////////////////////////////////////////////////////////
//	Melee
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/melfsetup.ssl)
INCLUDE FILE(%scsroot%/mage/ssl/bg2/meleeblocks/standard_mage.ssl)

//////////////////////////////////////////////////////////////////////////
// Renew and retreat
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/renew.ssl)


//////////////////////////////////////////////////////////////////////////
// Look for PCs
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER()

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/mill.ssl)

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/chase.ssl)

////////////////////////////////////////////////////////////////////////////
// Core magic block
///////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))
BEGIN LOOP(slowspell|| )
INCLUDE FILE(%scsroot%/mage/ssl/bg2/magetypes/standard_mage.ssl)
END LOOP

//////////////////////////////////////////////////////
///	If everyone is helpless, kill'em.
//////////////////////////////////////////////////////
	
IF
	See(NearestEnemyOf(Myself))
	OR(2)
		Class(Myself,MAGE)
		Class(Myself,MAGE_THIEF)
	BEGIN LOOP(scsloopvar||1;2;3;4;5;6)
		OR(3)
			!See(Playerscsloopvar)
			StateCheck(Playerscsloopvar,STATE_IMMOBILE)
			CheckStatGT(Playerscsloopvar,0,HELD)
	END LOOP
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/closedown.ssl)
