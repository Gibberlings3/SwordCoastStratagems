///////////// Key changes to vanilla Ascension to match Asc 2.0

DEFINE_ACTION_FUNCTION update_vanilla_ascension BEGIN

    OUTER_SPRINT component_loc ascension // Melissan calls this
    LAF check_label STR_VAR label=fin_ascension_main RET value1=value END
    LAF check_label STR_VAR label=dw_ascension_update RET value2=value END

    ACTION_IF !( value1 || value2 ) BEGIN

       LAF make_label STR_VAR label=dw_ascension_update END
       LAF extend_area_script STR_VAR area=ar6200 top=fix_ar6200 location=resource END
       COPY "gate5.spl" "override/gate5a.spl"
       LAF install STR_VAR files="usar1ca.bam usar1cae.bam" location=resource locbase=ascension END

    END
END

///////////// Turn flat Asc difficulty checks into SCS-style ones

// This code is not especially robust: it relies on the fact that no Ascension difficulty blocks are embedded into an OR()


DEFINE_ACTION_FUNCTION ascension_to_scs BEGIN

   OUTER_SPRINT component_loc ascension // Melissan calls this
   LAF check_label STR_VAR label=dw_ascension_to_scs RET value END
   ACTION_IF !value BEGIN
         LAF make_label STR_VAR label=dw_ascension_to_scs END

         LAF define_difficulty STR_VAR type=ascension RET difficulty_variable END

         ACTION_FOR_EACH file IN ar6200.bcs finmel01.dlg finmel01.bcs irenic2.dlg ppguy02.dlg finbalth.bcs BEGIN
              COPY_EXISTING "%file%" override
                     LPF scs_difficulty_swap STR_VAR difficulty_variable END
              BUT_ONLY
         END
   END
END
