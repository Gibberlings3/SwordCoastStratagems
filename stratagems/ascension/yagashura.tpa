DEFINE_ACTION_FUNCTION yagashura BEGIN

     ACTION_IF FILE_EXISTS_IN_GAME yagaft.bcs BEGIN
        LAF yaga_shura_main END
     END
     ACTION_IF FILE_EXISTS_IN_GAME finyaga.cre BEGIN
        LAF yaga_shura_finale END
     END

END

DEFINE_ACTION_FUNCTION yaga_shura_main BEGIN

      // yaga-shura's script (the non-combat one is hopefully robust against a bug where YS starts off slightly injured)
      
      LAF ssl_to_bcs STR_VAR script=~yaga01 yagaft~ location=~ssl~ END

      // Fix the bug with the Lieutenant Thief's darts being in a Quiver slot

      LAF edit_creature STR_VAR creature=yaga05 editstring=~remove_items=>dart03 add_items=>"dart03(20,WEAPON2)"~ END

      // Yaga-Shura's lieutenants, and fire giants, get immunity to his Lava Bombs (cheesy? perhaps, but it's too fiddly to work out
     // when he should and shouldn't use them, and the players get to do it with Comet and Dragon's Breath)

     LAF edit_creature STR_VAR creature=~yaga02 yaga03 yaga04 yaga05 ysfire02~ editstring=~immunity_to_spell=>yaga01~ END

     // Yaga-Shura's army doesn't hold back just because he's arrived

     LAF get_area_script STR_VAR area=ar5203 RET script END
     LAF swap_text STR_VAR files=~%script%.bcs~ swaps=~!Global("YagaShuraSpawn","AR5203",1)=>NULL~ END

     // Yaga-Shura fight gets an autosave just before YS turns up

     LAF extend STR_VAR file=~%script%~ top=yagasave location=resource END

     // on modern Ascension, a typo means yaga04 gets the wrong SPECIFICS
     
     LAF edit_creature STR_VAR creature=yaga04 editstring="specifics=>20" END
     
     // help scripts for the spellcasters
     
     LAF edit_creature STR_VAR creature="yaga02 yaga04" editstring="insert_script=>~dw#gphlp after yaga02~" END


END
DEFINE_ACTION_FUNCTION yaga_shura_finale BEGIN

      MAKE_PATCH
          resist_fire=>60
          resist_acid=>60
          resist_electricity=>60
          resist_cold=>20
          resist_slashing=>60
          resist_piercing=>60
          resist_crushing=>60
          resist_missile=>60
      END
      LAF edit_creature STR_VAR creature=finyaga edits=patch_data END
      LAF ssl_to_bcs STR_VAR script=finyaga location=~ssl~ END
END



