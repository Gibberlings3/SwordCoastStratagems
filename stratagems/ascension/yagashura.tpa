DEFINE_ACTION_FUNCTION yagashura BEGIN




     ACTION_IF FILE_EXISTS_IN_GAME yagaft.bcs BEGIN
        LAF yaga_shura_main END
     END
     ACTION_IF FILE_EXISTS_IN_GAME finyaga.cre BEGIN
        LAF yaga_shura_finale END
     END

END

DEFINE_ACTION_FUNCTION yaga_shura_main BEGIN


     LAF define_difficulty STR_VAR type=genai RET difficulty_variable END

     // yaga-shura's script

     LAF ssl_to_bcs STR_VAR script=yagaft location=ssl END

     // Yaga-Shura's lieutenants all get Potions of Fire Resistance (two already taken, and for Fighter/Thief, two carried)

     MAKE_PATCH
         add_effect_inline=>~opcode=>30 target=>1 power=>4 parameter1=>100 duration=>600 resist_dispel=>1~
     END
     LAF edit_creature STR_VAR creature="yaga02 yaga03 yaga04 yaga05" edits=patch_data END

     LAF edit_creature STR_VAR creature="yaga03 yaga05" editstring="add_items=>potn02" END


     // scripting for them to use those potions
     
     LAF extend STR_VAR script=yaga02 top=yagapotion location=resource END
     
     // Save game when Yaga returns

     LAF extend_area_script STR_VAR area=ar5203 top=yagasave location=resource END

     // Yaga-Shura's army doesn't hold back just because he's arrived

     LAF get_area_script STR_VAR area=ar5203 RET script END
     LAF swap_text STR_VAR files=~%script%.bcs~ swaps=~!Global("YagaShuraSpawn","AR5203",1)=>NULL~ END


END
DEFINE_ACTION_FUNCTION yaga_shura_finale BEGIN

     LAF include STR_VAR file=ascension_shared.tpa END
     LAF update_vanilla_ascension END     
     LAF ascension_to_scs END
     LAF define_difficulty STR_VAR type=ascension RET difficulty_variable END

      MAKE_PATCH
          resist_fire=>60
          resist_acid=>60
          resist_electricity=>60
          resist_cold=>20
          resist_slashing=>60
          resist_piercing=>60
          resist_crushing=>60
          resist_missile=>60
      END
      LAF edit_creature STR_VAR creature=finyaga edits=patch_data END
      LAF ssl_to_bcs STR_VAR script=finyaga location=~ssl~ END
END



