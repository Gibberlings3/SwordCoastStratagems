LAF include STR_VAR file=psionic_shared.tph locbase=psionic END

DEFINE_ACTION_FUNCTION abazigal_dragon BEGIN



    // sort out resources

    LAF define_difficulty STR_VAR type=dragon RET difficulty_variable END
    LAF shared_psionic_resources END

    // compile scripts

    LAF ssl_to_bcs STR_VAR script=~abazdrag~ location=~dragon/ssl~ variables="NoHitPointBoost=True" END
    LAF ssl_to_bcs STR_VAR script=~abazdg02~ location=~ascension/ssl~ END

    // Abazigal creature file

    MAKE_PATCH
       dragon_general=>null
       level_all=>32
       immunity=>electricity
       remove_spells=>all
       add_spells=>~MAGIC_MISSILE(5) HASTE WIZARD_LIGHTNING_BOLT SLOW(2) SECRET_WORD(2) GREATER_MALISON
                    BREACH(3) SHADOW_DOOR WIZARD_CHAOS DEATH_SPELL WIZARD_TRUE_SIGHT
                    PROTECTION_MAGIC_WEAPON_NOEFF WARDING_WHIP RUBY_RAY_OF_REVERSAL
                    WIZARD_FINGER_OF_DEATH MAZE POWER_WORD_BLIND IMPRISONMENT DRAGON_STONE_SKIN(3) 
                    ABAZIGAL_TRIGGER ABAZIGAL_SEQUENCER DRAGON_LOWER_ELECTRICAL DRAGON_MAGIC_MISSILE_SEQUENCER~

    END
    LAF edit_creature STR_VAR creature=dragblue edits=patch_data END

    // Tamah creature file
    
    MAKE_PATCH
       dragon_general=>null
       level_all=>25
       remove_spells=>all
       add_spells=>~MAGIC_MISSILE(3) DISPEL_MAGIC(2) SLOW SECRET_WORD
                    BREACH(2) DEATH_SPELL PROTECTION_MAGIC_WEAPON_NOEFF 
                    RUBY_RAY_OF_REVERSAL ABI_DALZIMS_HORRID_WILTING DRAGON_STONE_SKIN ULITHARID_PSIONIC_BLAST
                    MIND_FLAYER_DOMINATION PSIONIC_MAZE ULITHARID_CURE_SERIOUS_WOUNDS DRAGON_MAGIC_MISSILE_SEQUENCER
                    PSIONIC_SUPERIOR_INVISIBILITY TAMAH_SEQUENCER TAMAH_TRIGGER~
    END
    LAF edit_creature STR_VAR creature=abazdg02 edits=patch_data END

    // Tamah's claws and breath weapon

    LAF edit_item STR_VAR item=dragpur1 editstring=~dragon_weapon=>null~ END
    LAF edit_spell STR_VAR spell=abazdg02 editstring=~say_name=>3303 dragon_breath=>null~ END

END

