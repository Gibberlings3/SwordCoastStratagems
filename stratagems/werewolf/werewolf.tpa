INCLUDE ~%MOD_FOLDER%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION werewolf BEGIN

   LAF werewolf_cre END
   LAF kaishas_confrontation END

   LAF check_label STR_VAR label=dw#mage RET value END
   ACTION_IF value BEGIN
      LAF werewolf_shapechange END
      LAF werewolf_human_form END
   END


END


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION werewolf_cre BEGIN

   // give Kaishas and Baresh greater-werewolf rings
   
   LAF edit_creature STR_VAR creature=~%tutu_scriptk%aishwlf %tutu_var%barwlf~ editstring=~add_items=>%tutu_scriptr%ingwolf~ END

   // give greater werewolves/wolfweres Free Action

   LAF edit_creature STR_VAR creature=~%tutu_scriptk%aishwlf %tutu_var%barwlf %tutu_var%loupgar %tutu_scriptw%olfwegr~
                             editstring=~add_items=>freering(boots)~
   END

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION werewolf_shapechange BEGIN

   // complicated by TUTU/BGT issues
   
   // find the KAISHAS_CHANGE spell

   ACTION_IF GAME_IS ~bgt~ BEGIN
      OUTER_SPRINT KAISHAS_CHANGE bgwi925
   END
   ACTION_IF GAME_IS ~eet~ BEGIN
      OUTER_SPRINT KAISHAS_CHANGE ohwi925
   END
   
   // make a Baresh version

   LAF clone_spell STR_VAR spell=~%KAISHAS_CHANGE%=>dw#barwf~ END
   LAF swap_text STR_VAR files=dw#barwf.spl swaps=~%tutu_scriptk%aishwlf=>%tutu_var%barwlf~ END


END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION werewolf_human_form BEGIN

   // this whole part only applies if Smarter Mages is installed

   // scripts
   
   LAF ssl_to_bcs STR_VAR script=~baresh daese dw#selaa~ location=~ssl~ END


   // make Baresh into a L12 fighter

   MAKE_PATCH
      class=>FIGHTER
      race=>LYCANTHROPE
      general=>MONSTER
      add_items=>~%tutu_var%immune1 minhp1~
      script_override=>baresh
      script_class=>wtasight
      genai=>all
   END
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=baresh2 edits=patch_data END
   
   // fix problems with Baresh's wolf form
   
   MAKE_PATCH
      class=>WEREWOLF
      race=>LYCANTHROPE
      general=>MONSTER
   END
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=barwlf  edits=patch_data END

   // make Daese a L8 enchanter and Selaad a L10 enchanter
   

      MAKE_PATCH
         class=>mage
         level=>8
         kit=>ENCHANTER
         strip_script=>all
         script_override=>daese
         add_items=>~%tutu_var%immune1 minhp1 staf01~
         enforce_mage=>~accept_level game_is_bg1~
      END
      LAF edit_creature INT_VAR tv=1 STR_VAR creature=daese edits=patch_data END
      MAKE_PATCH
         class=>mage
         level=>10
         kit=>ENCHANTER
         strip_script=>all
         script_override=>dw#selaa
         add_items=>~%tutu_var%immune1 minhp1 staf01~
         enforce_mage=>~accept_level game_is_bg1~
      END
      LAF edit_creature INT_VAR tv=1 STR_VAR creature=mendas3 edits=patch_data END


   // make Baresh and Selaad attack as humans to start with
    COPY_EXISTING ~%tutu_var%menda4.dlg~ ~override~
          DECOMPILE_AND_PATCH BEGIN
          // lots of variants, as different versions of BG1 do this differently
          REPLACE_TEXTUALLY  ~ActionOverride("baresh",ApplySpell([^,]*,[^)]*))~ ~ActionOverride("baresh",Enemy())~
          REPLACE_TEXTUALLY  ~ActionOverride("baresh",ApplySpellRES([^,]*,[^)]*))~ ~ActionOverride("baresh",Enemy())~
          REPLACE_TEXTUALLY  ~ForceSpell(Myself,[^)]*)~ ~Enemy()~
       END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION kaishas_confrontation BEGIN

 ACTION_IF !FILE_EXISTS_IN_GAME "dw#hlpka.bcs" BEGIN // make sure the Calling version isn't already installed
   ADD_JOURNAL EXISTING @21614 @21648 @22000
   ACTION_IF GAME_IS ~bgee eet~ BEGIN // lowlevel check to hold onto Calling interoperability
    OUTER_SPRINT ee_erase_string "EraseJournalEntry(26907)
EraseJournalEntry(26908)
EraseJournalEntry(27107)
EraseJournalEntry(27260)
EraseJournalEntry(27261)
EraseJournalEntry(27262)
EraseJournalEntry(27293)
EraseJournalEntry(27295)
EraseJournalEntry(27027)
EraseJournalEntry(27028)
EraseJournalEntry(27108)
EraseJournalEntry(27294)
"
   END ELSE BEGIN
	OUTER_SPRINT ee_erase_string ""
   END
   LAF clone_creature STR_VAR creature=~%tutu_var%werewo=>dw#wwolf~ editstring=~insert_script_high=>dw#hlpka~ END
   LAF extend_area_script STR_VAR area=~%IsleofBalduranN%~ top=werewolfadd location=resource END
   LAF extend INT_VAR tv=1 STR_VAR file=kaisha bottom=kaishas location=resource END
   LAF install STR_VAR files=~dw#hlpka.baf dw#kcut1.baf dw#kcut2.baf dw#kcut3.baf werewolf.d~ location=resource END
   LAF swap_text STR_VAR files=~%tutu_var%ship.bcs~ swaps=~Dead("kaishwlf")=>OR(2)Dead("kaishwlf")Global("DMWWNoKillKaishas","GLOBAL",1)~ END
 END

END



