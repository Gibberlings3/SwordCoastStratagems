DEFINE_ACTION_FUNCTION resurrection_rod BEGIN

   MAKE_PATCH
      say_description=>5100
      patch_ability_inline=>~ability_icon=>sppr504b number_charges=>5 when_drained=>3~
      clone_ability_inline=>~ability_icon=>sppr607b ability_target=>1 at_end=>1~
      delete_effect=>~(parent_index=0 and opcode=17) or (parent_index=1 and opcode is_in [32 309 215])~
   END
   LAF edit_item STR_VAR item=rods03 edits=patch_data END

   APPEND tooltip.2da ~RODS03 12117 1453 -1~




   MAKE_PATCH
         match=>"item_resource=rods03"
         item_charges_1=>5
         item_charges_2=>5
   END

   // preliminary superfast filter
   
   OUTER_SPRINT area ~~
   OUTER_SPRINT store ~~
   OUTER_SPRINT creature ~~

   COPY_EXISTING_REGEXP GLOB ~.*\.cre~ override
           PATCH_IF INDEX_BUFFER (CASE_INSENSITIVE ~rods03~)>=0 BEGIN
              PUSH creature ~%SOURCE_RES%~
           END
   BUT_ONLY
   COPY_EXISTING_REGEXP GLOB ~.*\.are~ override
           PATCH_IF INDEX_BUFFER (CASE_INSENSITIVE ~rods03~)>=0 BEGIN
              PUSH area ~%SOURCE_RES%~
           END
   BUT_ONLY
   COPY_EXISTING_REGEXP GLOB ~.*\.sto~ override
           PATCH_IF INDEX_BUFFER (CASE_INSENSITIVE ~rods03~)>=0 BEGIN
              PUSH store ~%SOURCE_RES%~
           END
   BUT_ONLY

   ACTION_IF "%area%" STRING_COMPARE "" BEGIN
      LAF edit_area STR_VAR area editstring=~patch_item=>patch_data~ END
   END
   ACTION_IF "%creature%" STRING_COMPARE "" BEGIN
      LAF edit_creature STR_VAR creature editstring=~patch_item=>patch_data~ END
   END
   ACTION_IF "%store%" STRING_COMPARE "" BEGIN
      LAF edit_store STR_VAR store editstring=~patch_item=>patch_data~ END
   END








END