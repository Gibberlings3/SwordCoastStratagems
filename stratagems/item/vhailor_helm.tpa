DEFINE_ACTION_FUNCTION vhailor_helm BEGIN

   OUTER_SET proceed=1
   COPY_EXISTING "wmart2.sto" "%workspace%"
     PATCH_IF INDEX_BUFFER ("wa2helm")<0 BEGIN
        SET proceed=0
     END
   BUT_ONLY
   ACTION_IF proceed BEGIN
   OUTER_SPRINT difficulty_variable DMWW_genai_difficulty // do this manually as difficulty system probably isn't installed yet
   LAF ssl_to_bcs STR_VAR script=dw#grvha location=ssl END

   LAF edit_store STR_VAR store=wmart2 editstring=~remove_items=>wa2helm~ END
   LAF edit_creature STR_VAR creature=gromnir editstring=~add_items=>wa2helm(x1) insert_script_high=>dw#grvha~ END
   ACTION_IF FILE_CONTAINS_EVALUATED (simulacr.spl d0simul) BEGIN
           COPY_EXISTING simulacr.spl override
              REPLACE_TEXTUALLY ~d0simul~ ~dw#image~ (8)
   END ELSE BEGIN
          COPY_EXISTING simulacr.spl override
          LPF DELETE_EFFECT INT_VAR match_opcode=82 STR_VAR match_resource="dw#image" END
          LPF ADD_SPELL_EFFECT INT_VAR opcode=82 target=2 timing=1 parameter2=6 STR_VAR resource=dw#image END
   END

   COPY_EXISTING "gromnir.cre" override
      READ_ASCII 0x268 grom_script
   BUT_ONLY

<<<<<<<< .../stratagems-inline/image_top.baf
IF
  Allegiance(Myself,ENEMY)
  HasItem("wa2helm",Myself)
  AreaCheck("ar5002")
  Global("set_script","LOCALS",0)
THEN
    RESPONSE #100
       SetGlobal("set_script","LOCALS",1)
       ChangeAIScript("dw#grsim","OVERRIDE")
       ChangeAIScript("%grom_script%","CLASS")
END
>>>>>>>>

LAF extend STR_VAR script=dw#image top=image_top inline=yes END

<<<<<<<< .../stratagems-inline/dw#grsim.baf
IF
  Global("initialise_sim","LOCALS",0)
THEN
    RESPONSE #100
         FillSlot(SLOT_WEAPON)
         EquipMostDamagingMelee()
         DestroyItem("potn55")
         SetGlobal("initialise_sim","LOCALS",1)
         Continue()
END
>>>>>>>>

   COMPILE ".../stratagems-inline/dw#grsim.baf"



  END ELSE BEGIN
      LAF warning STR_VAR warning="Not moving Vhailor's Helm; some other mod has already moved it" END
  END



END
