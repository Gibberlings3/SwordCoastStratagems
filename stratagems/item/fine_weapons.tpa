DEFINE_ACTION_FUNCTION fine_weapons STR_VAR version="shatter" BEGIN


	OUTER_SET iwdification = FILE_EXISTS_IN_GAME "cdfs1ss.itm" // check for IWDification randomizer
	

   OUTER_SET shatter = ("%version%" STRING_EQUAL_CASE shatter)? 1 : 0

<<<<<<<< .../stratagems-inline/fineweapon.2da
nonmagic_wpn  magic_wpn   cd_version	hit:i dmg:i cost:i  BAM1     BAM2	kept_copy  desc_string:i  name_string:i
ax1h01	   ax1h02         cdhqaxe		1     1   	500   dw#axe01 dw#axe01 dw#axe1    401            402
blun06     blun07         cdhqmstr		1     1   	600   dw#mstar dw#mstar dw#msta1   403            404
bow03      bow04          cdhqlbow		2     0   	800   dw#bow02 dw#bow02 dw#lbow1   405            406
bow05      bow06          cdhqsbow		1     0   	700   dw#bow03 dw#bow03 dw#sbow1   407            408
dagg01     dagg02         cdhqdagg		1     1   	100   dw#dagg1 dw#dagg1 dw#dagg1   409            410
halb01     halb02         cdhqhalb		1     1   	700   dw#halb1 dw#halb1 dw#halb1   411            412
hamm01     hamm02         cdhqhamm		1     2   	500   dw#hamm1 dw#hamm1 dw#hamm1   413            414
staf01     staf02         cdhqstaf		1     1   	150   dw#stf01 dw#stf01 dw#stf01   415            416
sw1h01     sw1h02         cdhqsw1b		1     1   	800   dw#swd01 dw#swd01 dw#swd01   417            418
sw1h04     sw1h05         cdhqsw1l		1     1   	400   dw#swd02 dw#swd02 dw#swd02   419            420
sw1h07     sw1h08         cdhqsw1s		1     1   	250   dw#swd03 dw#swd03 dw#swd03   421            422
sper01	   sper02         cdhqsper		1		1   300   isper02  isper02  dw#sper1   423            424
blun04     blun05         cdhqmace		1     1   	500   iblun05  iblun05  dw#mace1   425            426
sw2h01     sw2h02         cdhqsw2h		1     1   	200   isw2h02  isw2h02  dw#swd04   427            428
sw1h20     sw1h22         cdhqscim		1     1   	350   isw1h20  isw1h20  dw#scim1   429            430
bow01      bow02          cdhqcbow		2     2   	850   ibow02   ibow02   dw#cbow1   431            432
blun02     blun03         cdhqflal		1     2   	750   iblun03  iblun03  dw#flai1   433            434
sw1h43	   sw1h44		  cdhqkat		1	  1   	1500  isw1h44  isw1h44  dw#katan   435			  436
sw1h46	   sw1h47		  cdhqwaki		1     1   	500   isw1h47  isw1h47  dw#wakis   437			  438
blun01	   blun36         cdhqclub		1     1   	400   dw#club1 dw#club1 dw#club1    439           440
xbow01     xbow02         cdhqcbhv		1     3   	1000   dw#xbow1 dw#xbow1 dw#xbow1   441            442
xbow04     xbow05         cdhqcblt		1     1   	800   dw#xbow2 dw#xbow2 dw#xbow2   443            444
>>>>>>>>


  LAF 2da_process_table INT_VAR inline=1 STR_VAR table=fineweapon.2da function=make_fine_weapon END

    COPY ~%MOD_FOLDER%/%component_loc%/icons~ ~override~

    ACTION_IF is_bg BEGIN
       LAF bg_items END
    END
    LAF fine_items_animate_dead END
	ACTION_IF iwdification BEGIN
		LAF fine_items_cd_compat END
	END

END

DEFINE_ACTION_FUNCTION make_fine_weapon
     INT_VAR hit=0
             dmg=0
             cost=0
             desc_string=0
             name_string=0
	 STR_VAR
			 nonmagic_wpn=""
             magic_wpn=""
             BAM1=""
             BAM2=""
             kept_copy=""
			 cd_version=""
BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "%tutu_var%%nonmagic_wpn%.itm" && !FILE_EXISTS_IN_GAME ~%kept_copy%.itm~ && FILE_EXISTS_IN_GAME "%tutu_var%%magic_wpn%.itm" BEGIN
        COPY_EXISTING "%magic_wpn%.itm" "override/%kept_copy%.itm"
	    itm.copy[%tutu_var%%nonmagic_wpn%=>%tutu_var%%magic_wpn%]
	    [
			m_both_names:=(AT name_string)
			desc:=(AT desc_string)
			LPF handle_unusable STR_VAR arguments="%desc%" RET m_unidentified_description=value END
			m_identified_description="-1"
			m_icon:="%BAM1%"
			m_price=iwdification?(cost / 2):cost // if IWDification present, lower cost bc items are more frequent
			m.ab.alter{s_icon:=%BAM2% s_attack_bonus=hit s_damage_bonus=dmg}  
			m.fx.alter{s_resource:="%tutu_var%%magic_wpn%"|match="~%s_resource%~==%tutu_var%%nonmagic_wpn%"} // mostly for ToF monks-use-staves
	    ]

        ACTION_IF is_bg1 BEGIN
			OUTER_SET shatter_opcode = (GAME_IS ~bgt~)? 309 : 146
			ACTION_IF shatter BEGIN
				itm.edit[%tutu_var%%magic_wpn%]
				[
					m.ab_fx.alter{s_resource:=%tutu_var%%magic_wpn%;; LPF ironcrisis_bgt STR_VAR arguments=%magic_wpn% END|match=~s_opcode=shatter_opcode AND "%s_resource%" STR_EQ %tutu_var%%nonmagic_wpn%~}
				]
				spl.copy[%tutu_var%%nonmagic_wpn%=>%tutu_var%%magic_wpn%|allow_missing:i=1]
				[
					m.ab_fx.alter{s_resource:=%tutu_var%%magic_wpn%|match=~s_opcode=112 AND "%s_resource%" STR_EQ %tutu_var%%nonmagic_wpn%~}
				]
			END ELSE BEGIN
				itm.edit[%tutu_var%%magic_wpn%]
				[
					m.ab_fx.delete{s_opcode=shatter_opcode AND "%s_resource%" STR_EQ %tutu_var%%nonmagic_wpn% }
				]
			END
        END
		ACTION_IF FILE_EXISTS_IN_GAME "%cd_version%.itm" BEGIN
			COPY_EXISTING "%tutu_var%%magic_wpn%.itm" "override/%cd_version%.itm"
		END
    END
END

DEFINE_PATCH_FUNCTION ironcrisis_bgt
    STR_VAR arguments=""
BEGIN
   INNER_ACTION BEGIN
    ACTION_IF GAME_IS ~bgt~ BEGIN
      <<<<<<<< .../stratagems-inline/crisis_add.baf
         IF
	InParty(Myself)
	Global("ENDOFBG1","GLOBAL",0)
	HasItemEquipedReal("%arguments%",Myself)
	Global("%arguments%","LOCALS",1)
THEN
	RESPONSE #50
		DisplayStringHead(Myself,74465) // My weapon has shattered!
		SetGlobal("%arguments%","LOCALS",0)
		TakeItemReplace("BGMISC56","%arguments%",Myself) // Flail
	RESPONSE #50
		SetGlobal("%arguments%","LOCALS",0)
		Continue()
END
      >>>>>>>>
       LAF extend INT_VAR inline=1 STR_VAR script=dplayer2 bottom=crisis_add END
     END
    END
END



DEFINE_ACTION_FUNCTION bg_items BEGIN

	LAF 2da_process_table INT_VAR inline=1 STR_VAR table=magic_items.2da function=bg1_items_main END
	COPY_EXISTING "%tutu_var%inn3351.sto" override
		ADD_STORE_ITEM "dw#axe1" AFTER "%tutu_var%AX1H02" #1 #0 #0 IDENTIFIED #1


END


DEFINE_ACTION_FUNCTION bg1_items_main
	STR_VAR resource=""
			old=""
			new=""
	BEGIN
		COPY_EXISTING "%resource%" override
			REPLACE_TEXTUALLY "%old%" "%new%" (8)


END



<<<<<<<<.../stratagems-inline/magic_items.2da
resource						old					new
%tutu_var%drasus.cre			%tutu_var%blun07	dw#msta1 
%tutu_var%wheber.cre			%tutu_var%dagg02	dw#dagg1
%FishingVillage_AnkhegPit%.are	%tutu_var%dagg02	dw#dagg1
%tutu_var%sonner.dlg			%tutu_var%blun03	dw#flail
%DryadFalls%.are				%tutu_var%halb02	dw#dagg1
%tutu_var%diyab.cre				%tutu_var%hamm02	dw#hamm1
%tutu_var%silke.cre				%tutu_var%staf02	dw#stf01
%tutu_var%ogrmba.cre			%tutu_var%sw1h02	dw#swd01
%tutu_var%telano.cre			%tutu_var%sw1h05	dw#swd02
%tutu_var%tuth.cre				%tutu_var%sw1h05	dw#swd02
%tutu_var%angelo.cre			%tutu_var%sw1h05	dw#swd02
%tutu_var%desret.cre			%tutu_var%sw1h05	dw#swd02
%tutu_var%nimbul.cre			%tutu_var%sw1h08	dw#swd03
%tutu_var%thaldo.cre			%tutu_var%sw1h08	dw#swd03
>>>>>>>>


DEFINE_ACTION_FUNCTION fine_items_animate_dead BEGIN

	COPY_EXISTING "skelsu07.cre" override
		REPLACE_TEXTUALLY sw1h05 dw#swd02 (8)
	IF_EXISTS
	COPY_EXISTING "skelsu11.cre" override
		REPLACE_TEXTUALLY sw1h02 dw#swd01 (8)
	IF_EXISTS

END

DEFINE_ACTION_FUNCTION fine_items_cd_compat BEGIN

	// IWDification compatibility mostly done already. We just need to get rid of high-Q ammo
	
	COPY_EXISTING "rndtres.2da" override
		REPLACE_TEXTUALLY "cdhqarow" "*"
		REPLACE_TEXTUALLY "cdhqbolt" "*"
		REPLACE_TEXTUALLY "cdhqbull" "*"
		PRETTY_PRINT_2DA
	BUT_ONLY


END
