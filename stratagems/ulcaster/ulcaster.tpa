DEFINE_ACTION_FUNCTION ulcaster BEGIN

       LAF define_difficulty STR_VAR type=genai RET difficulty_variable END

       LAF icharyd END
       LAF ulcaster_wolf END
END

DEFINE_ACTION_FUNCTION icharyd BEGIN

     // the weather - turn off to start with, have script to turn it off again after Ich dies
     
     LAF edit_area STR_VAR area=~%Ulcaster%~ editstring=~rain_probability=>0~ END
     LAF extend_area_script STR_VAR area=~%Ulcaster%~ top=dw#ichstorm location=resource END

     // icharyd's script
     
     LAF ssl_to_bcs STR_VAR script=dw#ichar location=~ssl~ END

     // icharyd creature edits
     
     MAKE_PATCH
        resist_magic=>90 //Icharyd is a sort of Skeleton Warrior, so deserves some magic resistance
        xp_value=>1500
        hitpoints=>80
        script_class=>dw#ichar
        add_items=>~minhp1(amulet)~
     END
     LAF edit_creature INT_VAR tv=1 STR_VAR creature=ichary edits=patch_data END
     // icharyd's spell and dialog
     
     LAF install STR_VAR file=~dw#ichar.spl dw#ichar.d~ location=resource END

END

DEFINE_ACTION_FUNCTION ulcaster_wolf BEGIN

      // scripting
      
      LAF ssl_to_bcs STR_VAR script=~dw#rewol dw#ulcgr dw#ulcwo~ location=~ulcaster/ssl~ END

      // lackeys
      
      LAF clone_creature STR_VAR creature=~%tutu_var%wolfdr=>dw#rewo~ editstring=~script_override=>dw#rewol~ END

      // howl-of-the-grave auxiliary
      
      LAF install STR_VAR file=dw#ulcgr.cre location=resource END
      LAF edit_creature STR_VAR creature=dw#ulcgr editstring=~script_override=>dw#ulcgr~ END

      // the wolf
      
      MAKE_PATCH
         say_both_names=>21705
         hitpoints=>200
         ac_natural=>~-2~
         ac_effective=>~-2~
         thac0=>11
         attacks=>2
         xp_value=>4000
         script_override=>"dw#ulcwo"
      END
      LAF clone_creature STR_VAR creature=~%tutu_var%wolfva=>dw#ulcwo~ edits=patch_data END

      MAKE_PATCH
         patch_actor_inline=>~match=>"actor_resource=%tutu_var%wolfva" actor_resource=>dw#ulcwo~
      END
      LAF edit_area STR_VAR area=~%UlcasterRuins%~ edits=patch_data END

END

