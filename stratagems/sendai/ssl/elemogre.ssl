BEGIN_ACTION_DEFINITION
	Name(ForceSpellRES)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpellRES(scsargument1,scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(ForceSpellMyselfRES)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpellRES(scsargument1,Myself)
END

BEGIN_ACTION_DEFINITION
	Name(Attack)
	TRIGGER
	ACTION
		RESPONSE #scsprob1
		SetGlobal("validtarget","LOCALS",1)
		EquipMostDamagingMelee()
		AttackOneRound(scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(AttackIfEmpty)
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		SetGlobal("validtarget","LOCALS",1)
		EquipMostDamagingMelee()
		AttackOneRound(scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(MoronAttack)
	TRIGGER
		False()
	ACTION
		RESPONSE #100
		NoAction()
END

BEGIN_ACTION_DEFINITION
	Name(MoveIfEmpty)
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		MoveToObject(scstarget)
END

/////////////////////////////////////////////////////////
/////              Rest and recover
////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("rest","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobal("rest","LOCALS",1)
		Continue()
END

IF
	Global("rest","LOCALS",1)
	!GlobalTimerNotExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
// reset all variables here
		ApplySpell(Myself,RESTORE_FULL_HEALTH) //healing spell
		Rest()
END

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END

///////////////////////////////////////////////////////////////////////////////
///	Bluster
///////////////////////////////////////////////////////////////////////////////

IF
	Detect([PC])
	Global("Display","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Display","LOCALS",1)
		DisplayStringHead(Myself,72788) // Who calls Ogremoch, Prince of Earth!
END

//////////////////////////////////////////////////////////////////////////////////
///	Stoneskins
//////////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	See(NearestEnemyOf(Myself))
	!CheckStatGT(Myself,2,STONESKINS)
THEN DO
	Action(ForceSpellMyselfRES,"dw#ogrem")
END

////////////////////////////////////////////////////////////////////////////////////
////	Earthquake
////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("earthquake","LOCALS")
	Target(Myself)
THEN DO
	Action(ForceSpellRES,"spogre01")
	SetGlobalTimer("earthquake","LOCALS",30)
	DisplayStringHead(Myself,72789) // Feel the wrath of Ogremoch!
END

////////////////////////////////////////////////////////////////////////////////////
///	Combat
////////////////////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||5)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
END LOOP
END LOOP