INCLUDE ~%MOD_FOLDER%/lib/ai_wrap.tph~
DEFINE_ACTION_FUNCTION sendai BEGIN

    LAF check_ini STR_VAR ini="Sendai_Component_Is_Sendai_Only" RET value END
    ACTION_IF !value BEGIN
        ACTION_FOR_EACH func IN drow odamaron illithid ogremoch BEGIN
           LAF ~sendai_%func%~ END
        END
    END

    LAF sendai_main END

    ACTION_IF FILE_EXISTS_IN_GAME ~dw#melis.cre~ BEGIN // check for Wheels of Prophecy
    
       LAF sendai_wheels END
    END


END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION sendai_drow BEGIN

          // reinforces central-area drow

          LAF clone_creature STR_VAR creature=~dglab01=>dw#sengl~ editstring=~insert_script_high=>gensht01~ END
          LAF clone_creature STR_VAR creature=~gortan3=>dw#senyl~ editstring=~insert_script_high=>gensht01 allegiance=>enemy~ END
          LAF swap_text STR_VAR files=~ar6104.are~ swaps=~rdsend1=>rdspid5~ END
          LAF extend_area_script STR_VAR area=ar6104 bottom=ar6104 location=resource END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION sendai_odamaron BEGIN

        // add Odamaron's followers; restore his (limited) dialogue

        // lesser lich
        MAKE_PATCH
              level=>24
              name1_string=>64207
              name2_string=>64207
              enforce_charclass=>null
        END
        ACTION_IF FILE_EXISTS_IN_GAME ~dw#lich1~ BEGIN 
           OUTER_SPRINT lichname dw#lich1
        END ELSE BEGIN
           OUTER_SPRINT lichname lich01
        END
        LAF clone_creature STR_VAR creature=~%lichname%=>dw#odam1~ edits=patch_data END

        // vampire mage (straight copy of BODTAN)
        
        MAKE_PATCH
           allegiance=>enemy
           name1_string=>64207
           name2_string=>64207
        END
        LAF clone_creature STR_VAR creature=~bodtan=>dw#odam2~ edits=patch_data END

        // cutscene

        LAF install STR_VAR files=~dw#slcut.baf~ location=resource END

        // dialog changes
        
        LAF edit_creature STR_VAR creature=senlich editstring=~insert_script_high=>shoutdl2~ END

	<<<<<<<< .../stratagems-inline/dw#senlich.d

	REPLACE_STATE_TRIGGER senlich 0 ~NumTimesTalkedTo(0)~
	ADD_TRANS_TRIGGER senlich 1 ~False()~
	ADD_TRANS_TRIGGER senlich 2 ~False()~
	EXTEND_BOTTOM senlich 1 2 
		IF ~~ THEN GOTO followers
	END

	APPEND senlich

	IF ~~ THEN BEGIN followers
	SAY @503
	IF ~~ THEN DO ~Enemy() StartCutSceneMode() StartCutScene("dw#slcut")~
	EXIT END

	END


	>>>>>>>>

        LAF install INT_VAR inline=1 STR_VAR files=~dw#senlich.d~ END
        
         // Only GOODCUTOFFs set off Odamaron's traps
         
        LAF swap_text STR_VAR files=~gt002.bcs gt019.bcs~ swaps=~ANYONE=>GOODCUTOFF~ END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION sendai_illithid BEGIN

	// Set up a rather less derisory battle with the illithids

        LAF extend_area_script STR_VAR area=ar6107 top=ar6107 location=resource END

        // mage thralls with antimagic attacks (some included in mage/override/b2/spellchoice.2da)
        
        MAKE_PATCH
           add_items=>dw#dispe
           say_both_names=>501
           enforce_charclass=>null
        END
        LAF clone_creature STR_VAR creature=~mage16b=>dw#thr01 mage16c=>dw#thr02~ edits=patch_data END

        // warrior thralls
        
        MAKE_PATCH
           allegiance=>enemy
           insert_script_high=>dw#thr01
           say_both_names=>502
        END
        LAF clone_creature STR_VAR creature=~sargrd07=>dw#thr03 sargrd08=>dw#thr04~ edits=patch_data END
        LAF install STR_VAR file=~dw#thr01.baf~ location=resource END

        // remove existing illithids

        LAF edit_area STR_VAR area=ar6107 editstring="delete_actor=>~actor_resource is_in [senill02 mindul01 umbhul01]~" END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION sendai_ogremoch BEGIN

       MAKE_PATCH
          hitpoints=>480
          strip_scs_scripts=>null
          strip_script=>wtasight
       END
       LAF edit_creature STR_VAR creature=elemogre edits=patch_data END

       LAF edit_item STR_VAR item=elemogre editstring=~enchantment=>5~ END

       LAF ssl_to_bcs STR_VAR script=elemogre location=~ssl~ END

       LAF install STR_VAR file=dw#ogrem.spl location=resource END
END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION sendai_main BEGIN

     // boost mages and priests
     
     LAF edit_creature STR_VAR creature=drofod02 editstring=~level=>16 enforce_charclass=>accept_level~ END
     LAF edit_creature STR_VAR creature=drofod04 editstring=~level=>17 enforce_charclass=>accept_level~ END

     // make statues wake up in a rather more timely manner

     COPY_EXISTING ~ar6108.bcs~ ~override~
	 REPLACE_BCS_BLOCK ~%MOD_FOLDER%\%component_loc%\resource\ar6108_old.baf~ ~%MOD_FOLDER%\%component_loc%\resource\ar6108_new.baf~

      // hide two imprisoned Balors, as insurance against party use of Imprisonment

     LAF clone_creature STR_VAR creature=~demosum2=>dw#sbaim~ editstring=~add_effect_inline=>"opcode=>211"~ END
     LAF extend_area_script STR_VAR area=ar6108 top=hiddenbalor location=resource END

     //deal with superabundance of drow katanas+2

     LAF install STR_VAR file=~dw#dwkat.itm~ location=resource END
     LAF edit_item STR_VAR item=dw#dwkat editstring=~say_name=>700 say_description=>701~ END

     ACTION_FOR_EACH areacode IN 6100 4500 2500 BEGIN
        LAF extend_area_script STR_VAR area=~ar%areacode%~ top=drowkat location=resource END
     END

    // need to make sure that drow encountered on the surface don't have adamantine weapons
    LAF clone_creature STR_VAR creature=~drofod01=>dw#drfou~ END

    LAF swap_text STR_VAR files=~amb6100a.bcs amb6100b.bcs~ swaps=~drofod01=>dw#drfou~ END
    LAF swap_text STR_VAR files=~drofod01.cre sendro01.cre senfod01.cre senken01.cre~ swaps=~sw1h55=>dw#dwkat~ END

    // a bit more drow extravagance
    
    LAF swap_text STR_VAR files=~sengua01.cre~ swaps=~chan12=>dwchan01~ END
    LAF swap_text STR_VAR files=~drofod01.cre drofod04.cre~ swaps=~chan13=>dwchan01~ END
    LAF swap_text STR_VAR files=~sengua04.cre~ swaps=~plat05=>dwplat01~ END
    LAF swap_text STR_VAR files=~drofod03.cre~ swaps=~xbow17=>dwxbow01 bolt02=>dwbolt03 bolt09=>dwbolt01~ END
    LAF swap_text STR_VAR files=~senorc02.cre senorc04.cre~ swaps=~xbow05=>dwxbow01 sw1h09=>dwsw1h02 bolt02=>dwbolt03~ END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION sendai_wheels BEGIN

	/// Wheels of Prophecy compatibility

        LAF clone_creature STR_VAR creature=~drofod01=>dw#vidr1 drofod03=>dw#vidr3 senorc04=>dw#vider~ editstring=~script_override=>dw#vilat gender=>EXTRA6~ END
        LAF clone_creature STR_VAR creature=~drofod02=>dw#vidr2 drofod04=>dw#vidr4~ editstring=~script_override=>dw#vilat gender=>EXTRA7~ END


END
