DEFINE_ACTION_FUNCTION collect_iwd_resources STR_VAR version=0 BEGIN

   // first install IWD spell fixes
   
   LAF include STR_VAR file=iwdfix.tph locbase=lib END
   LAF iwd_spell_fixes END


   ACTION_IF !IS_AN_INT version BEGIN
         OUTER_SET version=0
   END
   OUTER_SPRINT resource_loc "copyover"
   OUTER_SPRINT iwdspells_data "%component_loc%/data"
   OUTER_SPRINT iwdspells_lib "%component_loc%/lib"
   OUTER_SPRINT sfo_loc sfo
   OUTER_SPRINT ssl_loc ssl
   OUTER_SPRINT ds_loc ds
   OUTER_SPRINT iwdspells_resource "%component_loc%/resource"
   OUTER_SPRINT iwdspells_trabase lang
   OUTER_SET get_all_languages=1  // set to 0 when testing the install, because it speeds things up a lot

   OUTER_SPRINT component_loc "" // my conventions in DW_IWDSPELLS are inconsistent about checking component_loc; simplest to zap it

   LAF include STR_VAR file=get_iwd_spell_resources.tpa location="%iwdspells_lib%" END

   ACTION_IF (version=1||version=0) BEGIN

   // arcane spells

   LAF get_iwd_spell_resources
            STR_VAR
                file=iwd_arcane.2da 
                location="%iwdspells_data%"
                smtables_list=arcane_smtable.txt
                proj_list="arcane_proj.txt"
                resref_list="arcane_resrefs.txt"
                scroll_price_list=scroll_prices_arcane.txt
   END

   // divine spells

   LAF get_iwd_spell_resources
            INT_VAR
                CMW_string=1
            STR_VAR
                file=iwd_divine.2da
                location="%iwdspells_data%"
                smtables_list=divine_smtable.txt
                proj_list="divine_proj.txt"
                resref_list="divine_resrefs.txt"
                scroll_price_list=scroll_prices_divine.txt
   END

   END
   
   ACTION_IF (version=2||version=0) BEGIN


   // animations

   LAF include STR_VAR file=collect_animations.tpa location="%iwdspells_lib%" END
   ACTION_BASH_FOR "%scsroot%/%iwdspells_data%/anim" ".*\.ini" BEGIN
      LAF collect_iwd_animations STR_VAR ini="%BASH_FOR_FILESPEC%" anim_loc="%resource_loc%/anim/bam" wav_loc="%resource_loc%/anim/wav" END
   END

   // icon strings and BAMs

   LAF get_iwd_icons END

   // copy-only spell BAMs

   LAF include STR_VAR file=get_iwd_spell_bams.tpa location="%iwdspells_lib%" END
   LAF get_iwd_spell_bams STR_VAR file="bam_copy_arcane.2da" END
   LAF get_iwd_spell_bams STR_VAR file="bam_copy_divine.2da" END

   // TRA entries

   LAF process_tra_entries_all END

   END




END