DEFINE_ACTION_FUNCTION iwdspells_arcane STR_VAR version="" BEGIN

	LAF check_label STR_VAR label="dw_iwdspells_arcane_installed" RET value END
	ACTION_IF !value BEGIN
		LAF make_label STR_VAR label="dw_iwdspells_arcane_installed" END
		OUTER_SPRINT custom_includes "%version%" // pass through 'version' to conform to rules for SFO 'run' function

		INCLUDE "%MOD_FOLDER%/%component_loc%/lib/move_spell_resources.tph"

		LAF check_ini INT_VAR silent=1 STR_VAR section=iwdspells ini=no_cap_at_level_20 RET value END
		OUTER_SET truncate_at_level=value?"-1":20

		LAF check_ini INT_VAR silent=1 STR_VAR section=iwdspells ini=do_not_overwrite_old_spells RET value END
		OUTER_SET overwrite_always=!value

		OUTER_SPRINT resource_loc "%MOD_FOLDER%/%component_loc%/copyover"
		// greenify a few summoning spell icons
		
		ACTION_FOR_EACH spell IN
			CONJURE_EARTH_ELEMENTAL
			CONJURE_AIR_ELEMENTAL
			CONJURE_FIRE_ELEMENTAL
			SUMMON_PLANATAR_GOOD
			SUMMON_PLANATAR_EVIL
			CREATE_BONEGUARD
		BEGIN
			OUTER_SPRINT resref EVAL "%WIZARD_%spell%%"
			ACTION_IF FILE_EXISTS_IN_GAME "%resref%.spl" BEGIN
				LAF make_bams_green STR_VAR resref END
			END
		END
		
		//SR-relative settings
		
		ACTION_IF MOD_IS_INSTALLED "spell_rev/setup-spell_rev.tp2" 0 BEGIN
			OUTER_SPRINT overwrite_these_spells "overwrite_sr.2da"
			OUTER_SPRINT skip_these_spells "skip_sr.2da"
		END ELSE BEGIN
			OUTER_SPRINT overwrite_these_spells "overwrite.2da"
			OUTER_SPRINT skip_these_spells ""
		END
		
		// main install
		
		LAF install_spell_resources
			INT_VAR truncate_at_level
					overwrite_always
			STR_VAR resource_loc
					prefix=arcane
					file=iwd_arcane.2da
					location=data
					anim_remaps="anim_maps.2da"
					resource_overwrite="resource_overwrite_bg2.2da"
					old_style="%MOD_FOLDER%/%sfo_library_path%/data/spell_styles_iwd.2da"
					new_style="%MOD_FOLDER%/%sfo_library_path%/data/spell_styles_bg.2da"
					overwrite_these_spells
					skip_these_spells
					log="iwd_spells_installed.txt"
					afterwards="iwd_arcane_spells_postproduction"
					afterwards_tra="dw_iwdspells"
					custom_includes
		END
		
		// bams
		
		LAF install_spell_bams
			STR_VAR resource_loc
					location=data
					file=bam_copy_arcane.2da
		END
		
		// rebuild hotkeys
		
		ACTION_IF enhanced_edition BEGIN
			LAF rebuild_spell_hotkeys END
		END
		
		// oBG2 icons
		
		ACTION_IF !enhanced_edition BEGIN
		
			// main icons already installed?
			LAF check_label STR_VAR label="dw#iwdspells_obg2_icons_installed" RET proceed=value END
			ACTION_IF !proceed BEGIN
				// if not, check the ini
				LAF check_ini INT_VAR silent=1 STR_VAR section="iwdspells" ini=install_obg2_icons RET proceed=value END
			END
			ACTION_IF proceed BEGIN
				LAF run STR_VAR file=iwdspells_icons END
			END

		
		END
		
	END ELSE BEGIN// end of outer LABEL check

		PRINT "IWD arcane spells have already been installed and will not be installed again."

	END
END
