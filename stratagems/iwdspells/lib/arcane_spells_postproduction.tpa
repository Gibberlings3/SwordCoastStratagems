DEFINE_ACTION_FUNCTION arcane_spells_postproduction STR_VAR tra="" BEGIN

    ACTION_IF is_iwd BEGIN
           WITH_TRA "%tra%" BEGIN
              LAF force_blade END
            END
    END ELSE BEGIN
          LAF include STR_VAR file=summoned_monsters.tpa location="%iwdspells_lib%" END
          LAF level_immunity_mage END
          LAF expeditious_retreat_icon END
          ACTION_IF "%tra%" STRING_EQUAL_CASE "" BEGIN
            ACTION_IF !demivrgvs BEGIN
              LAF summoned_monsters_arcane END
                LAF include STR_VAR file=elemental_summoning.tpa location="%iwdspells_lib%" END
                LAF elemental_summoning RET WIZARD_CONJURE_WATER_ELEMENTAL_SCROLL=scroll_new END
            END
            LAF force_blade END
          END ELSE BEGIN
            WITH_TRA "%tra%" BEGIN
              ACTION_IF !demivrgvs BEGIN
                LAF summoned_monsters_arcane END
                LAF include STR_VAR file=elemental_summoning.tpa location="%iwdspells_lib%" END
                LAF elemental_summoning RET WIZARD_CONJURE_WATER_ELEMENTAL_SCROLL=scroll_new END
              END
              LAF force_blade END
            END
          END
    END

END

DEFINE_ACTION_FUNCTION force_blade BEGIN


    // rename the spell
    COPY_EXISTING "%WIZARD_MORDENKAINENS_SWORD_IWD%.SPL" override
        READ_STRREF NAME1 old_name
        SPRINT new_name @10
        SAY NAME1 "%new_name%"
        READ_STRREF 0x50 old_desc
        INNER_PATCH_SAVE new_desc "%old_desc%" BEGIN
           REPLACE_TEXTUALLY "%old_name%" "%new_name%"
        END
        SAY 0x50 "%new_desc%"
    BUT_ONLY

    // rename the scroll
    COPY_EXISTING "%WIZARD_MORDENKAINENS_SWORD_IWD_SCROLL%.ITM" override
        SAY NAME2 "%new_name%"
        SAY DESC "%new_desc%"
    BUT_ONLY
    
    // on BG2, reverse the icons, since the green one (copied over with SWORD_IWD) ought to be associated with the summons effect

    ACTION_IF !is_iwd BEGIN
       ACTION_FOR_EACH letter IN a b c BEGIN
         COPY_EXISTING "%WIZARD_MORDENKAINENS_SWORD_IWD%%letter%.bam" "%workspace%"
         COPY_EXISTING "%WIZARD_MORDENKAINENS_SWORD%%letter%.bam" "override/%WIZARD_MORDENKAINENS_SWORD_IWD%%letter%.bam"
         COPY "%workspace%/%WIZARD_MORDENKAINENS_SWORD_IWD%%letter%.bam" "override/%WIZARD_MORDENKAINENS_SWORD%%letter%.bam"
      END
    END
    
    ACTION_IF is_iwd BEGIN
    // on IWD, manually add a scroll to the store
     COPY_EXISTING edion.sto override
       ADD_STORE_ITEM "%WIZARD_MORDENKAINENS_SWORD_SCROLL%" AFTER "%WIZARD_SUFFOCATE_SCROLL%" #1 #0 #0  IDENTIFIED #1
     BUT_ONLY
    END


END

DEFINE_ACTION_FUNCTION level_immunity_mage BEGIN


    ACTION_FOR_EACH resource IN "%WIZARD_MINOR_GLOBE_OF_INVULNERABILITY%.spl" "%WIZARD_GLOBE_OF_INVULNERABILITY%.spl" "SPWM126.spl" "JWGLOBE.spl" "lich.itm" "rakring.itm" "demilich.itm" BEGIN
     ACTION_IF FILE_EXISTS_IN_GAME "%resource%" BEGIN
      COPY_EXISTING "%resource%" override
         LPF CLONE_EFFECT INT_VAR match_opcode=206 STR_VAR match_resource=SPPR105 resource="%WIZARD_SNILLOCS_SNOWBALL_SWARM%" END
      BUT_ONLY
     END
    END

END


DEFINE_ACTION_FUNCTION expeditious_retreat_icon BEGIN


    COPY_EXISTING "%WIZARD_EXPEDITIOUS_RETREAT%.spl" override
       LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=195 END


END


