/// operates *after* primary converter run, to sort out summons


DEFINE_ACTION_FUNCTION summoned_monsters_arcane BEGIN

    LAM read_in_saving_throws

/////////////////////////////////////////////////////
//// Create Boneguard
////
////  DEPRECATED for now, it's probably a bridge too
////  far and I'll do it in SCS
///////////////////////////////////////////////////

/////////////////////////////////////////////////////
//// MS7
///////////////////////////////////////////////////

ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_MONSTER_SUMMONING_7") BEGIN

//// Umber hulk - baseline is IWDEE's MS7 UH but it needs BG2-style immunities and confusion

   MAKE_PATCH
      delete_effect=>"opcode=233" // pointless cruft in IWD file
      remove_items=>umberhlk
      add_items=>"helmnoan umber01"
      patch_item_inline=>"unstealable=>1 undroppable=>1"
      swap_script=>"UHGAZE=>DW#UMBER GNSUMMM=>bdsum00"
      dv=>ms7umber
      xp_value=>0
   END
   LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=MS7UMBH edits=patch_data END
   LAF install STR_VAR file=dw#umber.baf location="%iwdspells_resource%" END

//// Ettin - baseline is BG2 ettin

   ACTION_IF !FILE_EXISTS_IN_GAME "pettin.cre" BEGIN
                 COPY "%scsroot%/%iwdspells_resource%/pettin.cre" override
                      SAY 0x8 @15
                      SAY 0xc @15
   END

   MAKE_PATCH
       allegiance=>controlled
       gender=>summoned
       dv=>ms7ettin
       no_corpse=>1
       swap_script=>"wtasight=>bdsum00"
       xp_value=>0
   END
   LAF clone_creature INT_VAR allow_missing=1 STR_VAR creature="PETTIN=>DW#MS7ET" edits=patch_data END

   LAF make_summoning_2da STR_VAR monsters="MS7UMBH DW#MS7ET" filename=msummo7 END
END

/////////////////////////////////////////////////////
//// MS6
///////////////////////////////////////////////////


ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_MONSTER_SUMMONING_6") BEGIN

/// Frost salamander  - base is IWDEE's MS6 salamander but needs BG2-style aura etc

     MAKE_PATCH
          delete_effect=>"opcode=233"
          remove_items=>salfros
          add_items=>"ring95 immune1 salgrice"
          class=>ELEMENTAL_WATER // best match - no SALAMANDER class in BG2
          dv=>MS6salac
          strip_script=>safrost
          patch_item_inline=>"unstealable=>1 undroppable=>1"
          add_proficiencies=>"spear=>2"
          swap_script=>"gnsummm=>bdsum00"
          xp_value=>0
     END
     LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=MS6SALC edits=patch_data END

//// Fire salamanader - ditto

     MAKE_PATCH
          delete_effect=>"opcode=233"
          add_item=>"ring95 immune1"
          class=>ELEMENTAL_FIRE// best match - no SALAMANDER class in BG2
          dv=>MS6salaf
          remove_items=>salfire
          add_items=>salgrfir
          strip_script=>safire
          patch_item_inline=>"unstealable=>1 undroppable=>1"
          add_proficiencies=>"spear=>2"
          swap_script=>"gnsummm=>bdsum00"
         xp_value=>0
     END
     LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=MS6SALF edits=patch_data END

//// giant troll - base is BG2 trolgi01

   ACTION_IF !FILE_EXISTS_IN_GAME "trolgi01.cre" BEGIN
                 COPY "%scsroot%/%iwdspells_resource%/trolgi01.cre" override
                      SAY 0x8 @17
                      SAY 0xc @17
   END

     MAKE_PATCH
       allegiance=>controlled
       gender=>summoned
       dv=>ms6gttrl
       no_corpse=>1
       swap_script=>"wtasight=>bdsum00"
       strip_script=>gensht01
       xp_value=>0
     END
     LAF clone_creature INT_VAR allow_missing=1 STR_VAR creature="trolgi01=>DW#MS6GT" edits=patch_data END

     LAF make_summoning_2da STR_VAR monsters="MS6SALC MS6SALF DW#MS6GT" filename=msummo6 END
END

/////////////////////////////////////////////////////
//// MS5
///////////////////////////////////////////////////

ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_MONSTER_SUMMONING_5") BEGIN

  /// ordinary troll

   ACTION_IF !FILE_EXISTS_IN_GAME "troll01.cre" BEGIN
            OUTER_SET troll_string=GAME_IS bgee ? 25772 : 3008
            COPY "%scsroot%/%iwdspells_resource%/troll01.cre" override
                      WRITE_LONG 0x8 troll_string
                      WRITE_LONG 0xc troll_string
   END

     MAKE_PATCH
       allegiance=>controlled
       gender=>summoned
       dv=>ms5troll
       no_corpse=>1
       swap_script=>"wtatroll=>bdsum00"
       xp_value=>0
     END
     LAF clone_creature INT_VAR allow_missing=1 STR_VAR creature="troll01=>DW#MS5TR" edits=patch_data END

 /// minotaur
 
     MAKE_PATCH
        delete_effect=>"opcode=233"
        immunity_to_spell=>"%WIZARD_MAZE% %PSIONIC_MAZE%"
        remove_items=>"s1-8m1"
        add_items=>"ax1h01"
        patch_item_inline=>"unstealable=>1 undroppable=>1"
        add_proficiencies=>"axe=>2"
        strength=>18
        str_ex=>100
        xp_value=>0
          swap_script=>"gnsummm=>bdsum00"
     END
     LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=ms5mino edits=patch_data END
     

     LAF make_summoning_2da STR_VAR monsters="MS5MINO DW#MS5TR" filename=msummo5 END
END

/////////////////////////////////////////////////////
//// MS4
///////////////////////////////////////////////////

ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_MONSTER_SUMMONING_4") BEGIN

     // ogre mage - based on unused monster-summon in BG2

   ACTION_IF !FILE_EXISTS_IN_GAME "ogremasu.cre" BEGIN
                 COPY "%scsroot%/%iwdspells_resource%/ogremasu.cre" override
   END

     LAF ds_resolve_stat STR_VAR id=ITEM_MM RET stat_ind END
     LAF ds_resolve_stat STR_VAR id=ITEM_MM_SEEN RET stat_ind END
     LAF ds_resolve_stat STR_VAR id=DO_NOT_TARGET_SPELLS RET stat_ind END
     LAF install STR_VAR file="ogremasu.baf" locbase="iwdspells/resource" END

     MAKE_PATCH
          allegiance=>controlled
          no_corpse=>1
          dv=>ms4ogrem
          add_proficiencies=>"singleweapon=>2 bastardsword=>2"
          remove_spells=>all
          add_spells=>"MAGIC_MISSILE(2) BURNING_HANDS(2) MIRROR_IMAGE MELF_ACID_ARROW LIGHTNING_BOLT GHOST_ARMOR"
          swap_script=>"wtasight=>bdsum00"
          xp_value=>0
     END
     LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=ogremasu edits=patch_data END

     /// yuan-ti

   ACTION_IF !FILE_EXISTS_IN_GAME "icyuan01.cre" BEGIN
                 COPY "%scsroot%/%iwdspells_resource%/icyuan01.cre" override
                      SAY 0x8 @16
                      SAY 0xc @16
   END

     MAKE_PATCH
       allegiance=>controlled
       gender=>summoned
       dv=>ms5yuant
       no_corpse=>1
       swap_script=>"wtasight=>bdsum00"
       xp_value=>0
     END
     LAF clone_creature INT_VAR allow_missing=1 STR_VAR creature="icyuan01=>DW#MS4YU" edits=patch_data END

     LAF make_summoning_2da STR_VAR monsters="OGREMASU DW#MS4YU" filename=msummo4 END
END

/////////////////////////////////////////////////////
//// MS3
///////////////////////////////////////////////////


ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_MONSTER_SUMMONING_3") BEGIN

    // ogre berserker

    MAKE_PATCH
       kit=>berserker
       enforce_class=>null
       remove_items=>ogre1
       add_items=>blun06
       add_proficiencies=>"FLAILMORNINGSTAR=>2"
       patch_item_inline=>"unstealable=>1 undroppable=>1"
       insert_script_high=>"dw#msogr"
       no_corpse=>1
       xp_value=>0
    END
    LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=ogregrsu edits=patch_data END
    LAF install STR_VAR file=dw#msogr.baf location="%iwdspells_resource%" END

    // hobgoblin elite
    MAKE_PATCH
       add_proficiencies=>"BASTARDSWORD=>2 LONGBOW=>2"
       no_corpse=>1
       xp_value=>0
    END
    LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=hobgobsu edits=patch_data END

    // ettercap

    MAKE_PATCH
       no_corpse=>1
       xp_value=>0
    END
    LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=ettercsu edits=patch_data END

    LAF make_summoning_2da STR_VAR monsters="OGREGRSU ETTERCSU HOBGOBSU" filename=msummo3 END

END

/////////////////////////////////////////////////////
//// MS2
///////////////////////////////////////////////////


ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_MONSTER_SUMMONING_2") BEGIN

    // gnoll
    
    MAKE_PATCH
       level=>3
       name1_string=>6627
       name2_string=>6627
       hitpoints=>24
       attacks=>1
       no_corpse=>1
       add_proficiencies=>"spear=>1"
       xp_value=>0
    END
    LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=gnollsu edits=patch_data END
    
    // ogrillon
    
    MAKE_PATCH
        attacks=>1
        no_corpse=>1
        xp_value=>0
    END
    LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=ogrelesu edits=patch_data END
    
    // lizard man
    
    MAKE_PATCH
        xp_value=>0
        level=>3
       swap_script=>"gnsummm=>bdsum00"
    END
    LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=ms2lizm edits=patch_data END
    COPY_EXISTING "#S1-12.itm" override WRITE_ASCII 0x22 QS
    // ogre

    MAKE_PATCH
        no_corpse=>1
       swap_script=>"gnsummm=>bdsum00"
    END
    LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=ogresu edits=patch_data END

    LAF make_summoning_2da STR_VAR monsters="MS2LIZM OGRELESU GNOLLSU OGRESU" filename=msummo2 END
END

/////////////////////////////////////////////////////
//// MS1
///////////////////////////////////////////////////

ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_MONSTER_SUMMONING_1") BEGIN

    // install some BG2-only files if needed

    ACTION_IF !is_bg2 BEGIN
      ACTION_DEFINE_ASSOCIATIVE_ARRAY bg2_crefiles BEGIN
         orc01=>11
         orc02=>12
         hobwar01=>13
         hobarc01=>14
      END

      ACTION_PHP_EACH bg2_crefiles AS crefile=>name BEGIN
                 COPY "%scsroot%/%iwdspells_resource%/%crefile%.cre" override
                      SAY 0x8 (AT "%name%")
                      SAY 0xc (AT "%name%")
      END
    END

    // goblins
    
    MAKE_PATCH
       patch_item_inline=>"unstealable=>1 undroppable=>1"
       xp_value=>0
       swap_script=>"gnsummm=>bdsum00"
       swap_script'=>"gnsummb=>bdsum00"
    END
    LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature="ms1goba ms1gobm" edits=patch_data END

    // hobgoblins

    MAKE_PATCH
       gender=>summoned
       no_corpse=>1
       class=>hobgoblin
       allegiance=>controlled
       xp_value=>0
       dv=>ms1hobgo
       strip_script=>"gensht01 hgarch01 hgwarr01"
       swap_script=>"wtasight=>bdsum00"
       patch_item_inline=>"unstealable=>1 undroppable=>1"
    END
    LAF clone_creature INT_VAR allow_missing=1 STR_VAR creature="hobwar01=>dw#ms1hw hobarc01=>dw#ms1ha" edits=patch_data END

    // orc archer
    MAKE_PATCH
       gender=>summoned
       level=>1
       hitpoints=>8
       thac0=>20
       attacks=>1
       no_corpse=>1
       allegiance=>controlled
       xp_value=>0
       dv=>ms1orcar
       strip_script=>"gensht01 oarch02"
       swap_script=>"wtasight=>bdsum00 wtarsgt=>bdsum00"
       animation_code=>59408
       remove_items=>rndtre03
       patch_item_inline=>"unstealable=>1 undroppable=>1"
       patch_item_inline'=>"match=>~resource=arow02~ resource=>arow01"
       enforce_saves=>null
    END
    LAF clone_creature INT_VAR allow_missing=1 STR_VAR creature="orc02=>dw#ms1oa" edits=patch_data END
    
    // orc warrior
    MAKE_PATCH
       gender=>summoned
       level=>1
       hitpoints=>8
       thac0=>20
       attacks=>1
       no_corpse=>1
       allegiance=>controlled
       xp_value=>0
       dv=>ms1orcwa
       strip_script=>"gensht01 orcwar"
       swap_script=>"wtasight=>bdsum00 wtarsgt=>bdsum00"
       remove_items=>rndtre03
       patch_item_inline=>"unstealable=>1 undroppable=>1"
       enforce_saves=>null
    END
    LAF clone_creature INT_VAR allow_missing=1 STR_VAR creature="orc01=>dw#ms1ow" edits=patch_data END

    LAF make_summoning_2da STR_VAR monsters="DW#MS1OW DW#MS1OA DW#MS1HW DW#MS1HA MS1GOBA MS1GOBM" filename=msummo1 END
END

/////////////////////////////////////////////////////
//// global - handle breakable items
///////////////////////////////////////////////////

ACTION_IF GAME_IS BGEE BEGIN

COPY_EXISTING hamm01.spl override  // missing effect here
   LPF DELETE_EFFECT INT_VAR match_opcode=318 match_parameter2=136 END
   LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=318 target=2 parameter1=6 parameter2=136 duration=1 END

ACTION_FOR_EACH spell IN
sw1h01
sw1h04
sw1h07
sw1h12
sw1h17
sw1h20
sw1h21
sw2h01
sw2h01b
ax1h01
dagg01
dagg06
dagg07
halb01
hamm01
sper01
blun02
blun04
blun06
blun08
BEGIN
   ACTION_IF FILE_EXISTS_IN_GAME "%spell%.spl" BEGIN
      COPY_EXISTING "%spell%.spl" override
           LPF DELETE_EFFECT INT_VAR match_opcode=318 match_parameter2=107 END
           LPF CLONE_EFFECT INT_VAR match_opcode=318 match_parameter2=136 parameter2=107 parameter1=6 END
      BUT_ONLY
   END
END
END


/////////////////////////////////////////////////////
//// shadow summons
///////////////////////////////////////////////////

ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_SHADES") BEGIN

   // the shadow umber hulk needs BGification

   MAKE_PATCH
      remove_items=>umberhlk
      add_items=>"helmnoan umber01"
      patch_item_inline=>"unstealable=>1 undroppable=>1"
      swap_script=>"UHGAZE=>DW#UMBER GNSUMMM=>bdsum00"
      xp_value=>0
   END
   LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature="SS3UMB8 SS3UMB9" edits=patch_data END
   
   // the shadow lizard men need visible weapons
   
   ACTION_FOR_EACH lizard IN ss2liz5 ss2liz6 ss2liz7 ss1liz3 ss1liz4 BEGIN
      COPY_EXISTING "%lizard%.cre" override 
           REPLACE_TEXTUALLY "[BS]1-12" "#S1-12" (8)
      BUT_ONLY
   END

END

/////////////////////////////////////////////////////
//// misc
///////////////////////////////////////////////////


   // everything needs BD scripts

   MAKE_PATCH
      xp_value=>0
      swap_script=>"GNSUMMM=>bdsum00"
   END
   LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature="SUMSHAD SS1GOB1 SS1GOB2 SS1GOB3 SS1LIZ3 SS1LIZ4 SS1TRL6 SS1TRL7 SS1TRL8 SS2GOB1 SS2GOB2 SS2GOB3 SS2LIZ5 SS2LIZ6 SS2LIZ7 SS3TRL7 SS3TRL8" edits=patch_data END // Summon Shadow
   LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature="ES8WATR ES8FIRE ES8ERTH ES8AIR" edits=patch_data END // elementals
   LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature="AD3SKLM" edits=patch_data END // soul eater



END

DEFINE_ACTION_FUNCTION summoned_monsters_divine BEGIN

/////////////////////////////////////////////////////
//// shamblers - put on a more standard format
///////////////////////////////////////////////////

ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("CLERIC_STALKER") BEGIN

   LAF clone_effect STR_VAR effect="SPEART1P=>DW#SHAMB" editstring="resource=>SSHAMB" END

   MAKE_PATCH
      match=>"opcode=67"
      opcode=>177
      target=>1
      parameter2=>2
      duration=>2400
      resource=>DW#SHAMB
   END
   LAF edit_spell STR_VAR spell="%CLERIC_STALKER%" editstring="patch_effect=>patch_data" END

   MAKE_PATCH
      allegiance=>controlled
      swap_script=>"EFATKMEL=>bdsum00"
   END
   LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature=SSHAMB edits=patch_data END
END


/////////////////////////////////////////////////////
//// giant insect - just map over to new scripts
///////////////////////////////////////////////////

ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("CLERIC_GIANT_INSECT") BEGIN

   MAKE_PATCH
      xp_value=>0
      swap_script=>"GNSUMMM=>bdsum00"
   END
   LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature="GISBOMB GISBORB" edits=patch_data END
END


END

<<<<<<<< .../stratagems-inline/summon_template.2da
2DA V1.0
0
     RESREF	Hit 	AreaHitAnimation
>>>>>>>>

DEFINE_ACTION_FUNCTION make_summoning_2da 
   STR_VAR filename=""
           monsters=""
           anim_hit="msumm1h"
           anim_area="msumm1x"
BEGIN
   COPY ".../stratagems-inline/summon_template.2da" "override/%filename%.2da"
   OUTER_SET counter=1
   OUTER_WHILE "%monsters%" STRING_COMPARE "" BEGIN
      LAF return_first_entry STR_VAR list="%monsters%" RET entry monsters=list END
      APPEND "%filename%.2da" "%counter% %entry% %anim_hit% %anim_area%"
      OUTER_SET counter +=1
   END
END
