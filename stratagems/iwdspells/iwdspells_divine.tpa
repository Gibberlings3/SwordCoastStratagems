DEFINE_ACTION_FUNCTION iwdspells_divine STR_VAR version="" BEGIN

	LAF check_label STR_VAR label="dw_iwdspells_divine_installed" RET value END
	ACTION_IF !value BEGIN
		LAF make_label STR_VAR label="dw_iwdspells_divine_installed" END


		OUTER_SPRINT custom_includes "%version%" // pass through 'version' to conform to rules for SFO 'run' function

		INCLUDE "%MOD_FOLDER%/%component_loc%/lib/move_spell_resources.tph"

		LAF check_ini INT_VAR silent=1 STR_VAR section=iwdspells ini=no_cap_at_level_20 RET value END
		OUTER_SET truncate_at_level=value?"-1":20

		LAF check_ini INT_VAR silent=1 STR_VAR section=iwdspells ini=overwrite_old_spells RET overwrite_always=value END


		OUTER_SPRINT resource_loc "%MOD_FOLDER%/%component_loc%/copyover"

		// install beetle animations (anim_beetle is checked before allowing GIANT_INSECT to install and then used by obg2_divine)

		ACTION_IF enhanced_edition BEGIN
			LAF anim_install STR_VAR resource_loc="%resource_loc%/anim" animation=beetle_black END
			OUTER_SET anim_beetle=1
		END ELSE BEGIN
			OUTER_SET anim_beetle=0
			ACTION_IF MOD_IS_INSTALLED ~INFINITYANIMATIONS/SETUP-INFINITYANIMATIONS.TP2~ ~500~ BEGIN // 'more base animations'
				ACTION_IF IDS_OF_SYMBOL (~animate~ ~BEETLE_BLACK~) > 0 THEN BEGIN
				  OUTER_SET anim_beetle = IDS_OF_SYMBOL (~animate~ ~BEETLE_BLACK~)
				END
			END
		END

		//SR-relative settings
		
		ACTION_IF MOD_IS_INSTALLED "spell_rev/setup-spell_rev.tp2" 0 BEGIN
			OUTER_SPRINT overwrite_these_spells "overwrite_sr.2da"
			OUTER_SPRINT skip_these_spells "skip_sr.2da"
		END ELSE BEGIN
			OUTER_SPRINT overwrite_these_spells "overwrite.2da"
			OUTER_SPRINT skip_these_spells ""
		END

		// main install
		
		LAF install_spell_resources
			INT_VAR truncate_at_level
					overwrite_always
			STR_VAR resource_loc
					prefix=divine
					file=iwd_divine.2da
					location=data
					anim_remaps="anim_maps.2da"
					resource_overwrite="resource_overwrite_bg2.2da"
					old_style="%MOD_FOLDER%/%sfo_library_path%/data/spell_styles_iwd.2da"
					new_style="%MOD_FOLDER%/%sfo_library_path%/data/spell_styles_bg.2da"
					overwrite_these_spells
					skip_these_spells
					log="iwd_spells_installed.txt"
					afterwards="iwd_divine_spells_postproduction"
					afterwards_tra="dw_iwdspells"
					custom_includes
		END

		// bams
		
		LAF install_spell_bams
			STR_VAR resource_loc
					location=data
					file=bam_copy_divine.2da
		END
			
		// rebuild hotkeys
		
		ACTION_IF enhanced_edition BEGIN
			LAF rebuild_spell_hotkeys END
		END
		
		// oBG2 icons
		
		ACTION_IF !enhanced_edition BEGIN
		
			// main icons already installed?
			LAF check_label STR_VAR label="dw#iwdspells_obg2_icons_installed" RET proceed=value END
			ACTION_IF !proceed BEGIN
				// if not, check the ini
				LAF check_ini INT_VAR silent=1 STR_VAR section="iwdspells" ini=install_obg2_icons RET proceed=value END
			END
			ACTION_IF proceed BEGIN
				LAF run STR_VAR file=iwdspells_icons END
			END

		
		END
	
	END ELSE BEGIN// end of outer LABEL check

		PRINT "IWD divine spells have already been installed and will not be installed again."

	END

END
