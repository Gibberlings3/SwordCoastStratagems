BEGIN_ACTION_DEFINITION
	Name(AttackIfEmpty)
	TRIGGER
	        !HotKey(E)
		!StateCheck(scstarget,STATE_INVISIBLE)
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		EquipMostDamagingMelee()
		SetGlobal("melee","LOCALS",1)
		AttackOneRound(scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(Spell)
	TRIGGER
	        !HotKey(E)
		!GlobalTimerNotExpired("castspell","LOCALS")
                HaveSpell(scsargument1)
		!StateCheck(scstarget,STATE_NOT_TARGETABLE)
		!CheckStatGT(scstarget,0,SANCTUARY)
		CheckStatLT(Myself,60,SPELLFAILUREMAGE)
		!CheckSpellState(scstarget,DO_NOT_TARGET_SPELLS)
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		Spell(scstarget,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(SpellMyself)
	TRIGGER
	        !HotKey(E)
		!GlobalTimerNotExpired("castspell","LOCALS")
		HaveSpell(scsargument1)
		CheckStatLT(Myself,60,SPELLFAILUREMAGE)
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		Spell(Myself,scsargument1)
END

IF TRIGGER
THEN DO
    Action(SpellMyself,WIZARD_GHOST_ARMOR)
END

IF TRIGGER
    OR(2)
       Range(NearestEnemyOf(Myself),15)
       HPPercentLT(Myself,80)
THEN DO
    Action(SpellMyself,WIZARD_MIRROR_IMAGE)
END

IF TRIGGER
     TargetBlock(EnemiesInOrderShort)
     TriggerBlock(MinorGlobe|Helpless)
THEN DO
     Action(Spell,WIZARD_LIGHTNING_BOLT|100|100)
END

IF TRIGGER
     TargetBlock(EnemiesInOrderShort)
     Range(scstarget,5)
THEN DO
     Action(Spell,WIZARD_BURNING_HANDS|100|100)
END

IF TRIGGER
     Allegiance(Myself,ENEMY)
     TargetBlock(PCSpellcasters)
     TriggerBlock(MinorGlobe|Helpless|SpellTurn)
THEN DO
     Action(Spell,WIZARD_MELF_ACID_ARROW|100|50)
END

IF TRIGGER
     !Allegiance(Myself,ENEMY)
     TargetBlock(EnemiesInOrderShort)
     TriggerBlock(MinorGlobe|Helpless|SpellTurn)
THEN DO
     Action(Spell,WIZARD_MELF_ACID_ARROW|100|50)
END

IF TRIGGER
     TargetBlock(EnemiesInOrderShort)
     TriggerBlock(MinorGlobe|Helpless|MagicMissile|SpellTurn)
     OR(2)
          !CheckSpellState(scstarget,ITEM_MM)
          !CheckSpellState(scstarget,ITEM_MM_SEEN)
THEN DO
     ApplySpellRES("dw#mmsee",scstarget)
     Action(Spell,WIZARD_MAGIC_MISSILE|100|50)
END



