INCLUDE ~%MOD_FOLDER%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION kobold BEGIN
        LAF kobold_core END
        LAF kobold_ambush END

END

DEFINE_ACTION_FUNCTION kobold_core BEGIN


      LAF check_label STR_VAR label=dw#help RET value END
      
      ACTION_IF value BEGIN
         OUTER_SPRINT help dw#gpsht
      END ELSE BEGIN
         OUTER_SPRINT help ~%tutu_var%koboldf~
      END

      // install needed files
      
      LAF install STR_VAR files=~dw#kobsp.baf dw#kobch.cre dw#kobgd.cre dw#kobsh.cre dw#kob01.itm dw#kob02.itm galtok.d~ location=resource END

      // add kobold-summoning script

      ACTION_FOR_EACH koboldtype IN obola_a obola_c obola_e obold_a obold_c obold_e oboldal BEGIN
         LAF edit_creature STR_VAR creature= ~%tutu_scriptk%%koboldtype%~ editstring=~insert_script_high=>dw#kobsp~ END
      END
      LAF edit_creature INT_VAR tv=1 STR_VAR creature=kobolda editstring=~insert_script_high=>dw#kobsp~ END

       // Commander
      
      MAKE_PATCH
          steal_sounds_from=>~%tutu_var%kobold~
          say_both_names=>21301
          script_race=>~%help%~
          remove_items=>~_arow01 _bow05~
          add_items=>~%tutu_var%bow05 %tutu_var%arow01(20,quiver2)~
          enforce_charclass=>is_bg1
      END
      LAF edit_creature STR_VAR creature=dw#kobch edits=patch_data END

      // guards
      
      MAKE_PATCH
          steal_sounds_from=>~%tutu_var%kobold~
          say_both_names=>21302
          script_race=>~%help%~
          enforce_charclass=>is_bg1
      END
      LAF edit_creature STR_VAR creature=dw#kobgd edits=patch_data END

      // shaman

      MAKE_PATCH
          steal_sounds_from=>~%tutu_var%kobold~
          say_both_names=>21303
          script_race=>~%help%~
          script_general=>~%tutu_var%mage5~
          enforce_charclass=>is_bg1
      END

      LAF edit_creature STR_VAR creature=dw#kobsh edits=patch_data END

      // items

      // daggers

      ACTION_IF enhanced_edition BEGIN
         OUTER_SET dagger_desc1=RESOLVE_STR_REF (@21316)
         OUTER_SET dagger_desc2=RESOLVE_STR_REF (@21317)
         OUTER_SET arrow_desc=RESOLVE_STR_REF (@21319)
      END ELSE BEGIN
         OUTER_SET dagger_desc1=RESOLVE_STR_REF (@21306)
         OUTER_SET dagger_desc2=RESOLVE_STR_REF (@21307)
         OUTER_SET arrow_desc=RESOLVE_STR_REF (@21309)
      END

      MAKE_PATCH
         say_unidentified_name=>21304
         say_name=>21305
         description1_string=>~%dagger_desc1%~
         description2_string=>~%dagger_desc2%~
      END
      LAF edit_item STR_VAR item=dw#kob01 edits=patch_data END
      
      // arrows
      MAKE_PATCH
         say_unidentified_name=>21308
         say_name=>21308
         description1_string=>~%arrow_desc%~
         description2_string=>~%arrow_desc%~
      END
      LAF edit_item STR_VAR item=dw#kob02 edits=patch_data END
      
      // on EE, make sure shamans can use poisoned arrows

      ACTION_IF enhanced_edition BEGIN

         MAKE_PATCH
            unusable_druid=>0
            add_effect_global_inline=>"opcode=>319 target=>1 parameter1=>11 parameter2=>5 timing=>2 mode=>1080"
         END
         LAF edit_item STR_VAR item=dw#kob02 edits=patch_data END

      END
END
DEFINE_ACTION_FUNCTION kobold_ambush BEGIN

     LAF extend_area_script STR_VAR area=~%NashkelMines_L3%~ top=ambush location=resource END

     // the "specifics" change means that they won't hear other kobolds' help cries

     LAF clone_creature STR_VAR creature=~dw#kobch=>dw#kbach %tutu_var%kobcomm=>dw#kbacm dw#kobsh=>dw#kbash dw#kobsh=>dw#kbas2 dw#kobgd=>dw#kbagd~
                                editstring=~specifics=>77~
     END

     LAF check_label STR_VAR label=dw#mage RET value END
     ACTION_IF value BEGIN

        LAF edit_creature STR_VAR creature=dw#kbash editstring=~kit=>INVOKER enforce_mage=>null~ END
        LAF edit_creature STR_VAR creature=dw#kbas2 editstring=~kit=>ENCHANTER enforce_mage=>null~ END
     END
END
