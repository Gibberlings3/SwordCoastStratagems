LAF include STR_VAR file=potion_shared.tph END

DEFINE_ACTION_FUNCTION potion
BEGIN

    LAF check_ini STR_VAR ini=potion_version RET version=value END

    LAF clone_item STR_VAR item=~dw#marker_file=>dw#haspt~ editstring=~droppable=>0~ END

//    ACTION_IF skip_tobex BEGIN OUTER_SET backup=1 END ELSE BEGIN OUTER_SET backup=0 END
    OUTER_SET backup=0
    LAUNCH_ACTION_MACRO define_potion_data
    LAF initialise_potions INT_VAR backup END
    LAUNCH_ACTION_MACRO read_in_potion_data

    OUTER_SET counter1=0
    OUTER_SET counter2=0
    COPY_EXISTING_REGEXP GLOB ~.*\.cre~ ~override~
         SPRINT filename ~%SOURCE_RES%~
         SET potions=0
         LPF potion_race_check RET value=value END
         PATCH_IF value BEGIN
            LPF CRE_is_dead RET value=value END
            PATCH_IF !value BEGIN
               LPF CRE_is_PC RET value=value END
               PATCH_IF !value BEGIN
                  LPF no_potions STR_VAR filename RET value=value END
                  PATCH_IF !value BEGIN
                     SET potions=1
                  END
               END
            END
         END
         PATCH_IF potions BEGIN
            LPF allocate_potions STR_VAR arguments= ~version=>%version% backup=>%backup%~ file_ext=CRE filename END
         END
         SET counter1+=1
         PATCH_IF counter1=100 BEGIN
            SET counter1=0
            SET counter2+=100
            PATCH_PRINT ~patched %counter2% files~
         END

    BUT_ONLY

    // special cases

    ACTION_IF is_bg1 BEGIN
       LAF edit_creature STR_VAR creature=zargal tv=1 editstring=~add_items=>"potn08(x2)"~ END
    END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION potion_race_check
RET value
BEGIN
   LPF CRE_read_race RET race=value END
   TO_UPPER race
   PATCH_MATCH ~%race%~ WITH
      HUMAN ELF HALF_ELF DWARF HALFLING GNOME HALFORC MIND_FLAYER RAKSHASA SAHUAGIN KUO-TOA DUERGAR GIANT GITHYANKI
   BEGIN
      SET value=1
   END
   DEFAULT
      SET value=0
   END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION no_potions
STR_VAR filename=""
RET value
BEGIN
   TO_LOWER filename
   PATCH_MATCH ~%filename%~ WITH
      kpglai01 spmugg spmugg3 lovem _lovem pridem _pridem avaricem _varicem fearm _fearm ppshad01 gorion _gorion BEGIN
         SET value=1
      END
      DEFAULT
         SET value=0
      END
END
