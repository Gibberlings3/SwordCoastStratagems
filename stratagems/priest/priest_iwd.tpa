

DEFINE_ACTION_FUNCTION priest_iwd STR_VAR version=0 BEGIN

   LAM read_in_iwd_priest_scripts
   ACTION_IF (version=0 || version=2) BEGIN
       LAF priest_edits_before_iwd END
   END
   ACTION_IF (version=0 || version=3) BEGIN
       LAF priest_edits_area_iwd END
       LAF priest_edits_creature_iwd END
   END
   ACTION_IF (version=0 || version=4) BEGIN
       LAF priest_edits_after_iwd END
   END




END

DEFINE_ACTION_FUNCTION priest_edits_before_iwd BEGIN
        
        // move some priest scripts to the bottom of the ordering

        LAF install STR_VAR file="d5yxnatk.baf ilteam1.baf dw#gntrp.baf effolpty.baf" location=resource END


        MAKE_PATCH
            match=>"actor_resource=orcsham"
            move_script_to_bottom=>cvorcsha
        END
        LAF edit_area STR_VAR area=ar1201 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
            move_script_to_bottom=>ktmytos
        END
        LAF edit_area STR_VAR area=ar3501 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           match=>"actor_resource=lmsham1 or actor_resource=lmsham2"
           swap_scripts=>"4001lmtr=>null"
           move_script_to_bottom=>4001ls1
        END
        LAF edit_area STR_VAR area=ar4001 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
            match=>"actor_resource=lmsham3"
            move_script_to_bottom=>d5yuanp1
        END
        LAF edit_area STR_VAR area=ar4001 editstring="patch_actor=>patch_data" END
        MAKE_PATCH
            move_script_to_bottom=>"d2talon1 d2talon2 d2talon3 d2talon4 d5yuanp1"
        END
        LAF edit_area STR_VAR area=ar4002 editstring="patch_actor=>patch_data" END


        MAKE_PATCH
            move_script_to_bottom=>"d4yuanp d4heal d4hirit"
        END
        LAF edit_area STR_VAR area=ar4004 editstring="patch_actor=>patch_data" END
        MAKE_PATCH
            move_script_to_bottom=>"d5yuanp1 d5yuanp2 d5yuanp3 d5yuanp4 d5hitort"
        END
        LAF edit_area STR_VAR area=ar4005 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           match=>"actor_resource=sdorcshm"
           swap_scripts=>"gntrpb1=>dw#gntrp"
           move_script_to_bottom=>shorcsh1
        END
        LAF edit_area STR_VAR area="ar5001 ar5002" editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           match=>"actor_resource=skaldar"
           swap_scripts=>"idislfix=>null"
           move_script_to_bottom=>idskald
        END
        LAF edit_area STR_VAR area=ar9300 editstring="patch_actor=>patch_data" END

        MAKE_PATCH
            move_script_to_bottom=>"idgsham idgsham1 idgsham2"
        END
        LAF edit_area STR_VAR area="ar9300" editstring="patch_actor=>patch_data" END

        MAKE_PATCH
           actor_resource=>xactile
           script_race=>null
           move_script_to_bottom=>ilxactil
        END
        LAF edit_area STR_VAR area="ar9602" editstring="patch_actor=>patch_data" END

        MAKE_PATCH
            move_script_to_bottom=>"ilsahep1"
        END
        LAF edit_area STR_VAR area="ar9603" editstring="patch_actor=>patch_data" END

       MAKE_PATCH
            move_script_to_bottom=>"ilsahhp1 ilsahup1"
        END
        LAF edit_area STR_VAR area="ar9604" editstring="patch_actor=>patch_data" END


        MAKE_PATCH
            move_script_to_bottom=>"secrie2"
        END
        LAF edit_area STR_VAR area="ar9708" editstring="patch_actor=>patch_data" END

        LAF edit_creature STR_VAR creature=sdelfch2 editstring="script_default=>sdelfcs1 script_general=>dw#gntrp" END
        LAF edit_creature STR_VAR creature=sdorshh4 editstring="script_default=>shorcsh1 script_general=>dw#gntrp" END
        LAF edit_creature STR_VAR creature=trcriek editstring="script_default=>ducriek script_general=>effolpty" END
        LAF edit_creature STR_VAR creature=trsph3 editstring="script_default=>duhero3 script_general=>effolpty" END
        LAF edit_creature STR_VAR creature=trsph5 editstring="script_default=>duhero5 script_general=>effolpty" END
        LAF edit_creature STR_VAR creature=cknight editstring="strip_script=>d4cleric insert_script_low=>d4cleric" END

        // make some acolytes unique
        
        MAKE_PATCH
           match=>"actor_x_coord_start=242 and actor_y_coord_start=815"
           actor_resource=>dwacoly1
        END
        LAF edit_area STR_VAR area=ar3601 editstring="patch_actor=>patch_data" END
        MAKE_PATCH
           match=>"actor_x_coord_start=283 and actor_y_coord_start=815"
           actor_resource=>dwacoly2
        END
        LAF edit_area STR_VAR area=ar3601 editstring="patch_actor=>patch_data" END
        MAKE_PATCH
           match=>"actor_x_coord_start=2971 and actor_y_coord_start=803"
           actor_resource=>dwacoly1
        END
        LAF edit_area STR_VAR area=ar3602 editstring="patch_actor=>patch_data" END
        MAKE_PATCH
           match=>"actor_x_coord_start=2960 and actor_y_coord_start=720"
           actor_resource=>dwacoly2
        END
        LAF edit_area STR_VAR area=ar3602 editstring="patch_actor=>patch_data" END
        COPY_EXISTING
            "acolyte.cre" "override/dwacoly1.cre"
            "acolyte.cre" "override/dwacoly2.cre"


        // make talonite priests unique

        MAKE_PATCH
           match=>"actor_x_coord_start=1169"
           actor_resource=>dwtalonf
        END
        LAF edit_area STR_VAR area=ar4002 editstring="patch_actor=>patch_data" END
        COPY_EXISTING talonaf.cre "override/dwtalonf.cre"
        MAKE_PATCH
           match=>"actor_x_coord_start=1274"
           actor_resource=>dwtalonm
        END
        LAF edit_area STR_VAR area=ar4002 editstring="patch_actor=>patch_data" END
        COPY_EXISTING talonam.cre "override/dwtalonm.cre"

        // make some yuan-ti priests unique

        COPY_EXISTING ar4005.are override
           SET index=1
           GET_OFFSET_ARRAY act_array ARE_V10_ACTORS
           PHP_EACH act_array AS ind=>offset BEGIN
                READ_ASCII (offset+0x80) actor_resource
                PATCH_IF "%actor_resource%" STRING_EQUAL_CASE yuantip BEGIN
                   WRITE_ASCIIE (offset+0x80) "dwyuap%index%"
                   INNER_ACTION BEGIN
                            COPY_EXISTING "yuantip.cre" "override/dwyuap%index%.cre"
                   END
                   SET index +=1
                END
           END

        // make some lizard-man shamans unique

        COPY_EXISTING ar4001.are override
           SET index=1
           GET_OFFSET_ARRAY act_array ARE_V10_ACTORS
           PHP_EACH act_array AS ind=>offset BEGIN
                READ_ASCII (offset+0x80) actor_resource
                PATCH_IF "%actor_resource%" STRING_EQUAL_CASE lmsham3 BEGIN
                   WRITE_ASCIIE (offset+0x80) "dwlsha%index%"
                   INNER_ACTION BEGIN
                            COPY_EXISTING "lmsham3.cre" "override/dwlsha%index%.cre"
                   END
                   SET index +=1
                END
           END

        COPY_EXISTING ar4002.are override
           SET index=1
           GET_OFFSET_ARRAY act_array ARE_V10_ACTORS
           PHP_EACH act_array AS ind=>offset BEGIN
                READ_ASCII (offset+0x80) actor_resource
                PATCH_IF "%actor_resource%" STRING_EQUAL_CASE lmshkid BEGIN
                   WRITE_ASCIIE (offset+0x80) "dwlmshk%index%"
                   INNER_ACTION BEGIN
                            COPY_EXISTING "lmshkid.cre" "override/dwlmshk%index%.cre"
                   END
                   SET index +=1
                END
           END

        // make some shadowed-elven clerics unique
        COPY_EXISTING ar5202.bcs override
           REPLACE_TEXTUALLY ~CreateCreatureEffect("sdelfch2"~ ~CreateCreatureEffect("dwselfc1"~
           REPLACE_TEXTUALLY ~CreateCreature("sdelfch2"~ ~CreateCreature("dwselfc2"~
        BUT_ONLY
        COPY_EXISTING "sdelfch2.cre" "override/dwselfc1.cre"
        COPY_EXISTING "sdelfch2.cre" "override/dwselfc2.cre"
        COPY_EXISTING "sdelfch1.cre" "override/dwselfc3.cre"
        COPY_EXISTING ar5202.are override
           REPLACE_TEXTUALLY sdelfch1 dwselfc3 (8)
           
        COPY_EXISTING ar5203.bcs override
           LPF unique_iwd_script_summons INT_VAR counter_start=1 STR_VAR original=sdelfch1 new_prefix=dwselfc END
        BUT_ONLY
        
        // priestess of shar only needs one script
        
        LAF edit_creature STR_VAR creature=pshar editstring="strip_script=>~lwshar2 lwshar3 lwshar4~" END
        
        // Criek only needs one script
        
        COPY_EXISTING ar9708.are override
          REPLACE_TEXTUALLY secrie13 "" (8)
        BUT_ONLY
        
        // jackalwere shamans need only one script, should be at bottom
        
        LAF swap_text STR_VAR files="ar9800.are ar9801.are" swaps="cajshah1=>null cajshah2=>null cajshah3=>null cajshah4=>null cajshah5=>null" END
        LAF edit_area STR_VAR area="ar9800 ar9801" editstring="patch_actor_inline=>~move_script_to_bottom=>cajsha1~" END
END

DEFINE_ACTION_FUNCTION priest_edits_after_iwd BEGIN

		cre.edit[highsumm][m.cre_add_spells{SUMMON_TROLLS(5) CLERIC_SUMMON_LIZARDMEN(5)}]


END

DEFINE_ACTION_FUNCTION priest_edits_area_iwd BEGIN

        COPY_EXISTING_REGEXP GLOB ".*\.are" override
          SPRINT areaname "%SOURCE_RES%"
          GET_OFFSET_ARRAY act_arr ARE_V10_ACTORS
          PHP_EACH act_arr AS act_ind=>act_off BEGIN
             READ_ASCII (0x80+act_off) filename_old
             READ_ASCII act_off actor_name (0x20)
             SET match=0
             FOR (script_off=0x58+act_off;script_off<0x80+act_off;script_off+=8) BEGIN
                READ_ASCII script_off script_old
                TO_LOWER script_old
                PATCH_IF (match && "%script_old%" STRING_COMPARE "") BEGIN
                    LPF warning STR_VAR warning="script %script_old% below priest script %script_priest% in area %areaname%, actor %actor_name% (%filename_old%) " END
                END ELSE BEGIN
                PATCH_IF VARIABLE_IS_SET $priest_scripts_iwd("%script_old%") BEGIN
                  SET match=1
                  PATCH_PRINT "Determining spells for %actor_name% (area %areaname%)"
				  READ_ASCII act_off+0x80 actor_resref
				  LPF log_this INT_VAR repeat=0 STR_VAR file=creatures_with_potions.txt arguments="%actor_resref%" END
                  SPRINT script_priest "%script_old%"
                  INNER_ACTION BEGIN
                   LAF priest_iwd_area_main STR_VAR script_old filename_old RET script filename END
                  END
                  PATCH_IF "%script%" STRING_COMPARE_CASE "" BEGIN
                   WRITE_ASCIIE script_off "%script%" (8)
                   WRITE_ASCIIE (0x80+act_off) "%filename%" (8)
                  END
                 END // end of VARIABLE_IS_SET loop
                END // end of match loop
             END
          END

        BUT_ONLY

END

DEFINE_ACTION_FUNCTION priest_edits_creature_iwd BEGIN

         COPY_EXISTING_REGEXP GLOB ".*\.cre" override
             SET match=0
             FOR (script_off=0x248;script_off<0x270;script_off+=8) BEGIN
                READ_ASCII script_off script_old
                TO_LOWER script_old
                PATCH_IF (match && "%script_old%" STRING_COMPARE "") BEGIN
                    LPF warning STR_VAR warning="script %script_old% below priest script in CRE file %SOURCE_RES%" END
                END ELSE BEGIN
                PATCH_IF VARIABLE_IS_SET $priest_scripts_iwd("%script_old%") BEGIN
                  SET match=1
				  LPF log_this INT_VAR repeat=0 STR_VAR file=creatures_with_potions.txt arguments="%SOURCE_RES%" END
                  PATCH_PRINT "Determining spells for creature file %SOURCE_RES%, script %script_old%"
                  PATCH_IF !FILE_EXISTS "%data_loc%/caster_scripts_built.txt" || !FILE_CONTAINS "%data_loc%/caster_scripts_built.txt" "%script_old%" BEGIN
                      SPRINT script "%script_old%"
                      LPF log_this STR_VAR file=caster_scripts_built.txt input="%script_old%" END
                  END ELSE BEGIN
                      LPF dw_unique_name STR_VAR old_name="%script_old%" suffix=bcs RET script=name END
                  END
				  SPRINT filename "%SOURCE_RES%"
                  LPF enforce_priest STR_VAR script END
                  INNER_ACTION BEGIN
                      ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%component_loc%/iwd/top/%script_old%.baf" BEGIN
                           LAF extend STR_VAR script top="%script_old%" location="iwd/top" END
                      END
                      ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%component_loc%/iwd/top/%script_old%.ssl" BEGIN
                           LAF extend INT_VAR ssl=1 STR_VAR script top="%script_old%" location="iwd/top" END
                      END
                  END
                 END // end of VARIABLE_IS_SET loop
                END // end of match loop
             END

         BUT_ONLY
END

DEFINE_ACTION_FUNCTION priest_iwd_area_main
   STR_VAR script_old=""
           filename_old=""
   RET script
       filename
BEGIN
   ACTION_TO_LOWER filename_old
   ACTION_TO_LOWER script_old
   ACTION_IF !FILE_EXISTS "%data_loc%/caster_scripts_built.txt" || !FILE_CONTAINS "%data_loc%/caster_scripts_built.txt" "%script_old%" BEGIN
      OUTER_SPRINT script "%script_old%"
      LAF log_this STR_VAR file=caster_scripts_built.txt input="%script_old%" END
   END ELSE BEGIN
      LAF dw_unique_name STR_VAR old_name="%script_old%" suffix=bcs RET script=name END
   END
   ACTION_IF FILE_EXISTS "%workspace%/%filename_old%_template.cre" BEGIN 
      LAF dw_unique_name STR_VAR old_name="%filename_old%" suffix=cre RET filename=name END
      COPY "%workspace%/%filename_old%_template.cre" "override/%filename%.cre"
   END ELSE BEGIN
      COPY_EXISTING "%filename_old%.cre" "%workspace%/%filename_old%_template.cre"
      OUTER_SPRINT filename "%filename_old%"
   END

   COPY_EXISTING "%filename%.cre" "override"
         PATCH_WITH_SCOPE BEGIN
			 SPRINT filename "%filename_old%"
			 LPF enforce_priest STR_VAR script END
		 END
   BUT_ONLY
   ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%component_loc%/iwd/top/%script_old%.baf" BEGIN
      LAF extend STR_VAR script top="%script_old%" location="iwd/top" END
   END
   ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%component_loc%/iwd/top/%script_old%.ssl" BEGIN
      LAF extend INT_VAR ssl=1 STR_VAR script top="%script_old%" location="iwd/top" END
   END
END

/////////////////////////////////////////////////////////////////////////////
/// collect the lists of priest scripts
/////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_MACRO read_in_iwd_priest_scripts BEGIN

     ACTION_IF !VARIABLE_IS_SET iwd_priest_scripts_read_in BEGIN
           OUTER_SPRINT iwd_priest_scripts_read_in ""
           LAF read_in_list STR_VAR file=priest_scripts.txt location=iwd force_case=lower RET_ARRAY priest_scripts_iwd=array END
     END
END

