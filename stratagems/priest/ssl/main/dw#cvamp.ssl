//////////////////////////////////////////////////////////////////////////////////
///
///   Define actions
///
////////////////////////////////////////////////////////////////////////////////////////

VARIABLE(IsNotMoron=True)  // unsure if needed
INCLUDE FILE(%MOD_FOLDER%/caster_shared/caster_definitions.ssl)
VARIABLE(SPELLFAILUREMAGE=SPELLFAILUREPRIEST)

////////////////////////////////////////////////////////////////
/// Labelling, break-invisibility, hostile-undead, panic, etc
////////////////////////////////////////////////////////////////////////////////////////


INCLUDE FILE(%MOD_FOLDER%/priest/ssl/main/preamble.ssl)

//////////////////////////////////////////////////////////////////////////////////
////	Vampiric stuff
//////////////////////////////////////////////////////////////////////////////////


IF
        IgnoreBlock(VampireLover)
	Die()
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,VAMPIRE_GAS_FORM_CHANGE)
END

IF
        IgnoreBlock(VampireLover)
	HPLT(Myself,10)
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,VAMPIRE_BAT_FORM_CHANGE)
END

IF
        IgnoreBlock(VampireLover)
	TimeGT(5)
	TimeLT(DUSK_END)
	AreaType(OUTDOOR)
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,VAMPIRE_BAT_FORM_CHANGE)
END

//////////////////////////////////////////////////////////////////////////////////


BEGIN_ACTION_DEFINITION
	Name(MoveIfEmpty)     // we're redefining this for the benefit of shapeshifters
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		EquipMostDamagingMelee()
		MoveToObject(scstarget)
END

//////////////////////////////////////////////////////////////////////////////////
////	Prep
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/priest/ssl/main/priestsetup.ssl)

//////////////////////////////////////////////////////////////////////////////////
////	If non-hostile, closedown - this script doesn't pretend to fight bad guys
//////////////////////////////////////////////////////////////////////////////////

IF
	!Allegiance(Myself,ENEMY)
	IgnoreBlock(IsPartyAlly)
THEN
	RESPONSE #100
		NoAction()
END

///////////////////////////////////////////////////////////////////////////
////	Flee Cloudkill spell
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/mage/ssl/generalblocks/fleecloud.ssl)

///////////////////////////////////////////////////////////////////////////
//	Turn undead
///////////////////////////////////////////////////////////////////////////

IF
	IgnoreBlock(IsDruid)
	See([GOODCUTOFF.UNDEAD])
	!StateCheck(LastSeenBy(Myself),STATE_DISABLED)
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("turninitialise","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("turninitialise","LOCALS",30)
		SetGlobalTimer("turning","LOCALS",4)
		Continue()
END

IF
	IgnoreBlock(IsDruid)
	See([GOODCUTOFF.UNDEAD])
	!StateCheck(LastSeenBy(Myself),STATE_DISABLED)
	!GlobalTimerNotExpired("castspell","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("turninitialise","LOCALS")
		GlobalTimerNotExpired("turning","LOCALS")
THEN
	RESPONSE #100
		Turn()
END


///////////////////////////////////////////////////////////////////////////
//	Melee
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/priest/ssl/meleeblocks/touchattack.ssl)
INCLUDE FILE(%MOD_FOLDER%/mage/ssl/meleeblocks/vampire_melee.ssl)

//////////////////////////////////////////////////////////////////////////
// Renew and retreat
//////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/priest/ssl/generalblocks/renew.ssl)

//////////////////////////////////////////////////////////////////////////
// Easiest-level block
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER()
INCLUDE FILE(%MOD_FOLDER%/priest/ssl/generalblocks/easy.ssl)

//////////////////////////////////////////////////////////////////////////
// Look for PCs
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER()

INCLUDE FILE(%MOD_FOLDER%/priest/ssl/generalblocks/chase.ssl)

////////////////////////////////////////////////////////////////////////////
// Core magic block
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/priest/ssl/priesttypes/vampire_priest.ssl)

//////////////////////////////////////////////////////
///	If everyone is helpless, kill'em.
//////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	BEGIN LOOP(scsloopvar||1;2;3;4;5;6)
		OR(3)
			!See(Playerscsloopvar)
			StateCheck(Playerscsloopvar,STATE_IMMOBILE)
			CheckStatGT(Playerscsloopvar,0,HELD)
	END LOOP
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

DEFAULT TRIGGER()
INCLUDE FILE(%MOD_FOLDER%/priest/ssl/generalblocks/closedown.ssl)
