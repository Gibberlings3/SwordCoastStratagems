//////////////////////////////////////////////////////////////////////////////////
////	Label casters
//////////////////////////////////////////////////////////////////////////////////

        IF
          Global("caster_label_ini","LOCALS",0)
        THEN
            RESPONSE #100
                     SetGlobal("caster_label_ini","LOCALS",1)
                     ApplySpellRES("dw#calab",Myself)
                     Continue()
        END



//////////////////////////////////////////////////////////////////////////////////
////  Protect against invisibility edge cases
//////////////////////////////////////////////////////////////////////////////////


        IF
          RequireBlock(EnhancedEdition)
          Global("invis_anim","LOCALS",0)
          StateCheck(Myself,STATE_INVISIBLE)
        THEN
            RESPONSE #100
                     SetGlobal("invis_anim","LOCALS",1)
                     ApplySpellRES("dw#hdani",Myself)
                     Continue()
        END

        IF
          RequireBlock(EnhancedEdition)
          Global("invis_anim","LOCALS",1)
          !StateCheck(Myself,STATE_INVISIBLE)
        THEN
            RESPONSE #100
                     SetGlobal("invis_anim","LOCALS",0)
                     ApplySpellRES("dw#blhda",Myself)
                     Continue()
        END
        
//////////////////////////////////////////////////////////////////////////////////
////	On IWD, include help script
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/help/ssl/scsvar_help_if_iwd.ssl)


//////////////////////////////////////////////////////////////////////////////////
////	Deal with rogue undead
//////////////////////////////////////////////////////////////////////////////////

IF
	AttackedBy([ENEMY.UNDEAD],DEFAULT)
	Allegiance(LastAttackerOf(Myself),ENEMY)
	General(LastAttackerOf(Myself),UNDEAD)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		ClearActions(LastAttackerOf(Myself))
		Continue()
END


//////////////////////////////////////////////////////////////////////////////////
//      Handle fear
//////////////////////////////////////////////////////////////////////////////////

IF
  StateCheck(Myself,STATE_PANIC)
THEN
    RESPONSE #100
             RandomWalkContinuous()
END

//////////////////////////////////////////////////////////////////////////////////
//     Remove elemental-transformation weapon, if appropriate
//////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
   RequireBlock(ImprovedShapeshift)
   SSLBoolean(CLERIC_ELEMENTAL_TRANSFORMATION_EARTH)
   TriggerBlock(NotHLAPriest)
   HasItem("dw#shnea",Myself)
THEN DO
     Action(Literal)
     DestroyItem("dw#shnea")
     Continue()
END

IF TRIGGER
   RequireBlock(ImprovedShapeshift)
   SSLBoolean(CLERIC_ELEMENTAL_TRANSFORMATION_FIRE)
   TriggerBlock(NotHLAPriest)
   HasItem("dw#shnfi",Myself)
THEN DO
     Action(Literal)
     DestroyItem("dw#shnfi")
     Continue()
END
