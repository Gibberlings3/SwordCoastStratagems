
////////////////////////////////////////////////////////////////////
////	Iron Skins
////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsDruid)
	RequireBlock(HasL4)
	GlobalTimerExpired("redefend","LOCALS")
	!CheckStatGT(Myself,2,STONESKINS)
THEN DO
	Action(SpellMyself,CLERIC_IRONSKIN|100|100)
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END

////////////////////////////////////////////////////////////////////
////	Divine Shell
////////////////////////////////////////////////////////////////////


IF TRIGGER
	IgnoreBlock(IsDruid)
	RequireBlock(HasL7)
	RequireBlock(Demivrgvs)
	!GlobalTimerNotExpired("divn","LOCALS")
	!CheckStatGT(Myself,0,CLERIC_SHIELD_OF_THE_ARCHONS)
	!CheckStatGT(Myself,0,WIZARD_SPELL_TURNING)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellRESMyself,"tg#divn"|100|100)
END

////////////////////////////////////////////////////////////////////////
////    My own blindness
////////////////////////////////////////////////////////////////////

IF TRIGGER
          RequireBlock(HasL5)
          RequireBlock(TruesightCuresBlindness)
          StateCheck(Myself,STATE_BLIND)
	  !Global("DMWWHLSM","GLOBAL",1)
THEN DO
       Action(SpellMyself,CLERIC_TRUE_SIGHT)
END

IF TRIGGER
          RequireBlock(HasL3)
          StateCheck(Myself,STATE_BLIND)
	  !Global("DMWWHLSM","GLOBAL",1)
THEN DO
       Action(SpellMyself,CLERIC_NEUTRALIZE_POISON)
       Action(SpellMyself,CLERIC_CURE_DISEASE)
       Action(SpellMyself,CLERIC_HEAL)
END


/////////////////////////////////////////////////////////////////////
////	Sanctuary
/////////////////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,70)
	!CheckStatGT(Myself,0,SANCTUARY)
THEN DO
	Action(SpellMyself,CLERIC_SANCTUARY|100|50)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,10,Level)
	HPPercentLT(scstarget,50)
	!CheckStatGT(Myself,20,Level)
	!CheckStatGT(Myself,0,SANCTUARY)
THEN DO
	Action(SpellMyself,CLERIC_SANCTUARY|100|50)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,14,Level)
	HPPercentLT(scstarget,50)
	CheckStatGT(Myself,20,Level)
	!CheckStatGT(Myself,0,SANCTUARY)
THEN DO
	Action(SpellMyself,CLERIC_SANCTUARY|100|50)
END

/////////////////////////////////////////////////////////////////////
////   Improved Invisibility (Avenger only)
/////////////////////////////////////////////////////////////////////

IF TRIGGER
          See(NearestEnemyOf(Myself))
          RequireBlock(IsAvenger)
          RequireBlock(HasL4)
          !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
          !StateCheck(Myself,STATE_INVISIBLE)
THEN DO
       Action(SpellRESMyself,"spdr401"|100|50)
END

//////////////////////////////////////////////////////////////////////
///	Physical Mirror
//////////////////////////////////////////////////////////////////////

IF TRIGGER
    IgnoreBlock(IsDruid)
	RequireBlock(HasL6)
	TargetBlock(EnemiesInOrderShort)
	GlobalTimerExpired("redefend","LOCALS")
	!CheckStatGT(Myself,0,CLERIC_PHYSICAL_MIRROR)
	OR(2)
		!Range(scstarget,10)
		StateCheck(scstarget,STATE_DISABLED)
THEN DO
	Action(SpellMyself,CLERIC_PHYSICAL_MIRROR|100|100)
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END




/////////////////////////////////////////////////////////////////////
////	Emergency healing for me
/////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(HasL6)
	HPPercentLT(Myself,50)
THEN DO
	Action(SpellMyself,CLERIC_HEAL)
END

IF TRIGGER
	RequireBlock(HasL5)
	HPPercentLT(Myself,50)
	CheckStatGT(Myself,0,SANCTUARY)
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)

END

IF TRIGGER
	HPPercentLT(Myself,50)
THEN DO
	Action(Potion,"potn55",@1055)
	Action(Potion,"dw#ptn55",@1055)
	Action(Potion,"potn52",@1055)
	Action(Potion,"dw#ptn52",@1052)
END

IF TRIGGER
	RequireBlock(HasL5)
	TargetBlock(EnemiesInOrderShort)
	HPPercentLT(Myself,50)
	OR(2)
		!Range(scstarget,10)
		StateCheck(scstarget,STATE_DISABLED)
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS|100|100)
END

//////////////////////////////////////////////////////////////////////
////	Remove Blindness for high-powered allies
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_BLIND)
	!Global("DMWWHLSM","GLOBAL",1)
	HPPercentLT(scstarget,75)
        RequireBlock(HasL6)
        CheckStatGT(scstarget,8,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Action(Spell,CLERIC_HEAL)
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_BLIND)
	!Global("DMWWHLSM","GLOBAL",1)
        RequireBlock(HasL3)
        CheckStatGT(scstarget,8,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Combine()
       Action(Spell,CLERIC_CURE_DISEASE)
       Action(Spell,CLERIC_NEUTRALIZE_POISON)
       Action(Spell,CLERIC_HEAL)
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END

//////////////////////////////////////////////////////////////////////
////	Clear clouds
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsDruid)
	RequireBlock(HasL3)
	IgnoreBlock(SpellsAreBG1)
	OR(4)
		Heard([ANYONE],404) // Cloudkill
		Heard([ANYONE],406) // Stinking Cloud
		Heard([ANYONE],408) // Death Fog
		Heard([ANYONE],409) // Incendiary Cloud
THEN DO
	Action(SpellMyself,CLERIC_ZONE_OF_SWEET_AIR)
END

IF TRIGGER
	IgnoreBlock(IsDruid)
	RequireBlock(HasL3)
	IgnoreBlock(SpellsAreBG1)
	OR(4)
		Heard([ANYONE],404) // Cloudkill
		Heard([ANYONE],406) // Stinking Cloud
		Heard([ANYONE],408) // Death Fog
		Heard([ANYONE],409) // Incendiary Cloud
	CheckStatGT(Myself,6,Level)
	!CheckStatGT(Myself,10,Level)
	!GlobalTimerNotExpired("sweetair","LOCALS")
THEN DO
	Action(SpellNoDecMyself,CLERIC_ZONE_OF_SWEET_AIR|100|100)
	SetGlobalTimer("sweetair","LOCALS",1000)
	OnContinue(SetGlobalTimer("sweetair","LOCALS",1000))
END

IF TRIGGER
	IgnoreBlock(IsDruid)
	RequireBlock(HasL3)
	IgnoreBlock(SpellsAreBG1)
	OR(4)
		Heard([ANYONE],404) // Cloudkill
		Heard([ANYONE],406) // Stinking Cloud
		Heard([ANYONE],408) // Death Fog
		Heard([ANYONE],409) // Incendiary Cloud
	CheckStatGT(Myself,10,Level)
	!GlobalTimerNotExpired("sweetair","LOCALS")
THEN DO
	Action(SpellNoDecMyself,CLERIC_ZONE_OF_SWEET_AIR)
	SetGlobalTimer("sweetair","LOCALS",1000)
	OnContinue(SetGlobalTimer("sweetair","LOCALS",1000))
END


//////////////////////////////////////////////////////////////////////
////	Healing for high-powered allies
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(HasL6)
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,10,Level)
	HPPercentLT(scstarget,50)
	!CheckStatGT(Myself,20,Level)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_HEAL|100|50)
END

IF TRIGGER
	RequireBlock(HasL6)
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,14,Level)
	HPPercentLT(scstarget,50)
	CheckStatGT(Myself,20,Level)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_HEAL|100|50)
END

IF TRIGGER
	RequireBlock(HasL5)
	IgnoreBlock(HasL7)
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,5,Level)
	HPPercentLT(scstarget,50)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(SpellArea,CLERIC_MASS_CURE|100|50)
END

IF TRIGGER
	RequireBlock(HasL7)
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,8,Level)
	HPPercentLT(scstarget,50)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(SpellArea,CLERIC_MASS_CURE|100|50)
END

/////////////////////////////////////////////////////////////////////////////
///	Are any of my own team panicked or paralysed? If so, bring them out. (But don't waste
///	it on low-powered scum)
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
    IgnoreBlock(IsDruid)
	RequireBlock(HasL6)
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_PANIC)
	CheckStatGT(scstarget,8,Level)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,CLERIC_REMOVE_FEAR)
END

IF TRIGGER
    IgnoreBlock(IsDruid)
	IgnoreBlock(HasL6)
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_PANIC)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,CLERIC_REMOVE_FEAR)
END

IF TRIGGER
	RequireBlock(HasL3)
	IgnoreBlock(HasL6)
	IgnoreBlock(IsDruid)
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,0,HELD)
	CheckStatGT(scstarget,8,Level)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,CLERIC_REMOVE_PARALYSIS)
END

IF TRIGGER
	RequireBlock(HasL3)
	IgnoreBlock(IsDruid)
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,0,HELD)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,CLERIC_REMOVE_PARALYSIS)
END

IF TRIGGER
	RequireBlock(HasL3)
	IgnoreBlock(IsDruid)
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_CHARMED)
	Allegiance(scstarget,GOODCUTOFF)
THEN DO
    Combine()
	Action(Spell,CLERIC_REMOVE_CURSE)
END

IF TRIGGER
	RequireBlock(HasL3)
	IgnoreBlock(IsDruid)
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_DISABLED)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(Spell,CLERIC_REMOVE_CURSE)
END

///////////////////////////////////////////////////////////////////////////////////////
////	More healing, if it's appropriate
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,95)
	OR(2)
		CheckStatGT(Myself,0,SANCTUARY)
		!See(NearestEnemyOf(Myself))
	OR(2)
		HPPercentLT(Myself,75)
		!See(NearestEnemyOf(Myself))
THEN DO
	Action(Potion,"potn55",@1055)
	Action(Potion,"potn52",@1052)
	Action(Potion,"dw#ptn55",@1055)
	Action(Potion,"dw#ptn52",@1052)
	ActionCondition(Potion,"potn08",@1008;OR(2)&!See(NearestEnemyOf(Myself))&CheckStatLT(Myself,11,Level))
	Action(SpellMyself,CLERIC_CURE_SERIOUS_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_MEDIUM_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_LIGHT_WOUNDS)
END

