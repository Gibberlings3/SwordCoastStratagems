
//uber-buffs

IF TRIGGER
	GlobalTimerExpired("redefend","LOCALS")
	TriggerBlock(HLAPriest)
	Range(NearestEnemyOf(Myself),5)
THEN DO
        SetGlobalTimer("redefend","LOCALS",15) //intentionally a bit longer
        Action(SpellL7RESMyself,CLERIC_FORESIGHT|100|100)
        Action(SpellL7RESMyself,CLERIC_DIVINE_SHELL|100|100)
END

////////////////////////////////////////////////////////////////////
////    Pro/weapons (only for Helm)
////////////////////////////////////////////////////////////////////

IF TRIGGER
        TriggerBlock(ILackWeaponProtection)
        !StateCheck(Myself,STATE_INVISIBLE)
        See(NearestEnemyOf(Myself))
        !GlobalTimerNotExpired("justdonepmw","LOCALS")
        Global("instantprep","LOCALS",1)
	Range(NearestEnemyOf(Myself),5)
THEN DO
        Action(SpellRESBooleanMyself,CLERIC_MANTLE)
        Action(SpellRESBooleanMyself,CLERIC_PROTECTION_FROM_MAGIC_WEAPONS)
        SetGlobalTimer("redefend","LOCALS",7)
        SetGlobalTimer("justdonepmw","LOCALS",7)
END


////////////////////////////////////////////////////////////////////
////    (Un)holy Aura (& cloak of chaos)
////////////////////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	TriggerBlock(HLAPriest)
	!GlobalTimerNotExpired("redefend","LOCALS")
THEN DO
	Action(SpellL7RESMyself,CLERIC_HOLY_AURA|100|100)
	Action(SpellL7RESMyself,CLERIC_UNHOLY_AURA|100|100)
	Action(SpellL7RESMyself,CLERIC_AURA_OF_CONFUSION|100|100)
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END



////////////////////////////////////////////////////////////////////
////    Divine Protection
////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(IsIWDDivine)
	GlobalTimerExpired("redefend","LOCALS")
	!CheckSpellState(Myself,SHIELD_OF_LATHANDER)
	TriggerBlock(ILackWeaponProtection)
	Range(NearestEnemyOf(Myself),5)
THEN DO
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
	Action(SpellL7Myself,CLERIC_GREATER_SHIELD_OF_LATHANDER|100|100)
	Action(SpellMyself,CLERIC_SHIELD_OF_LATHANDER|100|100)
END

////////////////////////////////////////////////////////////////////
////	Iron Skins
////////////////////////////////////////////////////////////////////

IF TRIGGER
	GlobalTimerExpired("redefend","LOCALS")
	TriggerBlock(StoneskinFailing)
THEN DO
	Action(SpellMyself,CLERIC_IRONSKIN|100|100)
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END

////////////////////////////////////////////////////////////////////////
////    My own blindness
////////////////////////////////////////////////////////////////////

IF TRIGGER
          RequireBlock(TruesightCuresBlindness)
          StateCheck(Myself,STATE_BLIND)
	!CheckSpellState(Myself,DO_NOT_CURE_BLINDNESS)
THEN DO
       Action(SpellMyself,CLERIC_TRUE_SIGHT)
END

IF TRIGGER
	RequireBlock(God:lolth)
    RequireBlock(TruesightCuresBlindness)
    StateCheck(Myself,STATE_BLIND)
	!CheckSpellState(Myself,DO_NOT_CURE_BLINDNESS)
THEN DO
       Action(SpellRESMyself,"%INNATE_CLERIC_TRUE_SIGHT%")
END

IF TRIGGER
          StateCheck(Myself,STATE_BLIND)
	!CheckSpellState(Myself,DO_NOT_CURE_BLINDNESS)
THEN DO
       Action(SpellMyself,CLERIC_NEUTRALIZE_POISON)
       Action(SpellMyself,CLERIC_CURE_DISEASE)
       Action(SpellMyself,CLERIC_HEAL)
END



/////////////////////////////////////////////////////////////////////
////	Sanctuary
/////////////////////////////////////////////////////////////////////


IF TRIGGER
	HPPercentLT(Myself,50)
	!CheckStatGT(Myself,0,SANCTUARY)
THEN DO
	Action(SpellRESBooleanMyself,CLERIC_INVISIBILITY|100|50)
	Action(SpellMyself,CLERIC_SANCTUARY|100|50)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,10,Level)
	HPPercentLT(scstarget,50)
	!CheckStatGT(Myself,20,Level)
	!CheckStatGT(Myself,0,SANCTUARY)
THEN DO
	Action(SpellRESBooleanMyself,CLERIC_INVISIBILITY|100|50)
	Action(SpellMyself,CLERIC_SANCTUARY|100|50)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,14,Level)
	HPPercentLT(scstarget,50)
	CheckStatGT(Myself,20,Level)
	!CheckStatGT(Myself,0,SANCTUARY)
THEN DO
	Action(SpellRESBooleanMyself,CLERIC_INVISIBILITY|100|50)
	Action(SpellMyself,CLERIC_SANCTUARY|100|50)
END

/////////////////////////////////////////////////////////////////////
////   Entropy Shield
/////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsIWDDivine)
        See(NearestEnemyOf(Myself))
	GlobalTimerExpired("redefend","LOCALS")
	!CheckStat(Myself,1,WIZARD_SPELL_IMMUNITY)
THEN DO
	SetGlobalTimer("redefend","LOCALS",8)
	Action(SpellMyself,CLERIC_ENTROPY_SHIELD|100|100)
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END

/////////////////////////////////////////////////////////////////////
////   Improved Invisibility (Avenger/Mask.Shar only)
/////////////////////////////////////////////////////////////////////

IF TRIGGER
		  RequireBlock(God:mask)
		  See(NearestEnemyOf(Myself))
          !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
          !StateCheck(Myself,STATE_INVISIBLE)
THEN DO
       Action(SpellRESMyself,"%INNATE_WIZARD_SHADOW_DOOR%"|100|50)
END

IF TRIGGER
          See(NearestEnemyOf(Myself))
          !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
          !StateCheck(Myself,STATE_INVISIBLE)
THEN DO
       Action(SpellRESBooleanMyself,CLERIC_SHADOW_DOOR|100|50)
       Action(SpellRESBooleanMyself,CLERIC_IMPROVED_INVISIBILITY|100|50)
END

//////////////////////////////////////////////////////////////////////
///	Physical Mirror
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	GlobalTimerExpired("redefend","LOCALS")
	!CheckStatGT(Myself,0,CLERIC_PHYSICAL_MIRROR)
	OR(2)
		!Range(scstarget,10)
		StateCheck(scstarget,STATE_DISABLED)
THEN DO
	Action(SpellMyself,CLERIC_PHYSICAL_MIRROR|100|100)
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END




/////////////////////////////////////////////////////////////////////
////	Emergency healing for me
/////////////////////////////////////////////////////////////////////

IF TRIGGER
          RequireBlock(IcasarachtFight)
          Target("icasa")
          HPPercentLT("icasa",75)
THEN DO
        Action(Spell,CLERIC_HEAL)
        Action(Spell,CLERIC_CURE_CRITICAL_WOUNDS)
        Action(Spell,CLERIC_CURE_SERIOUS_WOUNDS)
END

IF TRIGGER
	RequireBlock(God:ilmater)
	HPPercentLT(Myself,90)
	!GlobalTimerNotExpired("ilmater_endurance","LOCALS")
THEN DO
		SetGlobalTimer("ilmater_endurance","LOCALS",600)
	   Action(SpellRESMyself,"%spl_ilmater_endurance%")
END

IF TRIGGER
	HPPercentLT(Myself,80)
	!CheckStatGT(Myself,0,CLERIC_REGENERATION)
THEN DO
        Action(SpellMyself,CLERIC_REGENERATE)
END

IF TRIGGER
          HPPercentLT(Myself,50)
THEN DO   
          Action(ContingencyCleric,CURE_CRITICAL_WOUNDS)
          Action(ContingencyCleric,HEAL)
          Action(SequencerMyself,HEAL_SPELL_DEFLECTION_STONE_SKIN)
END


IF TRIGGER
	RequireBlock(God:ilmater)
	HPPercentLT(Myself,70)
THEN DO
        Action(SpellRESMyself,"%INNATE_CLERIC_HEAL%")
END

IF TRIGGER
	HPPercentLT(Myself,70)
THEN DO
        Action(SpellMyself,CLERIC_HEAL)
END

IF TRIGGER
	HPPercentLT(Myself,70)
	CheckStatGT(Myself,0,SANCTUARY)
	IgnoreBlock(IsIWDDivine|Demivrgvs)
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_SERIOUS_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_MEDIUM_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_LIGHT_WOUNDS)
END

IF TRIGGER
	HPPercentLT(Myself,70)
	CheckStatGT(Myself,0,SANCTUARY)
	RequireBlock(IsIWDDivine|Demivrgvs)
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_SERIOUS_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_MEDIUM_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_MODERATE_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_LIGHT_WOUNDS)
END

IF TRIGGER
	HPPercentLT(Myself,50)
THEN DO
	Action(Potion,"potn55",@101055)
	Action(Potion,"dw#ptn55",@101055)
	Action(Potion,"potn52",@101055)
	Action(Potion,"extheal",@101052)
	Action(Potion,"dw#ptn52",@101052)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	RequireBlock(IsIWDDivine|Demivrgvs)
	HPPercentLT(Myself,50)
	OR(2)
		!Range(scstarget,10)
		StateCheck(scstarget,STATE_DISABLED)
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS|100|100)
	Action(SpellMyself,CLERIC_CURE_SERIOUS_WOUNDS|100|100)
	Action(SpellMyself,CLERIC_CURE_MEDIUM_WOUNDS|100|100)
	Action(SpellMyself,CLERIC_CURE_MODERATE_WOUNDS|100|100)
	Action(SpellMyself,CLERIC_CURE_LIGHT_WOUNDS|100|100)
END

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	IgnoreBlock(IsIWDDivine|Demivrgvs)
	HPPercentLT(Myself,50)
	OR(2)
		!Range(scstarget,10)
		StateCheck(scstarget,STATE_DISABLED)
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS|100|100)
	Action(SpellMyself,CLERIC_CURE_SERIOUS_WOUNDS|100|100)
	Action(SpellMyself,CLERIC_CURE_MEDIUM_WOUNDS|100|100)
	Action(SpellMyself,CLERIC_CURE_LIGHT_WOUNDS|100|100)
END

//////////////////////////////////////////////////////////////////////
////	Remove Blindness or Feeblemind for high-powered allies
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(God:ilmater)
	TargetBlock(HumanoidVillains)
	OR(2)
            StateCheck(scstarget,STATE_BLIND)
            StateCheck(scstarget,STATE_FEEBLEMINDED)
	!CheckSpellState(scstarget,DO_NOT_CURE_BLINDNESS)
	HPPercentLT(scstarget,75)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Action(SpellRES,"%INNATE_CLERIC_HEAL%")
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END

IF TRIGGER
	RequireBlock(God:ilmater)
	TargetBlock(HumanoidVillains)
        StateCheck(scstarget,STATE_FEEBLEMINDED)
	!CheckSpellState(scstarget,DO_NOT_CURE_BLINDNESS)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Combine()
       Action(SpellRES,"%INNATE_CLERIC_HEAL%")
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END


IF TRIGGER
	TargetBlock(HumanoidVillains)
	OR(2)
            StateCheck(scstarget,STATE_BLIND)
            StateCheck(scstarget,STATE_FEEBLEMINDED)
	!CheckSpellState(scstarget,DO_NOT_CURE_BLINDNESS)
	HPPercentLT(scstarget,75)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Action(Spell,CLERIC_HEAL)
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
        StateCheck(scstarget,STATE_FEEBLEMINDED)
	!CheckSpellState(scstarget,DO_NOT_CURE_BLINDNESS)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Combine()
       Action(Spell,CLERIC_HEAL)
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END

IF TRIGGER
        IgnoreBlock(Demivrgvs)
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_BLIND)
	!CheckSpellState(scstarget,DO_NOT_CURE_BLINDNESS)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Combine()
       Action(Spell,CLERIC_CURE_DISEASE)
       Action(Spell,CLERIC_NEUTRALIZE_POISON)
       Action(Spell,CLERIC_HEAL)
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END

IF TRIGGER
        RequireBlock(Demivrgvs)
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_BLIND)
	!CheckSpellState(scstarget,DO_NOT_CURE_BLINDNESS)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Combine()
       Action(Spell,CLERIC_CURE_DISEASE)
       Action(Spell,CLERIC_HEAL)
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END

//////////////////////////////////////////////////////////////////////
////	Clear clouds
//////////////////////////////////////////////////////////////////////

IF TRIGGER
		Heard([ANYONE],404) // Cloudkill
        CheckStatLT(Myself,100,RESISTPOISON)
        !General(Myself,UNDEAD)
THEN DO
	Action(SpellMyself,CLERIC_ZONE_OF_SWEET_AIR)
END

IF TRIGGER
		Heard([ANYONE],406) // Stinking Cloud
		CheckStatLT(Myself,100,RESISTPOISON)
        !General(Myself,UNDEAD)
        !CheckStatGT(Myself,0,MINORGLOBE)
THEN DO
	Action(SpellMyself,CLERIC_ZONE_OF_SWEET_AIR)
END

IF TRIGGER
		Heard([ANYONE],408) // Death Fog
        CheckStatLT(Myself,100,RESISTACID)
THEN DO
	Action(SpellMyself,CLERIC_ZONE_OF_SWEET_AIR)
END

IF TRIGGER
		Heard([ANYONE],409) // Incendiary Cloud
        CheckStatLT(Myself,100,RESISTFIRE)
THEN DO
	Action(SpellMyself,CLERIC_ZONE_OF_SWEET_AIR)
END


//////////////////////////////////////////////////////////////////////
////	Healing for high-powered allies
//////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(HumanoidVillains)
	HPPercentLT(scstarget,50)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
	Range(scstarget,20)
THEN DO
	Action(SpellL7RESMyself,CLERIC_MASS_HEAL|100|50)
END

IF TRIGGER
	RequireBlock(God:ilmater)
	TargetBlock(HumanoidVillains)
	HPPercentLT(scstarget,50)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(SpellRES,"%INNATE_CLERIC_HEAL%"|100|50)
END


IF TRIGGER
	TargetBlock(HumanoidVillains)
	HPPercentLT(scstarget,50)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_HEAL|100|50)
END


IF TRIGGER
	TargetBlock(HumanoidVillains)
	HPPercentLT(scstarget,75)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Range(scstarget,20)
THEN DO
	Action(SpellMyself,CLERIC_MASS_CURE|100|50)
END

IF TRIGGER
        TargetBlock(HumanoidVillains)
        HPPercentLT(scstarget,75)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
THEN DO
        Action(Spell,CLERIC_CURE_CRITICAL_WOUNDS|100|50)
        Action(Spell,CLERIC_CURE_SERIOUS_WOUNDS|100|50)
        Action(Spell,CLERIC_CURE_MEDIUM_WOUNDS|100|50)
END

IF TRIGGER
        RequireBlock(IsIWDDivine|Demivrgvs)
        TargetBlock(HumanoidVillains)
        HPPercentLT(scstarget,75)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
THEN DO
        Action(Spell,CLERIC_CURE_MODERATE_WOUNDS|100|50)
END

IF TRIGGER
        TargetBlock(HumanoidVillains)
        HPPercentLT(scstarget,75)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
THEN DO
        Action(Spell,CLERIC_CURE_LIGHT_WOUNDS|100|50)
END

/////////////////////////////////////////////////////////////////////////////
///	Are any of my own team panicked, confused, or paralysed? If so, bring them out. (But don't waste
///	it on low-powered scum)
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(God:ilmater)
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_PANIC)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellAreaRES,"%INNATE_CLERIC_REMOVE_FEAR%")
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_PANIC)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,CLERIC_REMOVE_FEAR)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,0,HELD)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,CLERIC_REMOVE_PARALYSIS)
END

////////// on IWD, exaltation /////////////////////////////////

IF TRIGGER
    RequireBlock(IsIWDDivine)
	TargetBlock(HumanoidVillains)
	OR(3)
	   StateCheck(scstarget,STATE_SLEEPING)
	   StateCheck(scstarget,STATE_CONFUSED)
	   StateCheck(scstarget,STATE_STUNNED)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(Spell,CLERIC_EXALTATION)
END

IF TRIGGER
    RequireBlock(God:tyr)
	TargetBlock(HumanoidVillains)
	OR(3)
	   StateCheck(scstarget,STATE_SLEEPING)
	   StateCheck(scstarget,STATE_CONFUSED)
	   StateCheck(scstarget,STATE_STUNNED)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellRES,"%TYR_EXALTATION%")
END


///////// on SR, remove curse //////////////////////////////////

IF TRIGGER
        RequireBlock(Demivrgvs)
	TargetBlock(HumanoidVillains)
	OR(3)
          StateCheck(scstarget,STATE_CHARMED)
	  StateCheck(scstarget,STATE_CONFUSED)
	  StateCheck(scstarget,STATE_DISABLED)
	Allegiance(scstarget,ENEMY)
        CheckStatGT(scstarget,%minimum_healing_level%,LEVEL)
THEN DO
    Combine()
	Action(Spell,CLERIC_REMOVE_CURSE)
END

///////////////////////////////////////////////////////////////////////////////////////
////	More healing, if it's appropriate
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsIWDDivine|Demivrgvs)
	HPPercentLT(Myself,95)
	OR(2)
		CheckStatGT(Myself,0,SANCTUARY)
		!See(NearestEnemyOf(Myself))
	OR(2)
		HPPercentLT(Myself,75)
		!See(NearestEnemyOf(Myself))
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_SERIOUS_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_MEDIUM_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_LIGHT_WOUNDS)
	Action(Potion,"potn55",@101055)
	Action(Potion,"potn52",@101052)
	Action(Potion,"dw#ptn55",@101055)
	Action(Potion,"dw#ptn52",@101052)
	ActionCondition(Potion,"potn08",@101008;OR(2)&!See(NearestEnemyOf(Myself))&CheckStatLT(Myself,11,Level))
END

IF TRIGGER
        RequireBlock(IsIWDDivine|Demivrgvs)
	HPPercentLT(Myself,95)
	OR(2)
		CheckStatGT(Myself,0,SANCTUARY)
		!See(NearestEnemyOf(Myself))
	OR(2)
		HPPercentLT(Myself,75)
		!See(NearestEnemyOf(Myself))
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_SERIOUS_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_MEDIUM_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_MODERATE_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_LIGHT_WOUNDS)
	Action(Potion,"potn55",@101055)
	Action(Potion,"potn52",@101052)
	Action(Potion,"dw#ptn55",@101055)
	Action(Potion,"dw#ptn52",@101052)
	ActionCondition(Potion,"potn08",@101008;OR(2)&!See(NearestEnemyOf(Myself))&CheckStatLT(Myself,11,Level))
END


IF TRIGGER
   See(NearestEnemyOf(Myself))
   GlobalTimerExpired("redefend","LOCALS")
   !StateCheck(Myself,STATE_HASTED)
        Global("instantprep","LOCALS",1)
THEN DO
     Action(SpellRESBooleanMyself,CLERIC_IMPROVED_HASTE)
     Action(SpellRESBooleanMyself,CLERIC_HASTE)
END


