// summoning spells, L6-7

IF TRIGGER
   RequireBlock(IsIWDDivine)
   See(NearestEnemyOf(Myself))
   !GlobalTimerNotExpired("easy_summon","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     Action(SpellL7Myself,CLERIC_STALKER|100|50)
     SetGlobalTimer("easy_summon","LOCALS",12)
END

IF TRIGGER
   See(NearestEnemyOf(Myself))
   !GlobalTimerNotExpired("easy_summon","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     Action(SpellL7Myself,CLERIC_CONJURE_EARTH_ELEMENTAL|100|50)
       Action(SpellMyself,CLERIC_CONJURE_FIRE_ELEMENTAL|100|50)
       Action(SpellMyself,CLERIC_CONJURE_ANIMALS|100|50)
       Action(SpellMyself,CLERIC_AERIAL_SERVANT|100|50)
     SetGlobalTimer("easy_summon","LOCALS",12)
END

// basic buffs

IF TRIGGER
   Range(NearestEnemyOf(Myself),10)
   !GlobalTimerNotExpired("easybuff","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     SetGlobalTimer("easybuff","LOCALS",12)
     Action(SpellL7Myself,CLERIC_SHIELD_OF_THE_ARCHONS|100|100)
     Action(SpellL7RESMyself,CLERIC_FIENDISH_WARDING|100|100)
     Action(SpellMyself,CLERIC_REGENERATE|100|100)
     Action(SpellMyself,CLERIC_BLADE_BARRIER|100|100)
     Action(SpellMyself,CLERIC_RIGHTEOUS_MAGIC|100|100)
     Action(SpellMyself,CLERIC_HOLY_POWER|100|100)
     Action(SpellMyself,CLERIC_DRAW_UPON_HOLY_MIGHT|100|100)
END


// close range spells, L6-7

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   Range(scstarget,10)
   TriggerBlock(Easiest)
THEN DO
     Action(SpellL7Myself,CLERIC_NATURE_BEAUTY)
     Action(SpellL7Myself,CLERIC_HOLY_WORD)
     Action(Spell,CLERIC_DESTRUCTION)
     Action(SpellL7Myself,CLERIC_UNHOLY_WORD)
     Action(Spell,CLERIC_DOLOROUS_DECAY)
     Action(SpellReplaceMyself,ENEMY_CLERIC_HARM,CLERIC_HARM)
END


/// single-target or party-friendly spells, L6-7

IF TRIGGER
   RequireBlock(IsIWDDivine)
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest)
THEN DO
     Action(SpellL7,CLERIC_SYMBOL_PAIN)
     Action(SpellL7,CLERIC_SYMBOL_HOPELESSNESS)
	 Action(SpellRESBoolean,CLERIC_CHAIN_LIGHTNING)
END

IF TRIGGER
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest)
THEN DO
     Action(SpellL7,CLERIC_CONFUSION)
     Action(SpellL7,CLERIC_FINGER_OF_DEATH)
END

IF TRIGGER
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest)
THEN DO
     Action(SpellL7,CLERIC_CREEPING_DOOM)
     Action(SpellL7,CLERIC_SYMBOL_STUN)
     Action(SpellL7,CLERIC_SYMBOL_DEATH)
     Action(SpellL7,CLERIC_CONFUSION)
     Action(SpellL7,CLERIC_FINGER_OF_DEATH)
     Action(SpellL7RES,CLERIC_POWER_WORD_STUN) 
	 Action(SpellRESBoolean,CLERIC_POWER_WORD_SILENCE|100|50)

END

IF TRIGGER
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest|Enemy)
   !Range(scstarget,20)
THEN DO
     Combine()
     Action(SpellRESBoolean,CLERIC_CLOUDKILL|100|100)
END

// close range spells, levels 3-5

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   Range(scstarget,10)
   TriggerBlock(Easiest)
THEN DO
     Action(SpellMyself,CLERIC_SLAY_LIVING)
     Action(Spell,CLERIC_POISON)
     Action(SpellMyself,CLERIC_CLOAK_OF_FEAR)
END

IF TRIGGER
   RequireBlock(IsIWDDivine)
   Target(NearestEnemyOf(Myself))
   Range(scstarget,10)
   TriggerBlock(Easiest)
THEN DO
	 Action(SpellRESBoolean,CLERIC_CONTAGION)
     Action(Spell,CLERIC_CAUSE_DISEASE)
END

/// conal, close-range spells, L4-5

IF TRIGGER
   RequireBlock(IsIWDDivine)
   Target(NearestEnemyOf(Myself))
   Range(scstarget,5)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_THORN_SPRAY)
     Action(Spell,CLERIC_SMASHING_WAVE)
END

/// single-target or party-friendly spells, L4(ish)-5

IF TRIGGER
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_INSECT_PLAGUE)
	 Action(SpellRESBoolean,CLERIC_LOLTH_SPIDER_SWARM)
     Action(Spell,CLERIC_FLAME_STRIKE)
     Action(Spell,CLERIC_MENTAL_DOMINATION)
     Action(SpellRESBoolean,CLERIC_ICELANCE)
END

IF TRIGGER
   IgnoreBlock(Demivrgvs)
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest)
   AreaType(OUTDOOR)
THEN DO
     Action(Spell,CLERIC_CALL_LIGHTNING)
END

IF TRIGGER
   RequireBlock(Demivrgvs)
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_CALL_LIGHTNING)
END

IF TRIGGER
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_STATIC_CHARGE)
END


IF TRIGGER
   RequireBlock(IsIWDDivine)
   RequireBlock(CauseWoundsIWD)
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_CAUSE_CRITICAL_WOUNDS_IWD)
     Action(Spell,CLERIC_CAUSE_SERIOUS_WOUNDS_IWD)
     Action(Spell,CLERIC_CAUSE_MEDIUM_WOUNDS)
END

IF TRIGGER
   RequireBlock(IsIWDDivine)
   IgnoreBlock(CauseWoundsIWD)
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_CAUSE_MEDIUM_WOUNDS)
END

// summoning spells, L3-5

IF TRIGGER
   RequireBlock(IsIWDDivine)
   RequireBlock(SCSIWDDivine)
   See(NearestEnemyOf(Myself))
   !GlobalTimerNotExpired("easy_summon","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     Action(SpellMyself,CLERIC_GIANT_INSECT|100|50)
     SetGlobalTimer("easy_summon","LOCALS",12)
END

IF TRIGGER
   See(NearestEnemyOf(Myself))
   !GlobalTimerNotExpired("easy_summon","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     Action(SpellMyself,CLERIC_ANIMATE_DEAD|100|50)
       Action(SpellRESBooleanMyself,CLERIC_SPIDER_SPAWN|100|50)
       Action(SpellMyself,CLERIC_CALL_WOODLAND_BEINGS|100|50)
       Action(SpellMyself,CLERIC_ANIMAL_SUMMONING_2|100|50)
       Action(SpellMyself,CLERIC_ANIMAL_SUMMONING_1|100|50)
     SetGlobalTimer("easy_summon","LOCALS",12)
END

/// single-target or party-friendly spells, L1-3

IF TRIGGER
   TargetBlock(DistantEnemies)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_HOLY_SMITE)
     Action(Spell,CLERIC_UNHOLY_BLIGHT)
     Action(Spell,CLERIC_RIGID_THINKING)
     Action(Spell,CLERIC_HOLD_PERSON)
	 Action(SpellRESBoolean,CLERIC_HORROR)
     Action(Spell,CLERIC_SUMMON_INSECTS)
     Action(SpellRESBoolean,CLERIC_LOLTH_SUMMON_SPIDERS)
END

IF TRIGGER
   RequireBlock(IsIWDDivine)
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_ALICORN_LANCE)
     Action(Spell,CLERIC_SUNSCORCH)
     Action(SpellRESBoolean,CLERIC_ICE_KNIFE)
END

IF TRIGGER
   RequireBlock(IsIWDDivine)
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,10)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_CAUSE_MODERATE_WOUNDS)
     Action(Spell,CLERIC_CAUSE_LIGHT_WOUNDS)
	 Action(SpellRESBoolean,CLERIC_SHOCKING_GRASP)
	 Action(SpellRESBoolean,CLERIC_BURNING_HANDS)
END

IF TRIGGER
   RequireBlock(Demivrgvs)
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_SUNSCORCH)
END

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Easiest)
THEN DO
     Action(Spell,CLERIC_COMMAND)
     Action(Spell,CLERIC_DOOM)
END

// basic buffs

IF TRIGGER
   Range(NearestEnemyOf(Myself),10)
   !GlobalTimerNotExpired("easybuff","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     SetGlobalTimer("easybuff","LOCALS",12)
     Action(SpellMyself,CLERIC_SPIRITUAL_HAMMER|100|100)
     Action(SpellMyself,CLERIC_FLAME_BLADE|100|100)
END

IF TRIGGER
   RequireBlock(IsIWDDivine)
   Range(NearestEnemyOf(Myself),10)
   !GlobalTimerNotExpired("easybuff","LOCALS")
   TriggerBlock(Easiest)
THEN DO
     SetGlobalTimer("easybuff","LOCALS",12)
     Action(SpellMyself,CLERIC_BEAST_CLAW|100|100)
END


// melee

IF TRIGGER
   !GlobalTimerNotExpired("castspell","LOCALS")
   Target(NearestEnemyOf(Myself))
   TriggerBlock(Easiest)
THEN DO
     Action(Literal)
     Attack(scstarget)
END

