////////////////////////////////////////////////////
///
/// "Version" is for testing
/// 1: resource-building only
/// 2: mages-before only
/// 3: main loop only
/// 4: mages-after only
/// 5: clericmage only


LAF include STR_VAR file=priest_shared.tph END

DEFINE_ACTION_FUNCTION priest
BEGIN


   ACTION_IF !IS_AN_INT version BEGIN
         OUTER_SET version=0
   END

      OUTER_SPRINT type priest
      // data read-in
      LAUNCH_ACTION_MACRO read_in_spells_per_level
      LAUNCH_ACTION_MACRO read_in_spell_choices
      LAUNCH_ACTION_MACRO read_in_individual_overrides
      LAUNCH_ACTION_MACRO read_in_priest_scripts
      ACTION_IF !disable_hlas BEGIN
         LAM read_in_hla_choices
      END
      
      LAF define_difficulty STR_VAR type=priest RET difficulty_variable END
      LAF define_difficulty STR_VAR type=priest_prep RET prep_difficulty_variable=difficulty_variable END
      ACTION_IF is_bg2 BEGIN
         LAF define_difficulty STR_VAR type=priest_hla  END
      END


      // instant and prebuff spells

      LAUNCH_ACTION_MACRO read_in_instant_prebuff_spells

   ACTION_IF (version=0 || version=1) BEGIN

      // resources
      LAF make_priest_resources END

   END

      // core
      
 ACTION_IF is_iwd BEGIN
   ACTION_IF (version=0 || version>=2) BEGIN
      LAF run STR_VAR file=priest_iwd version END
   END
 END ELSE BEGIN
   ACTION_IF (version=0 || version=2) BEGIN
      LAF priest_edits_before END
   END
   ACTION_IF (version=0 || version=3) BEGIN
      LAF priest_edits_main  END
   END
   ACTION_IF (version=0 || version=4) BEGIN
      LAF priest_edits_after END
   END
 END
 ACTION_IF (version=0 || version=5) BEGIN

      // cleric-mages

      LAF check_label STR_VAR label=dw#mage RET value END
      ACTION_IF value BEGIN
              LAF run STR_VAR file=clericmage locbase="caster_shared/clericmage" END
      END
 END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Primary edit
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////



DEFINE_ACTION_FUNCTION priest_edits_main
BEGIN
    OUTER_SPRINT arguments ~~    
    LAF check_label STR_VAR label=dw#standardise_nobg2 RET value END
    ACTION_IF value BEGIN
       OUTER_SPRINT arguments ~%arguments% spells_are_bg1~
    END ELSE BEGIN
       OUTER_SPRINT arguments ~%arguments% spells_are_not_bg1~
    END
    // load in the innocent priests

    LAF innocent_priests RET list END

    // get a list of priests and append to the innocent list (this is hardcoded, on speed grounds)

    COPY_EXISTING_REGEXP GLOB ~.*\.cre~ ~override~
        READ_BYTE 0x273 class
        PATCH_MATCH class WITH
        3 // cleric
        15 // cleric_thief
        11 // druid
        8 // fighter_cleric
        16 // fighter_druid
        156 // flaming fist
        BEGIN
             SPRINT filename ~%SOURCE_RES%~
             LPF CRE_is_PC RET is_pc=value END
             LPF CRE_is_dead RET is_dead=value END
             LPF skipped_priest RET is_skipped=value END
             PATCH_IF (!is_dead && !is_pc && !is_skipped) BEGIN
                PUSH list ~%filename%~
             END

        END
        DEFAULT END
    BUT_ONLY

    LAF edit_creature STR_VAR creature= ~%list%~ editstring= ~priest_edits_main_patch=>"%arguments%"~ END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_edits_main_patch
      STR_VAR filename=""
              arguments=""
BEGIN
      LPF CRE_find_priest_scripts STR_VAR filename RET value=value END
      PATCH_IF value=1 BEGIN
          SPRINT arguments ~%arguments% game_is_bg1~
      END
      PATCH_IF value>0 BEGIN
         PATCH_PRINT ~Determining spells and script for %filename%~
         PATCH_SILENT
         LPF enforce_priest STR_VAR filename arguments END
      END

END

OUTER_SPRINT $SFO_do_not_parse_arguments("priest_edits_main_patch") ""

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION skipped_priest
      STR_VAR filename=""
      RET value
BEGIN
      SET value=0
      PATCH_FOR_EACH skip IN xzar BEGIN
         PATCH_IF ~%filename%~ STRING_EQUAL_CASE ~%skip%~ BEGIN
            SET value=1
         END
      END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Handle INNOCENTs
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION innocent_priests RET list BEGIN

    OUTER_SPRINT list ~~
    LAF install STR_VAR file="dw#innpr.baf dw#inndr.baf" location=resource END
    ACTION_IF is_bg1 BEGIN
       ACTION_FOR_EACH innocent IN AGNASI BENTAN BRATHL CADDER CHANTH
                       GALILE GAXIR KELDDA LASKAL MADARC
                       NALIN ORDULI PALLON POGHMA4 PRIHEL SHAELL TREMAI UNSHEY


       BEGIN
          ACTION_IF FILE_EXISTS_IN_GAME "%innocent%.cre" BEGIN // some differing conventions between vanilla/EE
             COPY_EXISTING "%tutu_var%%innocent%.cre" "%workspace%"
                 LPF CRE_is_innocent RET value END
             BUT_ONLY
             ACTION_IF value BEGIN
                OUTER_PUSH list ~%tutu_var%%innocent%~
             END
          END
       END
    END
    ACTION_IF is_bg2 BEGIN
       OUTER_PUSH list ~AMCLER02 SARTEM01 DOGHMA ECOLT~
    END
    ACTION_IF !enhanced_edition BEGIN
        LAF edit_creature STR_VAR creature= ~%list%~ editstring=~insert_script_high=>dw#innpr~ END
    END
    // special handling for druids
    ACTION_IF is_bg1 BEGIN
        ACTION_IF enhanced_edition BEGIN
              LAF edit_creature STR_VAR creature=laskal editstring=~class=>druid~ END
        END ELSE BEGIN
              LAF edit_creature STR_VAR creature=laskal editstring=~script_override=>dw#inndr~ tv=1 END
        END
    END

END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Pre-main edits (put things here if possible)
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION priest_edits_before BEGIN
   ACTION_IF is_bg2 BEGIN

         // HLA markers
     
         LAF clone_item STR_VAR item= ~dw#marker_file=>dw#hlapr~ editstring=~droppable=>0~ END
         LAF read_in_list STR_VAR file="hla.2da" RET list END
         OUTER_WHILE "%list%" STRING_COMPARE "" BEGIN
               LAF return_first_entry STR_VAR list RET creature=entry list END
               LAF edit_creature INT_VAR allow_missing=1 STR_VAR creature editstring="add_items=>dw#hlapr" END
         END

         // class corrections
         
         LAF edit_creature STR_VAR creature=drshnl01 editstring=~class=>DRUID~ END
         LAF edit_creature STR_VAR creature=~sahpri02 obssah02 sahamb03 sahamb06~ editstring=~class=>CLERIC~ END

         // missing scripts
         
         LAF edit_creature STR_VAR creature=cedruid2 editstring=~insert_script_low=>druid8b~ END

         // play-dead spell for Nilthiri
         ACTION_IF !enhanced_edition BEGIN
           MAKE_PATCH
            add_basic_ability=>null
            add_effect_inline=>~opcode=>28 target=>2 duration=>24 parameter2=>1 parameter1=>100~
            clone_entry_inline=>~type=>effect match=>"opcode=28" opcode=>"entry_index from [29 31 85 86 87 88 89]" number_to_add=>7~
            add_effect_inline'=>~opcode=>173 target=>2 duration=>24 parameter1=>100~
            add_effect_inline'0=>~opcode=>101 target=>2 duration=>24 parameter2=>25~
            add_effect_inline'1=>~opcode=>267 target=>2 duration=>24 parameter1=>14017~
            add_effect_inline'2=>~opcode=>267 target=>2 duration=>24 parameter1=>14662~
            add_effect_inline'3=>~opcode=>39 target=>2 duration=>24~
            add_effect_inline'4=>~opcode=>101 target=>2 duration=>24 parameter2=>2~
            add_effect_inline'5=>~opcode=>143 timing=>4 parameter1=>7 duration=>24 target=>2 resource=>monhp1~
            add_effect_inline'6=>~opcode=>112 target=>2 resource=>monhp1~
            add_effect_inline'7=>~opcode=>17 target=>2 timing=>4 duration=>24 parameter1=>30 parameter2=>1~
            add_effect_inline'8=>~opcode=>267 target=>2 duration=>24 parameter1=>20438~
           END
           LAF make_spell STR_VAR spell=dw#nilde edits=patch_data END
         END

         // missing weapons/armor
         
         LAF edit_creature STR_VAR creature=~druid10a druid10b druid12a druid12b
                                             druid6a druid8a druid8b gdruid trevil02
                                             tdr10a tdr10b tdr8a tdr8b cedruid1~
                                   editstring=~add_items=>staf01(WEAPON1)~
         END

         LAF edit_creature STR_VAR creature=~cedruid2 cechal02 cechalle druid10a druid10b druid12a druid12b druid6a druid8a druid8b~
                                   editstring=~add_items=>leat01~
         END


         // blind priests in beholder caves
         
         LAF install STR_VAR files=blind.baf location=resource END

     // Kuo-toa clerics need a Continue() in their truesight

     LAF check_label STR_VAR label="dw#mage" RET value END
     ACTION_IF !value BEGIN
             LAF swap_text STR_VAR files=~kuotoa.bcs~ swaps=~ReallyForceSpell(Myself,KOA_TRUE_SIGHT_NO_VIS)=>ReallyForceSpell(Myself,KOA_TRUE_SIGHT_NO_VIS)Continue()~ END
     END

     // Sendai statue needs to be quiet till activated
     
     	<<<<<<<< .../stratagems-inline/dw#send2.baf
	IF
		HasItem("stonring",Myself)
	THEN
		RESPONSE #100
			NoAction()
	END

	>>>>>>>>

        LAF install STR_VAR file=dw#send2.baf inline=yes END
        LAF edit_creature STR_VAR creature=sendai2 editstring=~insert_script_high=>dw#send2~ END


        // Berenn is too big to get certain protective effects (he looks stupid)
        
        LAF edit_creature STR_VAR creature=hgber01 editstring=~add_items=>dw#prbig~ END

	// Dawnmaster Kreel is supposed to wander around

        <<<<<<<< .../stratagems-inline/dw#kreel.baf
        IF
	!Global("inafight","GLOBAL",1)
	!See(NearestEnemyOf(Myself))
	HPPercent(Myself,100)
THEN
	RESPONSE #100
		RandomWalk()
END
        >>>>>>>>

        LAF install STR_VAR file=dw#kreel.baf inline=yes END
        LAF edit_creature STR_VAR creature=dawnmas editstring=~insert_script=>"dw#kreel below sht0902"~ END
        
        // Faldorn's 'fight NPC Cernd' script rather crudely uses a CURE_LIGHT_WOUNDS check to control her actions. We swap it for a global
        
      COPY_EXISTING "cefald01.bcs" override
       DECOMPILE_AND_PATCH BEGIN
         REPLACE_TEXTUALLY "HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)" ~Global("faldorn_cernd","LOCALS",0)~
         REPLACE_TEXTUALLY "RemoveSpell(CLERIC_CURE_LIGHT_WOUNDS)" ~SetGlobal("faldorn_cernd","LOCALS",1)~
       END
      BUT_ONLY
      
      // party priests who get vampirised
      
   // we assume 2.25MXP, enough to put Nalia at L16
   // Jaheira: L12/13 fighter-druid

   LAF edit_creature STR_VAR creature=vampjah editstring="class=>fighter_druid level1=>12 level2=>13 insert_script_low=>pries8a" END
   LAF DELETE_SCRIPT_BLOCK STR_VAR script=vampjah match=DOMINATION END

   // Viconia: L18 cleric

   LAF edit_creature STR_VAR creature=vampvic editstring="class=>cleric level1=>18 insert_script_low=>pries8a" END
   LAF DELETE_SCRIPT_BLOCK STR_VAR script=vampvic match=DOMINATION END

   // Anomen: L18 cleric

   LAF edit_creature STR_VAR creature=vampano editstring="class=>cleric level1=>18 insert_script_low=>pries8a" END
   LAF DELETE_SCRIPT_BLOCK STR_VAR script=vampano match=DOMINATION END

   // Gavin: L18 cleric

   ACTION_IF FILE_EXISTS_IN_GAME "b!gavamp.cre" BEGIN
      LAF edit_creature STR_VAR creature="b!gavamp" editstring="class=>cleric level1=>18 insert_script_low=>pries8a" END
      LAF DELETE_SCRIPT_BLOCK STR_VAR script="b!gavamp" match=DOMINATION END
   END





   END  // end of BG2 bit

   ACTION_IF is_bg1 BEGIN

   // missing scripts

       LAF edit_creature STR_VAR creature=x#black allow_missing=1 editstring=~insert_script_low=>priest3~ END
       LAF edit_creature STR_VAR creature="pumberl pumber2" tv=1 editstring="insert_script_low=>priest3" END
       LAF edit_creature STR_VAR creature=beland tv=1 editstring=~script_race=>priest1~ END

   // cloned creatures that need disambiguating

       LAF clone_creature STR_VAR creature="%tutu_var%pumber2=>dw#pumb3 %tutu_var%pumber2=>dw#pumb4" END
       MAKE_PATCH
          patch_actor_inline=>"match=>~actor_x_coord_start=1029 and actor_resource=%tutu_var%pumber2~ actor_resource=>dw#pumb3"
          patch_actor_inline'=>"match=>~actor_x_coord_start=936 and actor_resource=%tutu_var%pumber2~ actor_resource=>dw#pumb4"
       END
       LAF edit_area STR_VAR area="%BaldursGateDocks_WaterQueensHouse%" edits=patch_data END

    // in BGEE, Osmadi's Cursed Backbiter spear gets in the way of his shapeshift
       ACTION_IF enhanced_edition BEGIN
          LAF edit_creature STR_VAR creature=osmadi editstring="remove_items=>sper03 add_items=>sper03(inv) add_items=>staf01(WEAPON1)" END
       END

    // give Brathlen the right class
    
    LAF edit_creature STR_VAR creature=brathl tv=1 editstring="class=>cleric" END

    // prevent Corsone from attacking his own summons

       LAF install STR_VAR file=dw#corso.baf location=resource END
       LAF edit_creature STR_VAR creature=corsone tv=1 editstring=~swap_script=>"%tutu_scripts%eeenemy=>dw#corso"~ END

    END // end of BG1 bit

END
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Post-main edits (if possible put things in pre-main instead)
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION priest_edits_after BEGIN
   
   ACTION_IF VARIABLE_IS_SET CLERIC_CAUSE_SERIOUS_WOUNDS_IWD BEGIN
      OUTER_SPRINT variables "CauseWoundsIWD=True"
   END ELSE BEGIN
      OUTER_SPRINT variables ""
   END
   LAF ssl_to_bcs STR_VAR variables script=nymph location=~ssl/main~ locbase=priest END



   ACTION_IF is_bg2 BEGIN


   END // end of bg2 section

   ACTION_IF is_bg1 BEGIN

         LAF edit_creature STR_VAR creature=laskal tv=1 editstring="class=>innocent" END
   
   
   END


END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////
///      Find priest scripts. Return 1 if found with a BG1 script, 2 if with a BG2 script. If there's a replacement in priest/scripts,
///      compile it; otherwise, blank the slot
///////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION CRE_find_priest_scripts
   STR_VAR filename=""
   RET value
BEGIN
   LAUNCH_PATCH_MACRO read_in_priest_scripts
   SET value=0
   FOR (i=0x248;i<0x270;i+=8) BEGIN
      READ_ASCII i script
      TO_LOWER script
      INNER_PATCH_SAVE script ~%script%~ BEGIN
         REPLACE_TEXTUALLY "_" ""
      END
      SET here=0
      PATCH_FOR_EACH game IN 1 2 BEGIN
         PATCH_IF ($is( ~bg%game%~)=1 && VARIABLE_IS_SET $priest_script( ~bg%game%~ ~%script%~)) BEGIN
            SET here=1
            SET value=game
         END
      END
      PATCH_IF here=1 BEGIN
         SET replace=0
         INNER_ACTION BEGIN
              ACTION_IF FILE_EXISTS ~%scsroot%/%component_loc%/scripts/%script%.baf~ BEGIN
                 ACTION_IF is_tutu BEGIN
                    COPY ~%scsroot%/%component_loc%/scripts/%script%.baf~ ~%workspace%/_%script%.baf~
                    COMPILE EVALUATE_BUFFER ~%workspace%/_%script%.baf~
                    SILENT
                 END ELSE BEGIN
                    LAF install STR_VAR file= ~%script%.baf~ location=scripts END
                    SILENT
                 END
                 OUTER_SET replace=1
              END
         END
         PATCH_IF replace=0 BEGIN
            WRITE_ASCII i ~~ (8)
         END
      END
   END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////            Make needed resources                 //////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_priest_resources BEGIN

    // required for BG1 and BG2

         // nothing right now

    // required for BG2 and IWDEE
    
    ACTION_IF is_bg2 || is_iwd BEGIN

    // Greater Command should be party-friendly

    LAF edit_spell STR_VAR spell=CLERIC_GREATER_COMMAND editstring=~patch_ability_inline=>"projectile=>159"~ END


    END

    // required for BG2 only
    
    ACTION_IF is_bg2 BEGIN




     // allow NPCs to use Summon Elemental Prince

       LAF install STR_VAR file="elemzaam.baf elemsunn.baf elemchan.baf" location=resource END
       
       LAF edit_creature STR_VAR creature=elemchan editstring="name1_string=>2831" END
       LAF edit_creature STR_VAR creature=elemsunn editstring="name1_string=>2867" END
       LAF edit_creature STR_VAR creature=elemzaam editstring="name1_string=>2873" END

       LAF clone_spell STR_VAR spell="%CLERIC_GREATER_ELEMENTAL_SWARM%=>dw#elemp" editstring="name1_string=>~-1~ delete_effect=>~opcode=177~" END

        LAF clone_item STR_VAR item=~misc16=>dw#prbig~ editstring=~droppable=>0~ END // too large to use Shield of the Archons (looks stupid)
        LAF install STR_VAR files=~dw#bear.spl dw#ctrlu.spl dw#destu.spl~ location=resource END

      END // end of BG2-only block

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////
///      Read in priest scripts (i.e. those we aim to replace)
///////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_MACRO read_in_priest_scripts BEGIN
   OUTER_INNER_PATCH ~~ BEGIN
      LAUNCH_PATCH_MACRO read_in_priest_scripts
   END
END

DEFINE_PATCH_MACRO read_in_priest_scripts BEGIN
    PATCH_IF !(VARIABLE_IS_SET ~priest_scripts_read~) BEGIN
       SET priest_scripts_read=1
       PATCH_FOR_EACH script IN
            priest1 priest2 priest3 priest4 priest5 priest6
            seniyad druid3 x#shdr6 x#shdr8 x#andart x#beador x#mareth x#belgin
       BEGIN
          TO_LOWER ~script~
          SPRINT $priest_script_bg1(~%script%~) ~~
       END
       PATCH_FOR_EACH script IN
AMERAL DEMPRI      GENPRST1 PAINA     PRIES10A PRIES10B  PRIES10C PRIES12A
PRIES12C PRIES14A  PRIES14T PRIES18A  PRIES18B PRIES18C  PRIES18D PRIES6A
PRIES6B  PRIES6C   PRIES8A  PRIES8B   PRIES8C  PRIEST12  PRIET12B SAHPRI12  DAQILUE  HGNYA01
GPHEALER BERENN    BARL     CCLER20B  DRSHNL01 DRSHNL11  DRUID10A  DRUID10B DRUID12A
DRUID12B DRUID14G  DRUID6A  DRUID8A   DRUID8B  KBSHAM01  GORPRI1  HGSHAM01
SENDAI2  GORCAMB3  HGBER01  CEFALD01  SAHAMB03 SAHAMB06 THICLR10
BEGIN
          TO_LOWER ~script~
          SPRINT $priest_script_bg2(~%script%~) ~~
       END
    END
END




