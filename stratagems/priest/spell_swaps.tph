////////////////////////////// based on race //////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_spell_swap_race_preparatory
    STR_VAR race=""
            spells=""
    RET spells
BEGIN
    TO_LOWER race
    PATCH_MATCH ~%race%~ WITH
    VAMPIRE BEGIN
       LPF substitute_spells STR_VAR spells swap_out=FLAME_BLADE swap_in="" RET spells END
       LPF substitute_spells STR_VAR spells swap_out=SPIRITUAL_HAMMER swap_in="" RET spells END
    END
    DEFAULT END
END

////////////////////////////// based on specific creature type //////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_spell_swap_specific_preparatory
     STR_VAR filename=""
             spells=""
     RET spells
BEGIN
     TO_LOWER filename
     PATCH_MATCH "%filename%" WITH
     dwyuap2 dwyuap3 dwyuap4 dwyuap5 dwyuap6 dwyuap7
     talonam talonaf dwtalonm dwtalonf lsham1 lsham2 dwlsha5 dwlsha6 dwlsha10 dwlsha11 dwlsha12
     dwselfc1 dwselfc2 dwselfc3 sdelfpr dwacoly1 dwacoly2 dwlmshk1 dwlmshk2 dwlmshk3
     BEGIN
          LPF substitute_spells STR_VAR spells swap_out=RECITATION swap_in="" RET spells END
          LPF substitute_spells STR_VAR spells swap_out=PRAYER swap_in="" RET spells END
          LPF substitute_spells STR_VAR spells swap_out=BLESS swap_in="" RET spells END
          LPF substitute_spells STR_VAR spells swap_out=CHANT swap_in="" RET spells END
          LPF substitute_spells STR_VAR spells swap_out=RIGHTEOUS_WRATH_OF_THE_FAITHFUL swap_in="" RET spells END
          LPF substitute_spells STR_VAR spells swap_out=DEFENSIVE_HARMONY swap_in="" RET spells END
     END
     DEFAULT END
END
////////////////////////////// based on level //////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_spell_swap_level
     INT_VAR level=0
     STR_VAR spells=""
             kit=""
     RET spells
BEGIN
     PATCH_IF (level>14) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=CONJURE_ANIMALS swap_in=CONJURE_FIRE_ELEMENTAL RET spells=spells END
     END
     PATCH_IF (level>9) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=RIGID_THINKING swap_in=~MISCAST_MAGIC MISCAST_MAGIC RIGID_THINKING~ RET spells=spells END
     END


END


////////////////////////////// based on BG1 //////////////////////////////////////


DEFINE_PATCH_FUNCTION priest_spell_swap_bg1_preparatory
     STR_VAR spells=""
     RET spells
BEGIN
       LPF substitute_spells STR_VAR spells swap_out=~ARMOR_OF_FAITH ZONE_OF_SWEET_AIR CURE_DISEASE~ swap_in=~~ RET spells=spells END
END

DEFINE_PATCH_FUNCTION priest_spell_swap_bg1
     STR_VAR spells=""
     RET spells
BEGIN
      LPF substitute_spells STR_VAR spells swap_out=~MASS_CURE~ swap_in=~CURE_CRITICAL_WOUNDS~ RET spells=spells END
      LPF substitute_spells STR_VAR spells swap_out=~SUMMON_INSECTS~ swap_in=~CALL_LIGHTNING~ RET spells=spells END
      LPF substitute_spells STR_VAR spells swap_out=~CALL_WOODLAND_BEINGS~ swap_in=~ANIMAL_SUMMONING_1~ RET spells=spells END
      LPF substitute_spells STR_VAR spells swap_out=~CLOAK_OF_FEAR~ swap_in=~MENTAL_DOMINATION~ RET spells=spells END
      LPF substitute_spells STR_VAR spells swap_out=~HOLY_SMITE~ swap_in=~RIGID_THINKING MISCAST_MAGIC~ RET spells=spells END
      LPF substitute_spells STR_VAR spells swap_out=~UNHOLY_BLIGHT~ swap_in=~ANIMATE_DEAD ANIMATE_DEAD RIGID_THINKING MISCAST_MAGIC~ RET spells=spells END

END


////////////////////////////// based on vanilla IWD //////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_spell_swap_iwd
     STR_VAR spells=""
     RET spells
BEGIN
END

////////////////////////////// special //////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_spell_swap_misc
   STR_VAR spells=""
           filename=""
   RET spells
BEGIN
    // people with Wands of the Heavens don't need Flame Strike
    TO_LOWER filename
    PATCH_MATCH ~%filename%~ WITH
    aasim _aasim pbhunt04 saerk BEGIN
       LPF substitute_spells STR_VAR spells swap_out=FLAME_STRIKE swap_in=~SLAY_LIVING SLAY_LIVING MASS_CURE CURE_CRITICAL_WOUNDS~ RET spells=spells END

    END
    DEFAULT END
    // MR creatures don't use MAGIC_RESISTANCE
    LPF CRE_read_resist_magic RET mr=value END
    PATCH_IF mr>40 BEGIN
        LPF substitute_spells STR_VAR spells swap_out=MAGIC_RESISTANCE swap_in=MASS_CURE RET spells=spells END
    END
    // undead don't heal
    LPF CRE_read_general RET general=value END
    PATCH_IF general STRING_EQUAL_CASE UNDEAD BEGIN
       LPF substitute_spells STR_VAR spells swap_out=MASS_CURE swap_in=SLAY_LIVING RET spells=spells END
       LPF substitute_spells STR_VAR spells swap_out=CURE_CRITICAL_WOUNDS swap_in=SLAY_LIVING RET spells=spells END
       LPF substitute_spells STR_VAR spells swap_out=HEAL swap_in=HARM RET spells=spells END
    END
END

DEFINE_PATCH_FUNCTION priest_spell_swap_alignment
     STR_VAR filename=""
             spells=""
     RET spells
BEGIN
     TO_LOWER filename
     LPF CRE_is_good RET is_good=value END
     PATCH_IF is_good BEGIN
             LPF substitute_spells STR_VAR spells swap_out="SUMMON_DEVA_EVIL" swap_in="SUMMON_DEVA_GOOD" RET spells END
     END
END

