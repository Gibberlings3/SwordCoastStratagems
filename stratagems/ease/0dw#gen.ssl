IF
	HotKey(K)
	Global("DMWWNoMelee","LOCALS",0)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@607)
		SetGlobal("DMWWNoMelee","LOCALS",1)
END

IF
	HotKey(K)
	Global("DMWWNoMelee","LOCALS",1)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@608)
		SetGlobal("DMWWNoMelee","LOCALS",0)
END

IF
	HotKey(D)
	!Global("DMWWNoMelee","LOCALS",1)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
END

IF
	HotKey(F)
THEN
	RESPONSE #100
		EquipRanged()
END

IF
	HotKey(S)
	Global("DMWWBuff","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("DMWWBuff","LOCALS",1)
		DisplayStringHead(Myself,@601)
END

IF
	HotKey(S)
	Global("DMWWBuff","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("DMWWBuff","LOCALS",2)
		DisplayStringHead(Myself,@603)
END


IF
	HotKey(S)
	Global("DMWWBuff","LOCALS",2)
THEN
	RESPONSE #100
		SetGlobal("DMWWBuff","LOCALS",3)
		DisplayStringHead(Myself,@604)
END


IF
	HotKey(S)
	Global("DMWWBuff","LOCALS",3)
THEN
	RESPONSE #100
		SetGlobal("DMWWBuff","LOCALS",0)
		DisplayStringHead(Myself,@602)
END

BEGIN_ACTION_DEFINITION
	Name(Attack)
	TRIGGER
		ActionListEmpty()
	        !StateCheck(Myself,STATE_INVISIBLE)
	        !CheckStatGT(Myself,0,SANCTUARY)
	        !ModalState(TURNUNDEAD)
         	!HasItem("eneblade",Myself)
	        !HasItem("melfmet",Myself)
	        See(scstarget)
	        Allegiance(scstarget,ENEMY)
	        !InParty(scstarget)
	        !StateCheck(scstarget,STATE_REALLY_DEAD)
	        !StateCheck(scstarget,STATE_IMMOBILE)
	        !CheckStat(scstarget,1,HELD)
                !HasItem("morsword",scstarget)
	ACTION
		RESPONSE #scsprob1
		Attack(scstarget)
END

IF TRIGGER
   Target(LastAttackerOf(Myself))
   Target(NearestEnemyOf(Myself))
   Target(SecondNearestEnemyOf(Myself))
   Target(ThirdNearestEnemyOf(Myself))
   Target(FourthNearestEnemyOf(Myself))
   Range(LastSeenBy(Myself),5)
   !HasItem("morsword",scstarget)
   OR(2)
	!Global("DMWWNoMelee","LOCALS",1)
        InWeaponRange(LastSeenBy(Myself))
THEN DO
     Action(Attack)
END

IF TRIGGER
   Target(NearestEnemyOf(Myself))
   !HasItem("morsword",scstarget)
   OR(2)
	!Global("DMWWNoMelee","LOCALS",1)
        InWeaponRange(LastSeenBy(Myself))
THEN DO
     Action(Attack)
END

BEGIN_ACTION_DEFINITION
       Name(Buff)
       TRIGGER
          ActionListEmpty()
          CombatCounter(0)
	!Global("DMWWBuff","LOCALS",0)
	!Detect(NearestEnemyOf(Myself))
	!CheckStatGT(Myself,0,SPELLFAILUREscsargument2)
	HaveSpell(scsargument1)
	See(scstarget)
	ACTION
		RESPONSE #scsprob1
		ApplySpellRES("wesalac",Myself)
		Spell(scstarget,scsargument1)
END

BEGIN_ACTION_DEFINITION
       Name(BuffRES)
       TRIGGER
          ActionListEmpty()
          CombatCounter(0)
	!Global("DMWWBuff","LOCALS",0)
	!Detect(NearestEnemyOf(Myself))
	!CheckStatGT(Myself,0,SPELLFAILUREscsargument2)
	HaveSpellRES(scsargument1)
	See(scstarget)
	ACTION
		RESPONSE #scsprob1
		ApplySpellRES("wesalac",Myself)
		SpellRES(scsargument1,scstarget)
END

IF
	HotKey(Z)
	ActionListEmpty()
	CombatCounter(0)
	!Global("DMWWBuff","LOCALS",0)
	!Detect(NearestEnemyOf(Myself))
	!Global("ProEvilPaladin","LOCALS",1)
	!CheckStatGT(Myself,0,SPELLFAILUREPRIEST)
	HaveSpellRES("spcl213")
THEN
	RESPONSE #100
		SetGlobal("ProEvilPaladin","LOCALS",1)
		SetGlobalTimer("ProEvilPaladinTimer","LOCALS",60)
END

IF
	Global("ProEvilPaladin","LOCALS",1)
	OR(2)
		!HaveSpellRES("spcl213")
		GlobalTimerExpired("ProEvilPaladinTimer","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("ProEvilPaladin","LOCALS",0)
END

IF TRIGGER
   TargetBlock(PlayersInOrder)
   Global("ProEvilPaladin","LOCALS",1)
   !CheckStat(scstarget,1,PROTECTION_FROM_EVIL)
THEN DO
     Action(BuffRES,"spcl213",PRIEST)
END

IF
	HotKey(C)
	ActionListEmpty()
	CombatCounter(0)
	!Global("DMWWBuff","LOCALS",0)
	!Detect(NearestEnemyOf(Myself))
	!Global("ChaoticCommands","LOCALS",1)
	!CheckStatGT(Myself,0,SPELLFAILUREPRIEST)
	HaveSpell(CLERIC_CHAOTIC_COMMANDS)
THEN
	RESPONSE #100
		SetGlobal("ChaoticCommands","LOCALS",1)
		SetGlobalTimer("ChaoticCommandsTimer","LOCALS",60)
END

IF
	Global("ChaoticCommands","LOCALS",1)
	OR(2)
		!HaveSpell(CLERIC_CHAOTIC_COMMANDS)
		GlobalTimerExpired("ChaoticCommandsTimer","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("ChaoticCommands","LOCALS",0)
END

IF TRIGGER
   TargetBlock(PlayersInOrder)
   Global("ChaoticCommands","LOCALS",1)
   !CheckStat(scstarget,1,CLERIC_CHAOTIC_COMMANDS)
THEN DO
     Action(Buff,CLERIC_CHAOTIC_COMMANDS,PRIEST)
END

IF
	HotKey(V)
	ActionListEmpty()
	CombatCounter(0)
	!Global("DMWWBuff","LOCALS",0)
	!Detect(NearestEnemyOf(Myself))
	!Global("DeathWard","LOCALS",1)
	!CheckStatGT(Myself,0,SPELLFAILUREPRIEST)
	HaveSpell(CLERIC_DEATH_WARD)
THEN
	RESPONSE #100
		SetGlobal("DeathWard","LOCALS",1)
		SetGlobalTimer("DeathWardTimer","LOCALS",60)
END

IF
	Global("DeathWard","LOCALS",1)
	OR(2)
		!HaveSpell(CLERIC_DEATH_WARD)
		GlobalTimerExpired("DeathWardTimer","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("DeathWard","LOCALS",0)
END

IF TRIGGER
   TargetBlock(PlayersInOrder)
   Global("DeathWard","LOCALS",1)
   CheckStat(scstarget,0,SCRIPTINGSTATE2)
THEN DO
     Action(Buff,CLERIC_DEATH_WARD,PRIEST)
END

IF
	HotKey(B)
	ActionListEmpty()
	CombatCounter(0)
	!Global("DMWWBuff","LOCALS",0)
	!Detect(NearestEnemyOf(Myself))
	!Global("ProFire","LOCALS",1)
	OR(2)
		HaveSpell(CLERIC_PROTECTION_FROM_FIRE)
		HaveSpell(CLERIC_PROTECTION_FROM_LIGHTNING)
	!CheckStatGT(Myself,0,SPELLFAILUREPRIEST)
THEN
	RESPONSE #100
		SetGlobal("ProFire","LOCALS",1)
		SetGlobalTimer("ProFireTimer","LOCALS",60)
END

IF
	HotKey(B)
	ActionListEmpty()
	CombatCounter(0)
	!Global("DMWWBuff","LOCALS",0)
	!Detect(NearestEnemyOf(Myself))
	!Global("ProFire","LOCALS",1)
	HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
	!CheckStatGT(Myself,0,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
		SetGlobal("ProFire","LOCALS",1)
		SetGlobalTimer("ProFireTimer","LOCALS",60)
END

IF
	Global("ProFire","LOCALS",1)
	OR(2)
		!HaveSpell(CLERIC_PROTECTION_FROM_FIRE)
		GlobalTimerExpired("ProFireTimer","LOCALS")
	OR(2)
		!HaveSpell(CLERIC_PROTECTION_FROM_LIGHTNING)
		GlobalTimerExpired("ProFireTimer","LOCALS")
	OR(2)
		!HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		GlobalTimerExpired("ProFireTimer","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("ProFire","LOCALS",0)
END

IF TRIGGER
   TargetBlock(PlayersInOrder)
   Global("ProFire","LOCALS",1)
   CheckStatLT(scstarget,80,RESISTFIRE)
THEN DO
     Action(Buff,CLERIC_PROTECTION_FROM_FIRE,PRIEST)
END

IF TRIGGER
   TargetBlock(PlayersInOrder)
   Global("ProFire","LOCALS",1)
   CheckStatLT(scstarget,80,RESISTELECTRICITY)
THEN DO
     Action(Buff,CLERIC_PROTECTION_FROM_LIGHTNING,PRIEST)
END

IF TRIGGER
   TargetBlock(PlayersInOrder)
   Global("ProFire","LOCALS",1)
   CheckStatLT(scstarget,80,MAGICDAMAGERESISTANCE)
   !HasItem("clck26",scstarget)
THEN DO
     Action(Buff,WIZARD_PROTECTION_FROM_MAGIC_ENERGY,MAGE)
END

IF TRIGGER
   !InventoryFull(Myself)
   Target(Myself)
THEN DO
     Action(Buff,CLERIC_GOOD_BERRIES,PRIEST)
END

IF TRIGGER
   Target(Myself)
   CheckStat(Myself,0,STONESKINS)
   !Class(Myself,19) //sorceror
THEN DO
     Action(Buff,WIZARD_STONE_SKIN,MAGE)
END

IF TRIGGER
   Target(Myself)
   CheckStat(Myself,0,STONESKINS)
THEN DO
     Action(Buff,CLERIC_IRONSKIN,PRIEST)
END

IF TRIGGER
   Target(Myself)
   !CheckStat(Myself,3,SCRIPTINGSTATE5)
   !Class(Myself,19) // sorceror
THEN DO
     Action(Buff,WIZARD_ARMOR,MAGE)
END

IF TRIGGER
   Target(Myself)
   !HasItem("eneblade",Myself)
   !HasItem("melfmet",Myself)
   !Class(Myself,FIGHTER_MAGE)
   !Class(Myself,FIGHTER_MAGE_THIEF)
   !Class(Myself,BARD)
   !Class(Myself,19) // sorceror
THEN DO
   Action(Buff,WIZARD_MELF_METEOR,MAGE)
END

IF TRIGGER
   Target(Myself)
   !HasItem("brdflute",Myself)
   !InventoryFull(Myself)
THEN DO
   Action(BuffRES,"spcl921",MAGE)
END

BEGIN_ACTION_DEFINITION
   Name(Potion)
   TRIGGER
	ActionListEmpty()
	CombatCounter(0)
	GlobalGT("DMWWBuff","LOCALS",scsargument2)
	!Detect(NearestEnemyOf(Myself))
	HasItem(scsargument1,Myself)
   ACTION
         RESPONSE #scsprob1
             UseItem(scsargument1,Myself)
END

BEGIN_ACTION_DEFINITION
   Name(TimedItem)
   TRIGGER
        !GlobalTimerNotExpired("DMWWscsargument1timer","LOCALS")
	ActionListEmpty()
	CombatCounter(0)
	GlobalGT("DMWWBuff","LOCALS",scsargument2)
	!Detect(NearestEnemyOf(Myself))
	HasItem("scsargument1",Myself)
   ACTION
         RESPONSE #scsprob1
             SetGlobalTimer("DMWWscsargument1timer","LOCALS",2000)
             UseItem("scsargument1",Myself)
END

IF TRIGGER
   TargetBlock(PCsInOrder)
    HPPercentLT(scstarget,90)
    LevelGT(Myself,4)
    !Global("DMWWBlockAutoHealing","GLOBAL",1)
THEN DO
     Action(Buff,CLERIC_CURE_LIGHT_WOUNDS,PRIEST)
END

IF TRIGGER
    TargetBlock(PCsInOrder)
    HPPercentLT(scstarget,80)
    LevelGT(Myself,8)
    !Global("DMWWBlockAutoHealing","GLOBAL",1)
THEN DO
     Action(Buff,CLERIC_CURE_MEDIUM_WOUNDS,PRIEST)
END

IF TRIGGER
    TargetBlock(PCsInOrder)
    HPPercentLT(scstarget,70)
    LevelGT(Myself,10)
    !Global("DMWWBlockAutoHealing","GLOBAL",1)
THEN DO
     Action(Buff,CLERIC_CURE_SERIOUS_WOUNDS,PRIEST)
END

IF TRIGGER
    TargetBlock(PCsInOrder)
    HPPercentLT(scstarget,60)
    LevelGT(Myself,13)
    !Global("DMWWBlockAutoHealing","GLOBAL",1)
THEN DO
     Action(Buff,CLERIC_CURE_CRITICAL_WOUNDS,PRIEST)
END

IF TRIGGER
   HPPercentLT(Myself,100)
THEN DO
     Action(Potion,"gberry7",0)
     Action(Potion,"gberry6",0)
     Action(Potion,"gberry5",0)
     Action(Potion,"gberry4",0)
     Action(Potion,"gberry3",0)
     Action(Potion,"gberry2",0)
     Action(Potion,"gberry",0)
END


IF TRIGGER
   HPPercentLT(Myself,90)
THEN DO
     Action(Potion,"potn08",1)
END

IF TRIGGER
   HPPercentLT(Myself,70)
THEN DO
     Action(Potion,"potn52",2)
END


IF TRIGGER
THEN DO
     Action(TimedItem,clck25,0)
     Action(TimedItem,belt09,0)
     Action(TimedItem,boot12,0)
   //  Action(TimedItem,slng08,0)  this doesn't work, for reasons that aren't clear to me
   //  Action(TimedItem,slng09,0)
END


