<<<<<<<<.../stratagems-inline/blank
>>>>>>>>


/*
describe-library{}
{
Functions tied to SFO's own core functioning. Functions here should depend only on lib_fn and lib_tools.

}

*/


/*

document{sfo_load}
{
Given a list of space-separated sfo library functions, load those functions and (recursively) any libraries they depend on.
}


*/

DEFINE_ACTION_FUNCTION sfo_load
	STR_VAR library=""
BEGIN
	// sanity check
	ACTION_IF !VARIABLE_IS_SET sfo_setup_run || !sfo_setup_run BEGIN
		FAIL "You need to run the 'sfo_setup' macro before loading any SFO functions."
	END
	// default variable
	ACTION_IF !VARIABLE_IS_SET sfo_library_path BEGIN
		OUTER_SPRINT sfo_library_path sfo
	END
	// get the libraries into an array
	ACTION_CLEAR_ARRAY original_array
	ACTION_CLEAR_ARRAY load_array
	ACTION_TO_LOWER library
	OUTER_PATCH "%library% " BEGIN
		REPLACE_EVALUATE "\([^ ]+\) " BEGIN
			SPRINT library "%MATCH1%"
			PATCH_IF INDEX ("\." "%library%")<0 BEGIN
				SPRINT library "%library%.tph"
			END
			SPRINT $original_array("%library%") ""
			SPRINT $load_array("%library%") ""
		END
		""
	END
	COPY - "%MOD_FOLDER%/%sfo_library_path%/data/sfo_dependencies.2da" nowhere // have to do this manually since we haven't loaded lib_2da
		COUNT_2DA_COLS colcount
		READ_2DA_ENTRIES_NOW dependency_data colcount
		READ_2DA_ENTRIES_NOW dependency_columns (colcount - 1)
		FOR (col=1;col<colcount;++col) BEGIN
			READ_2DA_ENTRY_FORMER dependency_columns 0 (col - 1) header
			SPRINT $col_header("%col%") "%header%"
		END
		FOR (row=0;row<dependency_data;++row) BEGIN
			READ_2DA_ENTRY_FORMER dependency_data row 0 library
			PATCH_IF VARIABLE_IS_SET $load_array("%library%") BEGIN
				FOR (col=1;col<colcount;++col) BEGIN
					READ_2DA_ENTRY_FORMER dependency_data row col depends
					PATCH_IF depends BEGIN
						SPRINT depends_name $col_header("%col%")
						SPRINT $load_array("%depends_name%") ""
					END
				END
			END
		END
	
	ACTION_PHP_EACH load_array AS library=>discard BEGIN
		ACTION_MATCH "%library%" WITH
		"lib_sfo.tph" BEGIN
		END
		DEFAULT
			PRINT "Loading %library%"
			INCLUDE "%MOD_FOLDER%/%sfo_library_path%/%library%"
		END
	END
END



/*

document{sfo_path}
{
Given some or all of 'file', 'path', 'location' and 'locbase', return a full filespec file_path (e.g. %MOD_FOLDER%/lib/lib_soundset.tph) and the path bit (e.g. "%MOD_FOLDER%/lib).</p>
<p>
Determined as follows:
<ul>
<li>if 'path' is set, just use it.
<li>if not, and 'location' is set:
	<ul>
	<li>if 'locbase is set, use '%MOD_FOLDER%/locbase/location'
	<li>if not, and the 'component_loc' variable is set, use '%MOD_FOLDER%/component_loc/location'
	<li>otherwise, use '%MOD_FOLDER%/location'
	</ul>
<li>if not, and 'locbase' is set, use '%MOD_FOLDER%/locbase'
<li>otherwise, just use ""
</ul>	
}

*/

DEFINE_DIMORPHIC_FUNCTION sfo_path
	STR_VAR path=""
			location=""
			locbase=""
			file=""
	RET file_path
		path
BEGIN
	ACTION_IF !"%path%" STR_EQ "" BEGIN
		OUTER_SPRINT file_path "%path%/%file%"
	END ELSE
	ACTION_IF !"%location%" STR_EQ "" BEGIN
		ACTION_IF !"%locbase%" STR_EQ "" BEGIN
			OUTER_SPRINT path "%MOD_FOLDER%/%locbase%/%location%"	
		END ELSE
		ACTION_IF (VARIABLE_IS_SET component_loc) && !"%component_loc%" STR_EQ "" BEGIN
			OUTER_SPRINT path "%MOD_FOLDER%/%component_loc%/%location%"
		END ELSE BEGIN
			OUTER_SPRINT path "%MOD_FOLDER%/%location%"
		END	
	END ELSE
	ACTION_IF !"%locbase%" STR_EQ "" BEGIN
			OUTER_SPRINT path "%MOD_FOLDER%/%locbase%"
	END ELSE BEGIN
		OUTER_SPRINT path ""
	END
	ACTION_IF "%path%" STR_EQ "" BEGIN
		OUTER_SPRINT file_path "%file%"
	END ELSE BEGIN
		OUTER_SPRINT file_path "%path%/%file%"
	END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*
document{log_this}
{
Dump the string 'input' (synonym:'arguments') into the text file 'file' (by default placed in the %data_loc% directory, but you can override), creating it if necessary.

If repeat=0, only do this if it's not already there.
If new=1, wipe any existing contents
}


*/
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_DIMORPHIC_FUNCTION log_this
        INT_VAR repeat=1//boolean
				new=0//boolean
		STR_VAR file=""
                input=""
				arguments=""
                path="%data_loc%"
                location=""
                locbase=""
BEGIN
        ACTION_IF "%file%" STR_EQ "" BEGIN
			FAIL "No 'file' argment in log_this function"
		END
		OUTER_SPRINT input "%input%%arguments%"
		LAF sfo_path STR_VAR location locbase path file RET file_path END
        ACTION_IF !FILE_EXISTS ~%file_path%~ || new BEGIN
           COPY ~.../stratagems-inline/blank~ ~%file_path%~
        END
		ACTION_IF !repeat BEGIN
			LAF despecialize STR_VAR string="%input%" RET input_despec=string END
			OUTER_SET proceed=!FILE_CONTAINS_EVALUATED ("%file_path%" "%input_despec%\($\|%WNL%\|%MNL%\|%LNL\)")
		END ELSE BEGIN
			OUTER_SET proceed=1
		END
        ACTION_IF proceed BEGIN
           APPEND_OUTER ~%file_path%~ ~%input%~
        END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*
document{warning}
{
Dump the string 'warning' (synonym: 'arguments') into data_loc/sfo_warnings.txt, prepended with the component number and mod name.
If repeat=0, only do this if it's not already there.
}


*/
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_DIMORPHIC_FUNCTION warning
        INT_VAR repeat=0
		STR_VAR warning=""
				arguments=""
BEGIN
		PRINT "SFO warning: %warning%%arguments%"
		LAF log_this INT_VAR repeat STR_VAR file=sfo_warnings.txt input= ~%TP2_BASE_NAME% component %COMPONENT_NUMBER%: %warning%%arguments%~ END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*
document{regexp_warning}
{
Special case of 'warning' used specifically as a soft-fail for regexps.
}


*/
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_DIMORPHIC_FUNCTION regexp_warning
 STR_VAR file=""
		 parent="<unknown>"
BEGIN
	OUTER_PATCH "" BEGIN
		PATCH_IF "%file%" STRING_EQUAL_CASE "" BEGIN
			SPRINT file "%SOURCE_FILE%"
		END
		SPRINT warning "The mod's '%parent%' function has encountered a file it doesn't know how to patch, probably %file% (probably the file was broken by a previous mod). The error message was '%ERROR_MESSAGE%'. 
		WEIDU has skipped it and continued with installation, but this may cause instabilities."
		PATCH_WARN "%warning%"
		LPF warning STR_VAR warning END
	END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
document{sfo_handle_charsets}
{
SFO wrapper for HANDLE_CHARSETS.</p>

<p>Should be obsolete, replaced by charset_wrapper, except that it's still used in IWDSpells
}
*/
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////





DEFINE_ACTION_FUNCTION sfo_handle_charsets//deprecated
	INT_VAR sfo_from_utf8=0//boolean
	STR_VAR sfo_tra_path=""
			sfo_iconv_path="%sfo_tra_path%/iconv"
	RET sfo_tra_loc
		scs_tra_loc
BEGIN
	ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%sfo_iconv_path%/iconv.exe" && !VARIABLE_IS_SET SFO_charsets_handled BEGIN
		OUTER_SET SFO_charsets_handled=1
		ACTION_IF sfo_from_utf8=0 BEGIN
			ACTION_IF enhanced_edition BEGIN
				LAF HANDLE_CHARSETS 
					INT_VAR infer_charsets=1
							from_utf8=0
					STR_VAR	tra_path="%MOD_FOLDER%/%sfo_tra_path%"
							out_path="%ext_lang_loc%"
							iconv_path="%MOD_FOLDER%/%sfo_iconv_path%"
				END	
				OUTER_SPRINT sfo_tra_loc "%ext_lang_loc%"
			END ELSE BEGIN
				OUTER_SPRINT sfo_tra_loc "%MOD_FOLDER%/%sfo_tra_path%"
			END
			OUTER_SPRINT scs_tra_loc "%sfo_tra_loc%" // legacy
		END ELSE BEGIN
			ACTION_IF !enhanced_edition BEGIN
				LAF HANDLE_CHARSETS 
					INT_VAR infer_charsets=1
							from_utf8=1
					STR_VAR	tra_path="%MOD_FOLDER%/%sfo_tra_path%"
							out_path="%ext_lang_loc%"
							iconv_path="%MOD_FOLDER%/%sfo_iconv_path%"
				END	
				OUTER_SPRINT sfo_tra_loc "%ext_lang_loc%"
			END ELSE BEGIN
				OUTER_SPRINT sfo_tra_loc "%MOD_FOLDER%/%sfo_tra_path%"
			END
		END
	END ELSE BEGIN
		OUTER_SPRINT sfo_tra_loc "%sfo_tra_loc%"
	END
	OUTER_SPRINT scs_tra_loc "%sfo_tra_loc%" // legacy	
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
document{sfo_setup}
{
Set up the SFO directories and variables.

}

*/
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

 
DEFINE_ACTION_MACRO sfo_setup BEGIN
  
	OUTER_SET sfo_setup_run=1
  
 ////// Blank file

<<<<<<<< .../stratagems-inline/blank
>>>>>>>>
  
 //// readme

<<<<<<<< .../stratagems-inline/readme.txt
This folder is created by DavidW's mods for storing and building files. Other modders are welcome to use it; it's organised as follows:

Workspace: for building things. Put anything you like in here; don't expect it still to be there next time you check.

Lang: for storing HANDLE_CHARSETS-converted language files. Use a subfolder with the same name as your mod folder.

Data: for storing data that you want to read between components of your mod, or between different mods. Store data in a subfolder of 'Data'. I suggest using a folder with the same name as your mod folder itself; alternatively, use a folder with your modder prefix. Don't have your mod edit anything in someone else's folder without permission.

Markers: for putting small files that let other components of your mod, or other mods, detect what your mod has done. (This is not a good way to detect whether a component is installed at all, as it does not play nicely with automated installers; use REQUIRE/FORBID_COMPONENT and similar for that.) Since this folder is shared, use your modder prefix for any file stored in it. I suggest ".mrk" as a file suffix.

Backup: you can backup your mod here if you'd rather not use a folder in your own mod folder. Use
BACKUP "weidu_external/backup/[your mod folder name]" in your tp2. 
>>>>>>>>
  
  
   //////////////////////////////////////////////////////////////
   /// set the default variables if they're not set already
   //////////////////////////////////////////////////////////////

   ACTION_IF !VARIABLE_IS_SET sfo_library_path BEGIN
      OUTER_SPRINT sfo_libary_path sfo
   END
   
   // now that lua is inside sfo we're just going to hardcode this
   OUTER_SPRINT lua_loc "%sfo_library_path%/lua"

   ACTION_IF !VARIABLE_IS_SET external_loc BEGIN
      OUTER_SPRINT external_loc weidu_external
   END
   
   ACTION_IF !VARIABLE_IS_SET sfo_lite BEGIN
		OUTER_SET sfo_lite=0
   END

	ACTION_IF !VARIABLE_IS_SET sfo_handle_charsets BEGIN
		OUTER_SET sfo_handle_charsets=!sfo_lite
	END

	ACTION_IF !VARIABLE_IS_SET sfo_handle_ini BEGIN
		OUTER_SET sfo_handle_ini=!sfo_lite
	END

	ACTION_IF !VARIABLE_IS_SET sfo_handle_crossplatform BEGIN
		OUTER_SET sfo_handle_crossplatform=!sfo_lite
	END

	ACTION_IF !VARIABLE_IS_SET sfo_handle_fixes BEGIN
		OUTER_SET sfo_handle_fixes=!sfo_lite
	END

	ACTION_IF !VARIABLE_IS_SET sfo_load_structs BEGIN
		OUTER_SET sfo_load_structs=!sfo_lite
	END
	
	ACTION_IF !VARIABLE_IS_SET sfo_load_data BEGIN
		ACTION_IF !sfo_lite BEGIN
			OUTER_SPRINT sfo_load_data "data_spell_resrefs data_joinable_dvs"
		END ELSE BEGIN
			OUTER_SPRINT sfo_load_data ""
		END
	END
		


   ACTION_IF !VARIABLE_IS_SET sfo_tra_path BEGIN
		ACTION_IF DIRECTORY_EXISTS "%MOD_FOLDER%/lang" BEGIN
			OUTER_SPRINT sfo_tra_path lang
		END ELSE
		ACTION_IF DIRECTORY_EXISTS "%MOD_FOLDER%/tra" BEGIN
			OUTER_SPRINT sfo_tra_path tra
		END ELSE
		ACTION_IF DIRECTORY_EXISTS "%MOD_FOLDER%/languages" BEGIN
			OUTER_SPRINT sfo_tra_path languages
		END
   END
   
   ACTION_IF !VARIABLE_IS_SET sfo_setup_tra BEGIN
		OUTER_SPRINT sfo_setup_tra setup
   END
   
   ACTION_IF !VARIABLE_IS_SET sfo_tra_load BEGIN
		OUTER_SPRINT sfo_tra_load ""
   END
   
   ACTION_IF !VARIABLE_IS_SET sfo_extra_tra_folders BEGIN
	OUTER_SPRINT sfo_extra_tra_folders ""
   END

   ACTION_IF !VARIABLE_IS_SET sfo_iconv_path BEGIN
      OUTER_SPRINT sfo_iconv_path "%sfo_tra_path%/iconv"
   END

   ACTION_IF !VARIABLE_IS_SET sfo_default_language BEGIN
      OUTER_SPRINT sfo_default_language english
   END 
   
   OUTER_SPRINT SFO_default_language "%sfo_default_language%"
   OUTER_SPRINT base_language "%SFO_default_language%" // legacy
   
   ACTION_IF !VARIABLE_IS_SET ext_lang_loc BEGIN
		OUTER_SPRINT ext_lang_loc "%external_loc%/lang/%MOD_FOLDER%"
   END
   ACTION_IF !VARIABLE_IS_SET ext_audio_loc BEGIN
		OUTER_SPRINT ext_audio_loc "%external_loc%/audio/%MOD_FOLDER%"
   END
   
   ACTION_IF !VARIABLE_IS_SET ssl_loc BEGIN
		OUTER_SPRINT ssl_loc "%MOD_FOLDER%/bin"
   END

   ACTION_IF !VARIABLE_IS_SET sfo_from_utf8 BEGIN
	OUTER_SET sfo_from_utf8=1
   END
   
   ACTION_IF !VARIABLE_IS_SET sfo_ini_file BEGIN
		OUTER_SPRINT sfo_ini_file "%MOD_FOLDER%.ini"
   END
   ACTION_IF !VARIABLE_IS_SET sfo_ini_file_location BEGIN
		OUTER_SPRINT sfo_ini_file_location "%MOD_FOLDER%"
   END
   
   ACTION_IF !VARIABLE_IS_SET oggdec_path BEGIN
	OUTER_SPRINT oggdec_path "%MOD_FOLDER%/bin"
   END
 
   ACTION_IF !VARIABLE_IS_SET sox_path BEGIN
	OUTER_SPRINT sox_path "%MOD_FOLDER%/bin"
   END
 
   //////////////////////////////////////////////////////////////
   /// make the external folders
   //////////////////////////////////////////////////////////////

   MKDIR "%external_loc%"
   MKDIR "%external_loc%/workspace"
   MKDIR "%external_loc%/workspace/ssl_out"
   MKDIR "%external_loc%/data/%MOD_FOLDER%"
   MKDIR "%external_loc%/data/dw_shared"
   MKDIR "%external_loc%/markers"
   MKDIR "%external_loc%/lang"


   //////////////////////////////////////////////////////////////
   /// Install the readme
   //////////////////////////////////////////////////////////////
	
	COPY + ".../stratagems-inline/readme.txt" "%external_loc%"

   //////////////////////////////////////////////////////////////
   /// Set variables for the external locations
   //////////////////////////////////////////////////////////////

   OUTER_SPRINT workspace "%external_loc%/workspace"
   OUTER_SPRINT data_loc_root "%external_loc%/data"
   OUTER_SPRINT data_loc "%external_loc%/data/%MOD_FOLDER%"
   OUTER_SPRINT marker_loc "%external_loc%/markers"
   OUTER_SPRINT ext_lang_loc "%external_loc%/lang/%MOD_FOLDER%"
   OUTER_SPRINT data_loc_shared "%external_loc%/data/dw_shared"
   OUTER_SPRINT batch_loc "%external_loc%/batch"
   
    ///////////////////////////////////////////////////
	// check what install we're running on
	///////////////////////////////////////////////////

  OUTER_SET enhanced_edition = FILE_EXISTS_IN_GAME bgee.lua || FILE_EXISTS_IN_GAME monkfist.2da
  OUTER_SET is_tobex = (FILE_EXISTS "tobex.dll")
  OUTER_SET is_bg1 = GAME_IS ~bgee tutu tutu_totsc bgt eet bg1 totsc~ 
  OUTER_SET is_bg = is_bg1 // placate Cam
  OUTER_SET is_bg2 = GAME_IS ~bg2ee eet bgt tob soa~
  OUTER_SET is_sod = GAME_INCLUDES sod
  OUTER_SET is_iwd = GAME_IS ~iwdee iwd how totlm~
  OUTER_SET is_tutu = GAME_IS ~tutu tutu_totsc~
  OUTER_SET is_eet = GAME_IS ~eet~
  OUTER_SET is_bgt = GAME_IS ~bgt~
	OUTER_SET demivrgvs = FILE_EXISTS_IN_GAME ~dvimhere.mrk~ 
	OUTER_SET lefreut_ui = MOD_IS_INSTALLED "LEUI-BG1EE.tp2" 0 || MOD_IS_INSTALLED "LEUI.tp2" 0 || MOD_IS_INSTALLED "LEUI-SOD.tp2" 0 
	OUTER_SET dragonspear_ui = MOD_IS_INSTALLED "DRAGONSPEAR_UI++.TP2" 1	

  ACTION_IF !VARIABLE_IS_SET tutu_var BEGIN
     ACTION_IF is_tutu BEGIN
        OUTER_SPRINT tutu_var "_"
     END ELSE BEGIN
        OUTER_SPRINT tutu_var ""
     END
  END
  
  ACTION_IF is_eet BEGIN
     OUTER_SPRINT eet_var "_"
  END ELSE BEGIN
     OUTER_SPRINT eet_var ""
  END
 
  
    ///////////////////////////////////////////////////
	//set SSL variables
	///////////////////////////////////////////////////
 
  
     OUTER_SPRINT percentage "%"
	OUTER_SPRINT sslvariables "%percentage%MOD_FOLDER%percentage%=%MOD_FOLDER%&%percentage%tutu_var%percentage%=%tutu_var%"

   
END


/*
document{sfo_crossplatform}
{
Load appropriate values of the various variables for different versions of BG.
}
*/

DEFINE_ACTION_MACRO sfo_crossplatform BEGIN
 
	LOCAL_SPRINT letter ""
	LOCAL_SPRINT thegame ""
	LOCAL_SPRINT rows ""
	LOCAL_SPRINT var ""
	LOCAL_SPRINT val ""
	LOCAL_SPRINT area_array ""
	LOCAL_SPRINT name_array ""
	LOCAL_SET chapter_delta=0
	LOCAL_SET num=0

	ACTION_IF !VARIABLE_IS_SET sfo_crossplatform_set BEGIN
		
		OUTER_SET sfo_crossplatform_set=1
	 
		// file naming - general case (these are used enough that they tend to creep into even BG2 code, so it's worth defining them once and for all)

		ACTION_FOR_EACH letter IN a b c d e f g h i j k l m n o p q r s t u v w x y z BEGIN
			ACTION_IF is_tutu BEGIN
				OUTER_SPRINT ~tutu_script%letter%~ ~_~
			END ELSE BEGIN
				OUTER_SPRINT ~tutu_script%letter%~ ~%letter%~
			END
		END
		
		// the rest of this is skipped unless on a BG1 install
		
		ACTION_IF is_bg1 BEGIN
		
		// setup
		 
			ACTION_IF is_tutu BEGIN
				OUTER_SPRINT thegame tutu
			END ELSE 
			ACTION_IF GAME_IS bgt BEGIN
				OUTER_SPRINT thegame bgt
			END ELSE
			ACTION_IF GAME_IS bgee BEGIN
				OUTER_SPRINT thegame bgee
			END ELSE 
			ACTION_IF GAME_IS eet BEGIN
				OUTER_SPRINT thegame eet
			END ELSE
			ACTION_IF GAME_IS "bg1 totsc" BEGIN
				OUTER_SPRINT thegame obg
			END ELSE BEGIN
				FAIL ~Error in sfo_crossplatform: unrecognised BG1 game~
			END
		
		// afaict BGEE and oBG look the same
		ACTION_IF "%thegame%" STR_EQ obg BEGIN
			OUTER_SPRINT thegame bgee
		END
		
		// area codes (and associated area scripts)
			ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%sfo_library_path%/data/bg1_area_list.2da" BEGIN
				LAF 2da_read STR_VAR file=bg1_area_list.2da locbase="%sfo_library_path%/data" case=mixed RET_ARRAY area_array=array rows END
			END ELSE BEGIN
				LAF 2da_read INT_VAR inline=1 STR_VAR file=".../stratagems-inline/bg1_area_list.2da" case=mixed RET_ARRAY area_array=array rows END		
			END
			ACTION_PHP_EACH rows AS var=>discard BEGIN
				ACTION_IF "%thegame%" STR_EQ "bgt" BEGIN
					OUTER_SPRINT val $area_array("%var%" "bgt")
				END ELSE BEGIN
					OUTER_SPRINT val $area_array("%var%" "default")
				END
				ACTION_MATCH "%thegame%" WITH
				eet BEGIN
					OUTER_PATCH_SAVE val "%val%" BEGIN
						WRITE_ASCII 0x0 BG (2)
					END	
					OUTER_SPRINT val_bcs "%val%"
				END
				tutu BEGIN
					OUTER_PATCH_SAVE val_bcs "%val%" BEGIN
						INSERT_BYTES 0x0 1
						WRITE_ASCII 0x0 "_"
					END
					OUTER_PATCH_SAVE val "%val%" BEGIN
						WRITE_ASCII 0x0 FW (2)
					END
				END	
				DEFAULT
					OUTER_SPRINT val_bcs "%val%"
				END	
				OUTER_SPRINT "%var%" "%val%"
				OUTER_SPRINT "%var%_BCS" "%val_bcs%"
			END
	   
		// residuals
		
			ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%sfo_library_path%/data/bg1_file_names.2da" BEGIN
				LAF 2da_read STR_VAR file=bg1_file_names.2da locbase="%sfo_library_path%/data" case=mixed RET_ARRAY name_array=array rows END
			END ELSE BEGIN
				LAF 2da_read INT_VAR inline=1 STR_VAR file=".../stratagems-inline/bg1_file_names.2da" case=mixed RET_ARRAY name_array=array rows END
			END
			ACTION_PHP_EACH rows AS var=>discard BEGIN
				OUTER_SPRINT val $name_array("%var%" "%thegame%")
				ACTION_IF "%val%" STR_CMP "null" BEGIN
					OUTER_SPRINT "%var%" "%val%"
				END ELSE BEGIN
					OUTER_SPRINT "%var%" ""
				END
			END	
		
			 // chapters
		  
			OUTER_SET chapter_delta= ( ~%thegame%~ STRING_EQUAL bgt ? 1 : 0)
			OUTER_FOR (num=1;num<=7;num +=1) BEGIN
				OUTER_SET ~bg1_chapter_%num%~ =num + chapter_delta
			END


			  // testing
			  /*
			  PRINT ~tutu_var is "%tutu_var%"~
			  PRINT ~tutu_scriptbg is %tutu_scriptbg%~
			  PRINT ~tutu_scriptf is %tutu_scriptf%~
			  PRINT ~bg1_skeleton_warrior is %bg1_skeleton_warrior%~
			  PRINT ~bg1_chapter_4 is %bg1_chapter_4%~
			  PRINT ~Undercity_TempleofBhaal is %Undercity_TempleofBhaal%~
			  ACTION_READLN bloook
			  FAIL BLART
			  */

		END // end of BG1 section

		ACTION_IF is_bg2 BEGIN    // BG2 section

			OUTER_SET chapter_delta= ( ~%thegame%~ STRING_EQUAL eet ? 12 : 0)
			OUTER_FOR (num=1;num<=10;num +=1) BEGIN
				OUTER_SET ~bg2_chapter_%num%~ =num + chapter_delta
			END


		END
	END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
document{make_label}
{
Set a marker file, which can be checked by check_label. </p>

<p>Do not use make_label or check_label in your base tp2 to decide whether to install a component, as they are invisible to Project Infinity and the like.
}

*/
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_DIMORPHIC_FUNCTION make_label
	STR_VAR label=""
BEGIN
    COPY ~.../stratagems-inline/blank~ ~%marker_loc%/%label%.mrk~
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
document{check_label}
{
Check for a marker file, which can be set by make_label.</p>

<p>Do not use make_label or check_label in your base tp2 to decide whether to install a component, as they are invisible to Project Infinity and the like.
}
*/
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_DIMORPHIC_FUNCTION check_label
    STR_VAR label=""
			prefix=""
    RET value
BEGIN
   OUTER_SET value=FILE_EXISTS ~%marker_loc%/%prefix%%label%.mrk~ 
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
document{sfo_fix}
{
A few basic fixes:
<ul>
<li>make sure dir.ids is present and working
<li>add some possibly-missing entries to gtimes.ids and spell.ids
<li>remove + from missile.ids
<li>on BGEE, add some spell.ids entries missing that are present on SoD
</ul>
}
*/
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION sfo_fix BEGIN

	SILENT
	LAF check_label STR_VAR label="dw-sfo-fix" RET value END
	ACTION_IF !value BEGIN
		LAF make_label STR_VAR label="dw-sfo-fix" END
		
		////////////////////////////////////////////////////////////////////////////////
		/// make sure dir.ids is present and working
		////////////////////////////////////////////////////////////////////////////////

<<<<<<<<.../stratagems-inline/dir.ids
IDS
0 S
1 SSW
2 SW
3 SWW
4 W
5 NWW
6 NW
7 NNW
8 N
9 NNE
10 NE
11 NEE
12 E
13 SEE
14 SE
15 SSE
>>>>>>>>

        COPY ".../stratagems-inline/dir.ids" override
        COPY_EXISTING "action.ids" override
             REPLACE_TEXTUALLY "Face\*)" "Face*DIR)"
             REPLACE_TEXTUALLY "Direction\*)" "Direction*DIR)"
        BUT_ONLY

		////////////////////////////////////////////////////////////////////////////////
		// add a few possibly-missing IDS entries (direct from EEAsc)
		////////////////////////////////////////////////////////////////////////////////

        APPEND ~gtimes.ids~ ~6 ONE_ROUND~                    UNLESS ~ONE_ROUND~
        APPEND ~gtimes.ids~ ~12 TWO_ROUNDS~                    UNLESS ~TWO_ROUNDS~
        APPEND ~gtimes.ids~ ~18 THREE_ROUNDS~                    UNLESS ~THREE_ROUNDS~
        APPEND ~gtimes.ids~ ~24 FOUR_ROUNDS~                    UNLESS ~FOUR_ROUNDS~
        APPEND ~gtimes.ids~ ~30 FIVE_ROUNDS~                    UNLESS ~FIVE_ROUNDS~
        APPEND ~gtimes.ids~ ~54 NINE_ROUNDS~                    UNLESS ~NINE_ROUNDS~
        APPEND ~gtimes.ids~ ~60 TEN_ROUNDS~                     UNLESS ~TEN_ROUNDS~
        APPEND ~gtimes.ids~ ~12 TWO_MINUTES~                    UNLESS ~TWO_MINUTES~
        APPEND ~gtimes.ids~ ~18 THREE_MINUTES~                    UNLESS ~THREE_MINUTES~
        APPEND ~gtimes.ids~ ~24 FOUR_MINUTES~                    UNLESS ~FOUR_MINUTES~
        APPEND ~gtimes.ids~ ~30 FIVE_MINUTES~                    UNLESS ~FIVE_MINUTES~
		APPEND ~gtimes.ids~ ~10 TWO_TURNS~						UNLESS ~TWO_TURNS~
        APPEND ~gtimes.ids~ ~45 NINE_MINUTES~                    UNLESS ~NINE_MINUTES~
        APPEND ~gtimes.ids~ ~100 TWENTY_MINUTES~                    UNLESS ~TWENTY_MINUTES~
        APPEND ~gtimes.ids~ ~300 ONE_HOUR~                    UNLESS ~ONE_HOUR~
        APPEND ~gtimes.ids~ ~600 TWO_HOURS~                    UNLESS ~TWO_HOURS~
        APPEND ~gtimes.ids~ ~900 THREE_HOURS~                    UNLESS ~THREE_HOURS~
        APPEND ~gtimes.ids~ ~1200 FOUR_HOURS~                    UNLESS ~FOUR_HOURS~
        APPEND ~gtimes.ids~ ~1500 FIVE_HOURS~                    UNLESS ~FIVE_HOURS~
        APPEND ~gtimes.ids~ ~1800 SIX_HOURS~                    UNLESS ~SIX_HOURS~
        APPEND ~gtimes.ids~ ~2100 SEVEN_HOURS~                    UNLESS ~SEVEN_HOURS~
        APPEND ~gtimes.ids~ ~2400 EIGHT_HOURS~                    UNLESS ~EIGHT_HOURS~

		// on EE, make sure FarthestObject etc are present
		
		ACTION_IF enhanced_edition BEGIN
			APPEND ~object.ids~ ~105 FarthestEnemyOf~ UNLESS ~105 FarthestEnemyOf~			
			APPEND ~object.ids~ ~106 SecondFarthestEnemyOf~ UNLESS ~106 SecondFarthestEnemyOf~
			APPEND ~object.ids~ ~107 ThirdFarthestEnemyOf~ UNLESS ~107 ThirdFarthestEnemyOf~
			APPEND ~object.ids~ ~108 FourthFarthestEnemyOf~ UNLESS ~108 FourthFarthestEnemyOf~
			APPEND ~object.ids~ ~109 FifthFarthestEnemyOf~ UNLESS ~109 FifthFarthestEnemyOf~
			APPEND ~object.ids~ ~110 SixthFarthestEnemyOf~ UNLESS ~110 SixthFarthestEnemyOf~
			APPEND ~object.ids~ ~111 SeventhFarthestEnemyOf~ UNLESS ~111 SeventhFarthestEnemyOf~
			APPEND ~object.ids~ ~112 EighthFarthestEnemyOf~ UNLESS ~112 EighthFarthestEnemyOf~
			APPEND ~object.ids~ ~113 NinthFarthestEnemyOf~ UNLESS ~113 NinthFarthestEnemyOf~
			APPEND ~object.ids~ ~114 TenthFarthestEnemyOf~ UNLESS ~114 TenthFarthestEnemyOf~
		END
		
		// this is missing on oBG2
		
		APPEND ~animate.ids~ ~0xEF10 WATER_WEIRD~ UNLESS ~0xEF10~
		
		// append some sometimes-missing spell.ids entries
		
        ACTION_IF (is_bg && !GAME_IS "bg1 totsc")|| is_bg2 || GAME_IS "IWDEE" BEGIN
			ACTION_CLEAR_ARRAY extra_spell_ids_entries
			ACTION_DEFINE_ASSOCIATIVE_ARRAY extra_spell_ids_entries BEGIN
				2302=>WIZARD_REMOVE_MAGIC
				2705=>WIZARD_KHELBENS_WARDING_WHIP
				3122=>AVENGER_SHAPESHIFT_NATURAL_FORM
				3123=>DRUID_SHAPESHIFT_FROM_BLACKBEAR
				3124=>SHAPESHIFTER_SHAPESHIFT_NATURAL_FORM
				3150=>SHAPESHIFT_NATURAL_FORM_1
				3151=>SHAPESHIFT_NATURAL_FORM_2
				4611=>DRUID_SHAPESHIFT_BROWNBEAR
				4612=>DRUID_SHAPESHIFT_WOLF
				4613=>DRUID_SHAPESHIFT_BLACKBEAR
				4632=>AVENGER_SHAPESHIFT_SWORDSPIDER
				4633=>AVENGER_SHAPESHIFT_BABYWYVERN
				4634=>AVENGER_SHAPESHIFT_FIRESALAMANDER
				4643=>SHAPESHIFTER_SHAPESHIFT_WEREWOLF
				4644=>SHAPESHIFTER_SHAPESHIFT_GREATERWEREWOLF
				2490=>WIZARD_POLYMORPH_NATURAL_FORM
				2493=>WIZARD_POLYMORPH_FLIND
				2494=>WIZARD_POLYMORPH_OGRE
				2495=>WIZARD_POLYMORPH_SPIDER
				2496=>WIZARD_POLYMORPH_MUSTARD_JELLY
				2497=>WIZARD_POLYMORPH_BROWN_BEAR
				2498=>WIZARD_POLYMORPH_BLACK_BEAR
				2499=>WIZARD_POLYMORPH_WOLF
			END
			ACTION_PHP_EACH extra_spell_ids_entries AS ind=>sym BEGIN
				APPEND spell.ids "%ind%%TAB%%sym%" UNLESS "%sym%"
			END		
		END


		////////////////////////////////////////////////////////////////////////////////
		//deal with the broken objects in vBG2
		////////////////////////////////////////////////////////////////////////////////

		ACTION_IF !enhanced_edition BEGIN
			ACTION_FOR_EACH bad IN "iplot01k.itm" "iplot04g.itm" "iplot04h.itm" "iplot04i.itm" "xr2400.are" "xr2600.are" BEGIN
				ACTION_IF FILE_EXISTS_IN_GAME "%bad%" BEGIN
					DISABLE_FROM_KEY "%bad%"
				END
			END
		END
		
		////////////////////////////////////////////////////////////////////////////////
		//remove + signs from missile.ids
		////////////////////////////////////////////////////////////////////////////////

		COPY_EXISTING "missile.ids" override
			REPLACE_TEXTUALLY "\+" "plus_"
		BUT_ONLY

		////////////////////////////////////////////////////////////////////////////////
		//make the rows in weapprof unique
		////////////////////////////////////////////////////////////////////////////////

		COPY_EXISTING weapprof.2da override
			READ_2DA_ENTRIES_NOW weapprof_data 4
			SET_2DA_ENTRY_LATER weapprof_fix 1 0 LARGE_SWORD_BG1
			SET_2DA_ENTRY_LATER weapprof_fix 2 0 SMALL_SWORD_BG1
			SET_2DA_ENTRY_LATER weapprof_fix 3 0 BOW_BG1
			SET_2DA_ENTRY_LATER weapprof_fix 4 0 SPEAR_BG1
			SET_2DA_ENTRY_LATER weapprof_fix 5 0 BLUNT_BG1
			SET_2DA_ENTRY_LATER weapprof_fix 6 0 SPIKED_BG1
			SET_2DA_ENTRY_LATER weapprof_fix 7 0 AXE_BG1
			SET_2DA_ENTRY_LATER weapprof_fix 8 0 MISSILE_BG1
			FOR (row=9;row<weapprof_data;++row) BEGIN
				READ_2DA_ENTRY_FORMER weapprof_data row 0 rowname
				PATCH_IF VARIABLE_IS_SET $sfo_reserved_weapprof_fix("%rowname%") BEGIN
					id=2
					WHILE VARIABLE_IS_SET $sfo_reserved_weapprof_fix("%rowname%%id%") BEGIN
						++id
					END
					SET_2DA_ENTRY_LATER weapprof_fix row 0 "%rowname%%id%"
					SPRINT $sfo_reserved_weapprof_fix("%rowname%%id%") ""
				END ELSE BEGIN
					SPRINT $sfo_reserved_weapprof_fix("%rowname%") ""
				END
			END
			SET_2DA_ENTRIES_NOW weapprof_fix 4

		////////////////////////////////////////////////////////////////////////////////
		//sort out a sometimes-present XPLEVEL bug
		////////////////////////////////////////////////////////////////////////////////

		COPY_EXISTING xplevel.2da override
			LPF 2da_fix END

		////////////////////////////////////////////////////////////////////////////////
		//on games using campaign.2da, make sure the clastext row entries are all labeled
		//the same way
		////////////////////////////////////////////////////////////////////////////////


		ACTION_IF FILE_EXISTS_IN_GAME "campaign.2da" BEGIN 
			// get clastext data
			LAF 2da_read STR_VAR file=clastext.2da RET_ARRAY clastext_rows=rows clastext_array=array END
			//individuate clastext entries by CLASSID, KITID, FALLEN
			ACTION_PHP_EACH clastext_rows AS row=>discard BEGIN
				OUTER_SPRINT classid $clastext_array("%row%" CLASSID)
				OUTER_SPRINT kitid $clastext_array("%row%" KITID)
				OUTER_SPRINT fallen $clastext_array("%row%" FALLEN)
				OUTER_SPRINT $clastext_row_id("%classid%" "%kitid%" "%fallen%") "%row%"
			END
			// get localized clastext files
			LAF 2da_read STR_VAR file=campaign.2da RET_ARRAY campaign_array=array campaign_rows=rows END
			ACTION_PHP_EACH campaign_rows AS row=>discard BEGIN
				OUTER_SPRINT clastext_here $campaign_array("%row%" "CLASTEXT")
				ACTION_IF "%clastext_here%" STR_CMP "*" && FILE_EXISTS_IN_GAME "%clastext_here%.2da" BEGIN
					COPY_EXISTING "%clastext_here%.2da" override
						COUNT_2DA_COLS colcount
						READ_2DA_ENTRIES_NOW clastext_here_entries colcount
						FOR (row=0;row<clastext_here_entries;++row) BEGIN
							READ_2DA_ENTRY_FORMER clastext_here_entries row 0 rowid_here
							READ_2DA_ENTRY_FORMER clastext_here_entries row 1 classid
							READ_2DA_ENTRY_FORMER clastext_here_entries row 2 kitid
							READ_2DA_ENTRY_FORMER clastext_here_entries row 7 fallen
							PATCH_IF VARIABLE_IS_SET $clastext_row_id("%classid%" "%kitid%" "%fallen%") BEGIN
								SPRINT rowid $clastext_row_id("%classid%" "%kitid%" "%fallen%")
								PATCH_IF "%rowid%" STR_CMP "%rowid_here%" BEGIN
									SET_2DA_ENTRY row 0 colcount "%rowid%"
								END
							END ELSE BEGIN
								LPF warning STR_VAR warning="SFO warning: row id %rowid_here% in %clastext_here%.2da (ID %classid%,%kitid%,%fallen%) does not seem to correspond to an entry in main clastext file" END
							END
						END
					BUT_ONLY
				END
			END
		END



		////////////////////////////////////////////////////////////////////////////////
		//on EE, fix a couple of UI errors
		////////////////////////////////////////////////////////////////////////////////

		ACTION_IF enhanced_edition && !lefreut_ui BEGIN
		
		
			////////////////////////////////////////////////////////////////////////////////
			// put scrollbar on SoD UI
			////////////////////////////////////////////////////////////////////////////////

			ACTION_IF GAME_INCLUDES sod BEGIN
				COPY_EXISTING "ui.menu" override
					REPLACE_TEXTUALLY "var[ %TAB%]+currentChargenKit" "var currentChargenKit%WNL%%TAB%%TAB%scrollbar%TAB%'GUISCRC'"

			END

			////////////////////////////////////////////////////////////////////////////////
			// fix misc vanilla UI bugs 
			////////////////////////////////////////////////////////////////////////////////

			COPY_EXISTING "ui.menu" override
				REPLACE_TEXTUALLY ~SequenceOnce "~ ~-- SequenceOnce "~
				REPLACE_TEXTUALLY ~clickable false~ ~clickable lua "false"~

		
		
		
		END





	END
	VERBOSE

END


DEFINE_ACTION_MACRO sfo_batch_set BEGIN
	OUTER_SET SFO_batch_mode=1
	MKDIR "%batch_loc%"
	LAF new_file INT_VAR no_log=1 STR_VAR file="%MOD_FOLDER%.bat" path="%batch_loc%" arguments="setup-%MOD_FOLDER% --uninstall --force-install-list" END
	AT_EXIT "%batch_loc%/%MOD_FOLDER%.bat"
END


DEFINE_ACTION_FUNCTION sfo_batch_update BEGIN
	COPY "%batch_loc%/%MOD_FOLDER%.bat" "%batch_loc%"
		SPRINT string " %COMPONENT_NUMBER%"
		insert_point=BUFFER_LENGTH
		INSERT_BYTES insert_point STRING_LENGTH "%string%"
		WRITE_ASCII insert_point "%string%"
END
