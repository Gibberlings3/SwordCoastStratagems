/*
describe-library{
The function here is used to hide a particular ability button so that a specific kit can't use it.
}
brief-desc{hide an ability button for a class/kit}

*/


// Buttons are drawn from ENGINEST

/*
document{hide_ability_button_for_kit}
{
The kit 'kit' has the button(s) 'buttons' hidden by the UI. 'kit' is a rowname entry from kitlist.2da. 'Buttons' is a space-separated list of strings
which appear as 'button' in enginest.2da entries of form STRREF_GUI_BUTTONS_[button].


}



*/

DEFINE_ACTION_FUNCTION hide_ability_button_for_kit 
	STR_VAR kit="" 
			buttons="" 
BEGIN
	//do setup if needed
	LAF hide_ability_button_setup END
	// get the kit ID	
	COPY_EXISTING "kitlist.2da" override
		COUNT_2DA_COLS colcount
		READ_2DA_ENTRIES_NOW kitlist_data colcount
		FOR (row=0;row<kitlist_data;++row) BEGIN
			READ_2DA_ENTRY_FORMER kitlist_data row 1 kit_here
			PATCH_IF "%kit%" STRING_EQUAL_CASE "%kit_here%" BEGIN
				READ_2DA_ENTRY_FORMER kitlist_data row 0 kit_id
			END
		END
	BUT_ONLY
	OUTER_SPRINT write_data "dwHideButtonData[%kit_id%]={%WNL%"
	OUTER_WHILE "%buttons%" STRING_COMPARE "" BEGIN
		LAF return_first_pair STR_VAR list="%buttons%" RET key value buttons=list END
		COPY_EXISTING "enginest.2da" "%workspace%"
			TO_UPPER value
			REPLACE_EVALUATE "STRREF_GUI_BUTTONS_%value%[ %TAB%]+\([0-9]+\)" BEGIN
				SET tooltip_strref="%MATCH1%"
			END
			""
		BUT_ONLY
		ACTION_GET_STRREF tooltip_strref tooltip
		OUTER_SPRINT write_data  "%write_data%%TAB%[%key%]='%tooltip%',%WNL%"	
	END
	OUTER_SPRINT write_data "%write_data%}%WNL%%WNL%"
	APPEND "m_dw_htd.lua" "%write_data%"
END

DEFINE_ACTION_FUNCTION hide_ability_button_setup//internal
BEGIN

	ACTION_IF !FILE_EXISTS_IN_GAME "m_dw_htf.lua" BEGIN
		COPY ".../stratagems-inline/m_dw_htf.lua" override
		COPY ".../stratagems-inline/m_dw_htd.lua" override
		COPY_EXISTING "ui.menu" override
		LPF UI_alter_object STR_VAR object=WORLD_ACTIONBAR object_type=menu patch=patch_button END
	
	
	END


END

DEFINE_PATCH_FUNCTION patch_button//internal 
BEGIN

			REPLACE_TEXTUALLY ~buttonArray:GetButtonEnabled(\([0-9]+\))"~ ~buttonArray:GetButtonEnabled(\1) and not dwHideButton(\1)"~


END

<<<<<<<< .../stratagems-inline/m_dw_htf.lua
function dwHideButton(button)

	_,k=dwFindClassKit()
	if not dwHideButtonData[k] then 
		return false
	end
	str=dwHideButtonData[k][button]
	if not str then
		return false
	end
	tooltip=actionBarTooltip[button]
	if string.find(tooltip,str) then
		return true
	else
	    return false
	end

end
>>>>>>>>
<<<<<<<< .../stratagems-inline/m_dw_htd.lua
dwHideButtonData={}
>>>>>>>>